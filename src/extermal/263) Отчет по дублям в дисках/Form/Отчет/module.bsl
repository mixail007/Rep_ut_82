
Процедура ДействияФормыОтчетСформировать(Кнопка)
	
	ТабДок = ЭлементыФормы.ПолеТабличногоДокумента;
	Отчет(ТабДок);

	
КонецПроцедуры

Процедура Отчет(ТабДок) Экспорт
	
	Макет = ПолучитьМакет("Отчет");
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	А.Номенклатура.Наименование КАК Наименование,
	|	КОЛИЧЕСТВО(*) КАК Поле1
	|ПОМЕСТИТЬ ВТ_Дубли_Наименования
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах.Остатки(, Номенклатура.ВидТовара = ЗНАЧЕНИЕ(Перечисление.ВидыТоваров.Диски)) КАК ТоварыНаСкладахОстатки) КАК А
	|
	|СГРУППИРОВАТЬ ПО
	|	А.Номенклатура.Наименование
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(*) > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстатки.Номенклатура.Код КАК Код,
	|	ТоварыНаСкладахОстатки.Склад,
	|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			,
	|			Номенклатура.ВидТовара = ЗНАЧЕНИЕ(Перечисление.ВидыТоваров.Диски)
	|				И Номенклатура.Наименование В
	|					(ВЫБРАТЬ
	|						ВТ_Дубли_Наименования.Наименование
	|					ИЗ
	|						ВТ_Дубли_Наименования)) КАК ТоварыНаСкладахОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТоварыНаСкладахОстатки.Номенклатура.Наименование
	|ИТОГИ
	|	СУММА(КоличествоОстаток)
	|ПО
	|	Номенклатура";

	Результат = Запрос.Выполнить();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьПодвалТаблицы = Макет.ПолучитьОбласть("ПодвалТаблицы");
	ОбластьНоменклатура = Макет.ПолучитьОбласть("Номенклатура");
	ОбластьДетальныхЗаписей = Макет.ПолучитьОбласть("Детали");

	ТабДок.Очистить();
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	ТабДок.НачатьАвтогруппировкуСтрок();

	ВыборкаНоменклатура = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаНоменклатура.Следующий() Цикл
		ОбластьНоменклатура.Параметры.Заполнить(ВыборкаНоменклатура);
		ОбластьНоменклатура.Параметры.ДатаСоздания= ПолучитьМоментСозданияСсылки(ВыборкаНоменклатура.Номенклатура);
		ТабДок.Вывести(ОбластьНоменклатура, ВыборкаНоменклатура.Уровень());

		ВыборкаДетали = ВыборкаНоменклатура.Выбрать();

		Пока ВыборкаДетали.Следующий() Цикл
			ОбластьДетальныхЗаписей.Параметры.Заполнить(ВыборкаДетали);
			
			ТабДок.Вывести(ОбластьДетальныхЗаписей, ВыборкаДетали.Уровень());
		КонецЦикла;
	КонецЦикла;

	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	ТабДок.Вывести(ОбластьПодвалТаблицы);
	ТабДок.Вывести(ОбластьПодвал);

	
КонецПроцедуры


Функция ПолучитьМоментСозданияУникальногоИдентификатора(УИ) Экспорт
	
	УИ = СокрЛП(ВРег(СтрЗаменить("" + УИ, "-", "")));
	Если Не Сред(УИ, 13, 1) = "1" Тогда
		Возврат '00010101';
	КонецЕсли;
	интервал16 = ВРег(Сред(УИ, 14, 3) + Сред(УИ, 9, 4) + Сред(УИ, 1, 8));
	интервал10 = HexToDec(интервал16);
	интервал10сек = интервал10 / 10000000;
	
	Результат = Дата(1582, 10, 15) + интервал10сек;
	
	Возврат Результат + 4 * 3600;
	
КонецФункции

Функция ПолучитьМоментСозданияСсылки(Ссылка) Экспорт
	
	УИ = "" + Ссылка.УникальныйИдентификатор();
	
	Результат = ПолучитьМоментСозданияУникальногоИдентификатора(УИ);
	
	Возврат Результат;
	
КонецФункции

Функция HexToDec(знч16) Экспорт
	знч10 = 0;
	длн = СтрДлина(знч16);
	Для й = 1 По СтрДлина(знч16) Цикл
		знч10 = знч10 + Pow(16, длн - й) * ByteToDec(Сред(знч16, й, 1));
	КонецЦикла;
	Возврат знч10;
КонецФункции


Функция ByteToDec(знч16) Экспорт
	Если знч16 = "0" Тогда
		Возврат 0;
	ИначеЕсли знч16 = "1" Тогда
		Возврат 1;
	ИначеЕсли знч16 = "2" Тогда
		Возврат 2;
	ИначеЕсли знч16 = "3" Тогда
		Возврат 3;
	ИначеЕсли знч16 = "4" Тогда
		Возврат 4;
	ИначеЕсли знч16 = "5" Тогда
		Возврат 5;
	ИначеЕсли знч16 = "6" Тогда
		Возврат 6;
	ИначеЕсли знч16 = "7" Тогда
		Возврат 7;
	ИначеЕсли знч16 = "8" Тогда
		Возврат 8;
	ИначеЕсли знч16 = "9" Тогда
		Возврат 9;
	ИначеЕсли знч16 = "A" Тогда
		Возврат 10;
	ИначеЕсли знч16 = "B" Тогда
		Возврат 11;
	ИначеЕсли знч16 = "C" Тогда
		Возврат 12;
	ИначеЕсли знч16 = "D" Тогда
		Возврат 13;
	ИначеЕсли знч16 = "E" Тогда
		Возврат 14;
	ИначеЕсли знч16 = "F" Тогда
		Возврат 15;
	Иначе
		Возврат 0;
	КонецЕсли;
КонецФункции

