Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ОбщийОтчет.ИмяРегистра = "ТоварыНаСкладах";
	ОбщийОтчет.мВыбиратьИмяРегистра = Ложь;
	ОбщийОтчет.НП = Новый НастройкаПериода;
	ОбщийОтчет.мСтруктураДляОтбораПоКатегориям = Новый Структура;

	ОбщийОтчет.мНазваниеОтчета = "Ведомость по товарам на складах";

	СтруктураПредставлениеПолей = Новый Структура;
	МассивОтбора = Новый Массив;
	
	ЗаполнитьНачальныеНастройкиПоМакету(ПолучитьМакет("ПараметрыОтчетовОстаткиТоваровКомпании"), СтруктураПредставлениеПолей, МассивОтбора, ОбщийОтчет, "ОстаткиИОбороты");	
	
	//////////
	ОбщийОтчет.ПостроительОтчета.Текст = "ВЫБРАТЬ
	                                     |	СУММА(ТаблицаРегистра.КоличествоНачальныйОстаток) КАК КоличествоНачальныйОстаток,
	                                     |	СУММА(ТаблицаРегистра.КоличествоКонечныйОстаток) КАК КоличествоКонечныйОстаток,
	                                     |	СУММА(ТаблицаРегистра.КоличествоПриход) КАК КоличествоПриход,
	                                     |	СУММА(ТаблицаРегистра.КоличествоРасход) КАК КоличествоРасход,
	                                     |	СУММА(ТаблицаРегистра.КоличествоНачальныйОстаток * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ТаблицаРегистра.Номенклатура.ЕдиницаДляОтчетов.Коэффициент) КАК КоличествоЕдиницОтчетовНачальныйОстаток,
	                                     |	СУММА(ТаблицаРегистра.КоличествоКонечныйОстаток * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ТаблицаРегистра.Номенклатура.ЕдиницаДляОтчетов.Коэффициент) КАК КоличествоЕдиницОтчетовКонечныйОстаток,
	                                     |	СУММА(ТаблицаРегистра.КоличествоПриход * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ТаблицаРегистра.Номенклатура.ЕдиницаДляОтчетов.Коэффициент) КАК КоличествоЕдиницОтчетовПриход,
	                                     |	СУММА(ТаблицаРегистра.КоличествоРасход * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ТаблицаРегистра.Номенклатура.ЕдиницаДляОтчетов.Коэффициент) КАК КоличествоЕдиницОтчетовРасход,
	                                     |	СУММА(ТаблицаРегистра.КоличествоНачальныйОстаток * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК КоличествоБазовыхЕдНачальныйОстаток,
	                                     |	СУММА(ТаблицаРегистра.КоличествоКонечныйОстаток * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК КоличествоБазовыхЕдКонечныйОстаток,
	                                     |	СУММА(ТаблицаРегистра.КоличествоПриход * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК КоличествоБазовыхЕдПриход,
	                                     |	СУММА(ТаблицаРегистра.КоличествоРасход * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК КоличествоБазовыхЕдРасход,
	                                     |	ТаблицаРегистра.Склад КАК Склад,
	                                     |	ТаблицаРегистра.Номенклатура КАК Номенклатура,
	                                     |	ТаблицаРегистра.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	                                     |	ТаблицаРегистра.СерияНоменклатуры КАК СерияНоменклатуры,
	                                     |	ТаблицаРегистра.Качество КАК Качество,
	                                     |	СУММА(ВложенныйЗапрос.КоличествоОстаток) КАК КоличествоОстаток
	                                     |{ВЫБРАТЬ
	                                     |	ТаблицаРегистра.Склад.* КАК Склад,
	                                     |	ТаблицаРегистра.Номенклатура.* КАК Номенклатура,
	                                     |	ТаблицаРегистра.Номенклатура.Код КАК НоменклатураКод,
	                                     |	ТаблицаРегистра.ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры,
	                                     |	ТаблицаРегистра.СерияНоменклатуры.* КАК СерияНоменклатуры,
	                                     |	ТаблицаРегистра.Качество.* КАК Качество,
	                                     |	ТаблицаРегистра.Период,
	                                     |	ТаблицаРегистра.Регистратор.* КАК Регистратор,
	                                     |	(СУММА(ТаблицаРегистра.КоличествоНачальныйОстаток)) КАК КоличествоНачальныйОстаток,
	                                     |	(СУММА(ТаблицаРегистра.КоличествоКонечныйОстаток)) КАК КоличествоКонечныйОстаток,
	                                     |	(СУММА(ТаблицаРегистра.КоличествоПриход)) КАК КоличествоПриход,
	                                     |	(СУММА(ТаблицаРегистра.КоличествоРасход)) КАК КоличествоРасход,
	                                     |	(СУММА(ТаблицаРегистра.КоличествоНачальныйОстаток * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ТаблицаРегистра.Номенклатура.ЕдиницаДляОтчетов.Коэффициент)) КАК КоличествоЕдиницОтчетовНачальныйОстаток,
	                                     |	(СУММА(ТаблицаРегистра.КоличествоКонечныйОстаток * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ТаблицаРегистра.Номенклатура.ЕдиницаДляОтчетов.Коэффициент)) КАК КоличествоЕдиницОтчетовКонечныйОстаток,
	                                     |	(СУММА(ТаблицаРегистра.КоличествоПриход * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ТаблицаРегистра.Номенклатура.ЕдиницаДляОтчетов.Коэффициент)) КАК КоличествоЕдиницОтчетовПриход,
	                                     |	(СУММА(ТаблицаРегистра.КоличествоРасход * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ТаблицаРегистра.Номенклатура.ЕдиницаДляОтчетов.Коэффициент)) КАК КоличествоЕдиницОтчетовРасход,
	                                     |	(СУММА(ТаблицаРегистра.КоличествоНачальныйОстаток * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент)) КАК КоличествоБазовыхЕдНачальныйОстаток,
	                                     |	(СУММА(ТаблицаРегистра.КоличествоКонечныйОстаток * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент)) КАК КоличествоБазовыхЕдКонечныйОстаток,
	                                     |	(СУММА(ТаблицаРегистра.КоличествоПриход * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент)) КАК КоличествоБазовыхЕдПриход,
	                                     |	(СУММА(ТаблицаРегистра.КоличествоРасход * ТаблицаРегистра.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент)) КАК КоличествоБазовыхЕдРасход,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, ДЕНЬ)) КАК ПериодДень,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, НЕДЕЛЯ)) КАК ПериодНеделя,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, ДЕКАДА)) КАК ПериодДекада,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, МЕСЯЦ)) КАК ПериодМесяц,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, КВАРТАЛ)) КАК ПериодКвартал,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, ПОЛУГОДИЕ)) КАК ПериодПолугодие,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, ГОД)) КАК ПериодГод}
	                                     |ИЗ
	                                     |	РегистрНакопления.ТоварыНаСкладах.ОстаткиИОбороты(&ДатаНач, &ДатаКон, {(&Периодичность)}, , {(Склад).* КАК Склад, (Номенклатура).* КАК Номенклатура, (Номенклатура.Код) КАК НоменклатураКод, (ХарактеристикаНоменклатуры).* КАК ХарактеристикаНоменклатуры, (СерияНоменклатуры).* КАК СерияНоменклатуры, (Качество).* КАК Качество}) КАК ТаблицаРегистра
	                                     |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                                     |			ТоварыНаСкладахОстатки.Склад КАК Склад,
	                                     |			ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	                                     |			ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	                                     |			ТоварыНаСкладахОстатки.Качество КАК Качество,
	                                     |			СУММА(ТоварыНаСкладахОстатки.КоличествоОстаток) КАК КоличествоОстаток
	                                     |		ИЗ
	                                     |			РегистрНакопления.ТоварыНаСкладах.Остатки(&ДатаКон, ) КАК ТоварыНаСкладахОстатки
	                                     |		{ГДЕ
	                                     |			ТоварыНаСкладахОстатки.КоличествоОстаток,
	                                     |			ТоварыНаСкладахОстатки.Склад.*,
	                                     |			ТоварыНаСкладахОстатки.Номенклатура.*,
	                                     |			ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры.*,
	                                     |			ТоварыНаСкладахОстатки.СерияНоменклатуры.*,
	                                     |			ТоварыНаСкладахОстатки.Качество.*}
	                                     |		
	                                     |		СГРУППИРОВАТЬ ПО
	                                     |			ТоварыНаСкладахОстатки.Склад,
	                                     |			ТоварыНаСкладахОстатки.Номенклатура,
	                                     |			ТоварыНаСкладахОстатки.ХарактеристикаНоменклатуры,
	                                     |			ТоварыНаСкладахОстатки.Качество) КАК ВложенныйЗапрос
	                                     |		ПО ТаблицаРегистра.Номенклатура = ВложенныйЗапрос.Номенклатура
	                                     |			И ТаблицаРегистра.Склад = ВложенныйЗапрос.Склад
	                                     |{ГДЕ
	                                     |	ТаблицаРегистра.Склад.* КАК Склад,
	                                     |	ТаблицаРегистра.Номенклатура.* КАК Номенклатура,
	                                     |	ТаблицаРегистра.Номенклатура.Код КАК НоменклатураКод,
	                                     |	ТаблицаРегистра.ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры,
	                                     |	ТаблицаРегистра.СерияНоменклатуры.* КАК СерияНоменклатуры,
	                                     |	ТаблицаРегистра.Качество.* КАК Качество,
	                                     |	ТаблицаРегистра.Период,
	                                     |	ТаблицаРегистра.Регистратор.* КАК Регистратор,
	                                     |	ВложенныйЗапрос.КоличествоОстаток}
	                                     |
	                                     |СГРУППИРОВАТЬ ПО
	                                     |	ТаблицаРегистра.Склад,
	                                     |	ТаблицаРегистра.Номенклатура,
	                                     |	ТаблицаРегистра.ХарактеристикаНоменклатуры,
	                                     |	ТаблицаРегистра.СерияНоменклатуры,
	                                     |	ТаблицаРегистра.Качество
	                                     |{УПОРЯДОЧИТЬ ПО
	                                     |	ТаблицаРегистра.Склад.* КАК Склад,
	                                     |	ТаблицаРегистра.Номенклатура.* КАК Номенклатура,
	                                     |	ТаблицаРегистра.Номенклатура.Код КАК НоменклатураКод,
	                                     |	ТаблицаРегистра.ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры,
	                                     |	ТаблицаРегистра.СерияНоменклатуры.* КАК СерияНоменклатуры,
	                                     |	ТаблицаРегистра.Качество.* КАК Качество,
	                                     |	ТаблицаРегистра.Период,
	                                     |	ТаблицаРегистра.Регистратор.* КАК Регистратор,
	                                     |	КоличествоНачальныйОстаток,
	                                     |	КоличествоКонечныйОстаток,
	                                     |	КоличествоПриход,
	                                     |	КоличествоРасход,
	                                     |	КоличествоЕдиницОтчетовНачальныйОстаток,
	                                     |	КоличествоЕдиницОтчетовКонечныйОстаток,
	                                     |	КоличествоЕдиницОтчетовПриход,
	                                     |	КоличествоЕдиницОтчетовРасход,
	                                     |	КоличествоБазовыхЕдНачальныйОстаток,
	                                     |	КоличествоБазовыхЕдКонечныйОстаток,
	                                     |	КоличествоБазовыхЕдПриход,
	                                     |	КоличествоБазовыхЕдРасход}
	                                     |ИТОГИ
	                                     |	СУММА(КоличествоНачальныйОстаток),
	                                     |	СУММА(КоличествоКонечныйОстаток),
	                                     |	СУММА(КоличествоПриход),
	                                     |	СУММА(КоличествоРасход),
	                                     |	СУММА(КоличествоЕдиницОтчетовНачальныйОстаток),
	                                     |	СУММА(КоличествоЕдиницОтчетовКонечныйОстаток),
	                                     |	СУММА(КоличествоЕдиницОтчетовПриход),
	                                     |	СУММА(КоличествоЕдиницОтчетовРасход),
	                                     |	СУММА(КоличествоБазовыхЕдНачальныйОстаток),
	                                     |	СУММА(КоличествоБазовыхЕдКонечныйОстаток),
	                                     |	СУММА(КоличествоБазовыхЕдПриход),
	                                     |	СУММА(КоличествоБазовыхЕдРасход)
	                                     |ПО
	                                     |	ОБЩИЕ,
	                                     |	Склад,
	                                     |	Номенклатура
	                                     |{ИТОГИ ПО
	                                     |	ТаблицаРегистра.Номенклатура.* КАК Номенклатура,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, ДЕНЬ)) КАК ПериодДень,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, НЕДЕЛЯ)) КАК ПериодНеделя,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, ДЕКАДА)) КАК ПериодДекада,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, МЕСЯЦ)) КАК ПериодМесяц,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, КВАРТАЛ)) КАК ПериодКвартал,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, ПОЛУГОДИЕ)) КАК ПериодПолугодие,
	                                     |	(НАЧАЛОПЕРИОДА(ТаблицаРегистра.Период, ГОД)) КАК ПериодГод,
	                                     |	Склад.*,
	                                     |	ХарактеристикаНоменклатуры.*,
	                                     |	СерияНоменклатуры.*,
	                                     |	Качество.*}
	                                     |АВТОУПОРЯДОЧИВАНИЕ";
										 
	ОбщийОтчет.ПостроительОтчета.ВыбранныеПоля.Очистить();
										 
	Если ПолучитьЗначениеПоУмолчанию(ПараметрыСеанса.ТекущийПользователь, "УчетТолькоПоПодразделениюПользователя") Тогда
		ДоступноеПодразделение = ПараметрыСеанса.ТекущийПользователь.ОсновноеПодразделение;
		МассивОтбора.Добавить("Склад.Подразделение");
	КонецЕсли;
	
	ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей, ОбщийОтчет.ПостроительОтчета);
	ОчиститьДополнительныеПоляПостроителя(ОбщийОтчет.ПостроительОтчета);
	ЗаполнитьОтбор(МассивОтбора, ОбщийОтчет.ПостроительОтчета);
	
	Если ПолучитьЗначениеПоУмолчанию(ПараметрыСеанса.ТекущийПользователь, "УчетТолькоПоПодразделениюПользователя") Тогда
		ОбщийОтчет.ПостроительОтчета.Отбор.СкладПодразделение.Значение = ДоступноеПодразделение;
		ОбщийОтчет.ПостроительОтчета.Отбор.СкладПодразделение.Использование = Истина;
	КонецЕсли;
	
	// Установим дату начала отчета
	Если ЗначениеНеЗаполнено(ОбщийОтчет.ДатаНач) Тогда
		Если Не ЗначениеНеЗаполнено(глТекущийПользователь) Тогда
			ОбщийОтчет.ДатаНач = ПолучитьЗначениеПоУмолчанию(глТекущийПользователь,"ОсновнаяДатаНачалаОтчетов");
		КонецЕсли;
	КонецЕсли;
	
	ОбщийОтчет.ВыводитьИтогиПоВсемУровням=Истина;										 
	
КонецПроцедуры

Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок = Ложь, ВысотаЗаголовка = 0, ТолькоЗаголовок = Ложь) Экспорт

	Если ПолучитьЗначениеПоУмолчанию(ПараметрыСеанса.ТекущийПользователь, "УчетТолькоПоПодразделениюПользователя") Тогда
		ДоступноеПодразделение = ПараметрыСеанса.ТекущийПользователь.ОсновноеПодразделение;
    	ОбщийОтчет.ПостроительОтчета.Отбор.СкладПодразделение.Значение = ДоступноеПодразделение;
		ОбщийОтчет.ПостроительОтчета.Отбор.СкладПодразделение.Использование = Истина;
		ОбщийОтчет.ПостроительОтчета.Отбор.СкладПодразделение.ВидСравнения = ВидСравнения.Равно;
	КонецЕсли;
		
	ОбщийОтчет.СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок);
	
КонецПроцедуры

// Читает свойство Построитель отчета
//
// Параметры
//	Нет
//
Функция ПолучитьПостроительОтчета() Экспорт

	Возврат ОбщийОтчет.ПолучитьПостроительОтчета();

КонецФункции // ПолучитьПостроительОтчета()

// Настраивает отчет по переданной структуре параметров
//
// Параметры:
//	Нет.
//
Процедура Настроить(Параметры) Экспорт

	ОбщийОтчет.Настроить(Параметры, ЭтотОбъект);

КонецПроцедуры

// Возвращает основную форму отчета, связанную с данным экземпляром отчета
//
// Параметры
//	Нет
//
Функция ПолучитьОсновнуюФорму() Экспорт
	
	ОснФорма = ПолучитьФорму();
	ОснФорма.ОбщийОтчет = ОбщийОтчет;
	ОснФорма.ЭтотОтчет = ЭтотОбъект;
	Возврат ОснФорма;
	
КонецФункции // ПолучитьОсновнуюФорму()

// Возвращает форму настройки 
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	
//
Функция ПолучитьФормуНастройки() Экспорт
	
	ФормаНастройки = ОбщийОтчет.ПолучитьФорму("ФормаНастройка");
	Возврат ФормаНастройки;
	
КонецФункции // ПолучитьФормуНастройки()

// Процедура обработки расшифровки
//
// Параметры:
//	Нет.
//
Процедура ОбработкаРасшифровки(РасшифровкаСтроки, ПолеТД, ВысотаЗаголовка, СтандартнаяОбработка) Экспорт
	
	// Добавление расшифровки из колонки
	Если ТипЗнч(РасшифровкаСтроки) = Тип("Структура") Тогда
		
		// Расшифровка колонки находится в заголовке колонки
		РасшифровкаКолонки = ПолеТД.Область(ВысотаЗаголовка+2, ПолеТД.ТекущаяОбласть.Лево).Расшифровка;

		Расшифровка = Новый Структура;

		Для каждого Элемент Из РасшифровкаСтроки Цикл
			Расшифровка.Вставить(Элемент.Ключ, Элемент.Значение);
		КонецЦикла;

		Если ТипЗнч(РасшифровкаКолонки) = Тип("Структура") Тогда

			Для каждого Элемент Из РасшифровкаКолонки Цикл
				Расшифровка.Вставить(Элемент.Ключ, Элемент.Значение);
			КонецЦикла;

		КонецЕсли; 

		ОбщийОтчет.ОбработкаРасшифровкиСтандартногоОтчета(Расшифровка, СтандартнаяОбработка, ЭтотОбъект);

	КонецЕсли;
	
КонецПроцедуры // ОбработкаРасшифровки()

// Формирует структуру, в которую складываются настройки
//
Функция СформироватьСтруктуруДляСохраненияНастроек(ПоказыватьЗаголовок) Экспорт
	
	СтруктураНастроек = Новый Структура;
	
	ОбщийОтчет.СформироватьСтруктуруДляСохраненияНастроек(СтруктураНастроек, ПоказыватьЗаголовок);
	
	Возврат СтруктураНастроек;
	
КонецФункции

// Заполняет настройки из структуры - кроме состояния панели "Отбор"
//
Процедура ВосстановитьНастройкиИзСтруктуры(СохраненныеНастройки, ПоказыватьЗаголовок, Отчет=Неопределено) Экспорт
	
	// Если отчет, вызвавший порцедуру, не передан, то считаем, что ее вызвал этот отчет
	Если Отчет = Неопределено Тогда
		Отчет = ЭтотОбъект;
	КонецЕсли;

	ОбщийОтчет.ВосстановитьНастройкиИзСтруктуры(СохраненныеНастройки, ПоказыватьЗаголовок, Отчет);
	
	Если ПолучитьЗначениеПоУмолчанию(ПараметрыСеанса.ТекущийПользователь, "УчетТолькоПоПодразделениюПользователя") Тогда
		ДоступноеПодразделение = ПараметрыСеанса.ТекущийПользователь.ОсновноеПодразделение;
		ОбщийОтчет.ПостроительОтчета.Отбор.СкладПодразделение.Значение = ДоступноеПодразделение;
		ОбщийОтчет.ПостроительОтчета.Отбор.СкладПодразделение.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры
