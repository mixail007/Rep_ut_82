Перем ТаблицаСводная Экспорт;
Перем ТЗдляЗаказа Экспорт;

Процедура СформироватьОтчет(Таб, Пост) Экспорт
	
	ТаблицаСводная = Новый ТаблицаЗначений;
	ТЗдляЗаказа = Новый ТаблицаЗначений;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц=Новый МенеджерВременныхТаблиц;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ЗаказПоставщикуСезонныйТовары.Номенклатура,
	               |	СУММА(ЗаказПоставщикуСезонныйТовары.Количество) КАК Количество
	               |ПОМЕСТИТЬ ТЗзаказПоставщику
	               |ИЗ
	               |	Документ.ЗаказПоставщикуСезонный.Товары КАК ЗаказПоставщикуСезонныйТовары
	               |ГДЕ
	               |	ЗаказПоставщикуСезонныйТовары.Ссылка В(&СписокЗаказов)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЗаказПоставщикуСезонныйТовары.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВложенныйЗапрос.Номенклатура,
	               |	СУММА(ВложенныйЗапрос.КоличествоНаСкладе) КАК КоличествоНаСкладе,
	               |	СУММА(ВложенныйЗапрос.КоличествоЗаказПоставщикам) КАК КоличествоЗаказПоставщикам,
	               |	СУММА(ВложенныйЗапрос.КоличествоСезонныйЗаказ) КАК КоличествоСезонныйЗаказ,
	               |	СУММА(ВложенныйЗапрос.КоличествоОстаток) КАК КоличествоОстаток
	               |ПОМЕСТИТЬ ТЗостатков
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	               |		NULL КАК ЗаказПоставщику,
	               |		ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоНаСкладе,
	               |		0 КАК КоличествоЗаказПоставщикам,
	               |		0 КАК КоличествоСезонныйЗаказ,
	               |		ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
	               |	ИЗ
	               |		РегистрНакопления.ТоварыНаСкладах.Остатки(
	               |				,
	               |				Номенклатура.ВидТовара = ЗНАЧЕНИЕ(Перечисление.ВидыТоваров.Диски)
	               |					И НЕ Склад.ЗапретитьИспользование
	               |					И НЕ Склад.Транзитный
	               |					//УсловиеПоставщик
				   |) КАК ТоварыНаСкладахОстатки
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ЗаказыПоставщикамОстатки.Номенклатура,
	               |		ЗаказыПоставщикамОстатки.ЗаказПоставщику,
	               |		0,
	               |		ЗаказыПоставщикамОстатки.КоличествоОстаток,
	               |		0,
	               |		ЗаказыПоставщикамОстатки.КоличествоОстаток
	               |	ИЗ
	               |		РегистрНакопления.ЗаказыПоставщикам.Остатки(
	               |				,
	               |				Номенклатура.ВидТовара = ЗНАЧЕНИЕ(Перечисление.ВидыТоваров.Диски)
	               |					//УсловиеПоставщик
				   |) КАК ЗаказыПоставщикамОстатки
	               |	ГДЕ
	               |		ЗаказыПоставщикамОстатки.КоличествоОстаток > 0
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ЗаказыПоставщикамСезонныеОстатки.Номенклатура,
	               |		ЗаказыПоставщикамСезонныеОстатки.ЗаказПоставщикуСезонный,
	               |		0,
	               |		0,
	               |		ЗаказыПоставщикамСезонныеОстатки.КоличествоОстаток,
	               |		ЗаказыПоставщикамСезонныеОстатки.КоличествоОстаток
	               |	ИЗ
	               |		РегистрНакопления.ЗаказыПоставщикамСезонные.Остатки(
	               |				,
	               |				Номенклатура.ВидТовара = ЗНАЧЕНИЕ(Перечисление.ВидыТоваров.Диски)
	               |					//УсловиеПоставщик
				   |) КАК ЗаказыПоставщикамСезонныеОстатки
	               |	ГДЕ
	               |		ЗаказыПоставщикамСезонныеОстатки.КоличествоОстаток > 0) КАК ВложенныйЗапрос
	               |ГДЕ
	               |	ВложенныйЗапрос.КоличествоОстаток > 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТЗостатков.Номенклатура КАК Номенклатура,
	               |	ТЗостатков.Номенклатура.Код КАК НоменклатураКод,
	               |	ТЗостатков.КоличествоНаСкладе КАК КоличествоНаСкладе,
	               |	ТЗостатков.КоличествоЗаказПоставщикам КАК КоличествоЗаказПоставщикам,
	               |	ТЗостатков.КоличествоСезонныйЗаказ КАК КоличествоСезонныйЗаказ,
	               |	ТЗостатков.КоличествоОстаток КАК КоличествоОстаток,
	               |	КомплектующиеНоменклатуры.Комплектующая КАК Комплектующая,
	               |	КомплектующиеНоменклатуры.Количество * ТЗостатков.КоличествоОстаток КАК НеобходимоеКоличество,
	               |	КомплектующиеНоменклатуры.Количество КАК КоличествоНаОдинДиск,
	               |	ТЗзаказПоставщику.Количество КАК КоличествоВЗаказах,
	               |	КомплектующиеНоменклатуры.Количество * ТЗзаказПоставщику.Количество КАК НеобходимоеКоличествоВзаказах
	               |ПОМЕСТИТЬ ТЗостатковСкрышками
	               |ИЗ
	               |	ТЗостатков КАК ТЗостатков
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			РегКомплектующиеНоменклатуры.Номенклатура КАК Номенклатура,
	               |			РегКомплектующиеНоменклатуры.Комплектующая КАК Комплектующая,
	               |			РегКомплектующиеНоменклатуры.Количество КАК Количество
	               |		ИЗ
	               |			РегистрСведений.КомплектующиеНоменклатуры КАК РегКомплектующиеНоменклатуры
	               |		ГДЕ
	               |			РегКомплектующиеНоменклатуры.Номенклатура.ВидТовара = ЗНАЧЕНИЕ(Перечисление.ВидыТоваров.Диски)
	               |			И РегКомплектующиеНоменклатуры.Комплектующая.ВидТовара <> ЗНАЧЕНИЕ(Перечисление.ВидыТоваров.Диски)
	               |			И РегКомплектующиеНоменклатуры.ХарактеристикаНоменклатуры = &ПустаяХарактеристика
	               |			И РегКомплектующиеНоменклатуры.ХарактеристикаКомплектующей = &ПустаяХарактеристика) КАК КомплектующиеНоменклатуры
	               |		ПО ТЗостатков.Номенклатура = КомплектующиеНоменклатуры.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТЗзаказПоставщику КАК ТЗзаказПоставщику
	               |		ПО ТЗостатков.Номенклатура = ТЗзаказПоставщику.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТЗостатковСкрышками.Комплектующая,
	               |	СУММА(ТЗостатковСкрышками.НеобходимоеКоличество) КАК НеобходимоеКоличество,
	               |	СУММА(ТЗостатковСкрышками.НеобходимоеКоличествоВзаказах) КАК НеобходимоеКоличествоВзаказах
	               |ПОМЕСТИТЬ ТЗкомплектующих
	               |ИЗ
	               |	ТЗостатковСкрышками КАК ТЗостатковСкрышками
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТЗостатковСкрышками.Комплектующая
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТЗкомплектующих.Комплектующая КАК Комплектующая,
	               |	ТЗкомплектующих.Комплектующая.Код КАК Код,
	               |	ТЗкомплектующих.НеобходимоеКоличество КАК НеобходимоеКоличество,
	               |	ТЗкомплектующих.НеобходимоеКоличество КАК НеобходимоеКоличествоВзаказах,
	               |	ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) КАК ОстатокКомплектующих,
	               |	ВЫБОР
	               |		КОГДА ТЗкомплектующих.НеобходимоеКоличество + ЕСТЬNULL(ЗначенияТочкиЗаказаСрезПоследних.ЗначениеТочкиЗаказа, 0) > ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) + ЕСТЬNULL(ЗаказыНаКрышкиОстатки.КоличествоОстаток, 0)
	               |			ТОГДА (ВЫРАЗИТЬ((ТЗкомплектующих.НеобходимоеКоличество + ЕСТЬNULL(ЗначенияТочкиЗаказаСрезПоследних.ЗначениеТочкиЗаказа, 0) - ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ЗаказыНаКрышкиОстатки.КоличествоОстаток, 0)) / 4 + 0.4 КАК ЧИСЛО(15, 0))) * 4
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК НеобходимоЗаказать,
	               |	ВЫБОР
	               |		КОГДА ТЗкомплектующих.НеобходимоеКоличествоВзаказах > 0
	               |				И ТЗкомплектующих.НеобходимоеКоличество + ЕСТЬNULL(ЗначенияТочкиЗаказаСрезПоследних.ЗначениеТочкиЗаказа, 0) > ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) + ЕСТЬNULL(ЗаказыНаКрышкиОстатки.КоличествоОстаток, 0)
	               |			ТОГДА ВЫБОР
	               |					КОГДА ТЗкомплектующих.НеобходимоеКоличество + ЕСТЬNULL(ЗначенияТочкиЗаказаСрезПоследних.ЗначениеТочкиЗаказа, 0) - ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ЗаказыНаКрышкиОстатки.КоличествоОстаток, 0) > ТЗкомплектующих.НеобходимоеКоличествоВзаказах
	               |						ТОГДА (ВЫРАЗИТЬ(ТЗкомплектующих.НеобходимоеКоличествоВзаказах / 4 + 0.4 КАК ЧИСЛО(15, 0))) * 4
	               |					ИНАЧЕ (ВЫРАЗИТЬ((ТЗкомплектующих.НеобходимоеКоличество + ЕСТЬNULL(ЗначенияТочкиЗаказаСрезПоследних.ЗначениеТочкиЗаказа, 0) - ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ЗаказыНаКрышкиОстатки.КоличествоОстаток, 0)) / 4 + 0.4 КАК ЧИСЛО(15, 0))) * 4
	               |				КОНЕЦ
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК НеобходимоЗаказатьПоЗаказам,
	               |	ЕСТЬNULL(ЗначенияТочкиЗаказаСрезПоследних.ЗначениеТочкиЗаказа, 0) КАК МинимальныйОстаток,
	               |	ЕСТЬNULL(ЗаказыНаКрышкиОстатки.КоличествоОстаток, 0) КАК КоличествоЗаказано
	               |ИЗ
	               |	ТЗкомплектующих КАК ТЗкомплектующих
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
	               |				,
	               |				НЕ Склад.ЗапретитьИспользование
	               |					И НЕ Склад.Транзитный
	               |					И Номенклатура В
	               |						(ВЫБРАТЬ
	               |							ТЗкомплектующих.Комплектующая
	               |						ИЗ
	               |							ТЗкомплектующих)) КАК ТоварыНаСкладахОстатки
	               |		ПО ТЗкомплектующих.Комплектующая = ТоварыНаСкладахОстатки.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияТочкиЗаказа.СрезПоследних(
	               |				,
	               |				Склад = &ПустойСклад
	               |					И ХарактеристикаНоменклатуры = &ПустаяХарактеристика
	               |					И Подразделение = &ПустоеПодразделение) КАК ЗначенияТочкиЗаказаСрезПоследних
	               |		ПО ТЗкомплектующих.Комплектующая = ЗначенияТочкиЗаказаСрезПоследних.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПоставщикамКрышки.Остатки КАК ЗаказыНаКрышкиОстатки
	               |		ПО ТЗкомплектующих.Комплектующая = ЗаказыНаКрышкиОстатки.Номенклатура
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Код";
	
		
	
	Если ЗначениеЗаполнено(Пост) тогда
		ТекстЗапроса=СтрЗаменить(ТекстЗапроса,"//УсловиеПоставщик","И Номенклатура.ОсновнойПоставщик = &Поставщик");
	КонецЕсли;	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ПустойСклад", Справочники.Склады.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяХарактеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
    Запрос.УстановитьПараметр("ПустоеПодразделение", Справочники.Подразделения.ПустаяСсылка());
	Запрос.УстановитьПараметр("СписокЗаказов", СписокЗаказовПоставщику);
	Запрос.УстановитьПараметр("Поставщик", Пост);
	
	Макет = ПолучитьМакет("Макет");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	Таб.Вывести(ОбластьШапка);
	Таб.ФиксацияСверху = Таб.ВысотаТаблицы;
	
	РезультатЗапроса = Запрос.Выполнить();
	ТЗдляЗаказа = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.Прямой);
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(Выборка);
		ТекРасшифровка = Новый Структура;
		ТекРасшифровка.Вставить("Комплектующая", Выборка.Комплектующая);
		ТекРасшифровка.Вставить("НеобходимоеКоличество");
		ОбластьСтрока.Параметры.РасшифровкаНеобходимоеКоличество = ТекРасшифровка;
		ТекРасшифровкаЗаказано = Новый Структура;
		ТекРасшифровкаЗаказано.Вставить("Комплектующая", Выборка.Комплектующая);
		ТекРасшифровкаЗаказано.Вставить("УжеЗаказано");
		ОбластьСтрока.Параметры.РасшифровкаУжеЗаказано = ТекРасшифровкаЗаказано;
		
		Таб.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	ЗапросСводный=Новый Запрос;
	ЗапросСводный.МенеджерВременныхТаблиц=Запрос.МенеджерВременныхТаблиц;
	ЗапросСводный.Текст="ВЫБРАТЬ * ИЗ ТЗостатковСкрышками";
	ТаблицаСводная=ЗапросСводный.Выполнить().Выгрузить();

	
КонецПроцедуры	