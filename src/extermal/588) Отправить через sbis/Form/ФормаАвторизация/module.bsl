&НаКлиенте
Перем Кэш Экспорт;
&НаКлиенте
Функция сбисПолучитьФорму(ИмяФормы, Владелец = Неопределено)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Попытка
			ЭтотОбъект="";
		Исключение
		КонецПопытки;
		Возврат ПолучитьФорму("ВнешняяОбработка.СБИС.Форма."+ИмяФормы,,Владелец);
	КонецЕсли;
	Возврат ЭтотОбъект.ПолучитьФорму(ИмяФормы,Владелец);
КонецФункции
&НаКлиенте
Функция сбисЭлементФормы(Форма,ИмяЭлемента)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Возврат Форма.Элементы[ИмяЭлемента];
	КонецЕсли;
	Возврат Форма.ЭлементыФормы[ИмяЭлемента];
КонецФункции
&НаКлиенте
Функция сбисПолучитьСтраницу(Элемент, ИмяСтраницы)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Возврат Элемент.ПодчиненныеЭлементы[ИмяСтраницы];
	КонецЕсли;
	Возврат Элемент.Страницы[ИмяСтраницы];
КонецФункции
&НаКлиенте
Процедура ЗагрузитьПараметрыАвторизации()
// Процедура заполняет реквизиты формы из кэша	
	ЗапомнитьПароль = Кэш.Парам.ЗапомнитьПароль;
	ЗапомнитьСертификат = Кэш.Парам.ЗапомнитьСертификат;
	Если Не ЗапомнитьПароль Тогда
		Пароль="";
	Иначе
		Пароль = Кэш.Парам.Пароль;
	КонецЕсли;
	Если Не ЗапомнитьСертификат Тогда
		Сертификат="";
	Иначе
		Сертификат = Кэш.Парам.Сертификат;
	КонецЕсли;
    Логин = Кэш.Парам.Логин;
	ВходПоСертификату = Кэш.Парам.ВходПоСертификату;
КонецПроцедуры
&НаКлиенте
Процедура СохранитьПараметрыАвторизации()
// Процедура сохраняет параметры авторизации в кэш	
	Кэш.Парам.ЗапомнитьПароль = ЗапомнитьПароль;
	Кэш.Парам.ЗапомнитьСертификат = ЗапомнитьСертификат;
	Кэш.Парам.Логин = Логин;
	Если Не ЗапомнитьПароль Тогда
		Кэш.Парам.Пароль="";
	Иначе
		Кэш.Парам.Пароль = Пароль;
	КонецЕсли;
	Если Не ЗапомнитьСертификат Тогда
		Кэш.Парам.Сертификат="";
	КонецЕсли;
	Кэш.Парам.ВходПоСертификату = ВходПоСертификату;
КонецПроцедуры
&НаКлиенте
Процедура ВходПоСертификату(Элемент)
// Функция переключает варианты авторизации По сертификату/По паролю	
	ПанельДанныеАвторизации = сбисЭлементФормы(ЭтаФорма,"ПанельДанныеАвторизации");
	Если ВходПоСертификату Тогда //переключаем на вход по паролю
		ПоказатьВходПоПаролю();
		ВходПоСертификату = Ложь;
		Кэш.Парам.ВходПоСертификату = Ложь;
		Возврат;		
	КонецЕсли;
	ПоказатьВходПоСертификату();
	КоличествоСертификатов = СписокСертификатов.Количество();
	Если КоличествоСертификатов = 1 Тогда
		Сертификат = СписокСертификатов[0].Значение.Отпечаток;
		Результат = Кэш.Интеграция.АвторизоватьсяПоСертификату(Кэш,Сертификат,"");
		Если Результат<>Ложь Тогда
			ВходПоСертификату = Истина;
			Кэш.Парам.ИдентификаторСессии = Результат;
			СохранитьПараметрыАвторизации();
			ЭтаФорма.Закрыть("Ok");
		КонецЕсли;		
	КонецЕсли;
	ВходПоСертификату = Истина;
	Кэш.Парам.ВходПоСертификату = Истина;
КонецПроцедуры
&НаКлиенте
Функция Авторизоваться(Кэш) Экспорт
// Функция выполняет авторизацию на онлайне по сертификату или по логину/паролю	
	ТекстОшибки = "";
	Если Кэш.Парам.ВходПоСертификату Тогда
		ВыбранныйСертификат = сбисЭлементФормы(ЭтаФорма,"СписокСертификатов").ТекущиеДанные;
		Если ВыбранныйСертификат=Неопределено и Кэш.Парам.Сертификат="" Тогда
			ОшибкаАвторизации = "Выберите сертификат для авторизации";
			Возврат Ложь;
		КонецЕсли;
		Если ВыбранныйСертификат<>Неопределено Тогда
			Кэш.Парам.Сертификат = ВыбранныйСертификат.Значение.Отпечаток;
		КонецЕсли;			
		Результат = Кэш.Интеграция.АвторизоватьсяПоСертификату(Кэш,Кэш.Парам.Сертификат,ТекстОшибки);		
	иначе
		Результат = Кэш.Интеграция.АвторизоватьсяПоЛогинуПаролю(Кэш,Кэш.Парам.Логин,Кэш.Парам.Пароль,ТекстОшибки);
		КонецЕсли;		
	Если Результат=Ложь Тогда
		ОшибкаАвторизации = ТекстОшибки;
		ЭтаФорма.ТекущийЭлемент=сбисЭлементФормы(ЭтаФорма,"ПанельДанныеАвторизации");
		Возврат Ложь;
	КонецЕсли;
	ОшибкаАвторизации="";
	Кэш.Парам.ИдентификаторСессии = Результат;
	Возврат Истина;
КонецФункции	
&НаКлиенте
Процедура КнопкаВойтиНажатие(Кнопка)
// Функция выполняет авторизацию на онлайне и в случае успеха закрывает окно авторизации
	Если Авторизоваться(Кэш) Тогда
		СохранитьПараметрыАвторизации();
		ЭтаФорма.Закрыть("Ok");
	КонецЕсли;
	
КонецПроцедуры
&НаКлиенте
Процедура СписокСертификатовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
// Функция выполняет авторизацию на онлайне по выбранному сертификату и в случае успеха закрывает окно авторизации	
	Если Авторизоваться(Кэш) Тогда
		СохранитьПараметрыАвторизации();
		ЭтаФорма.Закрыть("Ok");
	КонецЕсли;	
КонецПроцедуры
&НаКлиенте
Процедура НастройкаСоединенияНажатие(Элемент)
// Процедура открывает окно настройки соединения	
	фрм = сбисПолучитьФорму("ФормаНастройкаСоединения",ЭтаФорма);
	СпособОбменаДо = Кэш.Парам.СпособОбмена;
	СпособОбмена=фрм.ОткрытьМодально();
	Если СпособОбменаДо <> Кэш.Парам.СпособОбмена Тогда  // изменился способ обмена
		ЭтаФорма.ВладелецФормы.ОпределитьФормуИнтеграции(Кэш, Кэш.Парам.СпособОбмена);
		ОбменВключен = Кэш.Интеграция.Включить(Кэш);
		Если ОбменВключен<>Истина Тогда
			Кэш.Парам.СпособОбмена = 1;
			СпособОбмена = 1;
		КонецЕсли;
		Если СпособОбмена=1 Тогда  // Каталог
			ЭтаФорма.Закрыть("Ok");
		Иначе
			Если Кэш.Парам.ВходПоСертификату Тогда  // если изменили способ обмена, надо заново получить список сертификатов
				ПоказатьВходПоСертификату();	
			КонецЕсли;				
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Кэш = ЭтаФорма.ВладелецФормы.Кэш;
	Если Кэш.Парам.СпособОбмена = 3 тогда // АПИ - авторизуемся только по логину-паролю
		сбисЭлементФормы(ЭтаФорма,"РежимВхода").Видимость = Ложь;
		Кэш.Парам.ВходПоСертификату = Ложь;
	Иначе
		сбисЭлементФормы(ЭтаФорма,"РежимВхода").Видимость = Истина;
	КонецЕсли;
	ЗагрузитьПараметрыАвторизации();
	ОшибкаАвторизации="";
	
	Если ВходПоСертификату Тогда 
		ПоказатьВходПоСертификату();
	Иначе		
		ПоказатьВходПоПаролю();
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура ПоказатьВходПоСертификату()
// Процедура показывает в окне автоизации вкладку для входа по сертификату	
	ТекстОшибки = "";
	ПанельДанныеАвторизации = сбисЭлементФормы(ЭтаФорма,"ПанельДанныеАвторизации");
	сбисЭлементФормы(ЭтаФорма,"РежимВхода").Заголовок = "По паролю";
	ПанельДанныеАвторизации.ТекущаяСтраница = сбисПолучитьСтраницу(ПанельДанныеАвторизации,"ПоСертификату");
	СписокСертификатов.Очистить();
	СписокСертификатов = Кэш.Интеграция.ПолучитьСписокСертификатовДляАвторизации(Кэш,ТекстОшибки);
	КоличествоСертификатов = СписокСертификатов.Количество();
	Если КоличествоСертификатов = 0 Тогда
		ОшибкаАвторизации = ТекстОшибки;
		Если ОшибкаАвторизации = "" Тогда
			ОшибкаАвторизации = "Сертификаты не найдены";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура ПоказатьВходПоПаролю()
// Процедура показывает в окне автоизации вкладку для входа по логину/паролю	
	ПанельДанныеАвторизации = сбисЭлементФормы(ЭтаФорма,"ПанельДанныеАвторизации");
	сбисЭлементФормы(ЭтаФорма,"РежимВхода").Заголовок = "По сертификату";
	ПанельДанныеАвторизации.ТекущаяСтраница = сбисПолучитьСтраницу(ПанельДанныеАвторизации,"ПоПаролю");
	ЭтаФорма.ТекущийЭлемент=ПанельДанныеАвторизации;	
КонецПроцедуры
&НаКлиенте
Процедура ПараметрыПриИзменении(Элемент)
	// Процедура записывает в Кэш измененный параметр	
	Кэш.Парам[Элемент.Имя] = ЭтаФорма[Элемент.Имя];
КонецПроцедуры