&НаКлиенте
Функция сбисПолучитьФорму(ИмяФормы)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Попытка
			ЭтотОбъект="";
		Исключение
		КонецПопытки;
		Возврат ПолучитьФорму("ВнешняяОбработка.СБИС.Форма."+ИмяФормы);
	КонецЕсли;
	Возврат ЭтотОбъект.ПолучитьФорму(ИмяФормы);
КонецФункции

&НаКлиенте
Функция СоздатьДокумент(Кэш, Вложение, ИниДок, СоставПакета, МассивОснований, Документ1С = Неопределено) Экспорт
// Функция пытается найти документ-основание СФ (в случае, если СФ пришел один), загружает СФ
	Если МассивОснований.Количество()=0 тогда   // пришел один СФ
		Ини = Кэш.Ини[Вложение.СтруктураФайла.Файл.Формат+"_"+СтрЗаменить(СтрЗаменить(Вложение.СтруктураФайла.Файл.ВерсияФормата,".", "_"), " ", "")];	
		
		Если ИниДок.Свойство("мОснование") Тогда   // перебираем основания в инишке фактуры
			Для каждого Основание Из ИниДок.мОснование Цикл
				Если Основание.Значение.Свойство("Основание_Документ") и Основание.Значение.Основание_Документ.Свойство("Тип") Тогда
					Попытка
						ТипыОснований = Новый ОписаниеТипов(Основание.Значение.Основание_Документ.Тип);
						Для Каждого РазделДок Из Ини.мДокумент Цикл   // Ищем в инишке фактуры мДокумент с типом, который может быть основанием СФ
							Если ТипыОснований.СодержитТип(Тип("ДокументСсылка."+РазделДок.Ключ)) Тогда	
								ИниПост = РазделДок.Значение;
	                            фрм = сбисПолучитьФорму("ФормаГлавноеОкно").сбисНайтиФормуФункции("СоздатьДокумент","Документ_"+РазделДок.Ключ,"Документ_Шаблон",Кэш);
								ТекущийДокументПоступления = Кэш.ОбщиеФункции.сбисВыбратьПодходящийДокумент(Вложение.Документы1С,РазделДок.Ключ);  // уже есть связанный документ поступления
								СписокДокументов = Новый СписокЗначений;
								Если Не ЗначениеЗаполнено(ТекущийДокументПоступления) Тогда  // если нет связанного документа поступления, пытаемся найти его по реквизитам основания из СФ
									РеквизитыДляПоиска = Новый Структура;
									РеквизитыДляПоиска.Вставить("Контрагент", СоставПакета.Контрагент);
									Если ИниПост.Свойство("Документ_ДатаВх") и ИниПост.Документ_ДатаВх.Свойство("Данные") Тогда
										ДатаПост = Кэш.ОбщиеФункции.РассчитатьЗначениеИзСтруктуры(ИниПост.Документ_ДатаВх.Данные, Вложение.СтруктураФайла);
										Попытка
											ДатаПост = Дата(Сред(ДатаПост,7,4),Сред(ДатаПост,4,2),Лев(ДатаПост,2));
											РеквизитыДляПоиска.Вставить("Дата", ДатаПост);
										Исключение
										КонецПопытки;
									КонецЕсли;
									Если ИниПост.Свойство("Документ_НомерВх") и ИниПост.Документ_НомерВх.Свойство("Данные") Тогда
										НомерПост = Кэш.ОбщиеФункции.РассчитатьЗначениеИзСтруктуры(ИниПост.Документ_НомерВх.Данные, Вложение.СтруктураФайла);
										Если ЗначениеЗаполнено(НомерПост) Тогда
											РеквизитыДляПоиска.Вставить("Номер", НомерПост);
										КонецЕсли;
									КонецЕсли;
									Если РеквизитыДляПоиска.Свойство("Дата") и РеквизитыДляПоиска.Свойство("Номер") Тогда
										Кэш.ОбщиеФункции.НайтиПодходящиеДокументыОпределенногоТипа(СписокДокументов, ИниПост, Кэш.Ини, Кэш.Парам, РеквизитыДляПоиска);
									КонецЕсли;										
								КонецЕсли;
								Если СписокДокументов.Количество()>0 Тогда  // если нашли подходящий документ, берем его, иначе создаем новый
									ДокументПоступления = СписокДокументов[0].Значение;
									Сообщить("Для документа "+ Вложение.Название+" найден подходящий документ-основание "+строка(ДокументПоступления));
								Иначе
									//Ответ = Вопрос("Для документа "+Вложение.Название+" не найден подходящий документ-основание. Создать новый документ-основание?",РежимДиалогаВопрос.ДаНет);
									//Если Ответ = КодВозвратаДиалога.Да Тогда
										ДокументПоступления = фрм.СоздатьДокумент(Кэш,Вложение,ИниПост,СоставПакета,МассивОснований, ТекущийДокументПоступления);
									//КонецЕсли;		
								КонецЕсли;
								Если ЗначениеЗаполнено(ДокументПоступления) Тогда
									МассивОснований.Добавить(ДокументПоступления);
								КонецЕсли;
								Прервать;
							КонецЕсли;
						КонецЦикла;
					Исключение
					КонецПопытки;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат сбисПолучитьФорму("Документ_Шаблон").СоздатьДокумент(Кэш, Вложение, ИниДок, СоставПакета, МассивОснований, Документ1С);
КонецФункции
