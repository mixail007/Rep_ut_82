Перем СтандартнаяВысотаЭлементов, ЗаполнитьРаботникаОрганизации;

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ПрочитатьНастройкиПоУмолчанию();
	ЗаполнитьРаботникаОрганизации();
	
	Если НеПоказыватьФормуТитула Тогда 
		
		СформироватьТитулПокупателя();
		
		Отказ= Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	КартинкаЗаголовка= ЭДО_БиблиотекаКартинок().КартинкаЗаголовка;
	
	ЗаполнитьПодписи();
	УправлениеФормой();
	
КонецПроцедуры


Процедура ДатаПолученияПоДатеДокументаПриИзменении(Элемент)
	
	УправлениеФормой_ДатаПолучения_УстановитьВидимость();
	
КонецПроцедуры


Процедура КнопкаЗаполнитьРаботникОрганизацииНажатие(Элемент)
	
	ЗаполнитьРаботникаОрганизации= НЕ ЗаполнитьРаботникаОрганизации;
	
	ЗаполнитьРаботникаОрганизации();
	
	УправлениеФормой();
	
КонецПроцедуры

Процедура КнопкаНастроитьПараметрыПодписанияНажатие(Кнопка)
	
	НоваяФорма = ПолучитьФорму("ФормаНастройкиПодписиУПД");
	НоваяФорма.Организация		= Организация;
	НоваяФорма.ДляКорректировки = ДляКорректировки;
	Результат = НоваяФорма.ОткрытьМодально();
	
	Если Результат = Истина Тогда
		ЗаполнитьРаботникаОрганизации();
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ЗаписатьНастройкиПоУмолчанию();
	
	СформироватьТитулПокупателя();
	
	Закрыть();
		
КонецПроцедуры


Процедура ФлажокРаботникСовпадаетСПодписантомПриИзменении(Элемент)
	
	ЗаполнитьРаботникаОрганизации();
	
	УправлениеФормой_РаботникОрганизации_УстановитьДоступность();
	УправлениеФормой_КнопкаВыполнить_УстановитьДоступность();
	
КонецПроцедуры

Процедура СодержаниеОперацииПриИзменении(Элемент)
	
	УправлениеФормой_КнопкаВыполнить_УстановитьДоступность();
	
КонецПроцедуры

Процедура РаботникОрганизацииДолжностьПриИзменении(Элемент)
	
	УправлениеФормой_КнопкаВыполнить_УстановитьДоступность();
	
КонецПроцедуры

Процедура РаботникОрганизацииФамилияПриИзменении(Элемент)
	
	УправлениеФормой_КнопкаВыполнить_УстановитьДоступность();
	
КонецПроцедуры

Процедура РаботникОрганизацииИмяПриИзменении(Элемент)
	
	УправлениеФормой_КнопкаВыполнить_УстановитьДоступность();
	
КонецПроцедуры

Процедура РаботникОрганизацииОснованиеПолномочийПриИзменении(Элемент)
	
	УправлениеФормой_КнопкаВыполнить_УстановитьДоступность();
	
КонецПроцедуры


Процедура УправлениеФормой()
	
	Если ДляКорректировки Тогда
		УправлениеФормой_НастроитьЭлементыУКД();
	Иначе
		УправлениеФормой_РаботникОрганизации_УстановитьДоступность();
	КонецЕсли;
	
	УправлениеФормой_СвернутьРазвернутьПанели();
	УправлениеФормой_ДатаПолучения_УстановитьВидимость();
	УправлениеФормой_КнопкаВыполнить_УстановитьДоступность();
	
КонецПроцедуры

Процедура УправлениеФормой_НастроитьЭлементыУКД()
	
	Заголовок = "Подписание УКД";
	ЭлементыФормы.НадписьВидОперации1.Заголовок = "Дата согласования";
	
КонецПроцедуры

Процедура УправлениеФормой_ДатаПолучения_УстановитьВидимость()
	
	ЭлементыФормы.ДатаПолучения.Видимость= НЕ ДатаПолученияПоДатеДокумента;
	
КонецПроцедуры

Процедура УправлениеФормой_СвернутьРазвернутьПанели()
	
	НоваяВысотаФормы= НоваяВысотаФормы();
	
	Если НоваяВысотаФормы > ЭтаФорма.Высота Тогда
		ЭтаФорма.Высота= НоваяВысотаФормы;
	КонецЕсли;
	
	Если КоличествоДокументовНаПодпись = 1 Тогда
		УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельДополнительныеДокументыНаПодпись, РежимСверткиЭлементаУправления.Верх);
	ИначеЕсли КоличествоДокументовНаПодпись > 1 Тогда
		Если ЭлементыФормы.НадписьДополнительныеДокументыНаПодпись.Высота <> 28 Тогда
			ЭлементыФормы.НадписьДополнительныеДокументыНаПодпись.Высота= 28;
		КонецЕсли;
		Если ЭлементыФормы.ПанельДополнительныеДокументыНаПодпись.Высота <> 33 Тогда
			ЭлементыФормы.ПанельДополнительныеДокументыНаПодпись.Высота= 33;
		КонецЕсли;
	КонецЕсли;
	
	Если ДляКорректировки Тогда 
		
		УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельВидОперации, РежимСверткиЭлементаУправления.Верх);
		УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельЗаполнитьРаботникиОрганизации, РежимСверткиЭлементаУправления.Верх);
		УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельРаботникОрганизации, РежимСверткиЭлементаУправления.Верх);
		
	Иначе
		
		Если ЗаполнитьРаботникаОрганизации Тогда
			УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельРаботникОрганизации, РежимСверткиЭлементаУправления.Нет);
			ЭлементыФормы.КнопкаЗаполнитьРаботникОрганизации.Заголовок= "Отменить заполнение";
		Иначе
			УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельРаботникОрганизации, РежимСверткиЭлементаУправления.Верх);
			ЭлементыФормы.КнопкаЗаполнитьРаботникОрганизации.Заголовок= "Заполнить";
		КонецЕсли;
		
	КонецЕсли;
	
	Если НоваяВысотаФормы < ЭтаФорма.Высота Тогда
		ЭтаФорма.Высота= НоваяВысотаФормы;
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеФормой_РаботникОрганизации_УстановитьДоступность()
	
	ЭлементыФормы.РаботникОрганизацииДолжность.Доступность= 		  НЕ РаботникСовпадаетСПодписантом;
	ЭлементыФормы.РаботникОрганизацииФамилия.Доступность= 			  НЕ РаботникСовпадаетСПодписантом;
	ЭлементыФормы.РаботникОрганизацииИмя.Доступность= 				  НЕ РаботникСовпадаетСПодписантом;
	ЭлементыФормы.РаботникОрганизацииОтчество.Доступность= 			  НЕ РаботникСовпадаетСПодписантом;
	ЭлементыФормы.РаботникОрганизацииОснованиеПолномочий.Доступность= НЕ РаботникСовпадаетСПодписантом;
	
КонецПроцедуры

Процедура УправлениеФормой_КнопкаВыполнить_УстановитьДоступность()
	
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.КнопкаВыполнить.Доступность=
	
	ЗначениеЗаполнено(ДатаПолучения) И ЗначениеЗаполнено(СодержаниеОперации)
	И (ДляКорректировки ИЛИ НЕ ЗаполнитьРаботникаОрганизации
		ИЛИ  (ЗначениеЗаполнено(РаботникОрганизацииДолжность) 
			И ЗначениеЗаполнено(РаботникОрганизацииФамилия)
			И ЗначениеЗаполнено(РаботникОрганизацииИмя)  
			И ЗначениеЗаполнено(РаботникОрганизацииОснованиеПолномочий)))
	
КонецПроцедуры


Процедура ЗаполнитьПодписи()
	
	ЗаполнитьПодписи_НадписьДополнительныеДокументыНаПодпись();
	ЗаполнитьПодписи_КнопкаВыполнить();
	
КонецПроцедуры

Процедура ЗаполнитьПодписи_НадписьДополнительныеДокументыНаПодпись()
	
	Если КоличествоДокументовНаПодпись = 0 Тогда
		ЭлементыФормы.НадписьДополнительныеДокументыНаПодпись.Заголовок= 
		"Среди выбранных могут быть документы из пакетов, по которым отправитель запретил обработку по отдельности. Такие пакеты будут подписаны целиком.";
	ИначеЕсли КоличествоДокументовНаПодпись > 1 Тогда
		
		ЭлементыФормы.НадписьДополнительныеДокументыНаПодпись.Заголовок= 
		"В месте с выбранным документом будет " + РезультатСклоненияСловаПодписано(КоличествоДокументовНаПодпись-1) + " еще " + СтрокаКоличествоДокументов(КоличествоДокументовНаПодпись-1) + " из пакета, по которому отправитель запретил обработку по отдельности.";
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПодписи_КнопкаВыполнить()
	
	Если КоличествоДокументовНаПодпись > 1 Тогда
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.КнопкаВыполнить.Текст= "Подписать " + СтрокаКоличествоДокументов(КоличествоДокументовНаПодпись);
	КонецЕсли;
	
КонецПроцедуры


Процедура ПрочитатьНастройкиПоУмолчанию()
	
	ДатаПолученияПоДатеДокумента= ОдинСАдаптер_НастройкиТекущегоПользователя_ПолучитьНастройку("ДиадокПереключательДатаПодписи") = 1;
	
	ДатаПолучения= ТекущаяДата();
	
	ВидОперации= ВосстановитьЗначение("Диадок_ВидОперацииУПД_ПоУмолчанию");
	
	СодержаниеОперации= ВосстановитьЗначение("Диадок_СодержаниеОперацииУПД_ПоУмолчанию");
	Если НЕ ЗначениеЗаполнено(СодержаниеОперации) Тогда
		Если ДляКорректировки Тогда
			СодержаниеОперации = "С изменением стоимости согласен";
		Иначе
			СодержаниеОперации = "Товары и услуги получены, работы приняты";
		КонецЕсли;	
	КонецЕсли;
	
	РаботникОрганизацииОснованиеПолномочий= ВосстановитьЗначение("Диадок_РаботникОрганизацииОснованиеПолномочийУПД_ПоУмолчанию");
	Если НЕ ЗначениеЗаполнено(РаботникОрганизацииОснованиеПолномочий) Тогда
		РаботникОрганизацииОснованиеПолномочий= "Должностные обязанности";
	КонецЕсли;
	
	ЗаполнитьРаботникаОрганизации= ВосстановитьЗначение("Диадок_ЗаполнитьРаботникаОрганизацииУПД_ПоУмолчанию") = Истина;
	
	РаботникСовпадаетСПодписантом= ОдинСАдаптер_НастройкиТекущегоПользователя_ПолучитьНастройку("ДиадокСовпадаетСПолучателем");
	
КонецПроцедуры

Процедура ЗаписатьНастройкиПоУмолчанию()
	
	ОдинСАдаптер_НастройкиТекущегоПользователя_УстановитьНастройку("ДиадокПереключательДатаПодписи", ?(ДатаПолученияПоДатеДокумента, 1, 0));
	
	СохранитьЗначение("Диадок_ВидОперацииУПД_ПоУмолчанию"		, ВидОперации);
	СохранитьЗначение("Диадок_СодержаниеОперацииУПД_ПоУмолчанию", СодержаниеОперации);
	
	СохранитьЗначение("Диадок_РаботникОрганизацииОснованиеПолномочийУПД_ПоУмолчанию", РаботникОрганизацииОснованиеПолномочий);
	СохранитьЗначение("Диадок_ЗаполнитьРаботникаОрганизацииУПД_ПоУмолчанию"			, ЗаполнитьРаботникаОрганизации);
	
КонецПроцедуры


Процедура СформироватьТитулПокупателя()
	
	ТитулПокупателя= ПолучитьМодульПрог("Модуль_Диадок_РаботаСКонтентом").Новый_UTDBuyerContent();
	
	ТитулПокупателя.Удалить("OtherIssuer");
	
	ДанныеСертификатаАвторизации = ПолучитьМодульПрог("Модуль_РаботаССерверомДиадок").ДанныеСертифкатаАвторизации(BoxID);
	Creator = Неопределено;
	Если СтрДлина(УдалитьЛишниеНулиИНН(ДанныеСертификатаАвторизации.Inn)) = 12 Тогда
		// это ФЛ или ИП
		ДанныеПодписанта = ПолучитьМодульПрог("Модуль_РаботаССерверомДиадок").ПрочитатьДанныеПодписанта(Организация, Ложь, ДляКорректировки);
		Creator = "ИП " + ДанныеПодписанта.Surname + " " + ДанныеПодписанта.FirstName + " " + ДанныеПодписанта.Patronymic;
	Иначе
		Creator = ДанныеСертификатаАвторизации.OrganizationName;
	КонецЕсли;
	ТитулПокупателя.Creator= Creator;
	
	Если ДляКорректировки Тогда
		ExtendedSignerDetails = ПолучитьМодульПрог("Модуль_РаботаССерверомДиадок").ПрочитатьДанныеПодписанта(Организация, Ложь, Истина);
		Если ЗначениеЗаполнено(ExtendedSignerDetails.OrganizationPowersBase) Тогда
			ТитулПокупателя.CreatorBase = Лев(ExtendedSignerDetails.OrganizationPowersBase, 120);
		КонецЕсли;
	КонецЕсли;
	
	ТитулПокупателя.AcceptanceDate=	  ?(ДатаПолученияПоДатеДокумента, DocumentDate, ДатаПолучения);
	ТитулПокупателя.OperationCode= 	  ВидОперации;
	ТитулПокупателя.OperationContent= СодержаниеОперации;
	
	Если ЗаполнитьРаботникаОрганизации И НЕ ДляКорректировки Тогда
		
		ТитулПокупателя.Employee.FirstName=  РаботникОрганизацииИмя;
		ТитулПокупателя.Employee.Surname= 	 РаботникОрганизацииФамилия;
		ТитулПокупателя.Employee.Patronymic= РаботникОрганизацииОтчество;
		
		ТитулПокупателя.Employee.EmployeePosition= РаботникОрганизацииДолжность;
		ТитулПокупателя.Employee.EmployeeBase= 	   РаботникОрганизацииОснованиеПолномочий;
		
	Иначе
		ТитулПокупателя.Удалить("Employee");
	КонецЕсли;
	
КонецПроцедуры

Функция РезультатСклоненияСловаПодписано(Знач Числительное)
	
	Числительное= Число(Прав(Формат(Числительное, "ЧГ="), 2));
	
	Если Числительное > 19 Тогда
		Числительное= Число(Прав(Формат(Числительное, "ЧГ="), 1));
	КонецЕсли;
	
	Если Числительное = 1 Тогда
		Возврат "подписан";
	Иначе
		Возврат "подписано";
	КонецЕсли;	
	
КонецФункции


Функция НоваяВысотаФормы()
	
	НоваяВысотаФормы= СтандартнаяВысотаЭлементов.ЭтаФорма;
	
	Если КоличествоДокументовНаПодпись = 1 Тогда
		НоваяВысотаФормы= НоваяВысотаФормы - СтандартнаяВысотаЭлементов.ПанельДополнительныеДокументыНаПодпись;
	ИначеЕсли КоличествоДокументовНаПодпись > 1 Тогда
		НоваяВысотаФормы= НоваяВысотаФормы - (СтандартнаяВысотаЭлементов.ПанельДополнительныеДокументыНаПодпись-33);
	КонецЕсли;
	
	Если НЕ ЗаполнитьРаботникаОрганизации Или ДляКорректировки Тогда
		НоваяВысотаФормы= НоваяВысотаФормы - СтандартнаяВысотаЭлементов.ПанельРаботникОрганизации;
	КонецЕсли;
	
	Если ДляКорректировки Тогда 
		НоваяВысотаФормы = НоваяВысотаФормы - СтандартнаяВысотаЭлементов.ПанельВидОперации;
		НоваяВысотаФормы = НоваяВысотаФормы - СтандартнаяВысотаЭлементов.ПанельЗаполнитьРаботникиОрганизации;
	КонецЕсли;	
	
	Возврат НоваяВысотаФормы;
	
КонецФункции

Процедура ЗаполнитьРаботникаОрганизации()
	
	Если ЗаполнитьРаботникаОрганизации И РаботникСовпадаетСПодписантом Тогда
		
		ДанныеПодписанта= ПолучитьМодульПрог("Модуль_РаботаССерверомДиадок").ПрочитатьДанныеПодписанта(Организация, Ложь, ДляКорректировки);
		
		РаботникОрганизацииИмя=  	  			ДанныеПодписанта.FirstName;
		РаботникОрганизацииФамилия=	  			ДанныеПодписанта.Surname;
		РаботникОрганизацииОтчество=  			ДанныеПодписанта.Patronymic;
		РаботникОрганизацииДолжность= 			ДанныеПодписанта.JobTitle;
		РаботникОрганизацииОснованиеПолномочий= ДанныеПодписанта.PowersBase;
		
	КонецЕсли;
	
КонецПроцедуры



ЗаполнитьРаботникаОрганизации= Ложь;

СтандартнаяВысотаЭлементов=
Новый Структура("ЭтаФорма, ПанельДополнительныеДокументыНаПодпись, ПанельРаботникОрганизации, ПанельВидОперации, ПанельЗаполнитьРаботникиОрганизации",
ЭтаФорма.Высота, ЭлементыФормы.ПанельДополнительныеДокументыНаПодпись.Высота, ЭлементыФормы.ПанельРаботникОрганизации.Высота,
ЭлементыФормы.ПанельВидОперации.Высота, ЭлементыФормы.ПанельЗаполнитьРаботникиОрганизации.Высота);