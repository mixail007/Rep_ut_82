Процедура ЗаполнитьДанные(Отказ)

	ТаблицаСертификатов.Очистить();
	Попытка
		
		Для ц = 0 по  CollectionPersonalCertificates.Count-1 цикл 
			
			PersonalCertificate =  CollectionPersonalCertificates.GetItem(ц);
			
			стр = ТаблицаСертификатов.Добавить();
			Стр.Наименование = PersonalCertificate.Name;
			Стр.ДатаВыдачи = PersonalCertificate.BeginDate;
			Стр.СрокДействия = PersonalCertificate.EndDate;
			Стр.ОтпечатокСертификата = PersonalCertificate.Thumbprint;
			Стр.Организация = PersonalCertificate.OrganizationName;
			Стр.Издатель = PersonalCertificate.IssuerName;
			
			Попытка
				Стр.ИНН=	PersonalCertificate.INN;
				Стр.КПП=	PersonalCertificate.Kpp;
			Исключение
			КонецПопытки;
			
			Если Стр.СрокДействия < НачалоДня(ТекущаяДата()) Тогда
				Стр.Ошибка = "Срок действия сертификата истек";
				Стр.ВДиадоке = Ложь;
			Иначе
				Стр.ВДиадоке = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
	Исключение
		СообщенияПользователям_ПоказатьСообщениеОбОшибке("ОшибкаРаботыВнешнейКомпоненты", ОписаниеОшибки());
	КонецПопытки;
	
	Попытка
		
		Если НЕ Модуль_РаботаССерверомДиадок.ПроверитьПодключение() Тогда
			ВызватьИсключение("##200 Текущие настройки не позволяют подключиться к серверу " + НаименованиеСистемы);
		КонецЕсли;
		
		Для каждого СтрокаТаблицы Из ТаблицаСертификатов Цикл
			Если СтрокаТаблицы.ВДиадоке Тогда
				Если НЕ Модуль_РаботаССерверомДиадок.ПроверитьДоступПоСертификату(СтрокаТаблицы.ОтпечатокСертификата) Тогда
					СтрокаТаблицы.Ошибка = "По сертификату нет доступа в " + НаименованиеСистемы;
					СтрокаТаблицы.ВДиадоке = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Исключение
		ПоказатьОшибкуПоСпецификатору(ОписаниеОшибки(), Отказ);
	КонецПопытки;
		
	ТаблицаСертификатов.Сортировать("ВДиадоке, Наименование, ДатаВыдачи убыв");
	
КонецПроцедуры

Процедура ПоказатьОшибкуПоСпецификатору(текстОшибки, Отказ)
	Спецификатор = РаботаССерверомДиадок_ПолучитьСтруктуруОшибкиВнешнейКомпоненты(текстОшибки).Спецификатор;
	
	Если Спецификатор = "InternetError" Тогда
		Если СообщенияПользователям_ПоказатьСообщениеОбОшибке("ОшибкаИДиагностикаИнтернета", текстОшибки) Тогда
			ЗаполнитьДанные(Отказ);
		Иначе 	
			Отказ= Истина;
		КонецЕсли;	
	Иначе
		СообщенияПользователям_ПоказатьСообщениеОбОшибке("ОшибкаРаботыВнешнейКомпоненты", текстОшибки);
	КонецЕсли;
КонецПроцедуры

Процедура ОбновитьПредставлениеПоРежиму()
	
	Если Режим = "АвторизацияПоСертификату" Тогда
		Заголовок = "Вход в "+НаименованиеСистемы+" по сертификату";
		ЭлементыФормы.РежимыАвторизации.ТекущаяСтраница = ЭлементыФормы.РежимыАвторизации.Страницы.ПоСертификату;
		ПереключениеРежимаВхода = "Войти по логину и паролю";
	ИначеЕсли Режим = "ВыборСертификата" Тогда
		Заголовок = "Выбор сертификата";
		ЭлементыФормы.РежимыАвторизации.ТекущаяСтраница = ЭлементыФормы.РежимыАвторизации.Страницы.ПоСертификату;
		ЭлементыФормы.ПереключениеРежимаВхода.Видимость=	Ложь;
		ЭлементыФормы.ПереключениеРежимаВхода.Доступность=	Ложь;
		ЭлементыФормы.КоманднаяПанель1.Кнопки.ВойтиВСистему.Текст=	"Выбрать";
		ЭлементыФормы.НадписьПояснение.Заголовок=	"Выбор сертификата для организации " + ПредставлениеОрганизации;
	Иначе
		Заголовок = "Вход в "+НаименованиеСистемы+" по логину";
		ЭлементыФормы.РежимыАвторизации.ТекущаяСтраница = ЭлементыФормы.РежимыАвторизации.Страницы.ПоЛогину;
		ПереключениеРежимаВхода = "Войти по сертификату";
	КонецЕсли;
	
	Если ТаблицаСертификатов.Количество() = 0 Тогда
		ЭлементыФормы.ПанельИнфНетСертификатов.Видимость = Истина;
	Иначе
		ЭлементыФормы.ПанельИнфНетСертификатов.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

Процедура ОтменаНажатие(Элемент)
	Закрыть();
КонецПроцедуры

Процедура ТаблицаСертификатовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	ВыбратьНажатие("");
КонецПроцедуры

Процедура ТаблицаСертификатовПриАктивизацииСтроки(Элемент)
	Если НЕ ЭлементыФормы.ТаблицаСертификатов.ТекущиеДанные.ВДиадоке Тогда
		Комментарий = ЭлементыФормы.ТаблицаСертификатов.ТекущиеДанные.Ошибка;
	Иначе
		Комментарий = "";
	КонецЕсли;
КонецПроцедуры

Процедура ТаблицаСертификатовПриПолученииДанных(Элемент, ОформленияСтрок)
	Для каждого ОформленияСтроки Из ОформленияСтрок Цикл
		Если НЕ ОформленияСтроки.ДанныеСтроки.ВДиадоке Тогда
			ОформленияСтроки.ЦветТекста = WEBЦвета.Серый;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПереключениеРежимаВходаНажатие(Элемент)
	Если Режим = "АвторизацияПоСертификату" Тогда
		Режим = "АвторизацияПоЛогину";
	Иначе
		 Режим = "АвторизацияПоСертификату";
	КонецЕсли;
	ОбновитьПредставлениеПоРежиму()
КонецПроцедуры

Процедура ВыбратьНажатие(Элемент)
	
	Отказ= Ложь;
	
	текстОшибки=	"";
	
	ПредставлениеПользователя = "Неизвестный пользователь";
	Если Режим = "АвторизацияПоСертификату" ИЛИ Режим = "ВыборСертификата" Тогда
		
		ТекДанные = ЭлементыФормы.ТаблицаСертификатов.ТекущиеДанные;
		
		Если ТекДанные = Неопределено ИЛИ НЕ ТекДанные.ВДиадоке  Тогда
			Предупреждение("Выберите действующий сертификат.",, НаименованиеСистемы);
			Возврат;
		КонецЕсли;
		ОтпечатокСертификата = ТекДанные.ОтпечатокСертификата;
		
		Попытка
			DiadocConnection=	Модуль_РаботаССерверомДиадок.CreateDiadocConnectionCertificate(ОтпечатокСертификата);
			Если ЗначениеЗаполнено(OrganizationId) Тогда
				Box=	DiadocConnection.GetOrganizationById(OrganizationId);
			КонецЕсли;
			ЗафиксироватьСобытиеGA("Форма_ОкноАвторизации","Auth-by-cert");
		Исключение
			ТекДанные.ВДиадоке=	Ложь;
			ТекДанные.Ошибка=	"По сертификату отсутствует доступ к организации";
			текстОшибки=	ОписаниеОшибки();
			ПоказатьОшибкуПоСпецификатору(текстОшибки, Отказ);
			ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.ТаблицаСертификатов;
		КонецПопытки;
		

	Иначе
		
		Если ПустаяСтрока(Логин) Тогда
			Предупреждение("Заполните логин.");
			ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.Логин;
			Возврат;
		КонецЕсли;
		
		Если ПустаяСтрока(Пароль) Тогда
			Предупреждение("Заполните пароль.");
			ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.Пароль;
			Возврат;
		КонецЕсли;
		
		ОтпечатокСертификата = "login:" + Логин;
		Попытка
			DiadocConnection=	Модуль_РаботаССерверомДиадок.CreateDiadocConnectionLogin(Логин, Пароль);
			ЗафиксироватьСобытиеGA("Форма_ОкноАвторизации","Auth-by-login");

		Исключение
			
			текстОшибки = ОписаниеОшибки();
			Спецификатор = РаботаССерверомДиадок_ПолучитьСтруктуруОшибкиВнешнейКомпоненты(текстОшибки).Спецификатор;
			ПоказатьОшибкуПоСпецификатору(текстОшибки, Отказ);
			Если Спецификатор = "AuthorizationBadLogin" Тогда
				ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.Логин;
			ИначеЕсли Спецификатор = "AuthorizationBadPassword" Тогда
				ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.Пароль;
			КонецЕсли;
					
		КонецПопытки;
		
	КонецЕсли;
	
	Если ТекстОшибки = "" Тогда
		ПараметрыDiadocConnection=	Новый Структура();
		ПараметрыDiadocConnection.Вставить("DiadocConnection", 		DiadocConnection);
		ПараметрыDiadocConnection.Вставить("ОтпечатокСертификата", 	ОтпечатокСертификата);
		Закрыть(ПараметрыDiadocConnection);
	КонецЕсли;
	
КонецПроцедуры


Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ЗафиксироватьСобытиеGA("Форма_ОкноАвторизации");

	ЗаполнитьДанные(Отказ);
	
	КартинкаЗаголовка= ЭДО_БиблиотекаКартинок().КартинкаЗаголовка;
	ЭтаФорма.Заголовок = "Вход в "+НаименованиеСистемы;
	
	ОбновитьПредставлениеПоРежиму();
	
КонецПроцедуры



