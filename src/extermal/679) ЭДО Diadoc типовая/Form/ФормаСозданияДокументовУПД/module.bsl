Перем ДокументОснование, СчетФактура, СчетФактураДляСопоставления;

Процедура ПриОткрытии()
	
	ЗаполнитьНачальноеПредставлениеФормы();
	
	ОбновитьСсылки();
	
	УправлениеФормой();

КонецПроцедуры

Процедура ПриЗакрытии()
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		ПолучитьМодульПрог("Модуль_Данные1С_СвязиОбъектов").Установить_DocumentID_Для_Документ(ДокументОснование, Document.DocumentID, Document.OrganizationId);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетФактура) Тогда
		ПолучитьМодульПрог("Модуль_Данные1С_СвязиОбъектов").Установить_DocumentID_Для_Документ(СчетФактура, Document.DocumentID, Document.OrganizationId);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНачальноеПредставлениеФормы()
	
	КартинкаЗаголовка= ЭДО_БиблиотекаКартинок().КартинкаЗаголовка;
	
КонецПроцедуры

Процедура УправлениеФормой()
	
	Если ВосстановитьЗначение("Диадок_РежимВводаВходящейНакладной") = "Сопоставление" Тогда
		РежимВводаДокументаОснование= "Сопоставить...";
	Иначе
		РежимВводаДокументаОснование= "Создать...";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		
		ЭлементыФормы.ДокументОснование.Заголовок= РежимВводаДокументаОснование;
		ЭлементыФормы.СчетФактура.Заголовок=  	   "Создать...";
		
		ЭлементыФормы.ДокументОснование.Доступность= Истина;
		ЭлементыФормы.СчетФактура.Доступность=  	 Ложь;
		
		ЭлементыФормы.СчетФактура.ЦветТекста=  WEBЦвета.Серый;
		ЭлементыФормы.СчетФактура.Гиперссылка= Ложь;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(СчетФактура) Тогда
		
		ЭлементыФормы.ДокументОснование.Заголовок= ДокументОснование;
		
		Если ЗначениеЗаполнено(СчетФактураДляСопоставления) Тогда
			ЭлементыФормы.СчетФактура.Заголовок=  "Сопоставить с " + СчетФактураДляСопоставления;
		Иначе
			ЭлементыФормы.СчетФактура.Заголовок=  "Создать...";
		КонецЕсли;
		
		ЭлементыФормы.ДокументОснование.Доступность= Истина;
		ЭлементыФормы.СчетФактура.Доступность=  	 Истина;
		
		ЭлементыФормы.СчетФактура.ЦветТекста=  WEBЦвета.Синий;
		ЭлементыФормы.СчетФактура.Гиперссылка= Истина;
		
	Иначе
		
		ЭлементыФормы.ДокументОснование.Заголовок= ДокументОснование;
		ЭлементыФормы.СчетФактура.Заголовок=  	  СчетФактура;
		
		ЭлементыФормы.ДокументОснование.Доступность= Истина;
		ЭлементыФормы.СчетФактура.Доступность=		 Истина;
		
		ЭлементыФормы.СчетФактура.ЦветТекста=  WEBЦвета.Синий;
		ЭлементыФормы.СчетФактура.Гиперссылка= Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьСсылки()
	
	МассивДокументов= ПолучитьМодульПрог("Модуль_Данные1С_СвязиОбъектов").DocumentID_2_МассивДокументов(Document.DocumentId, Document.OrganizationId);
	
	Для Каждого Документ ИЗ МассивДокументов Цикл
		
		Если ТипЗнч(Документ) = ОдинСАдаптер_Документы_ПолучитьТипДокументаСчетФактураПолученный() Тогда
			СчетФактура= Документ;
		Иначе
			ДокументОснование= Документ;
		КонецЕсли;
		
	КонецЦикла;
		
	ПолучитьСчетФактуруДляСопоставления();
	
КонецПроцедуры

Процедура ПолучитьСчетФактуруДляСопоставления()
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		СчетФактураДляСопоставления= ПолучитьМодульПрог("Модуль_ИнтеграцияОбщий").СчетФактураПолученныйДокументаОснования(ДокументОснование);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗакрытьНажатие(Элемент)
	
	Закрыть();
	
КонецПроцедуры

Процедура ДокументОснованиеНажатие(Элемент)
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		
		ДокументОснование.ПолучитьОбъект().ПолучитьФорму().Открыть();
		
	Иначе
		
		ДокументОснование= СоздатьСопоставитьДокументОснование(ЭлементыФормы.ДокументОснование.Заголовок = "Создать...");
		
		ПолучитьСчетФактуруДляСопоставления();
		
		УправлениеФормой();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СчетФактураНажатие(Элемент)
	
	Перем СчетФактураОбъект;
	
	Если ЗначениеЗаполнено(СчетФактура) Тогда
		СчетФактура.ПолучитьОбъект().ПолучитьФорму().Открыть();
	Иначе
		
		Если ЗначениеЗаполнено(СчетФактураДляСопоставления) Тогда
			СчетФактураОбъект= СчетФактураДляСопоставления.ПолучитьОбъект();
			ПолучитьМодульПрог("Модуль_ИнтеграцияОбщий").ЗаполнитьРеквизиты_СчетФактураПолученный(СчетФактураОбъект, Document);
		КонецЕсли;
		
		Если СчетФактураОбъект = Неопределено Тогда
			СчетФактураОбъект= ПолучитьМодульПрог("Модуль_ИнтеграцияОбщий").СоздатьДокумент_СчетФактураПолученный(Document, ДокументОснование);
		КонецЕсли;
		
		Если СчетФактураОбъект <> Неопределено Тогда
			
			СчетФактураОбъект.ПолучитьФорму().ОткрытьМодально();
			Если НЕ СчетФактураОбъект.Модифицированность() Тогда
				СчетФактура= СчетФактураОбъект.Ссылка;
			КонецЕсли;
			
			СчетФактураОбъект= Неопределено;
			
			УправлениеФормой();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция СоздатьСопоставитьДокументОснование(СоздатьДокументОснование)
	
	Если СоздатьДокументОснование Тогда
		
		Результат = ПолучитьФорму("ФормаВводаНакладной",, Document.OrganizationId + Document.DocumentId).ЗаполнитьИОткрытьФормуСозданияДокументаВ1С(Document, Document.GetContent());
		
		Если Результат = "Сопоставить" Тогда
			Возврат СоздатьСопоставитьДокументОснование(Ложь);
		Иначе
			Возврат Результат;
		КонецЕсли;
		
	Иначе
		
		Результат = ПолучитьФорму("ФормаВыбораДокументаДляСопоставленияСНакладной").ВыбратьДокумент(Document, "СопоставитьНакладную");
		
		Если Результат = "Ввести" Тогда
			Возврат СоздатьСопоставитьДокументОснование(Истина);
		Иначе
			Возврат Результат;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции
