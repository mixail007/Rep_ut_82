Перем ПолноеИмяФайла;

//{ ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ И ЭЛЕМЕНТОВ

	Процедура ПриОткрытии()
		
		ЗафиксироватьСобытиеGA("Форма_Приглашения_Контрагента");
		
		УправлениеФормой();
		
		ТекстПриглашения_Заполнить();
		
	КонецПроцедуры

	Процедура НадписьФайл_Нажатие(Элемент)
		
		Перем Файл;
		
		ИмяФайла= "";
		ПолноеИмяФайла= "";
		РазмерФайла= 0;
		
		Диалог= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		
		Если Диалог.Выбрать() Тогда 
			Файл= Новый Файл(Диалог.ПолноеИмяФайла);
		КонецЕсли;
		
		Если Файл <> Неопределено И Файл.Существует() Тогда
			ЭлементыФормы.НадписьФайл.Заголовок= Файл.Имя + " (" + Строка(ОКР(Файл.Размер()/1024, 1)) + " КБ)";
			ПолноеИмяФайла= Файл.ПолноеИмя;
		Иначе
			ЭлементыФормы.НадписьФайл.Заголовок= "Прикрепить документ";
		КонецЕсли;
		
	КонецПроцедуры
	
	Процедура ОсновныеДействияФормы_КнопкаОтправитьПриглашения_Нажатие(Кнопка)
		
		ПараметрыПриглашения= Новый Структура;
		ПараметрыПриглашения.Вставить("ТекстПриглашения", ТекстПриглашения);
		ПараметрыПриглашения.Вставить("ИмяФайла"		, ПолноеИмяФайла);
		
		Закрыть(ПараметрыПриглашения);
		
	КонецПроцедуры

//} ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ И ЭЛЕМЕНТОВ


//{ СЛУЖЕБНЫЕ ФУНКЦИИ И ПРОЦЕДУРЫ

	Процедура УправлениеФормой()
		
		КартинкаЗаголовка= ЭДО_БиблиотекаКартинок().КартинкаЗаголовка;
		
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.КнопкаОтправитьПриглашения.Текст = "Отправить приглашения ("+КоличествоПриглашений+")";
		
		Заголовок= Заголовок + " (" + ОрганизацияЭДО_Представление + ")";
		
		ЭлементыФормы.НадписьФайл.Картинка= ЭДО_БиблиотекаКартинок().Скрепка;
		
		СтрокаКонтекста = ПолучитьМодульПрог("Модуль_РаботаССерверомДиадок").СтрокаКонтекста(ОрганизацияЭДО_ID);
		
		Если НЕ ЗначениеЗаполнено(СтрокаКонтекста.ОтпечатокСертификата) Тогда
			
			ЭлементыФормы.НадписьФайл.Доступность = Ложь;
			ЭлементыФормы.НадписьФайл.ЦветТекста  = WebЦвета.Красный;
			ЭлементыФормы.НадписьФайл.Заголовок   = "Прикрепить документ можно только при авторизации по сертификату";
			
		КонецЕсли;
		
	КонецПроцедуры

	Процедура ТекстПриглашения_Заполнить()
		
		СтрокаКонтекста = ПолучитьМодульПрог("Модуль_РаботаССерверомДиадок").СтрокаКонтекста(ОрганизацияЭДО_ID);
		
		ТекстПриглашения=
		
		"Уважаемый контрагент!
		|
		|Сообщаем Вам, что компания " + ОрганизацияЭДО_Представление + " перешла на электронный обмен юридически значимыми документами.
		|Мы готовы выставлять и принимать счета-фактуры, акты и накладные, акты сверок, а также вести переписку с вами в электронном виде без дублирования на бумаге.
		|Чтобы подтвердить свое согласие и перейти на электронный документооборот, примите данное приглашение.
		|
		|" + "С уважением, " + СтрокаКонтекста.ДанныеСотрудника.Должность + " " + СтрокаКонтекста.ДанныеСотрудника.ФИО + ".";
		
	КонецПроцедуры

//} СЛУЖЕБНЫЕ ФУНКЦИИ И ПРОЦЕДУРЫ