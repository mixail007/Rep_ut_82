Перем Organization Экспорт;
Перем СписокДолжностей;
Перем CurrentUserId; 

Процедура ОсновныеДействияФормыЗакрыть(Кнопка)
	Закрыть();
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если ЗначениеЗаполнено(ФИОПолучателя)=Ложь Тогда
		Предупреждение("Не указан получатель документа.", , НаименованиеСистемы);
		Возврат
	КонецЕсли;	
	
	ПараметрыПолучателя=	Новый Структура();
	ПараметрыПолучателя.Вставить("ResolutionRequestType", ResolutionRequestType);
	ПараметрыПолучателя.Вставить("TargetDepartmentId", Подразделение.DepartmentId);
	ПараметрыПолучателя.Вставить("TargetUserId", ИДПолучателя);
	ПараметрыПолучателя.Вставить("ДолжностьПолучателя", ДолжностьПолучателя);
	ПараметрыПолучателя.Вставить("ФИОПолучателя", ФИОПолучателя);
	ПараметрыПолучателя.Вставить("Комментарий", Комментарий);
	ПараметрыПолучателя.Вставить("ЭтоТекущийПользователь", (ИДПолучателя=CurrentUserId) );
	
	Если ResolutionRequestType = "ApprovementRequest" Тогда
		ПараметрыПолучателя.Вставить("ДолжностьПодписанта", ДолжностьПодписанта);
		ПараметрыПолучателя.Вставить("ФИОПодписанта", 		ФИОПодписанта);
	
		ПолучитьМодульПрог("Модуль_Данные1С_СвязиОбъектов").УстановитьНастройкиПодписантаСогласование(ФИОПодписанта, ДолжностьПодписанта);
	Иначе
		ПараметрыПолучателя.Вставить("ДолжностьПодписанта", "");
		ПараметрыПолучателя.Вставить("ФИОПодписанта", 		"");
	КонецЕсли;
		
	Закрыть(ПараметрыПолучателя);
	
КонецПроцедуры

Процедура ФИОНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка=	Ложь;
	
	ВыбранноеЗначение=	ВыбратьИзСписка(СписокПолучателей, Элемент);
	Если НЕ ВыбранноеЗначение = Неопределено Тогда
		ФИОПолучателя=	ВыбранноеЗначение.Значение.Name;
		ИДПолучателя=	ВыбранноеЗначение.Значение.Id;
		ДолжностьПолучателя  = СписокДолжностей[ИДПолучателя];
	КонецЕсли;
	
КонецПроцедуры

Процедура ФИОПодписантаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка=	Ложь;
	
	ВыбранноеЗначение=	ВыбратьИзСписка(СписокПолучателей, Элемент);
	Если НЕ ВыбранноеЗначение = Неопределено Тогда
		ФИОПодписанта=			ВыбранноеЗначение.Значение.Name;
		ИДПодписанта=			ВыбранноеЗначение.Значение.Id;
		ДолжностьПодписанта = СписокДолжностей[ИДПодписанта];
	КонецЕсли;

КонецПроцедуры

Процедура ПодразделениеНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка=	Ложь;
	
	ВыбранноеПодразделение=	ПолучитьФорму("ФормаВыбораОрганизацииПодразделения").ВыбратьПодразделениеОрганизацииДиадок(Organization.Id, Department);
	СформироватьСтруктуруПодразделения(ВыбранноеПодразделение);
	
	ФИОПолучателя=	"";
	ИДПолучателя=	"";
	
	ОбновлениеФормы();
	
КонецПроцедуры

Процедура СформироватьСтруктуруПодразделения(ВыбранноеПодразделение)
	
	Если ВыбранноеПодразделение = Неопределено Тогда
		Подразделение=	Новый Структура("DepartmentID, ПредставлениеПодразделения", "", "");
	Иначе
		Если ТипЗнч(ВыбранноеПодразделение) = Тип("Структура") Тогда
			Подразделение=	Новый Структура("DepartmentID, ПредставлениеПодразделения", ВыбранноеПодразделение.DepartmentID, ВыбранноеПодразделение.ПредставлениеПодразделения);
		Иначе
			Подразделение=	Новый Структура("DepartmentID, ПредставлениеПодразделения", ВыбранноеПодразделение.ID, ВыбранноеПодразделение.Name);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	КартинкаЗаголовка= ЭДО_БиблиотекаКартинок().КартинкаЗаголовка;
	СформироватьСтруктуруПодразделения(Department);
	
	Если ResolutionRequestType = "ApprovementRequest" Тогда
		УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельПодписант, РежимСверткиЭлементаУправления.Нет);
	Иначе
		УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельПодписант, РежимСверткиЭлементаУправления.Верх);
	КонецЕсли;
	
	Если Режим = "ПередачаНаОбработку" Тогда
		УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельДолжность	, РежимСверткиЭлементаУправления.Верх);
		УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельОрганизация, РежимСверткиЭлементаУправления.Верх);
	ИначеЕсли Режим = "ПервичнаяПередачаНаОбработку" Тогда	
		УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельОрганизация, РежимСверткиЭлементаУправления.Верх);
	Иначе
		УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельДолжность	, РежимСверткиЭлементаУправления.Нет);
		УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельОрганизация, РежимСверткиЭлементаУправления.Нет);
	КонецЕсли;
	
	ЭтаФорма.Заголовок=	"Передача на " + ?(ResolutionRequestType = "ApprovementRequest", "согласование", "подписание");
	
	НастройкиПодписанта=	ПолучитьМодульПрог("Модуль_Данные1С_СвязиОбъектов").ПолучитьНастройкиПодписантаСогласование();
	ФИОПодписанта=			НастройкиПодписанта.ФИОПодписанта;
	ДолжностьПодписанта=	НастройкиПодписанта.ДолжностьПодписанта;
	
	ОбновлениеФормы();
	
КонецПроцедуры

Процедура ОбновлениеФормы()
	
	СписокПолучателей=		Новый СписокЗначений;
	СписокДолжностей = Новый Соответствие;
	Users =	Organization.GetUsers();
	Для ц = 0 по Users.Count-1 Цикл
		
		User = Users.getItem(ц);
		
		Если User.IsCurrentUser = Истина Тогда
			CurrentUserId =  User.Id
		КонецЕсли;
		
		Если (ResolutionRequestType = "ApprovementRequest" И User.Permissions.CanAddResolutions)
			ИЛИ (ResolutionRequestType = "SignatureRequest" И User.Permissions.CanSignDocuments) Тогда
			
			Если ЗначениеЗаполнено(Подразделение.DepartmentId) Тогда
				Если НЕ User.Permissions.UserDepartment = Неопределено И 
					User.Permissions.UserDepartment.Id = Подразделение.DepartmentId Тогда
					СписокПолучателей.Добавить(User, User.Name);
					СписокДолжностей.Вставить(User.ID, User.Position);
				КонецЕсли;
			Иначе
				СписокПолучателей.Добавить(User, User.Name);
				СписокДолжностей.Вставить(User.ID, User.Position);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;

	СписокПолучателей.СортироватьПоПредставлению();
	
	Если СписокПолучателей.Количество() = 0 Тогда
		ЭлементыФормы.ПанельПолучатели.ТекущаяСтраница=	ЭлементыФормы.ПанельПолучатели.Страницы.СтраницаНетПолучателей;
		ЭлементыФормы.НадписьНетПолучателей.Заголовок=	"В подразделении нет сотрудников с правом " + ?(ResolutionRequestType = "ApprovementRequest", "согласования", "подписи");
	Иначе
		ЭлементыФормы.ПанельПолучатели.ТекущаяСтраница=	ЭлементыФормы.ПанельПолучатели.Страницы.СтраницаВыборПолучателя;
	КонецЕсли;

	ПредставлениеПодразделения=		?(ЗначениеЗаполнено(Подразделение.ПредставлениеПодразделения), Подразделение.ПредставлениеПодразделения, "Головное подразделение");
	
КонецПроцедуры

CurrentUserId = "";