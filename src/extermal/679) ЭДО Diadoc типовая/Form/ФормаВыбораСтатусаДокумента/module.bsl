
Процедура ПриОткрытии()
	
	КартинкаЗаголовка= ЭДО_БиблиотекаКартинок().КартинкаЗаголовка;
	
	ЗаполнитьСписокСтатусов();
	
	ЭлементыФормы.СписокСтатусов.ТекущаяСтрока = СписокСтатусов.Строки.Найти(ТекущееЗначение, "кодФильтра", Истина);
	
КонецПроцедуры


Процедура СписокСтатусовПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		КодФильтра = ОформлениеСтроки.ДанныеСтроки.КодФильтра;
		
		Если КодФильтра = -1 ИЛИ КодФильтра = 0 Тогда
			ОформлениеСтроки.Шрифт = Новый Шрифт(,,Истина);
		КонецЕсли;
		
		Если КодФильтра = -1 Тогда
			ОформлениеСтроки.ЦветТекста = WebЦвета.Серый;
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

Процедура СписокСтатусовПриАктивизацииСтроки(Элемент)
	
	Элементыформы.КоманднаяПанель.Кнопки.Выбрать.Доступность = Элемент.ТекущаяСтрока.КодФильтра > -1;
	
КонецПроцедуры

Процедура СписокСтатусовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбраннаяСтрока.КодФильтра > -1 Тогда
		Закрыть(ВыбраннаяСтрока.КодФильтра);
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаВыбратьНажатие(Кнопка)
	
	ТекущаяСтрока = Элементыформы.СписокСтатусов.ТекущаяСтрока;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		Закрыть(ТекущаяСтрока.КодФильтра);
	КонецЕсли;
	
КонецПроцедуры


Процедура ЗаполнитьСписокСтатусов()
	
	СписокСтатусовНоваяСтрока(СписокСтатусов.Строки, "Все документы", 0);
	
	Если Направление <> "НаПодписании" Тогда
		
		НоваяСтрока = СписокСтатусовНоваяСтрока(СписокСтатусов.Строки, "Статус подписания", -1);
		
		СписокСтатусовНоваяСтрока(НоваяСтрока.Строки, "Завершен"	, 1);
		СписокСтатусовНоваяСтрока(НоваяСтрока.Строки, "Не завершен"	, 2);
		СписокСтатусовНоваяСтрока(НоваяСтрока.Строки, "Прекращен"	, 3);
		СписокСтатусовНоваяСтрока(НоваяСтрока.Строки, ?(Направление = "Outbound", "Требуется уточнение", "Ожидается уточнение"), 4);
		
	КонецЕсли;	
	
	НоваяСтрока = СписокСтатусовНоваяСтрока(СписокСтатусов.Строки, "Статус согласования", -1);
	
	СписокСтатусовНоваяСтрока(НоваяСтрока.Строки, "На согласовании"			, 11);
	СписокСтатусовНоваяСтрока(НоваяСтрока.Строки, "Согласован"				, 12);
	СписокСтатусовНоваяСтрока(НоваяСтрока.Строки, "Отказано в согласовании"	, 13);
	СписокСтатусовНоваяСтрока(НоваяСтрока.Строки, "Отказано в подписании"	, 14);
	
	Если Направление <> "НаПодписании" Тогда
		
		НоваяСтрока = СписокСтатусовНоваяСтрока(СписокСтатусов.Строки, "Статус аннулирования", -1);
		
		СписокСтатусовНоваяСтрока(НоваяСтрока.Строки, "Ожидается аннулирование"	, 21);
		СписокСтатусовНоваяСтрока(НоваяСтрока.Строки, "Требуется аннулирование"	, 22);
		СписокСтатусовНоваяСтрока(НоваяСтрока.Строки, "Документ аннулирован"	, 23);
		СписокСтатусовНоваяСтрока(НоваяСтрока.Строки, "Отказано в аннулировании", 24);
		
	КонецЕсли;	
	
КонецПроцедуры

Функция СписокСтатусовНоваяСтрока(КоллекцияСтрок, Наименование, КодФильтра)
	
	НоваяСтрока = КоллекцияСтрок.Добавить();
	НоваяСтрока.Наименование = Наименование;
	НоваяСтрока.КодФильтра 	 = КодФильтра;
	
	Возврат НоваяСтрока;
	
КонецФункции
