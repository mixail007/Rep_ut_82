Перем позТекстОшибкиВерх;
Перем позПанельЭДВерх;
Перем позФормаВысота;
Перем позПенальЭДВысота;
Перем ВерсияДанныхСсылкаНаОбъект, ФормаОткрытогоДокумента1С;

//{ СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
	
	Процедура ОбновитьФорму()
		ОтобразитьЭлектронныйДокумент();
		ОбновитьСопоставленныйДокумент();
	КонецПроцедуры
	
	Процедура ОбновитьСопоставленныйДокумент(ОбновитьДействия=Истина)
		
		ТекущийТекстОшибки =  ТекстОшибки;
		
		РезультатПредставления 			= ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").ПолучитьПредставлениеСсылкиНаДокумент(СсылкаНаОбъект, ЭДОбъект);
		Если РезультатПредставления.СсылкаНаДокументПредставление = "" Тогда
			УстановитьРежимСверткиЭлементаУправления(ЭлементыФормы.ПанельДокумент1С, РежимСверткиЭлементаУправления.Верх);
		Иначе 
			Элементыформы.СсылкаНаДокументПредставление.Заголовок = РезультатПредставления.СсылкаНаДокументПредставление;
			ДокументОснованиеДляСозданияСФ 	= РезультатПредставления.ДокументОснованиеДляСозданияСФ;
			СпособСоздания 					= РезультатПредставления.СпособСоздания;
		КонецЕсли;	
		
		МассивОшибок = ПолучитьМодульПрог("Модуль_Логика_СверкаДанных").ПолучитьМассивОшибокУчета(ЭДОбъект, СсылкаНаОбъект);
		ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").НастроитьПанельТекстОшибок(массивОшибок, ЭтаФорма); 
		
		Если ОбновитьДействия = Истина Тогда
			ОбновитьДействияПоДокументу();
		КонецЕсли;	
		
		Если ТекстОшибки <> текущийТекстОшибки Тогда
			Оповестить("ИзменениеДокумента1С", ЭДОбъект, ЭтаФорма);
		КонецЕсли;
		
	КонецПроцедуры
	
	Процедура ОбновитьЭДОбъект()
		
		ЭДОбъект = Модуль_РаботаССерверомДиадок.ОбновитьЭДоОбъектДиадока(ЭДОбъект, ЭтаФорма);
		
	КонецПроцедуры
	
	Процедура ОбновитьДействияПоДокументу()
		
		ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").НастроитьКоманднуюПанельКарточкиДокумента(ЭДОбъект, ЭтаФорма);
		
	КонецПроцедуры
	
	Процедура ОбновитьВизуализациюДокумента()
		
		ПредставлениеЭД = ПолучитьМодульПрог("Модуль_РаботаССерверомДиадок").ПредставлениеЭД(ЭДОбъект);
		
		Заголовок 								= ПредставлениеЭД;
		ЭлементыФормы.СсылкаНаКонтент.Заголовок = ПредставлениеЭД;
		
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Модуль_РаботаССерверомДиадок.ПолучитьСтруктуруПредставленияЭД(ЭДОбъект));
		
		ЭлементыФормы.ПанельПредупрежденияОСтаромФормате.Видимость = ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").ПредупреждатьОСтаромФормате(ЭДОбъект, DocumentContent);
		
	КонецПроцедуры	
	
	Процедура ОтобразитьЭлектронныйДокумент()
		ОбновитьЭДОбъект();
		ОбновитьДействияПоДокументу();
		ОбновитьВизуализациюДокумента();
	КонецПроцедуры
	
	Процедура ОбновитьСтатусЭД()
		ЭДОбъект = Модуль_РаботаССерверомДиадок.ОбновитьЭДоОбъектДиадока(ЭДОбъект, ЭтаФорма);
		ОтобразитьЭлектронныйДокумент();
	КонецПроцедуры
	
	Процедура ПриОткрытии()
		
		Если ЭДОбъект.Direction = "Inbound" Тогда 
			Отправитель = ЭДОбъект.Counteragent.Name;
			Получатель = ЭДОбъект.Organization.Name;
		Иначе
			Отправитель = ЭДОбъект.Organization.Name;
			Получатель = ЭДОбъект.Counteragent.Name;
		КонецЕсли;
		
		СсылкаНаОбъект= ПолучитьМодульПрог("Модуль_Данные1С_СвязиОбъектов").DocumentID_2_Документ(ЭДОбъект.DocumentId, ЭДОбъект.OrganizationId, ЭДОбъект);
		
		КартинкаЗаголовка= ЭДО_БиблиотекаКартинок().КартинкаЗаголовка;
		ЭлементыФормы.СсылкаНаДокументПредставление.ЦветТекста = WEBЦвета.Синий;
		ОбновитьФорму();
		
		ОбработкаСобытияПодключаемогоМодуля("ПриОткрытииФормы", Новый Структура("Форма, ИмяФормы", ЭтаФорма, "ФормаКарточкаДокументаСМетаданными"));
		
	КонецПроцедуры
	
	Процедура ОбработчикАвтообновления()
		
		Если ВерсияДанныхСсылкаНаОбъект <> ФормаОткрытогоДокумента1С.ВерсияДанных Тогда
			ОбновитьСопоставленныйДокумент();
			Оповестить("ОбновитьСтроку", ЭДОбъект, ЭтаФорма);
			ВерсияДанныхСсылкаНаОбъект = ФормаОткрытогоДокумента1С.ВерсияДанных;
		КонецЕсли;
		Если Не ФормаОткрытогоДокумента1С.Открыта() Тогда
			ОтключитьОбработчикОжидания("ОбработчикАвтообновления");
			ФормаОткрытогоДокумента1С = Неопределено;
		КонецЕсли;
		
	КонецПроцедуры
	
//} СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

//{ ДЕЙСТВИЯ ФОРМЫ
	
	
	Процедура ВыполнитьДействиеНадДокументом(ИдентификаторДействия, СписокОперацийПослеОбработки)
		Попытка 
			Если  ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").ВыполнитьДействиеНадДокументом(ЭДОбъект, ИдентификаторДействия, ЭтаФорма.Заголовок)=Истина Тогда 
				Если СписокОперацийПослеОбработки.свойство("ОбновитьЭДОбъект")=Истина Тогда  
					ОбновитьЭДОбъект();
				КонецЕсли;	
				Если СписокОперацийПослеОбработки.свойство("ОбновитьДействияПоДокументу")=Истина Тогда  
					ОбновитьДействияПоДокументу();
				КонецЕсли;	
				Если СписокОперацийПослеОбработки.свойство("ОбновитьВизуализациюДокумента")=Истина Тогда  
					ОбновитьВизуализациюДокумента();
				КонецЕсли;
				Если СписокОперацийПослеОбработки.свойство("ОбновитьСопоставленныйДокумент")=Истина Тогда  
					ОбновитьСопоставленныйДокумент();
					Оповестить("ОбновитьСтроку", ЭДОбъект, ЭтаФорма);
				КонецЕсли;
			КонецЕсли
		Исключение 
			ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").СообщениеОбОшибкеДиадок(ОписаниеОшибки(), ИдентификаторДействия);
		КонецПопытки;
	КонецПроцедуры	
	
	Процедура АннулироватьДокумент()
		ВыполнитьДействиеНадДокументом("АннулироватьДокумент", Новый Структура("ОбновитьЭДОбъект, ОбновитьДействияПоДокументу", Истина, Истина));
	КонецПроцедуры
	
	Процедура ОтказатьВАннулировании()
		ВыполнитьДействиеНадДокументом("ОтказатьВАннулировании", Новый Структура("ОбновитьЭДОбъект, ОбновитьДействияПоДокументу", Истина, Истина));
	КонецПроцедуры
	
	Процедура ОтправитьЗапросНаАннулирование()
		ВыполнитьДействиеНадДокументом("ОтправитьЗапросНаАннулирование", Новый Структура("ОбновитьЭДОбъект, ОбновитьДействияПоДокументу", Истина, Истина));
	КонецПроцедуры
	
	Процедура ПередатьНаСогласование()
		
		Если ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").ДействиеСЗалоченнымПакетомЗапрещено(ЭДОбъект,  "ПередатьНаСогласование") Тогда
			Возврат;
		КонецЕсли;
		
		Если Модуль_РаботаССерверомДиадок.ОтправитьЭДОбъектНаСогласование(Организация, ЭДОбъект, "ApprovementRequest", "ПередачаНаОбработку") Тогда
			ОбновитьФорму();
		КонецЕсли;
		
	КонецПроцедуры
	
	Процедура ПередатьНаПодпись()
		
		Если ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").ДействиеСЗалоченнымПакетомЗапрещено(ЭДОбъект,  "ПередатьНаПодпись") Тогда
			Возврат;
		КонецЕсли;
		
		Если Модуль_РаботаССерверомДиадок.ОтправитьЭДОбъектНаСогласование(Организация, ЭДОбъект, "SignatureRequest", "ПередачаНаОбработку") Тогда
			ОбновитьФорму();
		КонецЕсли;
		
	КонецПроцедуры
	
	Процедура ПодписатьДокумент()
		ВыполнитьДействиеНадДокументом("ПодписатьДокумент", Новый Структура("ОбновитьЭДОбъект, ОбновитьДействияПоДокументу, ОбновитьВизуализациюДокумента", Истина, Истина, Истина));
	КонецПроцедуры
	
	Процедура ПодписатьЗапрошенныйДокумент()		
		ВыполнитьДействиеНадДокументом("ПодписатьЗапрошенныйДокумент", Новый Структура("ОбновитьЭДОбъект, ОбновитьДействияПоДокументу, ОбновитьВизуализациюДокумента", Истина, Истина, Истина));		
	КонецПроцедуры
	
	Процедура ОтказатьВПодписи()
		ВыполнитьДействиеНадДокументом("ОтказатьВПодписи", Новый Структура("ОбновитьЭДОбъект, ОбновитьДействияПоДокументу, ОбновитьВизуализациюДокумента", Истина, Истина, Истина));
	КонецПроцедуры 
	
	Процедура СогласоватьДокумент()
		ВыполнитьДействиеНадДокументом("СогласоватьДокумент", Новый Структура("ОбновитьЭДОбъект, ОбновитьДействияПоДокументу", Истина, Истина));
	КонецПроцедуры
	
	Процедура ОтказатьВСогласовании()
		ВыполнитьДействиеНадДокументом("ОтказатьВСогласовании", Новый Структура("ОбновитьЭДОбъект, ОбновитьДействияПоДокументу", Истина, Истина));
	КонецПроцедуры	
	
	Процедура СсылкаНаКонтентНажатие(Элемент)
		ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").ОткрытьФормуНеформализованногоДокумента(ЭДОбъект);
	КонецПроцедуры
	
	Процедура СогласованиеПодробноНажатие(Элемент)
		ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").СогласованиеПодробноНажатие(ЭДОбъект,Организация)
	КонецПроцедуры                                 
	
	Процедура ОшибкиПодробноНажатие(Элемент)
		ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").ОшибкиПодробноНажатие(ЭДОбъект,Организация, МассивОшибок );
	КонецПроцедуры
	
	Процедура КоманднаяПанель1ПерейтиВДиадок(Кнопка)
		Модуль_РаботаССерверомДиадок.ПоказатьДокументВДиадоке(ЭДОбъект)
	КонецПроцедуры
	
	Процедура КоманднаяПанель1СтруктураПодчиненности(Кнопка)
		ПолучитьФорму("ФормаСвязейДокументов", ЭтаФорма).ПоказСтруктурыПодчиненности(ЭДОбъект);
	КонецПроцедуры
	
	Процедура КоманднаяПанельОтправитьФайл(Кнопка)
		ПолучитьФорму("ФормаВыгрузки",ЭтаФорма).ПрикрепитьФайлы(ЭДОбъект);					
	КонецПроцедуры
	
	Процедура ИзменитьПодразделениеНажатие(Элемент)
		ВыполнитьДействиеНадДокументом("ИзменитьПодразделение", Новый Структура("ОбновитьЭДОбъект, ОбновитьДействияПоДокументу", Истина, Истина));
	КонецПроцедуры
	
	Процедура КоманднаяПанельУдалитьДокумент(Кнопка)
		
		Если ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").ДействиеСЗалоченнымПакетомЗапрещено(ЭДОбъект,  "УдалитьДокумент") Тогда
			Возврат;
		КонецЕсли;
		
		ПолучитьФорму("ФормаУдаленияДокументов", ЭтаФорма).УдалениеДокументовКомплекта(ЭДОбъект);
		
	КонецПроцедуры
	
	Процедура КоманднаяПанельПеревыставитьДокумент(Кнопка)
		
		Если ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").ДействиеСЗалоченнымПакетомЗапрещено(ЭДОбъект,  "ПеревыставитьДокумент") Тогда
			Возврат;
		КонецЕсли;
		
		ПолучитьФорму("ФормаПеревыставленияДокументов", ЭтаФорма).ПеревыставлениеДокументовКомплекта(ЭДОбъект);	
		
	КонецПроцедуры
	
	Процедура КоманднаяПанельОтменитьСопоставлениеДокумент(Кнопка)
		Если ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").КоманднаяПанельОтменитьСопоставлениеДокумент(СсылкаНаОбъект, ЭДОбъект) = Истина Тогда 
			СсылкаНаОбъект = Неопределено;
			Оповестить("ИзменениеСвязиДД1С", Новый Структура("ТипСущности, Ссылка1С, СсылкаДД", "Документ", СсылкаНаОбъект, ЭДОбъект));
		КонецЕсли;
	КонецПроцедуры
	
	Процедура КоманднаяПанельЗапроситьУточнение(Кнопка)
		ВыполнитьДействиеНадДокументом("ЗапроситьУточнение", Новый Структура("ОбновитьЭДОбъект, ОбновитьДействияПоДокументу", Истина, Истина));
	КонецПроцедуры
	
	Процедура КоманднаяПанельОтправитьИсправление(Кнопка)
		ВыполнитьДействиеНадДокументом("ОтправитьИсправление", Новый Структура("ОбновитьЭДОбъект, ОбновитьДействияПоДокументу", Истина, Истина));
	КонецПроцедуры
	
	Процедура КоманднаяПанельОтправитьКорректировку(Кнопка)
		ВыполнитьДействиеНадДокументом("ОтправитьКорректировку", Новый Структура("ОбновитьЭДОбъект, ОбновитьДействияПоДокументу", Истина, Истина));
	КонецПроцедуры
	
	Процедура ЗапросНаУточнениеПодробноНажатие(Элемент)
		ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").ПоказатьЗапросНаУточнениеПодробно(ЭДОбъект, Организация)	
	КонецПроцедуры
	
	Процедура ПодключаемаяКоманда(Кнопка)
			
		ОбработкаСобытияПодключаемогоМодуля("ПодключаемаяКоманда", Новый Структура("Форма, ИмяФормы, Кнопка", ЭтаФорма, "ФормаКарточкаДокументаСМетаданными", Кнопка));
		
	КонецПроцедуры
	
//} ДЕЙСТВИЯ ФОРМЫ

//{ СОЗДАНИЕ ДОКУМЕНТА 1С
	
	Процедура СсылкаНаДокументПредставлениеНажатие(Элемент)
	
		ФормаОткрытогоДокумента1С = ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").ОбработатьДействиеСозданиеДокумента(ЭДОбъект, DocumentContent, СсылкаНаОбъект, СпособСоздания, ДокументОснованиеДляСозданияСФ);
		Если ФормаОткрытогоДокумента1С <> Неопределено Тогда
			ВерсияДанныхСсылкаНаОбъект = ФормаОткрытогоДокумента1С.ВерсияДанных;
			ПодключитьОбработчикОжидания("ОбработчикАвтообновления", 1);
		КонецЕсли;
		
	КонецПроцедуры
	
//} СОЗДАНИЕ ДОКУМЕНТА 1С

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ИзменениеСертификата" Тогда 
		ЭтаФорма.Закрыть();
	ИначеЕсли ИмяСобытия = "ИзменениеСвязиДД1С" Тогда
		Если ЭДОбъект<> Неопределено Тогда
			Если Параметр.ТипСущности = "Документ" И Параметр.СсылкаДД.OrganizationId = ЭДОбъект.OrganizationId И Параметр.СсылкаДД.DocumentId = ЭДОбъект.DocumentId Тогда
				СсылкаНаОбъект = Параметр.Ссылка1С;
				ОбновитьСопоставленныйДокумент();
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "УдалениеДокументов" Тогда 
		Если Параметр.Количество() <> 0 Тогда
			Для Каждого Элем Из Параметр Цикл
				Если ЭДОбъект.DocumentId = Элем.DocumentId Тогда
					ЭтаФорма.Закрыть();
				КонецЕсли;
			КонецЦикла;;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


Процедура ПодробностиПроНовыйФорматНажатие(Элемент)
	
	ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").ПоказатьИнформациюОНовыхФорматах(ЭДОбъект.ConfirmationDate);
	
КонецПроцедуры

