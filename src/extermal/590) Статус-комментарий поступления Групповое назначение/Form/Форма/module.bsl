
Функция ВсеГотовоДляНазначения()
	
	ВсеОК = Истина;
	
	Если ДокументыДляНазначения.Количество() = 0 Тогда
		ВсеОК = Ложь;
		Сообщить("Заполните документы для назначения");
	КонецЕсли;
	
	Если ИзменятьСтатус = Ложь И ИзменятьКомментарий = Ложь Тогда
		ВсеОК = Ложь;
		Сообщить("Укажите галочки ""Статус"" или ""Комментарий"" для назначения (или оба вместе)");
	КонецЕсли;
	
	Если ИзменятьСтатус И Статус.Пустая() Тогда
		Ткст = "Будет назначен пустой статус. Продолжить?";
		ПродолжитьЛи = Вопрос(Ткст, РежимДиалогаВопрос.ДаНет, 20, КодВозвратаДиалога.Нет, "Пустой статус", КодВозвратаДиалога.Нет);
		Если ПродолжитьЛи = КодВозвратаДиалога.Нет Тогда
			ВсеОК = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ИзменятьКомментарий И (НЕ ЗначениеЗаполнено(КомментарийПоступленияВозврата)) Тогда
		Ткст = "Будет назначен пустой комментарий. Продолжить?";
		ПродолжитьЛи = Вопрос(Ткст, РежимДиалогаВопрос.ДаНет, 20, КодВозвратаДиалога.Нет, "Пустой комментарий", КодВозвратаДиалога.Нет);
		Если ПродолжитьЛи = КодВозвратаДиалога.Нет Тогда
			ВсеОК = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВсеОК;
	
КонецФункции // ВсеГотовоДляНазначения()

Процедура ВыполнитьЗаписьСвойства(ДокСсылка, Свойство, Значение)
	
	МенеджерЗаписи = РегистрыСведений.ЗначенияСвойствОбъектов.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.Объект   = ДокСсылка;
	МенеджерЗаписи.Свойство = Свойство;
	
	Если ЗначениеЗаполнено(Значение) Тогда
		МенеджерЗаписи.Значение = Значение;
		МенеджерЗаписи.Записать();
	Иначе
		МенеджерЗаписи.Прочитать();
		Если ЗначениеЗаполнено(МенеджерЗаписи.Объект) Тогда
			МенеджерЗаписи.Удалить();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьЗаписьСвойства()

Процедура ВыполнитьНазначениеДляДокументов()
	
	СвойствоСтатусПоступленияВозврата      = ПланыВидовХарактеристик.СвойстваОбъектов.СтатусПоступленияВозврата;
	СвойствоКомментарийПоступленияВозврата = ПланыВидовХарактеристик.СвойстваОбъектов.КомментарийПоступленияВозврата;
	
	Для Каждого ТекСтр Из ДокументыДляНазначения Цикл
		Если ИзменятьСтатус Тогда
			ВыполнитьЗаписьСвойства(ТекСтр.ДокСсылка, СвойствоСтатусПоступленияВозврата, Статус);
		КонецЕсли;
		Если ИзменятьКомментарий Тогда
			ВыполнитьЗаписьСвойства(ТекСтр.ДокСсылка, СвойствоКомментарийПоступленияВозврата, КомментарийПоступленияВозврата);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ВыполнитьНазначениеДляДокументов()

Процедура ВывестиСвойстваВТаблицу(ДанныеСвойств)
	
	ВыборкаСвойств = ДанныеСвойств.Выбрать();
	
	СтруктПоиска = Новый Структура("ДокСсылка");
	
	Для Каждого ТекСтр Из ДокументыДляНазначения Цикл
		СтруктПоиска.ДокСсылка = ТекСтр.ДокСсылка;
		Если ВыборкаСвойств.НайтиСледующий(СтруктПоиска) Тогда
			ТекСтр.Статус                         = ВыборкаСвойств.Статус;
			ТекСтр.КомментарийПоступленияВозврата = ВыборкаСвойств.КомментарийПоступленияВозврата;
			ВыборкаСвойств.Сбросить();
		Иначе
			ТекСтр.Статус                         = Справочники.ЗначенияСвойствОбъектов.ПустаяСсылка();
			ТекСтр.КомментарийПоступленияВозврата = "";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ВывестиСвойстваВТаблицу()

Процедура ПоказатьНазначенныеСвойства()
	
	МасДокументы = ДокументыДляНазначения.ВыгрузитьКолонку("ДокСсылка");
	ДанныеСвойств = ПолучитьСтатусыКомментарииДокументов(МасДокументы);
	ВывестиСвойстваВТаблицу(ДанныеСвойств);
	
КонецПроцедуры // ПоказатьНазначенныеСвойства()

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	Если ВсеГотовоДляНазначения() Тогда
		ВыполнитьНазначениеДляДокументов();
		ПоказатьНазначенныеСвойства();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыбПериодНажатие(Элемент)
	НастройкаПериода = Новый НастройкаПериода;
	НастройкаПериода.РедактироватьКакИнтервал = Истина;
	НастройкаПериода.РедактироватьКакПериод = Истина;
	НастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;
	НастройкаПериода.УстановитьПериод(НачПериода, ?(КонПериода='0001-01-01', КонПериода, КонецДня(КонПериода)));
	Если НастройкаПериода.Редактировать() Тогда
		НачПериода = НастройкаПериода.ПолучитьДатуНачала();
		КонПериода = НастройкаПериода.ПолучитьДатуОкончания();
	КонецЕсли;
КонецПроцедуры

Функция РеквизитыЗаполнены()
	
	ВсеОК = Истина;
	
	Если Контрагент.Пустая() Тогда
		ВсеОК = Ложь;
		Сообщить("Укажите контрагента");
	КонецЕсли;
	
	Если Подразделение.Пустая() Тогда
		ВсеОК = Ложь;
		Сообщить("Укажите подразделение");
	КонецЕсли;
	
	Если НачПериода = '00010101000000' Тогда
		ВсеОК = Ложь;
		Сообщить("Укажите начало периода");
	КонецЕсли;
	
	Если КонПериода = '00010101000000' Тогда
		ВсеОК = Ложь;
		Сообщить("Укажите окончание периода");
	КонецЕсли;

	Если НачПериода > КонПериода Тогда
		ВсеОК = Ложь;
		Сообщить("Начало периода не должно превышать его окончание");
	КонецЕсли;

	Возврат ВсеОК;
	
КонецФункции // РеквизитыЗаполнены()

Процедура КоманднаяПанельДокументыДляНазначенияПоказатьПоступления(Кнопка)
	
	Если РеквизитыЗаполнены() Тогда
		ПоказатьПоступления();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьПоступления()
	
	ДанныеПоступления = ПолучитьДанныеПоступления();
	тзПоступления = ДанныеПоступления.Выгрузить();
	ДокументыДляНазначения.Загрузить(тзПоступления);
	
КонецПроцедуры // ПоказатьПоступления()

Функция ПолучитьДанныеПоступления()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Дата,
	|	ПоступлениеТоваровУслуг.Номер,
	|	ПоступлениеТоваровУслуг.Ссылка КАК ДокСсылка,
	|	ПоступлениеТоваровУслуг.СуммаДокумента КАК Сумма,
	|	ПоступлениеТоваровУслуг.СкладОрдер КАК Склад,
	|	ПоступлениеТоваровУслуг.Комментарий
	|ПОМЕСТИТЬ втПоступления
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Дата МЕЖДУ &НачПериода И &КонПериода
	|	И ПоступлениеТоваровУслуг.Контрагент = &Контрагент
	|	И ПоступлениеТоваровУслуг.Подразделение = &Подразделение
	|	И ПоступлениеТоваровУслуг.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПоступления.Дата,
	|	втПоступления.Номер,
	|	втПоступления.ДокСсылка,
	|	втПоступления.Сумма,
	|	втПоступления.Склад,
	|	втПоступления.Комментарий,
	|	зтСтатусы.Статус,
	|	зтКомментарии.КомментарийПоступленияВозврата
	|ИЗ
	|	втПоступления КАК втПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ЗначенияСвойствОбъектов.Объект КАК ДокСсылка,
	|			ЗначенияСвойствОбъектов.Значение КАК Статус
	|		ИЗ
	|			РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ГДЕ
	|			ЗначенияСвойствОбъектов.Объект ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|			И ЗначенияСвойствОбъектов.Объект В
	|					(ВЫБРАТЬ
	|						втПоступления.ДокСсылка
	|					ИЗ
	|						втПоступления КАК втПоступления)
	|			И ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.СтатусПоступленияВозврата)) КАК зтСтатусы
	|		ПО втПоступления.ДокСсылка = зтСтатусы.ДокСсылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ЗначенияСвойствОбъектов.Объект КАК ДокСсылка,
	|			ЗначенияСвойствОбъектов.Значение КАК КомментарийПоступленияВозврата
	|		ИЗ
	|			РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ГДЕ
	|			ЗначенияСвойствОбъектов.Объект ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|			И ЗначенияСвойствОбъектов.Объект В
	|					(ВЫБРАТЬ
	|						втПоступления.ДокСсылка
	|					ИЗ
	|						втПоступления КАК втПоступления)
	|			И ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.КомментарийПоступленияВозврата)) КАК зтКомментарии
	|		ПО втПоступления.ДокСсылка = зтКомментарии.ДокСсылка";
	
	Запрос.УстановитьПараметр("НачПериода"   , НачПериода);
	Запрос.УстановитьПараметр("КонПериода"   , КонецДня(КонПериода));
	Запрос.УстановитьПараметр("Контрагент"   , Контрагент);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
	
КонецФункции // ПолучитьДанныеПоступления()

Процедура СтатусПолеВыбораПриИзменении(Элемент)
	
	Если СтатусПолеВыбора = Неопределено Тогда
		Статус = Справочники.ЗначенияСвойствОбъектов.ПустаяСсылка();
	Иначе
		Статус = СтатусПолеВыбора.Значение;
	КонецЕсли;
	
	ИзменятьСтатус = ЗначениеЗаполнено(Статус);
	
КонецПроцедуры

Процедура КомментарийПоступленияВозвратаПриИзменении(Элемент)
	
	ИзменятьКомментарий = ЗначениеЗаполнено(КомментарийПоступленияВозврата);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	СтатусыСписок = ПолучитьСписокСтатусовПоступленийВозвратов();

	Для Каждого ТекЗнач Из СтатусыСписок Цикл
		ЭлементыФормы.СтатусПолеВыбора.СписокВыбора.Добавить(ТекЗнач);
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСписокСтатусовПоступленийВозвратов()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗначенияСвойствОбъектов.Ссылка
	|ИЗ
	|	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|ГДЕ
	|	ЗначенияСвойствОбъектов.Владелец = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.СтатусПоступленияВозврата)";
	Результат = Запрос.Выполнить();
	Выгрузка  = Результат.Выгрузить();
	Мас = Выгрузка.ВыгрузитьКолонку("Ссылка");
	Спис = Новый СписокЗначений();
	Спис.ЗагрузитьЗначения(Мас);
	
	Возврат Спис;
	
КонецФункции // ПолучитьСписокСтатусовПоступленийВозвратов()

Функция ПолучитьСтатусыКомментарииДокументов(МасДокументы)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(зтСтатусы.ДокСсылка, зтКомментарии.ДокСсылка) КАК ДокСсылка,
	|	зтСтатусы.Статус,
	|	зтКомментарии.КомментарийПоступленияВозврата
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗначенияСвойствОбъектов.Объект КАК ДокСсылка,
	|		ЗначенияСвойствОбъектов.Значение КАК Статус
	|	ИЗ
	|		РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|	ГДЕ
	|		ЗначенияСвойствОбъектов.Объект ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|		И ЗначенияСвойствОбъектов.Объект В(&МасДокументы)
	|		И ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.СтатусПоступленияВозврата)) КАК зтСтатусы
	|		ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ЗначенияСвойствОбъектов.Объект КАК ДокСсылка,
	|			ЗначенияСвойствОбъектов.Значение КАК КомментарийПоступленияВозврата
	|		ИЗ
	|			РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ГДЕ
	|			ЗначенияСвойствОбъектов.Объект ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|			И ЗначенияСвойствОбъектов.Объект В(&МасДокументы)
	|			И ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.КомментарийПоступленияВозврата)) КАК зтКомментарии
	|		ПО зтСтатусы.ДокСсылка = зтКомментарии.ДокСсылка";
	
	Запрос.УстановитьПараметр("МасДокументы", МасДокументы);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
	
КонецФункции // ПолучитьСтатусыКомментарииДокументов()
