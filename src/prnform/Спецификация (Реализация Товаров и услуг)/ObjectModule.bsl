Функция Печать() Экспорт

	ДатаОтпуска = ТекущаяДата();	

	//ВвестиДату(ДатаОтпуска, "Введите дату отгрузки", ЧастиДаты.Дата);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект);
	Запрос.УстановитьПараметр("ДатаДокумента", СсылкаНаОбъект.Дата);
	Запрос.УстановитьПараметр("ВидОтветственногоЛица", Перечисления.ОтветственныеЛицаОрганизации.Руководитель);
	Запрос.УстановитьПараметр("РольКонтактногоЛица", Справочники.РолиКонтактныхЛиц.НайтиПоНаименованию("Директор"));
	Запрос.УстановитьПараметр("ПустаяРольКонтактногоЛица", Справочники.РолиКонтактныхЛиц.ПустаяСсылка());
	
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Документ.Дата,
	|	Документ.Номер,
	|	Документ.Организация КАК Компания,
	|	Документ.Контрагент.НаименованиеПолное КАК Контрагент,
	|	Документ.Ответственный,
	|	ВЫБОР
	|		КОГДА Документ.Сделка ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА Документ.Сделка.ДатаОплаты
	|		ИНАЧЕ Документ.Дата
	|	КОНЕЦ КАК ДатаОплаты,
	|	ОтветственныеЛицаОрганизацииСрезПоследних.ОтветственноеЛицо,
	|	ОтветственныеЛицаОрганизацииСрезПоследних.ФизическоеЛицо КАК РуководительКомпании,
	|	ВЫБОР
	|		КОГДА КонтактныеЛица.Ссылка ССЫЛКА Справочник.КонтактныеЛица
	|			  И КонтактныеЛица.Роль = &РольКонтактногоЛица
	|			  И КонтактныеЛица.Роль <> &ПустаяРольКонтактногоЛица
	|			ТОГДА КонтактныеЛица.Представление
	|		ИНАЧЕ ""/_______________/""
	|	КОНЕЦ КАК РуководительКонтрагента
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизации.СрезПоследних(&ДатаДокумента, ОтветственноеЛицо = &ВидОтветственногоЛица) КАК ОтветственныеЛицаОрганизацииСрезПоследних
	|		ПО Документ.Организация = ОтветственныеЛицаОрганизацииСрезПоследних.СтруктурнаяЕдиница
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица КАК КонтактныеЛица
	|		ПО Документ.Контрагент = КонтактныеЛица.ОбъектВладелец
	|			И (КонтактныеЛица.Роль = &РольКонтактногоЛица)
	|ГДЕ
	|	Документ.Ссылка = &ТекущийДокумент";
		
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	
	ТабДокумент  = Новый ТабличныйДокумент;
	
	//ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_ТОРГ12";

	Макет = ПолучитьМакет("Спецификация");	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Номенклатура,
	|   ЕдиницаИзмерения,
	|   СУММА(Количество) КАК Количество,
	|   Цена,
	|   СУММА(Сумма) КАК Сумма
	|ИЗ
	|   Документ.РеализацияТоваровУслуг.Товары КАК Документ
	|ГДЕ
	|   Документ.Ссылка = &ТекущийДокумент
	|СГРУППИРОВАТЬ ПО
	|	Номенклатура,
	|   ЕдиницаИзмерения,
	|   Цена
	|УПОРЯДОЧИТЬ ПО
	|	ПОДСТРОКА(Номенклатура.НаименованиеПолное,1,50) ВОЗР
	|ИТОГИ
	|	СУММА(Количество) КАК Количество, 
	|	СУММА(Сумма) КАК Сумма 
	|ПО 
	|	Общие
	|";
	
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьШапка.Параметры.Заполнить(Шапка);
	ОбластьШапка.Параметры.ШапкаДата  	= Формат(Шапка.Дата, "ДФ=""дд ММММ гггг 'г.'""");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.Заполнить(Шапка);
	ОбластьПодвал.Параметры.ДатаОплаты = Формат(Шапка.ДатаОплаты, "ДФ=dd.MM.yyyy"); 
	ОбластьИтоги = Макет.ПолучитьОбласть("СтрокаИтоги");
	ОбластьСтроки = Макет.ПолучитьОбласть("СтрокаТаблицы");
	
	ТабДокумент.Вывести(ОбластьШапка);
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбщийИтог = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Если ОбщийИтог.Следующий() Тогда
	
		ОбластьИтоги.Параметры.Заполнить(ОбщийИтог);		
	
	КонецЕсли; 
	
	ТабЧасть = ОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ТабЧасть.Следующий() Цикл
		
		ОбластьСтроки.Параметры.Заполнить(ТабЧасть);
		ОбластьСтроки.Параметры.Номенклатура = ТабЧасть.Номенклатура.НаименованиеПолное;
		ТабДокумент.Вывести(ОбластьСтроки);
		
	КонецЦикла;
	
	ТабДокумент.Вывести(ОбластьИтоги);
	ТабДокумент.Вывести(ОбластьПодвал);
	
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
	
	Возврат ТабДокумент;
	
КонецФункции