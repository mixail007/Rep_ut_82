Функция Печать() Экспорт
	
	Если Не Метаданные.Имя = "УправлениеТорговлей" Тогда
		Сообщить("Печатная форма предназначена для использования в конфигурации ""Управление торговлей""", СтатусСообщения.Внимание);
	КонецЕсли;
	
	Если 	ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		Возврат ПечатьПоступления(СсылкаНаОбъект);
	КонецЕсли;
	
КонецФункции // Печать


Функция ПечатьПоступления ( СсылкаНаОбъект) Экспорт
	ТабДок=Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("ОтчетПоступление");
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслугТовары.НомерСтроки,
	|	ПоступлениеТоваровУслугТовары.Номенклатура.Код КАК Код,
	|	ПоступлениеТоваровУслугТовары.Номенклатура,
	|	ПоступлениеТоваровУслугТовары.Количество,
	|	Комплектующие.Комплектующая.КодСБИС КАК КодСБИС,
	|	Комплектующие.Комплектующая.Код КАК КодКрышки,
	|	Комплектующие.Комплектующая,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслугТовары.Номенклатура.Наименование ПОДОБНО ""%legeartis%""
	|			ТОГДА ЕСТЬNULL(КрышкиПроверены.Свойство, ЛОЖЬ)
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК КрышкиПроверены
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПоступлениеТоваровУслугТовары.НомерСтроки КАК НомерСтроки,
	|		ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|		ПоступлениеТоваровУслугТовары.Количество КАК Количество
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|	ГДЕ
	|		ПоступлениеТоваровУслугТовары.Ссылка = &Ссылка) КАК ПоступлениеТоваровУслугТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			КомплектующиеНоменклатуры.Номенклатура КАК Номенклатура,
	|			КомплектующиеНоменклатуры.Комплектующая КАК Комплектующая
	|		ИЗ
	|			РегистрСведений.КомплектующиеНоменклатуры КАК КомплектующиеНоменклатуры
	|		ГДЕ
	|			КомплектующиеНоменклатуры.Номенклатура В
	|					(ВЫБРАТЬ
	|						Документ.ПоступлениеТоваровУслуг.Товары.Номенклатура
	|					ИЗ
	|						Документ.ПоступлениеТоваровУслуг.Товары
	|					ГДЕ
	|						Документ.ПоступлениеТоваровУслуг.Товары.Ссылка = &Ссылка)
	|			И КомплектующиеНоменклатуры.Комплектующая В ИЕРАРХИИ(&Крышки)) КАК Комплектующие
	|		ПО ПоступлениеТоваровУслугТовары.Номенклатура = Комплектующие.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ЗначенияСвойствОбъектов.Объект КАК Номенклатура,
	|			ЗначенияСвойствОбъектов.Значение КАК Свойство
	|		ИЗ
	|			РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ГДЕ
	|			ЗначенияСвойствОбъектов.Свойство.Наименование = ""Крышки проверены""
	|			И ЗначенияСвойствОбъектов.Объект В
	|					(ВЫБРАТЬ
	|						Документ.ПоступлениеТоваровУслуг.Товары.Номенклатура
	|					ИЗ
	|						Документ.ПоступлениеТоваровУслуг.Товары
	|					ГДЕ
	|						Документ.ПоступлениеТоваровУслуг.Товары.Ссылка = &Ссылка)) КАК КрышкиПроверены
	|		ПО ПоступлениеТоваровУслугТовары.Номенклатура = КрышкиПроверены.Номенклатура";

	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	Запрос.УстановитьПараметр("Крышки", Справочники.Номенклатура.НайтиПоКоду("0080004"));

	Результат = Запрос.Выполнить();

	
	ОбластьЗаголовок= Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.ЗаголовокДокумента="Поступление №" +  СсылкаНаОбъект.Номер +" от "+Формат(СсылкаНаОбъект.Дата,"ДФ=dd.MM.yyyy") +". Покупатель: "+СсылкаНаОбъект.Контрагент.Наименование;
	ОбластьШапка= Макет.ПолучитьОбласть("Шапка");
	
	
	ТабДок.Очистить();

	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапка);
	
	ВыборкаДетали = Результат.Выбрать();

	
	Пока ВыборкаДетали.Следующий() Цикл
		ОбластьДетальныхЗаписей = Макет.ПолучитьОбласть("Детали");
		ОбластьДетальныхЗаписей.Параметры.Заполнить(ВыборкаДетали);
		Если ВыборкаДетали.КрышкиПроверены=ложь тогда
			ОбластьДетальныхЗаписей.Параметры.КрышкиНеПроверены="*";
		КонецЕсли;
		ТабДок.Вывести(ОбластьДетальныхЗаписей);
	КонецЦикла;
	
	ТабДок.АвтоМасштаб = Истина;
	
    Возврат ТабДок;
	КонецФункции