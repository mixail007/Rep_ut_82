// v1.03 (14.02.2012) 
// ВЗЯТО: http://infostart.ru/public/90540/, автор - Adeptus: http://infostart.ru/profile/244147/)
// ДОРАБОТАНО мной на предмет добавления кнопки вызова стандартного диалога выбора периода
// 
// 17.10.2012
// - Исправлена ошибка при выборе Второго квартала (кнопка "II"),
//   вместо периода "с 01.07.2012 по 31.12.2012" устанавливался период "с 01.06.2012 по 31.12.2012"
//   // В процедуре ПолугодиеНажатие, вместо:
//   // Период.НачалоПериода = Дата(ТекГод, ?(ПорядковыйНомер = 1, 1 ,6), 1);
//   // должно быть:
//   // Период.НачалоПериода = Дата(ТекГод, ?(ПорядковыйНомер = 1, 1 ,7), 1);
//   // Т.е. надо "6" исправить на "7".
//
//############################################################################
//
Перем Период, ТекГод;

Процедура ПриОткрытии()
	
	ЭлементыФормы.Год.Заголовок = Формат(Год(РабочаяДата), "ЧГ=");
	//ЭлементыФормы["Месяц" + Формат(РабочаяДата, "ДФ=MM")].ЦветТекстаКнопки = WebЦвета.Синий; //закоментировано MTV 14.02.2012
	РаскраситьКнопкуТекущегоМесяца(); //добавлено MTV 14.02.2012
	
КонецПроцедуры

Процедура СледующийГодНажатие(Элемент)
	
	ТекГод = Число(ЭлементыФормы.Год.Заголовок) + 1;
	ЭлементыФормы.Год.Заголовок = Формат(ТекГод, "ЧГ=");
	РаскраситьКнопкуТекущегоМесяца(); //добавлено MTV 14.02.2012
	
КонецПроцедуры

Процедура ПредыдущийГодНажатие(Элемент)
	
	ТекГод = Число(ЭлементыФормы.Год.Заголовок) - 1;
	ЭлементыФормы.Год.Заголовок = Формат(ТекГод, "ЧГ=");
	РаскраситьКнопкуТекущегоМесяца(); //добавлено MTV 14.02.2012

КонецПроцедуры

Процедура ГодНажатие(Элемент)
	
	Период.НачалоПериода = Дата(ЭлементыФормы.Год.Заголовок, 1, 1);
	Период.КонецПериода  = Дата(ЭлементыФормы.Год.Заголовок, 12, 31, 23, 59, 59);
	
	Закрыть(Период);
	
КонецПроцедуры

Процедура ПолугодиеНажатие(Элемент)
	
	ПорядковыйНомер = Число(Прав(Элемент.Имя, 2));
	
	Период.НачалоПериода = Дата(ТекГод, ?(ПорядковыйНомер = 1, 1 ,7), 1);
	Период.КонецПериода  = КонецМесяца(Дата(ТекГод, 6 * ПорядковыйНомер, 1));
	
	Закрыть(Период);
	
КонецПроцедуры

Процедура КварталНажатие(Элемент)
	
	ПорядковыйНомер = Число(Прав(Элемент.Имя, 2));
	
	Период.НачалоПериода = НачалоКвартала(Дата(ТекГод, 3 * ПорядковыйНомер, 1));
	Период.КонецПериода  = КонецМесяца(Дата(ТекГод, 3 * ПорядковыйНомер, 1));
	
	Закрыть(Период);
	
КонецПроцедуры

Процедура МесяцНажатие(Элемент)
	
	ПорядковыйНомер = Число(Прав(Элемент.Имя, 2));
	
	Период.НачалоПериода = Дата(ТекГод, ПорядковыйНомер, 1);
	Период.КонецПериода  = КонецМесяца(Дата(ТекГод, ПорядковыйНомер, 1));
	
	Закрыть(Период);
	
КонецПроцедуры

Процедура ДеньНажатие(Элемент)
	
	ПорядковыйНомер = Число(Прав(Элемент.Имя, 2));
		
	ЭлементыФормы.ПолеКалендаря.НачалоПериодаОтображения = НачалоМесяца(Дата(ТекГод, ПорядковыйНомер, 1));
	ЭлементыФормы.ПолеКалендаря.КонецПериодаОтображения  = КонецМесяца(Дата(ТекГод, ПорядковыйНомер, 1));
	
	// Для снятия ограничения на перемещение по Календарю за границы Месяц
	ЭлементыФормы.ПолеКалендаря.НачалоПериодаОтображения = Неопределено; //добавлено MTV 14.02.2012
	ЭлементыФормы.ПолеКалендаря.КонецПериодаОтображения  = Неопределено; //добавлено MTV 14.02.2012
	
КонецПроцедуры

Процедура ПолеКалендаряВыбор(Элемент, ВыбраннаяДата)
	
	Период.НачалоПериода = ПолеКалендаря;
	Период.КонецПериода  = КонецДня(ПолеКалендаря);

	Закрыть(Период);
	
КонецПроцедуры

Процедура ВыбратьНажатие(Элемент)
	
	ВыделенныеДаты = ЭлементыФормы.ПолеКалендаря.ВыделенныеДаты;
	
	День1 = ВыделенныеДаты[0];
	День2 = ВыделенныеДаты[ВыделенныеДаты.Количество() - 1];
	
	Период.НачалоПериода = Мин(День1, День2);
	Период.КонецПериода  = КонецДня(Макс(День1, День2));

	Закрыть(Период);
	
КонецПроцедуры

// Процедура и соответствующая кнопка добавлена MTV 14.02.2012
Процедура ВыбПериодНажатие(Элемент)
	
	НастройкаПериода = Новый НастройкаПериода;
	НастройкаПериода.УстановитьПериод(ЭтаФорма.ВладелецФормы.НачПериода, ?(ЭтаФорма.ВладелецФормы.КонПериода='0001-01-01', ЭтаФорма.ВладелецФормы.КонПериода, КонецДня(ЭтаФорма.ВладелецФормы.КонПериода)));
	НастройкаПериода.РедактироватьКакИнтервал = Истина;
	НастройкаПериода.РедактироватьКакПериод = Истина;
	НастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;
	Если НастройкаПериода.Редактировать() Тогда
		Период.НачалоПериода = НастройкаПериода.ПолучитьДатуНачала();
		Период.КонецПериода  = НастройкаПериода.ПолучитьДатуОкончания();
		Закрыть(Период);
	КонецЕсли;
	
КонецПроцедуры //ВыбПериодНажатие()

// Процедура добавлена MTV 14.02.2012
Процедура РаскраситьКнопкуТекущегоМесяца()
	
	Если ЭлементыФормы.Год.Заголовок = Формат(Год(РабочаяДата), "ЧГ=") Тогда
		ЭлементыФормы["Месяц" + Формат(РабочаяДата, "ДФ=MM")].ЦветТекстаКнопки = WebЦвета.Синий;
		ЭлементыФормы["Месяц" + Формат(РабочаяДата, "ДФ=MM")].ЦветФонаКнопки = WebЦвета.Персиковый;
	Иначе
		ЭлементыФормы["Месяц" + Формат(РабочаяДата, "ДФ=MM")].ЦветТекстаКнопки = ЦветаСтиля.ЦветТекстаКнопки;
		ЭлементыФормы["Месяц" + Формат(РабочаяДата, "ДФ=MM")].ЦветФонаКнопки = ЦветаСтиля.ЦветФонаКнопки;
	КонецЕсли;
	
КонецПроцедуры //РаскраситьКнопкуТекущегоМесяца()

Период = Новый Структура;
Период.Вставить("НачалоПериода");
Период.Вставить("КонецПериода");

ТекГод = Год(РабочаяДата);