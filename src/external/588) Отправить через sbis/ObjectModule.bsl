//Перем Парам Экспорт;

Функция сбисПолучитьФорму(ИмяФормы, Объект1С = Неопределено, ПараметрыФормы = Неопределено, ВладелецФормы = Неопределено)

	Если Объект1С = Неопределено Тогда
		Возврат ЭтотОбъект.ПолучитьФорму(ИмяФормы,ВладелецФормы);
	Иначе
		Возврат Объект1С.ПолучитьФорму(ИмяФормы,ВладелецФормы);
	КонецЕсли;	
КонецФункции

Функция сбисПараметрыМассыПрописью(Масса) Экспорт
	КоэффициентПересчетаВТонны     = Константы.КоэффициентПересчетаВТонны.Получить();
	МассаТонны = Масса * КоэффициентПересчетаВТонны;
	Если МассаТонны > 1 Тогда
		Коэффициент = 1;
		СтрокаФормат = "т, т, т, ж, кг, кг, кг, м, " + ?(Окр(МассаТонны) = МассаТонны, "0", "3");
	ИначеЕсли МассаТонны * 1000 > 1 Тогда
		Коэффициент = 1000;
		СтрокаФормат = "кг, кг, кг, м, г, г, г, м, " + ?(Окр(МассаТонны * Коэффициент) = МассаТонны * Коэффициент, "0", "3");
	Иначе
		Коэффициент = 1000000;
		СтрокаФормат = "г, г, г, м, г, г, г, м, 0";
	КонецЕсли;
	
	Возврат Новый Структура("Формат, Коэффициент", СтрокаФормат, Коэффициент*КоэффициентПересчетаВТонны);
	
КонецФункции

функция сбисОпределитьМассуИтогПрописью(МассаИтог) Экспорт
	Если ЗначениеЗаполнено(МассаИтог) Тогда
		ПараметрыМассыПрописью = сбисПараметрыМассыПрописью(МассаИтог);
		Возврат ЧислоПрописью(
			МассаИтог * ПараметрыМассыПрописью.Коэффициент,
			"ДП=Истина",
			ПараметрыМассыПрописью.Формат
		);
	Иначе
		Возврат "";
	КонецЕсли;
		
КонецФункции

функция сбисОпределитьМассуИтогПрописьюНеупрПрил(МассаИтог) Экспорт
	ЕдиницаИзмеренияВеса = Константы.ЕдиницаИзмеренияВеса.Получить();
	Если ЗначениеЗаполнено(ЕдиницаИзмеренияВеса) И МассаИтог > 0 Тогда
		Возврат ЧислоПрописью(МассаИтог, ,",,,,,,,,0")+ " " +ЕдиницаИзмеренияВеса.Наименование + ".";
	Иначе
		Возврат "";
	КонецЕсли;	
КонецФункции

функция сбисОпределитьМассуБруттоИтог(ТЧДок) Экспорт
	ЗаполненаЕдиницаИзмеренияВеса = ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить());
	Если ЗаполненаЕдиницаИзмеренияВеса Тогда
		МассаБруттоИтог = 0;
		Для Каждого СтрокаТЧ из ТЧДок Цикл
			Если СтрокаТЧ.Упаковка.Вес = 0 Тогда 
				МассаБруттоИтог = МассаБруттоИтог + СтрокаТЧ.Количество * СтрокаТЧ.Номенклатура.Вес
			Иначе 
				МассаБруттоИтог = МассаБруттоИтог +  СтрокаТЧ.КоличествоУпаковок * СтрокаТЧ.Упаковка.Вес
			КонецЕсли;		
		КонецЦикла;
		Возврат МассаБруттоИтог;
	Иначе
		Возврат 0
    КонецЕсли;		
КонецФункции	

//Процедура для вызова отправки документов по кнопке "Печать"
Функция Печать(СсылкаНаОбъект1С=Неопределено) Экспорт
	Если СсылкаНаОбъект1С<>Неопределено Тогда
		СсылкаНаОбъект=СсылкаНаОбъект1С
	КонецЕсли;
	ГлавноеОкно = сбисПолучитьФорму("ФормаГлавноеОкно");	
	
	//Дополнение для неуправляемой формы
	ГлавноеОкно.КаталогНастроек = ВосстановитьЗначение("СБИС_КаталогНастроек");	
	ОбъектыНазначения = Новый Массив();
	ОбъектыНазначения.Добавить(СсылкаНаОбъект);
	////////////////////////////////////
	
	ГлавноеОкно.КэшПодготовить();	
	ГлавноеОкно.Кэш.Текущий.Раздел = "3";
	ГлавноеОкно.Кэш.Текущий.ТипДок = СсылкаНаОбъект.Метаданные().Имя;
	
	ГлавноеОкно.Кэш.Парам = ВосстановитьЗначение("сбисПарам");
	
	//Подключим заплатки
	ГлавноеОкно.сбисПодключитьЗаплатки();
	
	ГлавноеОкно.сбисПолучитьПарам();
	
	ГлавноеОкно.ОпределитьФормуИнтеграции(ГлавноеОкно.Кэш,ГлавноеОкно.Кэш.Парам.СпособОбмена);
	ОбменВключен = ГлавноеОкно.Кэш.Интеграция.Включить(ГлавноеОкно.Кэш);
	Если ОбменВключен<>Истина Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если ГлавноеОкно.Кэш.Парам.СпособОбмена = 0 или ГлавноеОкно.Кэш.Парам.СпособОбмена = 2 или ГлавноеОкно.Кэш.Парам.СпособОбмена = 3 или ГлавноеОкно.Кэш.Парам.СпособОбмена = 4 Тогда // SDK
		формаАвторизации = сбисПолучитьФорму("ФормаАвторизация",,,ГлавноеОкно);
		Если (ГлавноеОкно.Кэш.Парам.ВходПоСертификату и ГлавноеОкно.Кэш.Парам.ЗапомнитьСертификат) или (НЕ ГлавноеОкно.Кэш.Парам.ВходПоСертификату и ГлавноеОкно.Кэш.Парам.ЗапомнитьПароль) Тогда
			УспешнаяАвторизация = формаАвторизации.Авторизоваться(ГлавноеОкно.Кэш);
		КонецЕсли;
		Если УспешнаяАвторизация<>Истина Тогда
			Результат = формаАвторизации.ОткрытьМодально();
			Если Результат = Неопределено Тогда
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;


	ГлавноеОкно.КаталогНастроекПриИзменении(Неопределено);
	
	Если ГлавноеОкно.Кэш.ИниВПорядке Тогда
		Для каждого Документ из ОбъектыНазначения Цикл
			ИмяДокумента = ГлавноеОкно.Кэш.ОбщиеФункции.ПолучитьРеквизитМетаданныхОбъекта(Документ, "Имя");
			
			//Получим настройки документа
			Ини="";
			Если ГлавноеОкно.Кэш.ини.Свойство(ИмяДокумента,Ини)=Ложь Тогда
				Сообщить("Выгрузка данного типа документов не поддерживается!");
				Возврат Неопределено;
			КонецЕсли;
			
			//Подготовим фильтр для запроса документов
			ПараметрыФильтра = Новый Структура;
			ПараметрыФильтра.Вставить( "ФильтрДокумент", Документ); 		
			ПараметрыФильтра.Вставить( "ФильтрДатаНач", ГлавноеОкно.ФильтрДатаНач); 
			ПараметрыФильтра.Вставить( "ФильтрДатаКнц", ГлавноеОкно.ФильтрДатаКнц); 
			ПараметрыФильтра.Вставить( "ФильтрКонтрагент", ГлавноеОкно.ФильтрКонтрагент);
			ПараметрыФильтра.Вставить( "ФильтрКонтрагентПодключен", ГлавноеОкно.ФильтрКонтрагентПодключен);
			ПараметрыФильтра.Вставить( "ФильтрОрганизация", ГлавноеОкно.ФильтрОрганизация);			
			ПараметрыФильтра.Вставить( "ФильтрСостояние", "Все документы");
			ПараметрыФильтра.Вставить( "ФильтрОтветственный", ГлавноеОкно.ФильтрОтветственный);
			ПараметрыФильтра.Вставить( "РазмерСтраницы", 1 ); 
		    ПараметрыФильтра.Вставить( "Страница", 1 );
        	ПараметрыФильтра.Вставить( "ВсеДокументы", Истина );
			
			// Добавляем пользовательские фильтры
			Если ГлавноеОкно.Кэш.ПараметрыФильтра.Количество()>0 Тогда
				ПараметрыФильтра.Вставить( "Пользовательские", Новый Структура);
				Для Каждого Элемент Из ГлавноеОкно.Кэш.ПараметрыФильтра Цикл
					ПараметрыФильтра.Пользовательские.Вставить( Элемент.Ключ, Элемент.Значение);		
				КонецЦикла;
			КонецЕсли;
						
			//Получим массив выгружаемых документов по запросу
			РаботаСДокументами = сбисПолучитьФорму("РаботаСДокументами1С");
			ТекстЗапроса = РаботаСДокументами.РассчитатьЗначение("ЗапросСпискаДокументов", Ини, ГлавноеОкно.Кэш);
			МассивДокументов1С = РаботаСДокументами.ЗаполнитьМассивДокументов1С(ТекстЗапроса, ПараметрыФильтра, ГлавноеОкно.КаталогНастроек);
			//Подготовим список документов
			СписокОтмеченныхДокументов = Новый СписокЗначений;
			Для каждого СтрокаТЗ из МассивДокументов1С Цикл
				СписокОтмеченныхДокументов.Добавить(СтрокаТЗ);
				ТекущаяСтрока = СтрокаТЗ;
				ТекущийРаздел = Ини.ДокументРаздел.Значение;
				//Откроем пакет документов
								
				ПозЗпт = Найти(ТекущийРаздел, ",");
				Если ПозЗпт>0 Тогда
					ТекущийРаздел = Лев(ТекущийРаздел, ПозЗпт-1);	
				КонецЕсли;
				фрм = ГлавноеОкно.сбисНайтиФормуФункции("ПоказатьДокумент","Раздел_"+СтрЗаменить(ТекущийРаздел, "'", "")+"_Шаблон",, ГлавноеОкно.Кэш);
				ГлавноеОкно.Кэш.ГлавноеОкно = ГлавноеОкно;
				фрм.ПоказатьДокумент(ГлавноеОкно.Кэш, ТекущаяСтрока);	
				
			КонецЦикла;	
			
		КонецЦикла;
	КонецЕсли;		

КонецФункции	

//Парам = Новый Структура();
//Кэш = Новый Структура();
