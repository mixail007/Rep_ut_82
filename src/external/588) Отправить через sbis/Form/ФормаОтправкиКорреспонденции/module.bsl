&НаКлиенте
Перем МестныйКэш Экспорт;
&НаКлиенте
Перем ДопПараметры Экспорт;
&НаКлиенте
Функция сбисПолучитьФорму(ИмяФормы)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Попытка
			ЭтотОбъект="";
		Исключение
		КонецПопытки;
		Возврат ПолучитьФорму("ВнешняяОбработка.СБИС.Форма."+ИмяФормы);
	КонецЕсли;
	Возврат ЭтотОбъект.ПолучитьФорму(ИмяФормы);
КонецФункции

&НаКлиенте
Процедура ПоказатьФорму(Кэш, ДополнительныеПараметры = Неопределено)Экспорт
	МестныйКэш = Кэш;
	ДопПараметры = ДополнительныеПараметры;
	ЭтаФорма.Открыть();
КонецПроцедуры
Процедура сбисЗаполнитьСписокКонтрагентов(Ини)
	// Заполняет дерево значений по справочнику
	Если Ини.Свойство("ЗапросСпискаКонтрагентов") Тогда
		Запрос = Новый Запрос(СтрЗаменить(Ини.ЗапросСпискаКонтрагентов.Значение,"'", ""));	
	Иначе
		ИмяСправочника = СокрЛП(Сред(Ини.Контрагенты.Значение, Найти(Ини.Контрагенты.Значение, ".")+1));
		ИмяРеквизитаИНН = СокрЛП(Сред(Ини.Контрагенты_ИНН.Значение, Найти(Ини.Контрагенты_ИНН.Значение, ".")+1));
		ИмяРеквизитаКПП = СокрЛП(Сред(Ини.Контрагенты_КПП.Значение, Найти(Ини.Контрагенты_КПП.Значение, ".")+1));
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ЗаписьСправочника.Ссылка,ЗаписьСправочника.Наименование,ЗаписьСправочника."+ИмяРеквизитаИНН+" КАК ИНН,ЗаписьСправочника."+ИмяРеквизитаКПП+" КАК КПП, Ложь КАК Отмечен ИЗ Справочник."+ИмяСправочника+" КАК ЗаписьСправочника ГДЕ (НЕ(ЗаписьСправочника.ЭтоГруппа) и НЕ ЗаписьСправочника.ПометкаУдаления)ИТОГИ ПО ЗаписьСправочника.Ссылка ТОЛЬКО ИЕРАРХИЯ");
	КонецЕсли;
	Контрагенты = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// определяем тип организации, очищаем текст сообщения и список файлов, заполняем список контрагентов
	Если МестныйКэш.Ини.Конфигурация.Свойство("Организации") Тогда
		ИмяСправочника = СокрЛП(Сред(МестныйКэш.Ини.Конфигурация.Организации.Значение, Найти(МестныйКэш.Ини.Конфигурация.Организации.Значение, ".")+1));
		ТипСправочника = "СправочникСсылка."+ИмяСправочника;
	Иначе
		ТипСправочника = "СправочникСсылка.Организации";
	КонецЕсли;
	ОписаниеТипа = Новый ОписаниеТипов(ТипСправочника);
	ЭлементыФормы.Организация.ОграничениеТипа = ОписаниеТипа;
	ЭлементыФормы.Организация.Значение = ОписаниеТипа.ПривестиЗначение(); 
	ЭлементыФормы.Организация.ВыбиратьТип = Ложь; //это на всякий случай 

	СписокФайлов.Очистить();
	ТекстСообщения = "";
	ТипПакета = "КоррИсх";
	Если ЗначениеЗаполнено(ДопПараметры) Тогда
		Если ДопПараметры.Свойство("СписокФайлов") Тогда
			ПослеВыбораФайлов(ДопПараметры.СписокФайлов, Неопределено);	
		КонецЕсли;
		Если ДопПараметры.Свойство("Организация") и ЗначениеЗаполнено(ДопПараметры.Организация) Тогда
			Организация = ДопПараметры.Организация;
		КонецЕсли;
		Если ДопПараметры.Свойство("ТипПакета") и ЗначениеЗаполнено(ДопПараметры.ТипПакета) Тогда
			ТипПакета = ДопПараметры.ТипПакета;
		КонецЕсли;
	КонецЕсли;
	сбисЗаполнитьСписокКонтрагентов(МестныйКэш.Ини.Конфигурация)
КонецПроцедуры
&НаКлиенте
Процедура ДобавитьВложение(Команда)
	// Процедура открывает диалог выбора файлов для отправки
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие); 
	ДиалогОткрытияФайла.МножественныйВыбор = Истина;
	ДиалогОткрытияФайла.Заголовок = "Выберите файлы";
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		МассивФайлов = ДиалогОткрытияФайла.ВыбранныеФайлы;
		ПослеВыбораФайлов(МассивФайлов, "");
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура ПослеВыбораФайлов(МассивФайлов, ДополнительныеПараметры) Экспорт
	// Процедура добавляет внешний файл в список файлов для отправки
	Если МассивФайлов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Для Каждого ИмяФайла Из МассивФайлов Цикл
		Файл = Новый Файл(ИмяФайла);
		НоваяСтр = СписокФайлов.Добавить();
		НоваяСтр.ПолноеИмяФайла = Файл.ПолноеИмя;
		НоваяСтр.ИмяФайла = Файл.Имя;
	КонецЦикла;
КонецПроцедуры
&НаКлиенте
Процедура ПометкаПриИзменении(Элемент)
	// Отмечает вложенные записи дерева значений при отметке группы
	ТекущиеДанные = ЭлементыФормы.Контрагенты.ТекущиеДанные;
	ПроставитьПометкиВниз(ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ПроставитьПометкиВниз(ТекущиеДанные)
	// Отмечает вложенные записи дерева значений при отметке группы (рекурсивно)
	Потомки = ТекущиеДанные.Строки;
	Значение = ТекущиеДанные.Отмечен;
	Для каждого Потомок из Потомки Цикл
		Потомок.Отмечен = Значение;
		ПроставитьПометкиВниз(Потомок);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсеДерево(Команда)
	// Отмечает все записи в дереве значений
	Потомки = Контрагенты.Строки;
	Для Каждого Потомок Из Потомки Цикл
		Потомок.Отмечен = ОтметитьВсе;
		ПроставитьПометкиВниз(Потомок);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВыбранныеСтроки(СписокОтмеченных, Потомки)
	// Получает выбранные строки дерева значений
	Для каждого Потомок из Потомки Цикл
		ПотомкиПотомка = Потомок.Строки;
		Если ПотомкиПотомка.Количество()=0 Тогда
			Если Потомок.Отмечен Тогда
				СписокОтмеченных.Добавить(Потомок.Ссылка);
			КонецЕсли;
		Иначе
			ПолучитьВыбранныеСтроки(СписокОтмеченных,ПотомкиПотомка);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ОткрытьЗаписьСправочника(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	// Открывает карточку записи справочника
	ОткрытьЗначение(ВыбраннаяСтрока.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	// Отправка корреспонденции по списку контрагентов
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Сообщить("Укажите организацию - отправителя");
		Возврат;
	КонецЕсли;
	Если НЕ (МестныйКэш.Ини.Конфигурация.Свойство("мСторона") и МестныйКэш.Ини.Конфигурация.мСторона.Свойство("НашаОрганизация") и МестныйКэш.Ини.Конфигурация.мСторона.Свойство("Контрагент")) Тогда
		Сообщить("В файле настроек конфигурации не указаны настройки для отправки корреспонденции");
		Возврат;	
	КонецЕсли;
	// получаем список отмеченных контрагентов
	СписокОтмеченных = Новый СписокЗначений;
	ПолучитьВыбранныеСтроки(СписокОтмеченных, Контрагенты.Строки);
	Если СписокОтмеченных.Количество() = 0 Тогда
		Сообщить("Отметьте получателей корреспонденции в списке контрагентов.");
		Возврат;	
	КонецЕсли;
	
	// кладем данные по организации и контрагентам в соответствие для последующего рассчета данных по инишке на сервере
	СоставПакета = Новый СписокЗначений;
	СоставПакета.Добавить(Организация);
	СоответствиеДанные = Новый Соответствие;
	СоответствиеДанные.Вставить(Организация, Новый Структура("ИмяИни,ДокументДанные","НашаОрганизация",МестныйКэш.Ини.Конфигурация.мСторона["НашаОрганизация"]));
	Для Каждого Элемент Из СписокОтмеченных Цикл
		СоставПакета.Добавить(Элемент.Значение);
		СоответствиеДанные.Вставить(Элемент.Значение, Новый Структура("ИмяИни,ДокументДанные","Контрагент",МестныйКэш.Ини.Конфигурация.мСторона["Контрагент"]));
	КонецЦикла;
	МестныйКэш.ОбщиеФункции.ПолучитьДанныеДокументов1С(СоответствиеДанные, Новый Структура("ТекущийПакет",Новый Структура), СоставПакета);
	// формируем пакеты
	МассивОшибок = Новый Массив;
	МассивПолучателей = Новый Массив;
	Для Каждого Элемент Из СоответствиеДанные Цикл
		Сторона = МестныйКэш.ОбщиеФункции.ПолучитьСторону(МестныйКэш,Элемент.Значение.ДокументДанные);
		Если Элемент.Ключ = Организация Тогда
			Если Сторона<>Неопределено Тогда
				НашаОрганизация = Сторона;
			Иначе
				Сообщить("Не заполнены данные организации");
				Возврат;
			КонецЕсли;
		Иначе
			Если Сторона<>Неопределено Тогда
				МассивПолучателей.Добавить(Сторона);
			Иначе
				МассивОшибок.Добавить(Новый Структура("ТекстОшибки, Ссылка1С", "Не заполнен ИНН", Элемент.Ключ));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Вложения = Новый Массив;
	Для Каждого Строка из СписокФайлов Цикл
		Вложения.Добавить(Новый Структура("ПолноеИмяФайла,ИмяФайла", Строка.ПолноеИмяФайла,Строка.ИмяФайла));
	КонецЦикла;
	МассивПакетов = Новый Массив;
	Для Каждого Контрагент Из МассивПолучателей Цикл
		МассивПакетов.Добавить(Новый Структура("НашаОрганизация,Контрагент,Тип,Вложение,Примечание", НашаОрганизация, Контрагент, ТипПакета, Вложения, ТекстСообщения));
	КонецЦикла;
	Для Каждого Ошибка Из МассивОшибок Цикл
		МассивПакетов.Добавить(Новый Структура("НашаОрганизация,Ошибка, Ссылка1С, Вложение",НашаОрганизация, Ошибка.ТекстОшибки, Ошибка.Ссылка1С, Новый Массив));
	КонецЦикла;
	
	МестныйКэш.СписокНоменклатуры.Очистить();
	ВремяНачала = ТекущаяДата();
	РезультатОтправки = Новый Структура("ТипыОшибок,Отправлено,НеОтправлено,НеСформировано,Ошибок,ДетализацияОшибок,ВсегоПакетов,ОшибкиДоОтправки,ДанныеПоСтатусам,ПорНомер,КоличествоСвободныхПотоков,ОтправленоСообщений,ПолученоОтветов,ВремяНачала,ВремяФормирования,ВремяОтправки,ДетализацияОтправки,ВремяЗаписиСтатусов,ВремяПолученияДанных,ВремяОжиданияОтвета", Новый СписокЗначений,0,0,0,0, Новый Соответствие,0,0,Новый Массив,0,МестныйКэш.КоличествоПотоковОтправки,0,0,ВремяНачала,0,0, Новый Соответствие,0,0,0);
	МестныйКэш.Вставить("РезультатОтправки",РезультатОтправки);
	
	// отправляем документы
	сбисПолучитьФорму("Документ_Шаблон").АктивироватьСертификатыОтправитьДокументы(МестныйКэш, МассивПакетов, Новый Массив, Новый СписокЗначений, Новый СписокЗначений, Истина);
КонецПроцедуры
