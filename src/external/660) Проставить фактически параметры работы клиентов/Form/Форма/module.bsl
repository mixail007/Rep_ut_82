
Процедура КнопкаВыполнитьНажатие(Кнопка)
	Если ЗначениеЗаполнено(Контрагент) тогда
		МенеджерЗаписи = РегистрыСведений.ФактическиеПараметрыРаботыКонтрагентов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Контрагент = Контрагент;
		МенеджерЗаписи.ПроцентПени = ПроцентПени;
		МенеджерЗаписи.СпособОтгрузки = СпособОтгрузки;
		МенеджерЗаписи.УсловияОплаты = УсловияОплаты;
		МенеджерЗаписи.ДопустимоеЧислоДнейЗадолженности = ДопустимоеЧислоДнейЗадолженности;
		МенеджерЗаписи.Записать();
	КонецЕсли;
КонецПроцедуры

Процедура КонтрагентПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Контрагент) тогда
		//Сначала поищем в регистре, если нет, то из контрагента
		МенеджерЗаписи = РегистрыСведений.ФактическиеПараметрыРаботыКонтрагентов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Контрагент = Контрагент;
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() тогда
			СпособОтгрузки = МенеджерЗаписи.способОтгрузки;
			УсловияОплаты = МенеджерЗаписи.УсловияОплаты;
			ДопустимоеЧислоДнейЗадолженности = МенеджерЗаписи.ДопустимоеЧислоДнейЗадолженности;
			ПроцентПени = МенеджерЗаписи.ПроцентПени;
		иначе
			СпособОтгрузки = Контрагент.способОтгрузки;
			УсловияОплаты = Контрагент.УсловияОплаты;
			ДопустимоеЧислоДнейЗадолженности = Контрагент.ДопустимоеЧислоДнейЗадолженности;
			ПроцентПени = Контрагент.ПроцентПени;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	// Вставить содержимое обработчика.
КонецПроцедуры

Процедура ОсновныеДействияФормыкнОтчетОСравнении(Кнопка)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФактическиеПараметрыРаботыКонтрагентов.Контрагент,
		|	ФактическиеПараметрыРаботыКонтрагентов.СпособОтгрузки,
		|	ФактическиеПараметрыРаботыКонтрагентов.УсловияОплаты,
		|	ФактическиеПараметрыРаботыКонтрагентов.ДопустимоеЧислоДнейЗадолженности,
		|	ФактическиеПараметрыРаботыКонтрагентов.ПроцентПени,
		|	ФактическиеПараметрыРаботыКонтрагентов.Контрагент.ОсновнойМенеджерКонтрагента КАК ОсновнойМенеджер
		|ПОМЕСТИТЬ втРегистр
		|ИЗ
		|	РегистрСведений.ФактическиеПараметрыРаботыКонтрагентов КАК ФактическиеПараметрыРаботыКонтрагентов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Контрагенты.Ссылка,
		|	Контрагенты.ДопустимоеЧислоДнейЗадолженности,
		|	Контрагенты.ПроцентПени,
		|	Контрагенты.способОтгрузки,
		|	Контрагенты.УсловияОплаты
		|ПОМЕСТИТЬ втКонтрагенты
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Покупатель
		|	И Контрагенты.Ссылка В
		|			(ВЫБРАТЬ
		|				втРегистр.Контрагент
		|			ИЗ
		|				втРегистр)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРегистр.Контрагент,
		|	втРегистр.СпособОтгрузки КАК СпособОтгрузкиФакт,
		|	втРегистр.УсловияОплаты КАК УсловияОплатыФакт,
		|	втРегистр.ДопустимоеЧислоДнейЗадолженности КАК ДопустимоеЧислоДнейЗадолженностиФакт,
		|	втРегистр.ПроцентПени КАК ПроцентПениФакт,
		|	втКонтрагенты.ДопустимоеЧислоДнейЗадолженности,
		|	втКонтрагенты.ПроцентПени,
		|	втКонтрагенты.способОтгрузки,
		|	втКонтрагенты.УсловияОплаты,
		|	втРегистр.ОсновнойМенеджер
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	втРегистр КАК втРегистр
		|		ЛЕВОЕ СОЕДИНЕНИЕ втКонтрагенты КАК втКонтрагенты
		|		ПО втРегистр.Контрагент = втКонтрагенты.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт.Контрагент КАК Контрагент,
		|	вт.СпособОтгрузкиФакт,
		|	вт.УсловияОплатыФакт,
		|	вт.ДопустимоеЧислоДнейЗадолженностиФакт,
		|	вт.ПроцентПениФакт,
		|	вт.ДопустимоеЧислоДнейЗадолженности,
		|	вт.ПроцентПени,
		|	вт.способОтгрузки,
		|	вт.УсловияОплаты,
		|	вт.ОсновнойМенеджер КАК ОсновнойМенеджер
		|ИЗ
		|	вт КАК вт
		|ГДЕ
		|	(вт.СпособОтгрузкиФакт <> вт.способОтгрузки
		|			ИЛИ вт.УсловияОплатыФакт <> вт.УсловияОплаты
		|			ИЛИ вт.ДопустимоеЧислоДнейЗадолженностиФакт <> вт.ДопустимоеЧислоДнейЗадолженности
		|			ИЛИ вт.ПроцентПениФакт <> вт.ПроцентПени)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОсновнойМенеджер,
		|	Контрагент
		|ИТОГИ ПО
		|	ОсновнойМенеджер
		|АВТОУПОРЯДОЧИВАНИЕ";

	Результат = Запрос.Выполнить();

	Макет = ПолучитьМакет("Макет");
	ТабДок = Новый ТабличныйДокумент;
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
	ОбластьМенеджер = Макет.ПолучитьОбласть("ОбластьМенеджер");
	ОбластьКонтрагент = Макет.ПолучитьОбласть("ОбластьКонтрагент");
	ВыборкаОсновнойМенеджер = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТабДок.Вывести(ОбластьШапка);
	ТабДок.НачатьАвтогруппировкуСтрок();
	Пока ВыборкаОсновнойМенеджер.Следующий() Цикл
		// Вставить обработку выборки ВыборкаОсновнойМенеджер
		ОбластьМенеджер.Параметры.Заполнить(ВыборкаОсновнойМенеджер);
		ТабДок.Вывести(ОбластьМенеджер,1);
		ВыборкаДетальныеЗаписи = ВыборкаОсновнойМенеджер.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			        ОбластьКонтрагент.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
					ТабДок.Вывести(ОбластьКонтрагент,2);
		КонецЦикла;
		
	КонецЦикла;
	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	ТабДок.ОтображатьЗаголовки = ложь;
	ТабДок.Показать();
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

КонецПроцедуры
