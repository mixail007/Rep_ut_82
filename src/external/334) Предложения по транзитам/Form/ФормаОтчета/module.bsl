
Процедура КнопкаСформироватьНажатие(Кнопка)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказПоставщикуСезонныйТовары.Номенклатура,
		|	СУММА(ЗаказПоставщикуСезонныйТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	Документ.ЗаказПоставщикуСезонный.Товары КАК ЗаказПоставщикуСезонныйТовары
		|ГДЕ
		|	ЗаказПоставщикуСезонныйТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказПоставщикуСезонныйТовары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказПокупателяСезонныйТовары.Номенклатура,
		|	СУММА(ЗаказПокупателяСезонныйТовары.Количество) КАК Количество,
		|	ЗаказПокупателяСезонныйТовары.Ссылка.Контрагент,
		|	МИНИМУМ(ЗаказПокупателяСезонныйТовары.Ссылка.Дата) КАК Дата
		|ПОМЕСТИТЬ ЗаказыСезонные
		|ИЗ
		|	Документ.ЗаказПокупателяСезонный.Товары КАК ЗаказПокупателяСезонныйТовары
		|ГДЕ
		|	ЗаказПокупателяСезонныйТовары.Ссылка.ПометкаУдаления = ЛОЖЬ
		|	И НЕ ЗаказПокупателяСезонныйТовары.Ссылка.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.Пустаяссылка)
		|	И КОНЕЦПЕРИОДА(ЗаказПокупателяСезонныйТовары.Ссылка.ДатаДействияПо, ДЕНЬ) >= &ДатаКон
		|	И НЕ ЗаказПокупателяСезонныйТовары.Ссылка.ДатаДействияПо = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|	И НЕ ЗаказПокупателяСезонныйТовары.Ссылка.Транзит
		|	И ЗаказПокупателяСезонныйТовары.Номенклатура В
		|			(ВЫБРАТЬ
		|				втТовары.Номенклатура
		|			ИЗ
		|				втТовары КАК втТовары)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказПокупателяСезонныйТовары.Номенклатура,
		|	ЗаказПокупателяСезонныйТовары.Ссылка.Контрагент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ЗначенияСвойствОбъектов.Объект КАК Справочник.Номенклатура) КАК Номенклатура,
		|	ЗначенияСвойствОбъектов.Значение КАК Завод
		|ПОМЕСТИТЬ втЗаводы
		|ИЗ
		|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|ГДЕ
		|	ЗначенияСвойствОбъектов.Свойство.Наименование = ""Завод""
		|	И ЗначенияСвойствОбъектов.Объект В
		|			(ВЫБРАТЬ
		|				втТовары.Номенклатура
		|			ИЗ
		|				втТовары КАК втТовары)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	А.Контрагент КАК Контрагент,
		|	втЗаводы.Завод КАК Завод,
		|	А.Номенклатура КАК Номенклатура,
		|	А.ПродажиКоличество КАК ПродажиКоличество,
		|	А.ЗаказыКоличество КАК ЗаказыКоличество,
		|	А.ЗаказыКоличество - А.ПродажиКоличество КАК НевыбраноКоличество,
		|	втТовары.Количество КАК КоличествоВЗаказе
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЕСТЬNULL(ПродажиОбороты.ДоговорКонтрагента.Владелец, ЗаказыСезонные.Контрагент) КАК Контрагент,
		|		ЕСТЬNULL(ПродажиОбороты.Номенклатура, ЗаказыСезонные.Номенклатура) КАК Номенклатура,
		|		СУММА(ЕСТЬNULL(ПродажиОбороты.КоличествоОборот, 0)) КАК ПродажиКоличество,
		|		МАКСИМУМ(ЗаказыСезонные.Количество) КАК ЗаказыКоличество
		|	ИЗ
		|		ЗаказыСезонные КАК ЗаказыСезонные
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продажи.Обороты(
		|					,
		|					&ДатаКон,
		|					,
		|					НЕ ЗаказПокупателя.Транзит
		|						И Номенклатура В
		|							(ВЫБРАТЬ
		|								втТовары.Номенклатура
		|							ИЗ
		|								втТовары КАК втТовары)) КАК ПродажиОбороты
		|			ПО (ПродажиОбороты.ДоговорКонтрагента.Владелец = ЗаказыСезонные.Контрагент)
		|				И (ПродажиОбороты.Номенклатура = ЗаказыСезонные.Номенклатура)
		|				И ЗаказыСезонные.Дата <= ПродажиОбороты.ДокументПродажи.Дата
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ЕСТЬNULL(ПродажиОбороты.ДоговорКонтрагента.Владелец, ЗаказыСезонные.Контрагент),
		|		ЕСТЬNULL(ПродажиОбороты.Номенклатура, ЗаказыСезонные.Номенклатура)) КАК А
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаводы КАК втЗаводы
		|		ПО А.Номенклатура = втЗаводы.Номенклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТовары КАК втТовары
		|		ПО А.Номенклатура = втТовары.Номенклатура
		|ГДЕ
		|	А.ЗаказыКоличество - А.ПродажиКоличество > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагент,
		|	Номенклатура
		|ИТОГИ
		|	СУММА(ПродажиКоличество),
		|	СУММА(ЗаказыКоличество),
		|	СУММА(НевыбраноКоличество),
		|	СУММА(КоличествоВЗаказе)
		|ПО
		|	ОБЩИЕ,
		|	Контрагент,
		|	Завод,
		|	Номенклатура
		|АВТОУПОРЯДОЧИВАНИЕ";

    	Запрос.УстановитьПараметр("Ссылка",ЗаказСезонный);
		Запрос.УстановитьПараметр("ДатаКон",ДатаДействияПо);

		
	Результат = Запрос.Выполнить();
    тз=Результат.Выгрузить();
	тз.Очистить();
	тз.Колонки.Добавить("ЗаказатьТранзитом", Новый ОписаниеТипов("Число"));

	ВыборкаОбщийИтог = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
    ВыборкаОбщийИтог.Следующий();
	ВыборкаКонтрагент = ВыборкаОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаКонтрагент.Следующий() Цикл

		ВыборкаЗавод = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Контрагент=ВыборкаКонтрагент.Контрагент;
		Пока ВыборкаЗавод.Следующий() Цикл
			Если   ВыборкаЗавод.НевыбраноКоличество-100 > КоличествоВКонтейнере тогда    /////
				Завод=ВыборкаЗавод.Завод;
				ВыборкаНоменклатура = ВыборкаЗавод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Коэфф=ВыборкаЗавод.НевыбраноКоличество/КоличествоВКонтейнере;
				Коэфф=?(Коэфф<1,1,Коэфф);
				Пока ВыборкаНоменклатура.Следующий() Цикл
					нстр=тз.Добавить();
					ЗаполнитьЗначенияСвойств(нстр,ВыборкаНоменклатура);
					нстр.Контрагент=Контрагент;
					нстр.Завод=Завод;
					ЗаказатьТранзитом = Цел(Цел(ВыборкаНоменклатура.НевыбраноКоличество/Коэфф)/4)*4;
					нстр.ЗаказатьТранзитом=ЗаказатьТранзитом;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	

	Макет = ПолучитьМакет("Макет1");
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	тз.Контрагент,
		|	тз.Завод,
		|	тз.Номенклатура,
		|	тз.НевыбраноКоличество,
		|	тз.КоличествоВЗаказе,
		|	тз.ЗаказатьТранзитом
		|ПОМЕСТИТЬ втТЗ
		|ИЗ
		|	&тз КАК тз
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЗ.Контрагент КАК Контрагент,
		|	втТЗ.Завод КАК Завод,
		|	втТЗ.Номенклатура КАК Номенклатура,
		|	втТЗ.НевыбраноКоличество КАК НевыбраноКоличество,
		|	втТЗ.КоличествоВЗаказе КАК КолвоВСЗ,
		|	втТЗ.ЗаказатьТранзитом КАК КолвоТранзит
		|ИЗ
		|	втТЗ КАК втТЗ
		|ИТОГИ
		|	СУММА(НевыбраноКоличество),
		|	СУММА(КолвоВСЗ),
		|	СУММА(КолвоТранзит)
		|ПО
		|	Контрагент,
		|	Завод,
		|	Номенклатура";

		Запрос.УстановитьПараметр("тз",тз);
		Результат = Запрос.Выполнить();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьПодвалТаблицы = Макет.ПолучитьОбласть("ПодвалТаблицы");
	ОбластьКонтрагент = Макет.ПолучитьОбласть("Контрагент");
	ОбластьЗавод = Макет.ПолучитьОбласть("Завод");
	ОбластьНоменклатура = Макет.ПолучитьОбласть("Номенклатура");
    ТабДок=новый ТабличныйДокумент;
	ТабДок.Очистить();
	ОбластьЗаголовок.Параметры.СЗПост=ЗаказСезонный;
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	ТабДок.НачатьАвтогруппировкуСтрок();

	ВыборкаКонтрагент = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаКонтрагент.Следующий() Цикл
		ОбластьКонтрагент.Параметры.Заполнить(ВыборкаКонтрагент);
		ТабДок.Вывести(ОбластьКонтрагент, ВыборкаКонтрагент.Уровень());

		ВыборкаЗавод = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаЗавод.Следующий() Цикл
			ОбластьЗавод.Параметры.Заполнить(ВыборкаЗавод);
	        ТабДок.Вывести(ОбластьЗавод, ВыборкаЗавод.Уровень());
			Если ВыборкаЗавод.КолвоТранзит < КоличествоВКонтейнере-50 тогда
				ТабДок.Область(ТабДок.ВысотаТаблицы,5,ТабДок.ВысотаТаблицы,5).ЦветТекста=WebЦвета.Красный;
			КонецЕсли;
			ВыборкаНоменклатура = ВыборкаЗавод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

			Пока ВыборкаНоменклатура.Следующий() Цикл
				ОбластьНоменклатура.Параметры.Заполнить(ВыборкаНоменклатура);
				ТабДок.Вывести(ОбластьНоменклатура, ВыборкаНоменклатура.Уровень());
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	ТабДок.Вывести(ОбластьПодвалТаблицы);
	ТабДок.Вывести(ОбластьПодвал);

	ТабДок.Показать();
КонецПроцедуры

Процедура ПриОткрытии()
	КоличествоВКонтейнере=1600;
КонецПроцедуры
