
Процедура ДействияФормыСформировать(Кнопка)

	ЭлементыФормы.ДГ.Очистить();
	ЭлементыФормы.ДГ.Обновление = Ложь;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ЦеныПовышения.Номенклатура.Код КАК НоменклатураКод,
	               |	ПРЕДСТАВЛЕНИЕ(ЦеныПовышения.Номенклатура) КАК НоменклатураПредставление,
	               |	ЦеныПовышения.Номенклатура.ВидТовара КАК НоменклатураВидТовара,
	               |	ЦеныПовышения.Номенклатура КАК Номенклатура,
	               |	ЦеныПовышения.ВариантКод,
	               |	ЦеныПовышения.Период КАК Период0,
	               |	МАКСИМУМ(ЕСТЬNULL(ЦеныПовышения1.Период, &Завтра)) КАК Период1,
	               |	МИНИМУМ(РАЗНОСТЬДАТ(ЦеныПовышения.Период, ЕСТЬNULL(ЦеныПовышения1.Период, &Завтра), ДЕНЬ)) КАК Продолжительность,
	               |	ЦеныПовышения.Цена КАК Цена
	               |ИЗ
	               |	РегистрСведений.ЦеныПовышения КАК ЦеныПовышения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |			ЦеныПовышенияВыкл.Период КАК Период,
	               |			ЦеныПовышенияВыкл.Номенклатура КАК Номенклатура,
	               |			ЦеныПовышенияВыкл.ВариантКод КАК ВариантКод,
	               |			ЦеныПовышенияВыкл.Цена КАК Цена
	               |		ИЗ
	               |			РегистрСведений.ЦеныПовышения КАК ЦеныПовышенияВыкл
	               |		ГДЕ
	               |			ЦеныПовышенияВыкл.Период МЕЖДУ &ДатаНач И &ДатаКон
				   |//			И ЦеныПовышенияВыкл.Номенклатура = &Номенклатура
				   |			) КАК ЦеныПовышения1
	               |		ПО ЦеныПовышения.Номенклатура = ЦеныПовышения1.Номенклатура
	               |			И ЦеныПовышения.ВариантКод = ЦеныПовышения1.ВариантКод
	               |			И ЦеныПовышения.Период < ЦеныПовышения1.Период
	               |ГДЕ
	               |	ЦеныПовышения.Период МЕЖДУ &ДатаНач И &ДатаКон
	               |	И ЦеныПовышения.Цена >= 0
	               |//	И ЦеныПовышения.Номенклатура = &Номенклатура
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЦеныПовышения.Номенклатура.Код,
	               |	ЦеныПовышения.Номенклатура.ВидТовара,
	               |	ЦеныПовышения.Номенклатура,
	               |	ЦеныПовышения.ВариантКод,
	               |	ЦеныПовышения.Период,
	               |	ЦеныПовышения.Цена
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НоменклатураВидТовара,
	               |	НоменклатураКод,
	               |	Период0,
	               |	Период1
	               |АВТОУПОРЯДОЧИВАНИЕ";
	Запрос.УстановитьПараметр("ДатаНач", НачПериода);
	Запрос.УстановитьПараметр("ДатаКон", КонПериода);
	Запрос.УстановитьПараметр("Завтра", КонецДня(ТекущаяДата())+1 );
	Если ФлНоменклатураОтбор тогда
		Запрос.Текст = стрЗаменить(Запрос.Текст,"//","");
		Запрос.УстановитьПараметр("Номенклатура", НоменклатураОтбор);
	КонецЕсли;	
	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		Точка = ЭлементыФормы.ДГ.УстановитьТочку(Выборка.НоменклатураКод+" - " + Выборка.НоменклатураПредставление 
												//+ " : " + формат(Выборка.Цена,"ЧДЦ=2")+"р." 
									            );
												
		Точка.Расшифровка = Выборка.Номенклатура;
		
		Серия = ЭлементыФормы.ДГ.УстановитьСерию(Выборка.ВариантКод);
		
		Значение = ЭлементыФормы.ДГ.ПолучитьЗначение(Точка, Серия);
	 	Интервал = Значение.Добавить();
		Интервал.Начало = Выборка.Период0;
		Интервал.Конец  = Выборка.Период1;
		Если Год(Выборка.Период0)=Год(Выборка.Период1) тогда
			стрФормат0 = "ДФ='dd MMM'";
		Иначе	
			стрФормат0 = "ДЛФ=DD";
		КонецЕсли;	
		Интервал.Расшифровка = Выборка.НоменклатураКод +" c "+формат(Выборка.Период0,стрФормат0)+" до "+формат(Выборка.Период0 + Выборка.Продолжительность*86400,"ДЛФ=DD")+" = "
										+строка(Выборка.Продолжительность)+" дн. Цена: "+формат(Выборка.Цена,"ЧДЦ=2; ЧН=-")+" р.";
	КонецЦикла;

	ЭлементыФормы.ДГ.Обновление = Истина;

КонецПроцедуры

Процедура ВыбПериодНажатие(Элемент)
	НастройкаПериода = Новый НастройкаПериода;
	НастройкаПериода.РедактироватьКакИнтервал = Истина;
	НастройкаПериода.РедактироватьКакПериод = Истина;
	НастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;
	НастройкаПериода.УстановитьПериод(НачПериода, ?(КонПериода='0001-01-01', КонПериода, КонецДня(КонПериода)));
	Если НастройкаПериода.Редактировать() Тогда
		НачПериода = НастройкаПериода.ПолучитьДатуНачала();
		КонПериода = НастройкаПериода.ПолучитьДатуОкончания();
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	НачПериода = ДобавитьМесяц( НачалоМесяца( ТекущаяДата() ), -1);
	КонПериода = ТекущаяДата();
КонецПроцедуры

Процедура НоменклатураОтборОчистка(Элемент, СтандартнаяОбработка)
	ФлНоменклатураОтбор = ЛОЖЬ;
КонецПроцедуры

Процедура НоменклатураОтборПриИзменении(Элемент)
	ФлНоменклатураОтбор = НЕ НоменклатураОтбор.Пустая();
КонецПроцедуры


