
Процедура УстановитьНастройку(НаименованиеНастройки, Значение) Экспорт
	
	Если ПустаяСтрока(НаименованиеНастройки) Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьПодсистемуКонтурDiadoc Тогда
		
		Запись= РегистрыСведений.КонтурDiadoc_ДополнительныеРеквизиты.СоздатьМенеджерЗаписи();
		Запись.Свойство= НаименованиеНастройки;
		Запись.Объект= 	 ПолучитьМодульПрог("Модуль_1САдаптер").ТекущийПользователь();
		
	Иначе
		
		НастройкаСсылка= ПланыВидовХарактеристик.НастройкиПользователей.НайтиПоНаименованию(НаименованиеНастройки);
		
		Если НЕ ЗначениеЗаполнено(НастройкаСсылка) Тогда 
			ВызватьИсключение "Запись настройки пользователя:""" + НаименованиеНастройки + """ невозможна. Настройка не создана";
		КонецЕсли; 
		
		Запись= РегистрыСведений.НастройкиПользователей.СоздатьМенеджерЗаписи();
		Запись.Пользователь= ПолучитьМодульПрог("Модуль_1САдаптер").ТекущийПользователь();
		Запись.Настройка= 	 НастройкаСсылка;
		
	КонецЕсли;
	
	Если ИспользоватьПодсистемуКонтурDiadoc Тогда
		Запись[ПолучитьМодульПрог("Модуль_1САдаптер").СтруктураНастроекПользователя()[НаименованиеНастройки].ПолеЗначения]= Значение;
	Иначе
		Запись.Значение= Значение;
	КонецЕсли;
	
	Запись.Записать();
		
КонецПроцедуры

Функция ПолучитьНастройку(Настройка, ЗначениеПоУмолчанию) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Настройка) Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Если ИмяФормыПрикладногорешенияДляИнтеграцииДиадок() = "Модуль_ИнтеграцияАсторТД" Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Если НЕ ИспользоватьПодсистемуКонтурDiadoc Тогда
		
		Запрос= Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Значение
		|ИЗ
		|	РегистрСведений.НастройкиПользователей
		|ГДЕ
		|	Пользователь = &ТекущийПользователь И Настройка = &Настройка");
		
		Если ТипЗнч(Настройка) = Тип("Строка") Тогда
			Запрос.Текст= СтрЗаменить(Запрос.Текст, " Настройка", " Настройка.Наименование");
		КонецЕсли;
	
	Иначе
		
		Запрос= Новый Запрос(
		"ВЫБРАТЬ
		|	"+ПолучитьМодульПрог("Модуль_1САдаптер").СтруктураНастроекПользователя()[Настройка].ПолеЗначения+" КАК Значение
		|ИЗ
		|	РегистрСведений.КонтурDiadoc_ДополнительныеРеквизиты
		|ГДЕ
		|	Свойство = &Настройка И Объект = &ТекущийПользователь");
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТекущийПользователь", ПолучитьМодульПрог("Модуль_1САдаптер").ТекущийПользователь());
	Запрос.УстановитьПараметр("Настройка"		   , Настройка);
	
	РезультатЗапроса= Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка= РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Возврат Выборка.Значение;
		
	Иначе
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли; 
	
КонецФункции


Функция ПолучитьНастройку1С(Настройка) Экспорт
	
	Если ИмяФормыПрикладногорешенияДляИнтеграцииДиадок() = "Модуль_ИнтеграцияАсторТД" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос= Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Значение
	|ИЗ
	|	РегистрСведений.НастройкиПользователей
	|ГДЕ
	|	Пользователь = &ТекущийПользователь И Настройка = &Настройка");
	
	Если ТипЗнч(Настройка) = Тип("Строка") Тогда
		Запрос.Текст= СтрЗаменить(Запрос.Текст, " Настройка", " Настройка.Наименование");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТекущийПользователь", ПолучитьМодульПрог("Модуль_1САдаптер").ТекущийПользователь());
	Запрос.УстановитьПараметр("Настройка"		   , Настройка);
	
	РезультатЗапроса= Запрос.Выполнить();
	Запрос= Неопределено;
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка= РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Возврат Выборка.Значение;
		
	КонецЕсли; 
	
КонецФункции

Функция ПолучитьНастройку1С_СкладПоУмолчанию() Экспорт
	
	Возврат ПолучитьНастройку1С("Основной склад");
	
КонецФункции


Функция ПроверитьНаличиеНастроекПользователя(НаименованияНастроекПользователя) Экспорт
	
	НенайденныеНастройки = Новый Массив;
	Для каждого НаименованиеНастройки Из НаименованияНастроекПользователя Цикл
		Если НЕ ЗначениеЗаполнено(ПланыВидовХарактеристик.НастройкиПользователей.НайтиПоНаименованию(НаименованиеНастройки)) Тогда
			НенайденныеНастройки.Добавить(НаименованиеНастройки);
		КонецЕсли;
	КонецЦикла;
	
	Возврат НенайденныеНастройки;
	
КонецФункции

Функция СоздатьНастройкиПользователя(СтруктураНастроекПользователя) Экспорт
	
	СозданныеНастройки = Новый Массив;
	
	Для Каждого ТекущаяНастройка Из СтруктураНастроекПользователя Цикл
		
		НаименованиеНастройки = ТекущаяНастройка.Ключ;
		ТипЗначенияНастройки  = ТекущаяНастройка.Значение.ТипЗначения;
		
		НастройкаСсылка = ПланыВидовХарактеристик.НастройкиПользователей.НайтиПоНаименованию(НаименованиеНастройки);
		
		Если НЕ ЗначениеЗаполнено(НастройкаСсылка) Тогда
			
			НастройкаОбъект = ПланыВидовХарактеристик.НастройкиПользователей.СоздатьЭлемент();
			НастройкаОбъект.Родитель = ПолучитьИЛИСоздатьГруппуНастроекПользователя_Диадок();
			НастройкаОбъект.Наименование = НаименованиеНастройки;
			НастройкаОбъект.ТипЗначения = ТипЗначенияНастройки;
			НастройкаОбъект.Записать();
			
			СозданныеНастройки.Добавить(НаименованиеНастройки);
			
		ИначеЕсли НастройкаСсылка.ТипЗначения <> ТипЗначенияНастройки Тогда
			
			НастройкаОбъект = НастройкаСсылка.ПолучитьОбъект();
			НастройкаОбъект.ТипЗначения = ТипЗначенияНастройки;
			НастройкаОбъект.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СозданныеНастройки;
	
КонецФункции

Функция ПолучитьНеобходимыеДляРаботыТипы() Экспорт
	
	НеобходимыеДляРаботыТипы = Новый Массив;
	НеобходимыеДляРаботыТипы.Добавить(Метаданные.ПланыВидовХарактеристик.НастройкиПользователей);
	
	Возврат НеобходимыеДляРаботыТипы;
	
КонецФункции


Функция ПолучитьИЛИСоздатьГруппуНастроекПользователя_Диадок()
	
	ГруппаНастроекСсылка = планыВидовХарактеристик.НастройкиПользователей.НайтиПоНаименованию("Параметры подсистемы ДИАДОК");
	
	Если ЗначениеЗаполнено(ГруппаНастроекСсылка) Тогда 
		Возврат ГруппаНастроекСсылка;
	КонецЕсли;
	
	ГруппаНастроекОбъект = ПланыВидовХарактеристик.НастройкиПользователей.СоздатьГруппу();
	ГруппаНастроекОбъект.Наименование =  "Параметры подсистемы ДИАДОК";
	ГруппаНастроекОбъект.УстановитьНовыйКод();
	ГруппаНастроекОбъект.Записать();
	
	Возврат ГруппаНастроекОбъект.Ссылка;
	
КонецФункции

Функция ПроверитьПВХНастроекНаКвалификаторСтроки() Экспорт
	
	Если Метаданные.ПланыВидовХарактеристик.НастройкиПользователей.Тип.КвалификаторыСтроки.Длина < 50 Тогда
		Предупреждение("В конфигурации плане видов характеристик ""Настройки пользователей"" 
		|тип значения характеристик ""Строка"" имеет недостаточную длинну
		|необходимо минимум 50 символов", 60);
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции


