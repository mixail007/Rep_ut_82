Функция ПолучитьДанныеДоверенности() Экспорт
	
	ДанныеДоверенности=	ПустаяСтруктураДоверенности();
	
	ДанныеДоверенности.Дата=  ОдинСАдаптер_НастройкиТекущегоПользователя_ПолучитьНастройку("ДиадокДатаДоверенность");
	ДанныеДоверенности.Номер= ОдинСАдаптер_НастройкиТекущегоПользователя_ПолучитьНастройку("ДиадокНомерДоверенность");
	
	ДанныеДоверенности.ОрганизацияДоверенностьВыдал= ОдинСАдаптер_НастройкиТекущегоПользователя_ПолучитьНастройку("ДиадокОрганизацияДоверенностьВыдал");
	ДанныеДоверенности.ДолжностьДоверенностьВыдал= 	 ОдинСАдаптер_НастройкиТекущегоПользователя_ПолучитьНастройку("ДиадокДолжностьДоверенностьВыдал");
	ДанныеДоверенности.ФамилияДоверенностьВыдал= 	 ОдинСАдаптер_НастройкиТекущегоПользователя_ПолучитьНастройку("ДиадокФамилияДоверенностьВыдал");
	ДанныеДоверенности.ИмяДоверенностьВыдал= 		 ОдинСАдаптер_НастройкиТекущегоПользователя_ПолучитьНастройку("ДиадокИмяДоверенностьВыдал");
	ДанныеДоверенности.ОтчествоДоверенностьВыдал= 	 ОдинСАдаптер_НастройкиТекущегоПользователя_ПолучитьНастройку("ДиадокОтчествоДоверенностьВыдал");
	ДанныеДоверенности.ДополнительныеСведенияВыдал=  ОдинСАдаптер_НастройкиТекущегоПользователя_ПолучитьНастройку("ДиадокДополнительныеСведенияВыдал");

	Возврат ДанныеДоверенности;
	
КонецФункции

Функция ПустаяСтруктураДоверенности() Экспорт
	
	ПустаяСтруктураДоверенности=	Новый Структура;
	
	ПустаяСтруктураДоверенности.Вставить("Дата");
	ПустаяСтруктураДоверенности.Вставить("Номер");
	
	ПустаяСтруктураДоверенности.Вставить("ОрганизацияДоверенностьВыдал");
	ПустаяСтруктураДоверенности.Вставить("ДолжностьДоверенностьВыдал");
	ПустаяСтруктураДоверенности.Вставить("ФамилияДоверенностьВыдал");
	ПустаяСтруктураДоверенности.Вставить("ИмяДоверенностьВыдал");
	ПустаяСтруктураДоверенности.Вставить("ОтчествоДоверенностьВыдал");
	ПустаяСтруктураДоверенности.Вставить("ДополнительныеСведенияВыдал");

	Возврат ПустаяСтруктураДоверенности;
	
КонецФункции

Функция ПолучитьПредставлениеДоверенности() Экспорт
	
	ДанныеДоверенности=	ПолучитьДанныеДоверенности();
	
	ПредставлениеДоверенности=	"";
	
	Если ЗначениеЗаполнено(ДанныеДоверенности.Номер) И ЗначениеЗаполнено(ДанныеДоверенности.Дата) Тогда
		ПредставлениеДоверенности=	"№" + ДанныеДоверенности.Номер + " от " + Формат(ДанныеДоверенности.Дата, "ДФ=dd.MM.yyyy");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеДоверенности.ОрганизацияДоверенностьВыдал) Тогда
		ПредставлениеДоверенности=	ПредставлениеДоверенности + ", " + ДанныеДоверенности.ОрганизацияДоверенностьВыдал;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеДоверенности.ДолжностьДоверенностьВыдал) Тогда
		ПредставлениеДоверенности=	ПредставлениеДоверенности + ?(ЗначениеЗаполнено(ПредставлениеДоверенности), ", ", "") + ДанныеДоверенности.ДолжностьДоверенностьВыдал;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеДоверенности.ФамилияДоверенностьВыдал) 
		ИЛИ ЗначениеЗаполнено(ДанныеДоверенности.ИмяДоверенностьВыдал) 
		ИЛИ ЗначениеЗаполнено(ДанныеДоверенности.ОтчествоДоверенностьВыдал) Тогда
		
		ПредставлениеДоверенности=	ПредставлениеДоверенности + ?(ЗначениеЗаполнено(ПредставлениеДоверенности), ", ", "") 
		+ ДанныеДоверенности.ФамилияДоверенностьВыдал  
		+ " " + ДанныеДоверенности.ИмяДоверенностьВыдал 
		+ " " + ДанныеДоверенности.ОтчествоДоверенностьВыдал;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеДоверенности.ДополнительныеСведенияВыдал) Тогда
		ПредставлениеДоверенности=	ПредставлениеДоверенности + ?(ЗначениеЗаполнено(ПредставлениеДоверенности), ", ", "") + ДанныеДоверенности.ДополнительныеСведенияВыдал;
	КонецЕсли;
	
	Возврат ПредставлениеДоверенности;
	
КонецФункции

Функция ПолучитьСтруктуруПодписи(ЭДОбъект, DocumentType, НеПоказыватьФормуТитула= Ложь, КоличествоДокументовНаПодпись= 1, ДатаПолученияПоДатеДокумента= Ложь) Экспорт
	
	Если DocumentType = "UniversalTransferDocument" Тогда 
		
		ФормаТитула= ПолучитьФорму("ФормаТитулПокупателяУПД");
		
		ФормаТитула.Организация= ПолучитьМодульПрог("Модуль_Данные1С_СвязиОбъектов").BoxIDDepartment_2_Организация(ЭДОбъект.OrganizationID, ЭДОбъект.Department);
		
		ФормаТитула.BoxID= 			  ЭДОбъект.OrganizationId;
		ФормаТитула.DocumentDate= 	  ЭДОбъект.DocumentDate;
		
		ФормаТитула.ПредставлениеДокумента=		   ПолучитьМодульПрог("Модуль_РаботаССерверомДиадок").ПредставлениеЭД(ЭДОбъект);
		ФормаТитула.НеПоказыватьФормуТитула= 	   НеПоказыватьФормуТитула;
		ФормаТитула.КоличествоДокументовНаПодпись= КоличествоДокументовНаПодпись;
		
		Если ЭдОбъект.AttachmentVersion = "UniversalCorrectionDocument" Тогда 
			ФормаТитула.ДляКорректировки = Истина;
		Иначе
			ФормаТитула.ДляКорректировки = Ложь;
		КонецЕсли;	
		
		ФормаТитула.ОткрытьМодально();
		
		ДатаПолученияПоДатеДокумента= ФормаТитула.ДатаПолученияПоДатеДокумента;
		
		Если ФормаТитула.ТитулПокупателя <> Неопределено Тогда
			Возврат ФормаТитула.ТитулПокупателя;
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли DocumentType = "XmlTorg12" Тогда 
		ФормаПодписи = ПолучитьФорму("ФормаТитулПокупателя");
	Иначе 
		ФормаПодписи = ПолучитьФорму("ФормаТитулПокупателяАкт");
	КонецЕсли;
	
	ФИОПодписанта= ПолучитьМодульПрог("Модуль_РаботаССерверомДиадок").ПолучитьФИОПодписанта(ЭДОбъект.OrganizationId);
	
	ОрганизацияЭДО_Представление= ПолучитьМодульПрог("Модуль_РаботаССерверомДиадок").ДанныеСертифкатаАвторизации(ЭДОбъект.OrganizationId).OrganizationName;
	Если ОрганизацияЭДО_Представление = Неопределено Тогда
		ОрганизацияЭДО_Представление= ПолучитьМодульПрог("Модуль_РаботаССерверомДиадок").Organization(ЭДОбъект.OrganizationId).Name;
	КонецЕсли;
	
	ФормаПодписи.ПредставлениеДокумента=		ПолучитьМодульПрог("Модуль_РаботаССерверомДиадок").ПредставлениеЭД(ЭДОбъект);
	ФормаПодписи.ПредставлениеПодписи=			ОрганизацияЭДО_Представление + ", " + ФИОПодписанта;
	ФормаПодписи.ОрганизацияЭДО=				ОрганизацияЭДО_Представление;
	ФормаПодписи.ИНН=							ЭДОбъект.Organization.INN;
	ФормаПодписи.ФИО=							ФИОПодписанта;
	ФормаПодписи.Организация=					ПолучитьМодульПрог("Модуль_Данные1С_СвязиОбъектов").BoxIDDepartment_2_Организация(ЭДОбъект.OrganizationID, ЭДОбъект.Department);
	ФормаПодписи.НеПоказыватьФормуТитула=		НеПоказыватьФормуТитула;
	ФормаПодписи.КоличествоДокументовНаПодпись= КоличествоДокументовНаПодпись;
	
	ФормаПодписи.ОткрытьМодально();
	
	Если ФормаПодписи.СтруктураПодписи <> Неопределено Тогда
		Возврат ФормаПодписи.СтруктураПодписи;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции
