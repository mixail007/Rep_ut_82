Процедура УдалениеДокументовКомплекта(DocumentПар) Экспорт
	Document = DocumentПар;
	BoxId = Document.OrganizationId;
	DocumentId = Document.DocumentId;
	Направление = Document.Direction;
	
	ИнициализироватьФорму();
	
	Открыть();
КонецПроцедуры

Функция ИнициализироватьФорму()
	ВклВсе = Ложь;
	ДеревоДокументов.Очистить();
	Модуль_РаботаССерверомДиадок.ПолучитьДеревоСвязейДокументов(Document, ДеревоДокументов, Истина);

	Если ДеревоДокументов.Количество() > 0 Тогда
		Продавец = Модуль_РаботаССерверомДиадок.ПредставлениеПродавца(ДеревоДокументов[0].ЭДОбъект);
		Покупатель = Модуль_РаботаССерверомДиадок.ПредставлениеПокупателя(ДеревоДокументов[0].ЭДОбъект);
		
		ПерваяСтрока = ДеревоДокументов[0];
		ОформлениеСтроки = ЭлементыФормы.ДеревоДокументов.ОформлениеСтроки(ПерваяСтрока);
		ПолучитьМодульПрог("Модуль_Логика_СверкаДанных").ОформитьСтрокуДанныхЭД(ОформлениеСтроки, Document);
		РасшифровкаОшибок = ПерваяСтрока.ТекстОшибки;
	Иначе
		РасшифровкаОшибок = "";
	КонецЕсли;
	УстановитьВклПоDocumentID(ДеревоДокументов,DocumentID);	
КонецФункции

Процедура ДеревоДокументовПередНачаломИзменения(Элемент, Отказ)
	Если Элемент.ТекущаяКолонка.Имя = "Вкл" Тогда
		Возврат;
	КонецЕсли;
	
	КоманднаяПанель1ОткрытьКарточкуДокумента("");
	Отказ = Истина;
КонецПроцедуры

Процедура КоманднаяПанель1ОткрытьКарточкуДокумента(Кнопка)
	текущаяСтрока = ЭлементыФормы.ДеревоДокументов.ТекущиеДанные;
	Если  текущаяСтрока = Неопределено Тогда
		Предупреждение("Выберите документ.",, НаименованиеСистемы);
		Возврат
	КонецЕсли;
	ПолучитьМодульПрог("Модуль_Логика_ПоведениеФорм").ОткрытьКарточкуДокументаДД(текущаяСтрока.ЭДОбъект, ЭтаФорма);
КонецПроцедуры

Процедура ДеревоДокументовПриПолученииДанных(Элемент, ОформленияСтрок)
	ПолучитьМодульПрог("Модуль_Логика_СверкаДанных").ОформитьСписокЭД(ОформленияСтрок);
	Для Каждого стр Из ОформленияСтрок цикл 
		текДанные = стр.ДанныеСтроки;
		Если текДанные.DocumentId = DocumentId Тогда
			стр.Шрифт = Новый Шрифт(стр.Шрифт,,, Истина);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ДеревоДокументовПриАктивизацииСтроки(Элемент)
	ТекщиеДанные = Элемент.ТекущиеДанные;
	Если ТекщиеДанные <>Неопределено Тогда
		РасшифровкаОшибок = ТекщиеДанные.ТекстОшибки;
	КонецЕсли;	
КонецПроцедуры

Процедура КоманднаяПанель1Обновить(Кнопка)
	ДеревоДокументов.Очистить();
	ИнициализироватьФорму();
КонецПроцедуры

Процедура ДеревоДокументовПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

Процедура ОбновитьОбъектЭДО(КоллекцияСтрок,МассивОформленийСтрок, ЭДОбъект)
	Для каждого строка из КоллекцияСтрок цикл 
		Если (строка.BoxID =  ЭДОБъект.OrganizationId) и (строка.DocumentID =  ЭДОБъект.DocumentID) Тогда
			строка.ЭДОбъект = ЭДОбъект;
			МассивОформленийСтрок.Добавить(Элементыформы.ДеревоДокументов.ОформлениеСтроки(строка));
			Модуль_РаботаССерверомДиадок.ЗаполнитьПараметрыДокументаВДереве(строка,ЭДОБъект);
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры	

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если (ИмяСобытия = "ОбновитьСтроку") или ( ИмяСобытия = "ИзменениеДокумента1С") Тогда 
		// Параметр - ЭДОбъект
		МассивОформленийСтрок = Новый Массив();
		//подменим объект ЭД
		ОбновитьОбъектЭДО(ДеревоДокументов, МассивОформленийСтрок, Параметр);
		ДеревоДокументовПриПолученииДанных("", МассивОформленийСтрок);
		Если ЗначениеЗаполнено(Элементыформы.ДеревоДокументов.ТекущиеДанные) Тогда 
			РасшифровкаОшибок = Элементыформы.ДеревоДокументов.ТекущиеДанные.ТекстОшибки;
		КонецЕсли;	
	ИначеЕсли ИмяСобытия = "УдалениеДокументов" Тогда
		Если ЭтаФорма.Открыта() Тогда
			ЭтаФорма.Закрыть();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура УдалитьНажатие(Элемент)
	ВыборкаСтрок = ДеревоДокументов.НайтиСтроки(Новый Структура("Вкл", Истина));
	Если ВыборкаСтрок.Количество()=0 Тогда
		Предупреждение("Выберите хотя бы один документ для удаления.",, НаименованиеСистемы);
		Возврат;
	КонецЕсли;	
	
	КолВоДоков100 = Число(Прав(Строка(ВыборкаСтрок.Количество()), 2));
	КолВоДоков10 = Число(Прав(Строка(КолВоДоков100),1));
	Если (КолВоДоков100>=5) и (КолВоДоков100<=20) Тогда
		КолВоДоков = Строка(КолВоДоков100)+ " документов";
	Иначе
		Если КолВоДоков10 = 1 Тогда
			КолВоДоков = Строка(КолВоДоков10)+ " документ";
		ИначеЕсли КолВоДоков10>=2 и КолВоДоков10<=4 Тогда
			КолВоДоков = Строка(КолВоДоков10)+ " документа";
		Иначе
			КолВоДоков = Строка(КолВоДоков10)+ " документов";
		КонецЕсли;
	КонецЕсли;
	  
	Если  вопрос("Вы действительно хотите удалить "+КолВоДоков+"?", режимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет) = кодВозвратаДиалога.Нет Тогда 
		Возврат
	КонецЕсли;
	
	МассивДокументов = Новый Массив;
	
	Для Каждого Стр из ДеревоДокументов Цикл
		Если Стр.Вкл Тогда	
			
			МассивДокументов.Добавить(Стр.ЭДОбъект);
			ПолучитьМодульПрог("Модуль_Данные1С_СвязиОбъектов").Установить_DocumentID_Для_Документ(Стр.ПервичныйДокумент, , );
			
			Попытка
				Стр.ЭДОбъект.Delete();
			Исключение
				
				ОписаниеОшибки=	ОписаниеОшибки();
				ТекстОшибки=	ОписаниеОшибки;
				Если Найти(ТекстОшибки, "is already delete") Тогда
					ТекстОшибки=	"Документ " + ПолучитьФорму("Модуль_РаботаССерверомДиадок").ПредставлениеЭД(Стр.ЭДОбъект) + " уже был удален.";
				Иначе
					ТекстОшибки=	"Ошибка удаления документа";
				КонецЕсли;
				
				ФормаСообщенияОбОшибке= Получитьформу("Модуль_СообщенияДляПользователей_Форма_ВыводОшибки");
				ФормаСообщенияОбОшибке.ПоказатьОшибку("Ошибка удаления", ТекстОшибки, ОписаниеОшибки);
				
			КонецПопытки;
			
		КонецЕсли;
	КонецЦикла;
	
	Оповестить("УдалениеДокументов",МассивДокументов, ЭтаФорма);
	
КонецПроцедуры

Процедура ВклВсеПриИзменении(Элемент)
	Для каждого стр из ДеревоДокументов цикл 
		стр.вкл = ВклВсе;
	КонецЦикла;	
КонецПроцедуры

Функция ОпределитьВключеныЛиВсеФлажки(Дерево)
	Для каждого строка Из Дерево Цикл
		Если Не строка.вкл Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

Процедура ДеревоДокументовПриИзмененииФлажка(Элемент, Колонка)
	ВклВсе = ОпределитьВключеныЛиВсеФлажки(ДеревоДокументов);
КонецПроцедуры

Процедура УстановитьВклПоDocumentID(ДеревоДокументов,DocumentID)	
	Для Каждого стр Из ДеревоДокументов цикл 
		Если стр.DocumentId = DocumentId Тогда
			стр.Вкл = Истина;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ДеревоДокументовПередНачаломДобавления(Элемент, Отказ, Копирование)
	отказ = Истина;
КонецПроцедуры

Процедура ПриОткрытии()
	КартинкаЗаголовка= ЭДО_БиблиотекаКартинок().КартинкаЗаголовка;
	ЭтаФорма.Заголовок = "Удаление документов в "+НаименованиеСистемы;
КонецПроцедуры



