Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ПостроительОтчета = ОбщийОтчет.ПостроительОтчета;
	
	СтруктураПредставлениеПолей = Новый Структура;
	МассивОтбора = Новый Массив;
	//ЗаполнитьНачальныеНастройкиПоМакету(ПолучитьМакет("ПараметрыОтчетовПродажиКомпании"), СтруктураПредставлениеПолей, МассивОтбора, ОбщийОтчет, "СписокКроссТаблица");
	Текст = "ВЫБРАТЬ
	        |	СУММА(А.КоличествоТекущийГод) КАК КоличествоТекущийГод,
	        |	СУММА(А.КоличествоПрошлыйГод) КАК КоличествоПрошлыйГод,
	        |	СУММА(А.КоличествоТекущийГод) - СУММА(А.КоличествоПрошлыйГод) КАК АбсолютноеОтклонение,
	        |	ВЫБОР
	        |		КОГДА СУММА(А.КоличествоПрошлыйГод) > 0
	        |			ТОГДА СУММА(А.КоличествоТекущийГод) / СУММА(А.КоличествоПрошлыйГод)
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК ОтносительноеОтклонение,
	        |	СУММА(А.СтоимостьТекущийГод) КАК СтоимостьТекущийГод,
	        |	СУММА(А.СтоимостьПрошлыйГод) КАК СтоимостьПрошлыйГод,
	        |	СУММА(А.СтоимостьТекущийГод) - СУММА(А.СтоимостьПрошлыйГод) КАК СтоимостьАбсолютноеОтклонение,
	        |	ВЫБОР
	        |		КОГДА СУММА(А.СтоимостьПрошлыйГод) > 0
	        |			ТОГДА СУММА(А.СтоимостьТекущийГод) / СУММА(А.СтоимостьПрошлыйГод)
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК СтоимостьОтносительноеОтклонение,
	        |	СУММА(А.ВаловаяПрибыльТекущийГод) КАК ВаловаяПрибыльТекущийГод,
	        |	СУММА(А.ВаловаяПрибыльПрошлыйГод) КАК ВаловаяПрибыльПрошлыйГод,
	        |	СУММА(А.ВаловаяПрибыльТекущийГод) - СУММА(А.ВаловаяПрибыльПрошлыйГод) КАК ВаловаяПрибыльАбсолютноеОтклонение,
	        |	ВЫБОР
	        |		КОГДА СУММА(А.ВаловаяПрибыльПрошлыйГод) > 0
	        |			ТОГДА СУММА(А.ВаловаяПрибыльТекущийГод) / СУММА(А.ВаловаяПрибыльПрошлыйГод)
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК ВаловаяПрибыльОтносительноеОтклонение
	        |{ВЫБРАТЬ
	        |	А.Номенклатура.*,
	        |	А.Контрагент.*}
	        |ИЗ
	        |	(ВЫБРАТЬ
	        |		ЕСТЬNULL(ПродажиТекущийГод.Номенклатура, ПродажиПрошлыйГод.Номенклатура) КАК Номенклатура,
	        |		ЕСТЬNULL(ПродажиТекущийГод.КоличествоОборот, 0) КАК КоличествоТекущийГод,
	        |		ЕСТЬNULL(ПродажиПрошлыйГод.КоличествоОборот, 0) КАК КоличествоПрошлыйГод,
	        |		ЕСТЬNULL(ПродажиТекущийГод.СтоимостьОборот, 0) КАК СтоимостьТекущийГод,
	        |		ЕСТЬNULL(ПродажиПрошлыйГод.СтоимостьОборот, 0) КАК СтоимостьПрошлыйГод,
	        |		ЕСТЬNULL(ПродажиТекущийГод.ДоговорКонтрагента.Владелец, ПродажиПрошлыйГод.ДоговорКонтрагента.Владелец) КАК Контрагент,
	        |		ПродажиПрошлыйГод.ВаловаяПрибыль КАК ВаловаяПрибыльПрошлыйГод,
	        |		ПродажиТекущийГод.ВаловаяПрибыль КАК ВаловаяПрибыльТекущийГод
	        |	ИЗ
	        |		(ВЫБРАТЬ
	        |			ПродажиОбороты.Номенклатура КАК Номенклатура,
	        |			СУММА(ПродажиОбороты.КоличествоОборот) КАК КоличествоОборот,
	        |			СУММА(ПродажиОбороты.СтоимостьОборот) КАК СтоимостьОборот,
	        |			ПродажиОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	        |			СУММА(ВЫБОР
	        |					КОГДА ПродажиОбороты.СтоимостьОборот ЕСТЬ NULL 
	        |						ТОГДА 0
	        |					ИНАЧЕ ПродажиОбороты.СтоимостьОборот
	        |				КОНЕЦ) - СУММА(ВЫБОР
	        |					КОГДА ПродажиСебестоимостьОбороты.СтоимостьОборот ЕСТЬ NULL 
	        |						ТОГДА 0
	        |					ИНАЧЕ ПродажиСебестоимостьОбороты.СтоимостьОборот
	        |				КОНЕЦ) КАК ВаловаяПрибыль
	        |		ИЗ
	        |			РегистрНакопления.Продажи.Обороты(&ДатаНач, &ДатаКон, Регистратор, {(Номенклатура).*}) КАК ПродажиОбороты
	        |				ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПродажиСебестоимость.Обороты(&ДатаНач, &ДатаКон, Регистратор, ) КАК ПродажиСебестоимостьОбороты
	        |				ПО ПродажиОбороты.Номенклатура = ПродажиСебестоимостьОбороты.Номенклатура
	        |					И ПродажиОбороты.ЗаказПокупателя = ПродажиСебестоимостьОбороты.ЗаказПокупателя
	        |					И ПродажиОбороты.Подразделение = ПродажиСебестоимостьОбороты.Подразделение
	        |					И ПродажиОбороты.Регистратор = ПродажиСебестоимостьОбороты.Регистратор
	        |		
	        |		СГРУППИРОВАТЬ ПО
	        |			ПродажиОбороты.Номенклатура,
	        |			ПродажиОбороты.ДоговорКонтрагента) КАК ПродажиТекущийГод
	        |			ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	        |				ПродажиОбороты.Номенклатура КАК Номенклатура,
	        |				СУММА(ПродажиОбороты.КоличествоОборот) КАК КоличествоОборот,
	        |				СУММА(ПродажиОбороты.СтоимостьОборот) КАК СтоимостьОборот,
	        |				ПродажиОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	        |				СУММА(ВЫБОР
	        |						КОГДА ПродажиОбороты.СтоимостьОборот ЕСТЬ NULL 
	        |							ТОГДА 0
	        |						ИНАЧЕ ПродажиОбороты.СтоимостьОборот
	        |					КОНЕЦ) - СУММА(ВЫБОР
	        |						КОГДА ПродажиСебестоимостьОбороты.СтоимостьОборот ЕСТЬ NULL 
	        |							ТОГДА 0
	        |						ИНАЧЕ ПродажиСебестоимостьОбороты.СтоимостьОборот
	        |					КОНЕЦ) КАК ВаловаяПрибыль
	        |			ИЗ
	        |				РегистрНакопления.Продажи.Обороты(ДОБАВИТЬКДАТЕ(&ДатаНач, ГОД, -1), ДОБАВИТЬКДАТЕ(&ДатаКон, ГОД, -1), Регистратор, {(Номенклатура).*}) КАК ПродажиОбороты
	        |					ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПродажиСебестоимость.Обороты(ДОБАВИТЬКДАТЕ(&ДатаНач, ГОД, -1), ДОБАВИТЬКДАТЕ(&ДатаКон, ГОД, -1), Регистратор, ) КАК ПродажиСебестоимостьОбороты
	        |					ПО ПродажиОбороты.Номенклатура = ПродажиСебестоимостьОбороты.Номенклатура
	        |						И ПродажиОбороты.Регистратор = ПродажиСебестоимостьОбороты.Регистратор
	        |						И ПродажиОбороты.ЗаказПокупателя = ПродажиСебестоимостьОбороты.ЗаказПокупателя
	        |						И ПродажиОбороты.Подразделение = ПродажиСебестоимостьОбороты.Подразделение
	        |			
	        |			СГРУППИРОВАТЬ ПО
	        |				ПродажиОбороты.Номенклатура,
	        |				ПродажиОбороты.ДоговорКонтрагента) КАК ПродажиПрошлыйГод
	        |			ПО ПродажиТекущийГод.Номенклатура = ПродажиПрошлыйГод.Номенклатура
	        |				И ПродажиТекущийГод.ДоговорКонтрагента = ПродажиПрошлыйГод.ДоговорКонтрагента) КАК А
	        |{ГДЕ
	        |	А.Номенклатура.*,
	        |	А.Контрагент.*}
	        |{УПОРЯДОЧИТЬ ПО
	        |	А.Номенклатура.*,
	        |	А.Контрагент.*}
	        |ИТОГИ
	        |	СУММА(КоличествоТекущийГод),
	        |	СУММА(КоличествоПрошлыйГод),
	        |	СУММА(КоличествоТекущийГод) - СУММА(КоличествоПрошлыйГод) КАК АбсолютноеОтклонение,
	        |	ВЫБОР
	        |		КОГДА СУММА(КоличествоПрошлыйГод) > 0
	        |			ТОГДА 100 * (СУММА(КоличествоТекущийГод) / СУММА(КоличествоПрошлыйГод) - 1)
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК ОтносительноеОтклонение,
	        |	СУММА(СтоимостьТекущийГод),
	        |	СУММА(СтоимостьПрошлыйГод),
	        |	СУММА(СтоимостьТекущийГод) - СУММА(СтоимостьПрошлыйГод) КАК СтоимостьАбсолютноеОтклонение,
	        |	ВЫБОР
	        |		КОГДА СУММА(СтоимостьПрошлыйГод) > 0
	        |			ТОГДА 100 * (СУММА(СтоимостьТекущийГод) / СУММА(СтоимостьПрошлыйГод) - 1)
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК СтоимостьОтносительноеОтклонение,
	        |	СУММА(ВаловаяПрибыльТекущийГод),
	        |	СУММА(ВаловаяПрибыльПрошлыйГод),
	        |	СУММА(ВаловаяПрибыльТекущийГод) - СУММА(ВаловаяПрибыльПрошлыйГод) КАК ВаловаяПрибыльАбсолютноеОтклонение,
	        |	ВЫБОР
	        |		КОГДА СУММА(ВаловаяПрибыльПрошлыйГод) > 0
	        |			ТОГДА 100 * (СУММА(ВаловаяПрибыльТекущийГод) / СУММА(ВаловаяПрибыльПрошлыйГод) - 1)
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК ВаловаяПрибыльОтносительноеОтклонение
	        |ПО
	        |	ОБЩИЕ
	        |{ИТОГИ ПО
	        |	А.Номенклатура.*,
	        |	А.Контрагент.*}";

	СтруктураПредставлениеПолей = Новый Структура("
	|Номенклатура,
	|КоличествоТекущийГод,
	|КоличествоПрошлыйГод,
	|АбсолютноеОтклонение,
	|ОтносительноеОтклонение,
	
	|СтоимостьТекущийГод,
	|СтоимостьПрошлыйГод,
	|СтоимостьАбсолютноеОтклонение,
	|СтоимостьОтносительноеОтклонение,
	
	
	|ВаловаяПрибыльТекущийГод,
	|ВаловаяПрибыльПрошлыйГод,
	|ВаловаяПрибыльАбсолютноеОтклонение,
	|ВаловаяПрибыльОтносительноеОтклонение", 
	
	"Номенклатура",
	"Количество тек. год",
	"Количество прош. год",
	"Количество Абс. отклонение",
	"Количество Отн. отклонение в %",
	
	"Стоимость тек. год",
	"Стоимость прош. год",
	"Стоимость Абс. отклонение",
	"Стоимость Отн. отклонение в %",
	
	"Валовая прибыль тек. год",
	"Валовая прибыль прош. год",
	"Валовая прибыль Абс. отклонение",
	"Валовая прибыль Отн. отклонение в %");
	

	
	ПостроительОтчета.Текст = Текст;
	
	ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей, ПостроительОтчета);
	ОчиститьДополнительныеПоляПостроителя(ПостроительОтчета);
	ЗаполнитьОтбор(МассивОтбора, ПостроительОтчета);
	ОбщийОтчет.мВыбиратьИмяРегистра = Ложь;
	ОбщийОтчет.ВыводитьПоказателиВСтроку = Истина;
	ОбщийОтчет.ВыводитьИтогиПоВсемУровням = Истина;
	
	ОбщийОтчет.ЗаполнитьПоказатели("КоличествоТекущийГод", "Количество тек. год", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("КоличествоПрошлыйГод", "Количество прошл. год", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("АбсолютноеОтклонение", "Количество Абс. отклонение", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("ОтносительноеОтклонение", "Количество Отн. отклонение %", Истина, "ЧЦ=15; ЧДЦ=2");

	ОбщийОтчет.ЗаполнитьПоказатели("СтоимостьТекущийГод", "Стоимость тек. год", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("СтоимостьПрошлыйГод", "Стоимость прошл. год", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("СтоимостьАбсолютноеОтклонение", "Стоимость Абс.отклонение", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("СтоимостьОтносительноеОтклонение", "Стоимость Отн. отклонение %", Истина, "ЧЦ=15; ЧДЦ=2");

	ОбщийОтчет.ЗаполнитьПоказатели("ВаловаяПрибыльТекущийГод", "Валовая прибыль тек. год", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("ВаловаяПрибыльПрошлыйГод", "Валовая прибыль прошл. год", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("ВаловаяПрибыльАбсолютноеОтклонение", "Валовая прибыль Абс.отклонение", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("ВаловаяПрибыльОтносительноеОтклонение", "Валовая прибыль Отн. отклонение %", Истина, "ЧЦ=15; ЧДЦ=2");
	
	
	ОбщийОтчет.мНазваниеОтчета = "Продажи в рублях и в штуках - Сравнение по аналитике";
	ОбщийОтчет.ВыводитьИтогиПоВсемУровням = Ложь;
	ОбщийОтчет.мВыбиратьИспользованиеСвойств = Истина;
	
	// Установим дату начала отчета
	Если ЗначениеНеЗаполнено(ОбщийОтчет.ДатаНач) Тогда
		Если Не ЗначениеНеЗаполнено(глТекущийПользователь) Тогда
			ОбщийОтчет.ДатаНач = ПолучитьЗначениеПоУмолчанию(глТекущийПользователь,"ОсновнаяДатаНачалаОтчетов");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт

	//ОбщийОтчет.мСтруктураСвязиПоказателейИИзмерений.Вставить("РентабельностьПродаж", Новый Структура("ТакогоИзмеренияНетВОтчете")); // Рентабельность не выводится в итогах по группировкам, только в детальных записях
	//ОбщийОтчет.мСтруктураСвязиПоказателейИИзмерений.Вставить("ПроцентНаценки", Новый Структура("ТакогоИзмеренияНетВОтчете")); // Рентабельность не выводится в итогах по группировкам, только в детальных записях
	ОбщийОтчет.СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок);

КонецПРоцедуры

// Читает свойство Построитель отчета
//
// Параметры
//	Нет
//
Функция ПолучитьПостроительОтчета() Экспорт

	Возврат ОбщийОтчет.ПолучитьПостроительОтчета();

КонецФункции // ПолучитьПостроительОтчета()

// Настраивает отчет по переданной структуре параметров
//
// Параметры:
//	Нет.
//
Процедура Настроить(Параметры) Экспорт

	ОбщийОтчет.Настроить(Параметры, ЭтотОбъект);

КонецПроцедуры

// Возвращает основную форму отчета, связанную с данным экземпляром отчета
//
// Параметры
//	Нет
//
Функция ПолучитьОсновнуюФорму() Экспорт
	
	ОснФорма = ПолучитьФорму();
	ОснФорма.ОбщийОтчет = ОбщийОтчет;
	ОснФорма.ЭтотОтчет = ЭтотОбъект;
	Возврат ОснФорма;
	
КонецФункции // ПолучитьОсновнуюФорму()

// Возвращает форму настройки 
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	
//
Функция ПолучитьФормуНастройки() Экспорт
	
	ФормаНастройки = ОбщийОтчет.ПолучитьФорму("ФормаНастройка");
	Возврат ФормаНастройки;
	
КонецФункции // ПолучитьФормуНастройки()

// Процедура обработки расшифровки
//
// Параметры:
//	Нет.
//
Процедура ОбработкаРасшифровки(РасшифровкаСтроки, ПолеТД, ВысотаЗаголовка, СтандартнаяОбработка) Экспорт
	
	// Добавление расшифровки из колонки
	Если ТипЗнч(РасшифровкаСтроки) = Тип("Структура") Тогда
		
		// Расшифровка колонки находится в заголовке колонки
		РасшифровкаКолонки = ПолеТД.Область(ВысотаЗаголовка+2, ПолеТД.ТекущаяОбласть.Лево).Расшифровка;

		Расшифровка = Новый Структура;

		Для каждого Элемент Из РасшифровкаСтроки Цикл
			Расшифровка.Вставить(Элемент.Ключ, Элемент.Значение);
		КонецЦикла;

		Если ТипЗнч(РасшифровкаКолонки) = Тип("Структура") Тогда

			Для каждого Элемент Из РасшифровкаКолонки Цикл
				Расшифровка.Вставить(Элемент.Ключ, Элемент.Значение);
			КонецЦикла;

		КонецЕсли; 

		ОбщийОтчет.ОбработкаРасшифровкиСтандартногоОтчета(Расшифровка, СтандартнаяОбработка, ЭтотОбъект);

	КонецЕсли;
	
КонецПроцедуры // ОбработкаРасшифровки()

// Формирует структуру, в которую складываются настройки
//
Функция СформироватьСтруктуруДляСохраненияНастроек(ПоказыватьЗаголовок) Экспорт
	
	СтруктураНастроек = Новый Структура;
	
	ОбщийОтчет.СформироватьСтруктуруДляСохраненияНастроек(СтруктураНастроек, ПоказыватьЗаголовок);
	
	Возврат СтруктураНастроек;
	
КонецФункции

// Заполняет настройки из структуры - кроме состояния панели "Отбор"
//
Процедура ВосстановитьНастройкиИзСтруктуры(СохраненныеНастройки, ПоказыватьЗаголовок, Отчет=Неопределено) Экспорт
	
	// Если отчет, вызвавший порцедуру, не передан, то считаем, что ее вызвал этот отчет
	Если Отчет = Неопределено Тогда
		Отчет = ЭтотОбъект;
	КонецЕсли;

	ОбщийОтчет.ВосстановитьНастройкиИзСтруктуры(СохраненныеНастройки, ПоказыватьЗаголовок, Отчет);
	
КонецПроцедуры

ОбщийОтчет.ИмяРегистра = "Продажи";