Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок = Ложь, ВысотаЗаголовка = 0, ТолькоЗаголовок = Ложь) Экспорт
    ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("НачДата", НачалоДня(ОбщийОтчет.ДатаНач));
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("КонДата", НачалоДня(КонецДня(ОбщийОтчет.ДатаКон)+1));
	ОбщийОтчет.мРежимВводаПериода = 2;
	ОбщийОтчет.СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок);

КонецПроцедуры

//=======================ГЛАВНАЯ=============================
Процедура ЗаполнитьНачальныеНастройки() Экспорт
	СтруктураПредставлениеПолей = Новый Структура;
	МассивОтбора = Новый Массив;
	ПостроительОтчета = ОбщийОтчет.ПостроительОтчета;
	
	Текст= "ВЫБРАТЬ
	       |	ДТГ.Вид КАК Вид,
	       |	ДТГ.ДоговорКонтрагента.ОтветственноеЛицо КАК Менеджер,
	       |	ДТГ.Контрагент КАК Контрагент,
	       |	ДТГ.ДоговорКонтрагента КАК ДоговорКонтрагента,
	       |	ДТГ.Сделка КАК Сделка,
	       |	ДТГ.Номенклатура КАК Номенклатура,
	       |	ДТГ.СуммаВЗаказе КАК СуммаВЗаказе,
	       |	ДТГ.Регистратор КАК Регистратор,
	       |	ДТГ.Сумма КАК Сумма
	       |{ВЫБРАТЬ
	       |	Номенклатура.*,
	       |	Менеджер.*,
	       |	Контрагент.*,
	       |	ДоговорКонтрагента.*,
	       |	Сделка.*,
	       |	Регистратор.*}
	       |ИЗ
	       |	(ВЫБРАТЬ
	       |		ВЫБОР
	       |			КОГДА ВзаиморасчетыСКонтрагентамиОстатки.СуммаУпрОборот > 0
	       |				ТОГДА ""Дебиторка""
	       |			ИНАЧЕ ""Оплата""
	       |		КОНЕЦ КАК Вид,
	       |		Заказы.Номенклатура КАК Номенклатура,
	       |		ВзаиморасчетыСКонтрагентамиОстатки.ДоговорКонтрагента.Владелец КАК Контрагент,
	       |		ВзаиморасчетыСКонтрагентамиОстатки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	       |		ВзаиморасчетыСКонтрагентамиОстатки.Сделка КАК Сделка,
	       |		ВзаиморасчетыСКонтрагентамиОстатки.Регистратор КАК Регистратор,
	       |		ВЫБОР
	       |			КОГДА ЕСТЬNULL(Заказы.СуммаДокумента, 0) = 0
	       |				ТОГДА 0
	       |			ИНАЧЕ ВЫБОР
	       |					КОГДА ВзаиморасчетыСКонтрагентамиОстатки.Сделка.СуммаВключаетНДС
	       |						ТОГДА ВЫРАЗИТЬ(Заказы.Сумма / Заказы.СуммаДокумента * ВзаиморасчетыСКонтрагентамиОстатки.СуммаУпрОборот КАК ЧИСЛО(15, 2))
	       |					ИНАЧЕ ВЫРАЗИТЬ((Заказы.Сумма + Заказы.СуммаНДС) / Заказы.СуммаДокумента * ВзаиморасчетыСКонтрагентамиОстатки.СуммаУпрОборот КАК ЧИСЛО(15, 2))
	       |				КОНЕЦ
	       |		КОНЕЦ КАК Сумма,
	       |		ВЫБОР
	       |			КОГДА НЕ ВзаиморасчетыСКонтрагентамиОстатки.СуммаУпрОборот > 0
	       |				ТОГДА -Заказы.Сумма
	       |			ИНАЧЕ Заказы.Сумма
	       |		КОНЕЦ КАК СуммаВЗаказе
	       |	{ВЫБРАТЬ
	       |		Номенклатура.*,
	       |		Контрагент.*,
	       |		ДоговорКонтрагента.*,
	       |		Сделка.*,
	       |		Регистратор.*}
	       |	ИЗ
	       |		РегистрНакопления.ВзаиморасчетыСКонтрагентами.Обороты(
	       |				&НачДата,
	       |				&КонДата,
	       |				Регистратор,
	       |				ДоговорКонтрагента.Владелец.Покупатель = ИСТИНА
	       |					И ДоговорКонтрагента.ВедениеВзаиморасчетов = &ПоЗаказам) КАК ВзаиморасчетыСКонтрагентамиОстатки
	       |			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	       |				ЗаказПокупателяТовары.Ссылка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	       |				ЗаказПокупателяТовары.Ссылка КАК Сделка,
	       |				ЗаказПокупателяТовары.Номенклатура КАК Номенклатура,
	       |				ЗаказПокупателяТовары.Сумма КАК Сумма,
	       |				ЗаказПокупателяТовары.СуммаНДС КАК СуммаНДС,
	       |				ЗаказПокупателяТовары.Ссылка.СуммаДокумента КАК СуммаДокумента
	       |			ИЗ
	       |				Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
	       |			ГДЕ
	       |				НЕ ЗаказПокупателяТовары.Ссылка.ПометкаУдаления
	       |				И ЗаказПокупателяТовары.Ссылка.Проведен
	       |				И ЗаказПокупателяТовары.Ссылка.Проверен
	       |				И ЗаказПокупателяТовары.Ссылка.Дата >= &ДатаЗак) КАК Заказы
	       |			ПО ВзаиморасчетыСКонтрагентамиОстатки.ДоговорКонтрагента = Заказы.ДоговорКонтрагента
	       |				И ВзаиморасчетыСКонтрагентамиОстатки.Сделка = Заказы.Сделка
	       |	ГДЕ
	       |		Заказы.СуммаДокумента > 0
	       |	{ГДЕ
	       |		(ВЫБОР
	       |				КОГДА ВзаиморасчетыСКонтрагентамиОстатки.СуммаУпрОборот > 0
	       |					ТОГДА ""Дебиторка""
	       |				ИНАЧЕ ""Кредиторка""
	       |			КОНЕЦ) КАК Вид,
	       |		ВзаиморасчетыСКонтрагентамиОстатки.ДоговорКонтрагента.Владелец.* КАК Контрагент,
	       |		ВзаиморасчетыСКонтрагентамиОстатки.ДоговорКонтрагента.*,
	       |		ВзаиморасчетыСКонтрагентамиОстатки.Сделка.*,
	       |		Заказы.Номенклатура.*,
	       |		(ВЫБОР
	       |				КОГДА ЕСТЬNULL(Заказы.СуммаДокумента, 0) = 0
	       |					ТОГДА 0
	       |				ИНАЧЕ ВЫБОР
	       |						КОГДА ВзаиморасчетыСКонтрагентамиОстатки.Сделка.СуммаВключаетНДС
	       |							ТОГДА ВЫРАЗИТЬ(Заказы.Сумма / Заказы.СуммаДокумента * ВзаиморасчетыСКонтрагентамиОстатки.СуммаУпрОборот КАК ЧИСЛО(15, 2))
	       |						ИНАЧЕ ВЫРАЗИТЬ((Заказы.Сумма + Заказы.СуммаНДС) / Заказы.СуммаДокумента * ВзаиморасчетыСКонтрагентамиОстатки.СуммаУпрОборот КАК ЧИСЛО(15, 2))
	       |					КОНЕЦ
	       |			КОНЕЦ) КАК Сумма,
	       |		ВзаиморасчетыСКонтрагентамиОстатки.Регистратор.*,
	       |		(ВЫБОР
	       |				КОГДА НЕ ВзаиморасчетыСКонтрагентамиОстатки.СуммаУпрОборот > 0
	       |					ТОГДА -Заказы.Сумма
	       |				ИНАЧЕ Заказы.Сумма
	       |			КОНЕЦ) КАК СуммаВЗаказе}) КАК ДТГ
	       |{ГДЕ
	       |	ДТГ.Вид,
	       |	ДТГ.ДоговорКонтрагента.ОтветственноеЛицо.* КАК Менеджер,
	       |	ДТГ.Контрагент.*,
	       |	ДТГ.ДоговорКонтрагента.*,
	       |	ДТГ.Номенклатура.*,
	       |	ДТГ.Сделка.*,
	       |	ДТГ.Регистратор.*}
	       |ИТОГИ
	       |	СУММА(СуммаВЗаказе),
	       |	СУММА(Сумма)
	       |ПО
	       |	ОБЩИЕ,
	       |	Номенклатура,
	       |	Сделка,
	       |	Менеджер,
	       |	Контрагент,
	       |	ДоговорКонтрагента,
	       |	Регистратор,
	       |	Вид
	       |{ИТОГИ ПО
	       |	Менеджер.*,
	       |	Контрагент.*,
	       |	ДоговорКонтрагента.*,
	       |	Вид,
	       |	Номенклатура.*,
	       |	Сделка.*,
	       |	Регистратор.*,
	       |	СуммаВЗаказе,
	       |	Сумма}
	       |АВТОУПОРЯДОЧИВАНИЕ";


ПостроительОтчета.Параметры.Вставить("ДатаЗак", '20130101');// не ранее 01.01.2013г.
ПостроительОтчета.Параметры.Вставить("ПоЗаказам", Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказам);
	
	СтруктураПредставлениеПолей = Новый Структура("
	|Вид,
	|Менеджер,
	|Контрагент,
	|ДоговорКонтрагента,
	|Сделка,
	|Номенклатура,
	|СуммаВЗаказе,
	|Регистратор,
	|Сумма", 
	
	"Вид",
	"Менеджер",
	"Контрагент",
	"Договор контрагента",
	"Сделка",
	"Номенклатура",
	"Сумма по заказу",
	"Регистратор",
	"Сумма взаиморасчетов");

	
		
	ПостроительОтчета.Текст = Текст;
	МассивОтбора = Новый Массив;
	ОбщийОтчет.ЗаполнитьПоказатели("СуммаВЗаказе", "Сумма по заказу", Истина, "ЧЦ=15; ЧДЦ=2");
	ОбщийОтчет.ЗаполнитьПоказатели("Сумма", "Сумма оплаты", Истина, "ЧЦ=15; ЧДЦ=2");
	ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей, ПостроительОтчета);
	ОчиститьДополнительныеПоляПостроителя(ПостроительОтчета);
	ЗаполнитьОтбор(МассивОтбора, ПостроительОтчета);
	
	
	ОбщийОтчет.мНазваниеОтчета = "Распределение денег по товарам";

КонецПроцедуры

// Читает свойство Построитель отчета
//
// Параметры
//	Нет
//
Функция ПолучитьПостроительОтчета() Экспорт

	Возврат ОбщийОтчет.ПолучитьПостроительОтчета();

КонецФункции // ПолучитьПостроительОтчета()

// Настраивает отчет по переданной структуре параметров
//
// Параметры:
//	Нет.
//
Процедура Настроить(Параметры) Экспорт

	ОбщийОтчет.Настроить(Параметры, ЭтотОбъект);

КонецПроцедуры

// Возвращает основную форму отчета, связанную с данным экземпляром отчета
//
// Параметры
//	Нет
//
Функция ПолучитьОсновнуюФорму() Экспорт
	
	ОснФорма = ПолучитьФорму();
	ОснФорма.ОбщийОтчет = ОбщийОтчет;
	ОснФорма.ЭтотОтчет = ЭтотОбъект;
	Возврат ОснФорма;
	
КонецФункции // ПолучитьОсновнуюФорму()

// Возвращает форму настройки 
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	
//
Функция ПолучитьФормуНастройки() Экспорт
	
	ФормаНастройки = ОбщийОтчет.ПолучитьФорму("ФормаНастройка");
	Возврат ФормаНастройки;
	
КонецФункции // ПолучитьФормуНастройки()

// Процедура обработки расшифровки
//
// Параметры:
//	Нет.
//
Процедура ОбработкаРасшифровки(РасшифровкаСтроки, ПолеТД, ВысотаЗаголовка, СтандартнаяОбработка) Экспорт
	
	// Добавление расшифровки из колонки
	Если ТипЗнч(РасшифровкаСтроки) = Тип("Структура") Тогда
		
		// Расшифровка колонки находится в заголовке колонки
		РасшифровкаКолонки = ПолеТД.Область(ВысотаЗаголовка+2, ПолеТД.ТекущаяОбласть.Лево).Расшифровка;

		Расшифровка = Новый Структура;

		Для каждого Элемент Из РасшифровкаСтроки Цикл
			Расшифровка.Вставить(Элемент.Ключ, Элемент.Значение);
		КонецЦикла;

		Если ТипЗнч(РасшифровкаКолонки) = Тип("Структура") Тогда

			Для каждого Элемент Из РасшифровкаКолонки Цикл
				Расшифровка.Вставить(Элемент.Ключ, Элемент.Значение);
			КонецЦикла;

		КонецЕсли; 

		ОбщийОтчет.ОбработкаРасшифровкиСтандартногоОтчета(Расшифровка, СтандартнаяОбработка, ЭтотОбъект);

	КонецЕсли;
	
КонецПроцедуры // ОбработкаРасшифровки()

// Формирует структуру, в которую складываются настройки
//
Функция СформироватьСтруктуруДляСохраненияНастроек(ПоказыватьЗаголовок) Экспорт
	
	СтруктураНастроек = Новый Структура;
	
	ОбщийОтчет.СформироватьСтруктуруДляСохраненияНастроек(СтруктураНастроек, ПоказыватьЗаголовок);
	
	Возврат СтруктураНастроек;
	
КонецФункции

// Заполняет настройки из структуры - кроме состояния панели "Отбор"
//
Процедура ВосстановитьНастройкиИзСтруктуры(СохраненныеНастройки, ПоказыватьЗаголовок, Отчет=Неопределено) Экспорт
	
	// Если отчет, вызвавший порцедуру, не передан, то считаем, что ее вызвал этот отчет
	Если Отчет = Неопределено Тогда
		Отчет = ЭтотОбъект;
	КонецЕсли;

	ОбщийОтчет.ВосстановитьНастройкиИзСтруктуры(СохраненныеНастройки, ПоказыватьЗаголовок, Отчет);
	
КонецПроцедуры

ОбщийОтчет.ИмяРегистра = "ВзаиморасчетыСКонтрагентами";
ОбщийОтчет.мНазваниеОтчета = "Деньги в товарных группах";
ОбщийОтчет.мВыбиратьИмяРегистра = Ложь;
ОбщийОтчет.мВыбиратьИспользованиеСвойств=Ложь;
ОбщийОтчет.мРежимВводаПериода = 1;
ОбщийОтчет.ВыводитьПоказателиВСтроку=Истина;