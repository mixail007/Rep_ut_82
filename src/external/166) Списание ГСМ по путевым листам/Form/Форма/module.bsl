
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ТребованиеНакладная = Документы.ТребованиеНакладная.СоздатьДокумент();
	ТребованиеНакладная.Дата = КонПериода;
	ЗаполнитьШапкуДокумента(ТребованиеНакладная,глТекущийПользователь);
	УстановитьНомерДокумента(ТребованиеНакладная);
	ТребованиеНакладная.Склад = Склад;
	ТребованиеНакладная.Подразделение = Подразделение;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНач",НачПериода);
	Запрос.УстановитьПараметр("ДатаКон",КонецДня(КонПериода));
	Запрос.Текст = "ВЫБРАТЬ
	               |	Периоды.Материал,
	               |	СУММА((Периоды.Ссылка.ПробегПриВозвращении - Периоды.Ссылка.ПробегПриВыезде) * НормыРасходаМатериалов.Количество * КоэффициентРасходаТоплива.Коэффициент / 100) КАК КоличествоСписать
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ПутевойЛист.Ссылка КАК Ссылка,
	               |		ПутевойЛист.Автомобиль КАК Автомобиль,
	               |		НормыРасходаМатериалов.Материал КАК Материал,
	               |		МАКСИМУМ(ЕСТЬNULL(НормыРасходаМатериалов.Период, 0)) КАК ПериодНормы,
	               |		МАКСИМУМ(ЕСТЬNULL(КоэффициентРасходаТоплива.Период, 0)) КАК ПериодКоэффициента
	               |	ИЗ
	               |		Документ.ПутевойЛист КАК ПутевойЛист
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормыРасходаМатериалов КАК НормыРасходаМатериалов
	               |			ПО ПутевойЛист.Автомобиль = НормыРасходаМатериалов.Автомобиль
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентРасходаТоплива КАК КоэффициентРасходаТоплива
	               |			ПО ПутевойЛист.Автомобиль = КоэффициентРасходаТоплива.Автомобиль
	               |	ГДЕ
	               |		ПутевойЛист.Дата >= ЕСТЬNULL(НормыРасходаМатериалов.Период, 0)
	               |		И ПутевойЛист.Дата >= ЕСТЬNULL(КоэффициентРасходаТоплива.Период, 0)
	               |		И (НЕ ПутевойЛист.ПометкаУдаления)
	               |		И ПутевойЛист.Дата МЕЖДУ &ДатаНач И &ДатаКон
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ПутевойЛист.Ссылка,
	               |		НормыРасходаМатериалов.Материал,
	               |		ПутевойЛист.Автомобиль) КАК Периоды
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормыРасходаМатериалов КАК НормыРасходаМатериалов
	               |		ПО Периоды.ПериодНормы = НормыРасходаМатериалов.Период
	               |			И Периоды.Материал = НормыРасходаМатериалов.Материал
	               |			И Периоды.Автомобиль = НормыРасходаМатериалов.Автомобиль
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентРасходаТоплива КАК КоэффициентРасходаТоплива
	               |		ПО Периоды.Автомобиль = КоэффициентРасходаТоплива.Автомобиль
	               |			И Периоды.ПериодКоэффициента = КоэффициентРасходаТоплива.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Периоды.Материал";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТребованиеНакладная.Материалы.Добавить();
		НоваяСтрока.Номенклатура = Выборка.Материал;
		
		НоваяСтрока.ЕдиницаИзмерения = НоваяСтрока.Номенклатура.ЕдиницаХраненияОстатков;
		НоваяСтрока.Коэффициент      = НоваяСтрока.ЕдиницаИзмерения.Коэффициент;
		
		ЗаполнитьСпособСписанияОстаткаТоваровТабЧасти(НоваяСтрока, ТребованиеНакладная);
		
		НоваяСтрока.Количество = Выборка.КоличествоСписать;
		НоваяСтрока.СтатьяЗатрат = СтатьяЗатрат;
		НоваяСтрока.Качество = Справочники.Качество.Новый
		
	КонецЦикла;
	
	ТребованиеНакладная.ПолучитьФорму().Открыть();
	
КонецПроцедуры

Процедура ВыбПериодНажатие(Элемент)
	НастройкаПериода = Новый НастройкаПериода;
	НастройкаПериода.УстановитьПериод(НачПериода, ?(КонПериода='0001-01-01', КонПериода, КонецДня(КонПериода)));
	НастройкаПериода.РедактироватьКакИнтервал = Истина;
	НастройкаПериода.РедактироватьКакПериод = Истина;
	НастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;
	Если НастройкаПериода.Редактировать() Тогда
		НачПериода = НастройкаПериода.ПолучитьДатуНачала();
		КонПериода = НастройкаПериода.ПолучитьДатуОкончания();
	КонецЕсли;
КонецПроцедуры
