
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	Если НЕ ЗначениеЗаполнено(Склад) Тогда
		Сообщить("Не вуказан склад");
		Возврат;
	КонецЕсли;
	
	Если Товары.Количество() = 0 Тогда
		Сообщить("Не заполнены товары для списания");
		Возврат;
	КонецЕсли;
	
	ТаблицаКомментариев = Товары.Выгрузить();
	ТаблицаКомментариев.Свернуть("Комментарий");  //в комментарии содержится Контрагент, для которого списываются крышки
	
	Для каждого СтрТК ИЗ ТаблицаКомментариев Цикл
		
		ДокСписание = Документы.СписаниеТоваров.СоздатьДокумент();
		ДокСписание.ОтражатьВУправленческомУчете = Истина;
		ДокСписание.Дата						 = ТекущаяДата();
		ДокСписание.Организация					 = Справочники.Организации.НайтиПоКоду("00001");
		ДокСписание.Склад						 = Склад;
		ДокСписание.Ответственный				 = глТекущийПользователь;
		ДокСписание.Комментарий					 = СтрТК.Комментарий;
		
		Отбор = Новый Структура();
		Отбор.Вставить("Комментарий", СтрТК.Комментарий);
		Строки = Товары.НайтиСтроки(Отбор);
		
		Для каждого СтрМассива ИЗ Строки Цикл
			Если НЕ ЗначениеЗаполнено(СтрМассива.СписаниеТоваров) Тогда
				СтрокаТЧ = ДокСписание.Товары.Добавить();
				СтрокаТЧ.Номенклатура	 = СтрМассива.Номенклатура;
				СтрокаТЧ.Количество		 = СтрМассива.Количество;
				СтрокаТЧ.Склад			 = Склад;
			КонецЕсли;
		КонецЦикла;
		
		Если ДокСписание.Товары.Количество() > 0 Тогда
			ДокСписание.Записать(РежимЗаписиДокумента.Проведение);
			Для каждого СтрМассива ИЗ Строки Цикл
				СтрМассива.СписаниеТоваров = ДокСписание.Ссылка;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


Процедура ТоварыНоменклатураОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодСБИС", "%"+Текст+"%");
	Запрос.УстановитьПараметр("НоменклатурнаяГруппа", Справочники.НоменклатурныеГруппы.НайтиПоКоду("00024")); //Крепежные материалы и комплектующие
	Запрос.Текст = "ВЫБРАТЬ
	               |	Номенклатура.Ссылка
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |ГДЕ
	               |	Номенклатура.КодСБИС ПОДОБНО &КодСБИС
	               |	И Номенклатура.НоменклатурнаяГруппа = &НоменклатурнаяГруппа";
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Количество() > 0 Тогда
		Значение = Новый СписокЗначений();
		Пока Результат.Следующий() Цикл
			Значение.Добавить(Результат.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


Процедура ПриОткрытии()
	
	Склад = Справочники.Склады.НайтиПоКоду("00393"); //Ошиповка (комплектующие материалы)
	
КонецПроцедуры


Процедура ТоварыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.Код.УстановитьТекст(ДанныеСтроки.Номенклатура.Код);
	ОформлениеСтроки.Ячейки.КодСБИС.УстановитьТекст(ДанныеСтроки.Номенклатура.КодСБИС);
	
КонецПроцедуры


Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("Макет");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьСтрока	 = Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги 	 = Макет.ПолучитьОбласть("Итоги");
	
	ОбластьЗаголовок.Параметры.Склад = Склад;
	ТабДок.Вывести(ОбластьЗаголовок);
	
	Для каждого СтрТЧ ИЗ Товары Цикл
		ОбластьСтрока.Параметры.НомерПП		 = СтрТЧ.НомерСтроки;
		ОбластьСтрока.Параметры.Код			 = СтрТЧ.Номенклатура.Код;
		ОбластьСтрока.Параметры.КодСБИС		 = СтрТЧ.Номенклатура.КодСБИС;
		ОбластьСтрока.Параметры.Номенклатура = СтрТЧ.Номенклатура;
		ОбластьСтрока.Параметры.Комментарий	 = СтрТЧ.Комментарий;
		ОбластьСтрока.Параметры.Списание	 = СтрТЧ.СписаниеТоваров;
		ОбластьСтрока.Параметры.Количество	 = СтрТЧ.Количество;
		ТабДок.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	ОбластьИтоги.Параметры.КоличествоИтого = Товары.Итог("Количество");
	ТабДок.Вывести(ОбластьИтоги);
	
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Показать();
	
КонецПроцедуры

