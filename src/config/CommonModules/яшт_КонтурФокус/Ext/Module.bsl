//Контур.Фокус
//Процедцра размещает на форме кнопку с надписью с краткой информацией о результате наблюдения за контрагентом.
// Параметры:
//	Форма - Форма элемента справочника или форма документа в которой есть панель "ОсновныеДействияФормы"
//          В передаваемой форме должен быть обработчик нажатия кнопки "кнОткрытьОтчетКонтурФокус(Элемент)"
Процедура КонтурФокусПоказатьИнформациюОКонтрагентеНаФорме(Форма) Экспорт 
	
	//Проверим использование контур фокуса
	Если Не Константы.КонтурФокусИспользуется.Получить() Тогда 
		Возврат;	
	КонецЕсли;	
	
	ЕстьОсновнаяПанель = Форма.ЭлементыФормы.Найти("ОсновныеДействияФормы") <> Неопределено; 
	Если Не ЕстьОсновнаяПанель Тогда 
		Возврат;
	КонецЕсли;
	
	//Проверим есть ли в переданной форме контрагент
	//Форма может быть как справочника так и документа
	ИНН = "";
	Если Форма.метаданные().Реквизиты.Найти("Контрагент") <> Неопределено Тогда 
		ИНН = Форма.Контрагент.ИНН;
	ИначеЕсли 
		Форма.метаданные().Реквизиты.Найти("ИНН") <> Неопределено Тогда 
		ИНН = Форма.ИНН;
	КонецЕсли;
	
	//По пустому ИНН не стоит ничего искать.Не найдет ничего.
	Если ИНН  = "" Тогда 
		Возврат;
	КонецЕсли; 
	          	
	Данные = ПолучитьДанныеПроверкиКонтрагентаПоДаннымИБ(ИНН);
	//Если Данные = Неопределено Тогда 
	//	Возврат;
	//КонецЕсли;
	
	КрасныйКоличество	= ?(Фокус_ПолучитьЗначение(Данные,"""redStatements"":true")=Истина,1,0);   
	ЖелтыйКоличество	= ?(Фокус_ПолучитьЗначение(Данные,"""yellowStatements"":true")=Истина,1,0);  
	ЗеленыйКоличество	= ?(Фокус_ПолучитьЗначение(Данные,"""greenStatements"":true")=Истина,1,0);
	
	КартинкаКнопки = Неопределено;
	
	Если КрасныйКоличество>0 Тогда
		КартинкаКнопки  = БиблиотекаКартинок.КонтурФокус_Красный;
		ЗаголовокКнопки = "Контур.Фокус " + "- есть критичные факты";
	ИначеЕсли ЖелтыйКоличество>0 Тогда
		КартинкаКнопки  = БиблиотекаКартинок.КонтурФокус_Желтый;
		ЗаголовокКнопки = "Контур.Фокус " + "- есть факты, на которые стоит обратить внимание";
	ИначеЕсли ЗеленыйКоличество>0 Тогда
		КартинкаКнопки  = БиблиотекаКартинок.КонтурФокус_Зеленый;
		ЗаголовокКнопки = "Контур.Фокус " + "- есть факты ведения хоз. деятельности";
	ИначеЕсли Данные = Неопределено Тогда 
		КартинкаКнопки  = Новый Картинка;
		ЗаголовокКнопки = "Контур.Фокус " + "- наблюдение за контрагентом не ведется";
	Иначе	
		КартинкаКнопки  = БиблиотекаКартинок.КонтурФокус_Пустой;
		ЗаголовокКнопки = "Контур.Фокус " + "- не найдено существенных фактов";
	КонецЕсли;
	     	
	//Добавим кнопку на основную панель 
	КомПанель = Форма.ЭлементыФормы.ОсновныеДействияФормы;
	Если КомПанель.Кнопки.Найти("кнОткрытьОтчетКонтурФокус") = Неопределено Тогда 
		КнопкаКонтурФокус = КомПанель.Кнопки.Добавить("кнОткрытьОтчетКонтурФокус",ТипКнопкиКоманднойПанели.Действие,ЗаголовокКнопки,Новый Действие("кнОткрытьОтчетКонтурФокус"));
		КнопкаКонтурФокус.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
		КнопкаКонтурФокус.Картинка = КартинкаКнопки;
		КнопкиФормы = Форма.ЭлементыФормы.ОсновныеДействияФормы.Кнопки;
		КнопкиФормы.Сдвинуть(КнопкаКонтурФокус,(-1)*КнопкиФормы.Количество()-1); // сдвигаем кнопку в крайнее правое положение
	Иначе
		КнопкаКонтурФокус = КомПанель.Кнопки.кнОткрытьОтчетКонтурФокус;
		КнопкаКонтурФокус.Картинка  = КартинкаКнопки;
		КнопкаКонтурФокус.Текст     = ЗаголовокКнопки;
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьДанныеПроверкиКонтрагентаПоДаннымИБ(ИНН) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Фокус_ИсторияРезультатов.Значение,
		|	Фокус_ИсторияРезультатов.ДатаВремя КАК ДатаВремя
		|ИЗ
		|	Справочник.Фокус_ИсторияРезультатов КАК Фокус_ИсторияРезультатов
		|ГДЕ
		|	Фокус_ИсторияРезультатов.ИНН = &ИНН
		|	И Фокус_ИсторияРезультатов.ИмяМетода = &ИмяМетода
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаВремя УБЫВ";
	
	Запрос.УстановитьПараметр("ИмяМетода", "req");
	Запрос.УстановитьПараметр("ИНН", ИНН);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		Возврат ВыборкаДетальныеЗаписи.Значение;
	КонецЕсли;
	
КонецФункции

Функция Фокус_ПолучитьЗначение(Данные,ИмяПоля) Экспорт
	
	Возврат Найти(Данные,ИмяПоля) <> 0;
	
КонецФункции

Процедура ОткрытьОтчетКонтурФокус(Источник) Экспорт
	
	Контрагент = Неопределено;
	
	Если Источник.метаданные().Реквизиты.Найти("Контрагент") <> Неопределено Тогда 
		Если ЗначениеЗаполнено(Источник.Контрагент) Тогда 
			Контрагент = Источник.Контрагент;
		КонецЕсли;	
	ИначеЕсли 
		Источник.метаданные().Имя = "Контрагенты"  Тогда 
		Контрагент = Источник.Ссылка;
	КонецЕсли;
	
	Если Контрагент = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	СписокКонтрагентов = Новый ТаблицаЗначений;
	СписокКонтрагентов.Колонки.Добавить("Контрагент",Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	НовСтрСписокКонтрагентов = СписокКонтрагентов.Добавить();
	НовСтрСписокКонтрагентов.Контрагент = Контрагент;
	
	МодульКонтурФокус = Обработки.КонтурФокус.Создать();
	ТД = МодульКонтурФокус.Фокус_ВыводДанных_ПоказатьСработавшиеМаркеры(,,,СписокКонтрагентов);
	ТД.ОтображатьСетку = Ложь;
	ТД.ТолькоПросмотр = Истина;
	ТД.ОтображатьЗаголовки = Ложь;
	ТД.Показать("Информация о контрагенте");
	
КонецПроцедуры

Процедура КонтурФокусОбновитьДанныеКонтрагентов(Источник) Экспорт
	
	//Попытка
		ОбработкаОбъект = Обработки.КонтурФокус.Создать();
		ОбработкаОбъект.Фокус_Наблюдение_ОбновитьДанные();
		ОбработкаОбъект.Фокус_Наблюдение_СоздатьМаркеры();
		ОбработкаОбъект.Фокус_Наблюдение_ВыполнитьДействия();
		ОбработкаОбъект.ОбновитьДанныеРГДанныеПоСудам();
	//Исключение
	//КонецПопытки;
	
КонецПроцедуры

