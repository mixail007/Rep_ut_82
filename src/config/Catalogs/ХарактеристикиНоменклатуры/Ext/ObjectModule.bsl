////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ)

	Если НЕ ОбменДанными.Загрузка
	   И НЕ Владелец.ВестиУчетПоХарактеристикам Тогда
		СообщитьОбОшибке(
		"Для номенклатуры """ + Владелец + """ не ведется учет по характеристикам.
		|Характеристика """ + Наименование + """ не может быть записана.", 
		Отказ);

	КонецЕсли;
	
	
	//ОС и НМА
	
	Если НЕ ОбменДанными.Загрузка Тогда 
		
		ПроверитьРегистрацию(Владелец, ЭтотОбъект);
	
	КонецЕсли;
	
	//----- ОС и НМА
	
	Если НЕ Отказ Тогда
	
		обЗаписатьПротоколИзменений(ЭтотОбъект);
	
	КонецЕсли; 

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция устанавливает новое наименование характеристики по значениям свойств.
//
// Параметры:
//  КоллекцияЗначенийСвойств - коллекция значений, имеющая свойство Значение.
//
// Возвращаемое значение:
//  Строка - сформированное наименование.
//
Функция СформироватьНаименование(КоллекцияЗначенийСвойств) Экспорт

	Строка = "";

	Для каждого ЭлементКоллекции Из КоллекцияЗначенийСвойств Цикл
		Если НЕ ЗначениеНеЗаполнено(ЭлементКоллекции.Значение) Тогда
			Строка = Строка + ЭлементКоллекции.Значение + ", ";
		КонецЕсли;
	КонецЦикла;

	Строка = Лев(Строка, СтрДлина(Строка) - 2);

	Если ПустаяСтрока(Строка) Тогда
		Строка = "<Свойства не назначены>";
	КонецЕсли;

	Возврат Строка;

КонецФункции
