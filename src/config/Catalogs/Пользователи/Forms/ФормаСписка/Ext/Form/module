Перем мТаблицаПрав;

Процедура СправочникСписокПриАктивизацииСтроки(Элемент)
	
	ЭлементыФормы.СправочникДерево.ТекущаяСтрока = ЭлементыФормы.СправочникСписок.ТекущийРодитель;
	
КонецПроцедуры

Процедура СправочникСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки <> Неопределено Тогда
		
		Если НЕ (ДанныеСтроки.Ссылка.ЭтоГруппа) Тогда
			НайденаяСтрока = мТаблицаПрав.Найти(ДанныеСтроки.Код, "Код");
			
			Если НайденаяСтрока <> Неопределено Тогда
				
				Если НЕ ЗначениеНеЗаполнено(НайденаяСтрока.ЕстьПароль) Тогда
					
					Если НайденаяСтрока.ЕстьПароль Тогда
						
						ОформлениеСтроки.Ячейки.ПР.ЗначениеКартинки = БиблиотекаКартинок.ПользовательСАутентификацией;
						
					Иначе
						
						ОформлениеСтроки.Ячейки.ПР.ЗначениеКартинки = БиблиотекаКартинок.Пользователь;
						
					КонецЕсли;
					
					ОформлениеСтроки.Ячейки.ПР.ОтображатьКартинку = Истина;
					ОформлениеСтроки.Ячейки.ПР.ИндексКартинки     = 0;
					
					
					Если НайденаяСтрока.ВиденВСписке Тогда
						
						ОформлениеСтроки.Ячейки.ПК.ОтображатьКартинку = Ложь;
						ОформлениеСтроки.Ячейки.ПК.ИндексКартинки     = 1;
						
					Иначе
						
						ОформлениеСтроки.Ячейки.ПК.ОтображатьКартинку = Истина;
						ОформлениеСтроки.Ячейки.ПК.ИндексКартинки     = 0;
						
					КонецЕсли;
					
				Иначе
					
					ОформлениеСтроки.Ячейки.ПР.ЗначениеКартинки = БиблиотекаКартинок.Остановить;
					ОформлениеСтроки.Ячейки.ПР.ОтображатьКартинку = Истина;
					ОформлениеСтроки.Ячейки.ПР.ИндексКартинки     = 0;
					ОформлениеСтроки.Ячейки.ПК.ОтображатьКартинку = Ложь;
					
					
				КонецЕсли; 
				
			КонецЕсли;
			
			Если ДанныеСтроки.Ссылка.Заблокирован Тогда
				
				ОформлениеСтроки.ЦветТекста = WebЦвета.Красный;
				
			КонецЕсли; 
			
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь,
	|	Пользователи.Ссылка.Код КАК Код
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.ЭтоГруппа = ЛОЖЬ";	
	
	мТаблицаПрав = Запрос.Выполнить().Выгрузить();
	мТаблицаПрав.Колонки.Добавить("ЕстьПароль");
	мТаблицаПрав.Колонки.Добавить("ВиденВСписке");
	
	Для каждого СтрокаТаблицы Из мТаблицаПрав Цикл
		
		НайденныйПользователь = Неопределено;
		
		Попытка
			
			НайденныйПользователь = ПользователиИнформационнойБазы.НайтиПоИмени(СокрЛП(СтрокаТаблицы.Код));
			
		Исключение
			
			НайденныйПользователь = Неопределено;
			
		КонецПопытки; 
		
		
		Если НайденныйПользователь <> Неопределено Тогда
			
			СтрокаТаблицы.ВиденВСписке = НайденныйПользователь.ПоказыватьВСпискеВыбора;
			СтрокаТаблицы.ЕстьПароль   = НайденныйПользователь.ПарольУстановлен;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	УстановитьОтборСправочникаПользователи(СправочникСписок.Отбор, ЭлементыФормы.СправочникСписок);
	
КонецПроцедуры


мТаблицаПрав = Новый ТаблицаЗначений;
мТаблицаПрав.Очистить();