
Процедура КоманднаяПанельСписокГруппыДействиеПодбор(Кнопка)

	Если ЭтоНовый() Тогда

		Ответ = Вопрос("Элемент еще не записан. Записать?", РежимДиалогаВопрос.ОКОтмена);

		Если Ответ = КодВозвратаДиалога.ОК Тогда

			Если Не ЗаписатьВФорме() Тогда

				Возврат;
	
			КонецЕсли;

		Иначе

			Возврат;

		КонецЕсли;

	КонецЕсли;

	ДействиеПодбор();

КонецПроцедуры // КоманднаяПанельСписокГруппыДействиеПодбор()

// Процедура вызывает сервисный механизм для подбора номеклатуры в табличную часть.
//
// Параметры:
//  ТабличнаяЧасть - табличная часть, в которую осуществляется подбор.
//
Процедура ДействиеПодбор()

	СписокВидовПодбора = Новый СписокЗначений();
	СписокВидовПодбора.Добавить(, "По справочнику");

	СтруктураПараметровПодбора = Новый Структура();
	
	СтруктураПараметровПодбора.Вставить("Команда",             "");
	СтруктураПараметровПодбора.Вставить("СписокВидовПодбора" , СписокВидовПодбора);
	СтруктураПараметровПодбора.Вставить("Заголовок",           "Подбор номенклатуры в номенклатурную группу " + Наименование);
	СтруктураПараметровПодбора.Вставить("ЕстьКоличество",      Ложь);

	// Открываем форму подбора.
	ФормаПодбора = Обработки.ПодборНоменклатуры.ПолучитьФорму("ОсновнаяФорма", ЭтаФорма, ЭтаФорма);
	ФормаПодбора.мИспользоватьСерии                          = Ложь;
	ФормаПодбора.мИспользоватьХарактеристики                 = Ложь;
	ФормаПодбора.ОбработкаОбъект.СтруктураИсходныхПараметров = СтруктураПараметровПодбора;
	ФормаПодбора.Открыть();

КонецПроцедуры // ДействиеПодбор(

Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)
	
	Если ТипЗнч(ЗначениеВыбора) = Тип("Структура") Тогда
		
		ОбработкаПодбора(ЗначениеВыбора);
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаВыбора()

Процедура ОбработкаПодбора(ЗначениеВыбора)

	Номенклатура = ЗначениеВыбора.Номенклатура;
	
	Если Номенклатура.ЭтоГруппа Тогда
		
		Предупреждение("В состав номенклатурной группы могут включаться только элементы номенклатуры!");
		
	Иначе
		НовСтрСписокНоменклатуры = СписокНоменклатуры.Добавить();
		НовСтрСписокНоменклатуры.КодНоменклатуры = Номенклатура.Код;
		НовСтрСписокНоменклатуры.Номенклатура    = Номенклатура;
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаПодбора()

Процедура СписокНоменклатурыНоменклатураПриИзменении(Элемент)
	
	Элементыформы.СписокНоменклатуры.ТекущаяСтрока.КодНоменклатуры = Элемент.Значение.Код;
	
КонецПроцедуры

Процедура СписокНоменклатурыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	Если ДанныеСтроки.Номенклатура.ЭтоГруппа Тогда
		ОформлениеСтроки.Шрифт = Новый Шрифт(,, Истина);
	КонецЕсли;

КонецПроцедуры



