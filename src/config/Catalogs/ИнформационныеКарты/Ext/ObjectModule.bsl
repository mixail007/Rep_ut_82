
Процедура ПередЗаписью(Отказ)
	
	Если Не ОбменДанными.Загрузка Тогда
		
		Если Не ЭтоГруппа Тогда
			
			Если ТипКарты = Перечисления.ТипыИнформационныхКарт.Дисконтная Тогда
				
				Если ВидСкидки = Перечисления.ВидыСкидокИнформационныхКарт.Накопительная Тогда
					
					Если ПроцентСкидки <> 0 Тогда
						ПроцентСкидки = 0;
					КонецЕсли; 
					
				Иначе
					
					Если СтатусДисконтнойКарты = Перечисления.СтатусыДисконтнойКарты.Активна
						И ПроцентСкидки = 0 Тогда
						СообщитьОбОшибке("Запись информационной карты """ + Наименование + """ (" + Код + "): не заполнен процент скидки. Элемент не записан!", Отказ);
					КонецЕсли; 
					
					Если Не РазрешитьРасширенныеОперацииСДисконтнымиКартами() Тогда
						
						Если ПроцентСкидки <> Константы.ПроцентСкидкиVIPКарты.Получить() Тогда
							СообщитьОбОшибке("Запись информационной карты """ + Наименование + """ (" + Код + "): процент скидки не равен текущему проценту скидки VIP-карты. Элемент не записан!", Отказ);
						КонецЕсли; 
						
						Если ЗначениеНеЗаполнено(Основание) Тогда
							СообщитьОбОшибке("Запись информационной карты """ + Наименование + """ (" + Код + "): для VIP-карты не указана карта основание. Элемент не записан!", Отказ);
						КонецЕсли; 
						
						Если Не (Основание.ТипКарты = Перечисления.ТипыИнформационныхКарт.Дисконтная
							И Основание.ВидСкидки = Перечисления.ВидыСкидокИнформационныхКарт.Накопительная) Тогда
							СообщитьОбОшибке("Запись информационной карты """ + Наименование + """ (" + Код + "): карта основание для VIP-карты не дисконтная или не накопительная. Элемент не записан!", Отказ);
						КонецЕсли; 
						
					КонецЕсли; 
				КонецЕсли; 
				
				Если Не яштАдминистративныеФункцииДоступны() Тогда
					
					Если Не ЗначениеНеЗаполнено(ВладелецКарты) Тогда
						СообщитьОбОшибке("Запись информационной карты """ + Наименование + """ (" + Код + "): поле ""Владелец карты"" не должно заполняться для дисконтной карты. Элемент не записан!", Отказ);
					КонецЕсли; 
					
					Если ЭтоПрефиксКода Тогда
						СообщитьОбОшибке("Запись информационной карты """ + Наименование + """ (" + Код + "): флаг ""Это префикс кода"" не может быть установлен. Элемент не записан!", Отказ);
					КонецЕсли; 
					
				КонецЕсли; 
				
				Если Ссылка.СтатусДисконтнойКарты = Перечисления.СтатусыДисконтнойКарты.Неактивна
					И СтатусДисконтнойКарты = Перечисления.СтатусыДисконтнойКарты.Активна Тогда
					
					ДатаАктивации = ТекущаяДата();
					
				КонецЕсли; 
				
			КонецЕсли; 
			
		КонецЕсли; 
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		обЗаписатьПротоколИзменений(ЭтотОбъект);
		
	КонецЕсли; 
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если Не ОбменДанными.Загрузка Тогда
	
		Если Не ЭтоГруппа Тогда
		
			// аннулировать карту-основание
			Если Не ЗначениеНеЗаполнено(Основание)
				И Основание.СтатусДисконтнойКарты <> Перечисления.СтатусыДисконтнойКарты.Аннулирована Тогда
				
				врОб = Основание.ПолучитьОбъект();
				врОб.СтатусДисконтнойКарты = Перечисления.СтатусыДисконтнойКарты.Аннулирована;
				Попытка
					врОб.Записать();
				Исключение
					СообщитьОбОшибке("Запись информационной карты """ + Наименование + """ (" + Код + "): не удалось аннулировать карту-основание. Элемент не записан! Текст ошибки: " + ОписаниеОшибки(), Отказ);
					Возврат;
				КонецПопытки;
				
			КонецЕсли; 

		КонецЕсли; 
	
	КонецЕсли; 
	
КонецПроцедуры
