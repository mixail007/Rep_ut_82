Перем мОбновитьФайлОбработки, мТекущаяСтрока;

Процедура ПринадлежностьПередНачаломДобавления(Элемент, Отказ, Копирование)
	Отказ = Истина;
	Форма = ПолучитьФорму("ФормаВыбораПринадлежности", Элемент);
	Форма.РежимВыбора = Истина;
	Форма.УжеВыбранные = СтуктураЗаполненных();
	мТекущаяСтрока = Неопределено;
	Если Форма.Открыта() Тогда
		Форма.СписокДокументов.Очистить();
	КонецЕсли;
	Форма.Открыть();
КонецПроцедуры


Процедура ПринадлежностьОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЕСли мТекущаяСтрока = Неопределено Тогда
		НоваяСтрока = Принадлежность.Добавить();
	Иначе
		НоваяСтрока = мТекущаяСтрока;
	КонецЕсли;	
		
	//НоваяСтрока.ИмяОбъектаМетаданных = ВыбранноеЗначение.ПолноеИмя();
	Если Метаданные.Документы.Содержит(ВыбранноеЗначение) Тогда
		НоваяСтрока.СсылкаОбъекта = Документы[ВыбранноеЗначение.Имя].ПустаяСсылка();
		НоваяСтрока.ПредставлениеОбъекта = "Документ """ + ?(ВыбранноеЗначение.Синоним="", ВыбранноеЗначение.Имя, ВыбранноеЗначение.Синоним)+"""";
	ИначеЕсли Метаданные.Справочники.Содержит(ВыбранноеЗначение) Тогда
		НоваяСтрока.СсылкаОбъекта = Справочники[ВыбранноеЗначение.Имя].ПустаяСсылка();
		НоваяСтрока.ПредставлениеОбъекта = "Справочник """ + ?(ВыбранноеЗначение.Синоним="", ВыбранноеЗначение.Имя, ВыбранноеЗначение.Синоним)+"""";
	КонецЕслИ;	
	
КонецПроцедуры

Функция СтуктураЗаполненных(ТекущийОбъект = Неопределено)
	Результат = Новый Соответствие;
	Для Каждого СтрокаПринадлежности ИЗ Принадлежность Цикл		
		ЕСли ТекущийОбъект = Неопределено Тогда
			Результат.Вставить(СтрокаПринадлежности.СсылкаОбъекта.Метаданные(), Истина);
		ИначеЕсли ТекущийОбъект <> Неопределено И СтрокаПринадлежности.СсылкаОбъекта <> ТекущийОбъект Тогда
			Результат.Вставить(СтрокаПринадлежности.СсылкаОбъекта.Метаданные(), Истина);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

Процедура ПринадлежностьПредставлениеОбъектаНачалоВыбора(Элемент, СтандартнаяОбработка)
	Форма = ПолучитьФорму("ФормаВыбораПринадлежности", ЭлементыФормы.Принадлежность);
	Форма.РежимВыбора = Истина;
	Форма.УжеВыбранные = СтуктураЗаполненных(ЭлементыФормы.Принадлежность.ТекущиеДанные.СсылкаОбъекта);
	мТекущаяСтрока = ЭлементыФормы.Принадлежность.ТекущаяСтрока;
	Если Форма.Открыта() Тогда
		Форма.СписокДокументов.Очистить();
	КонецЕсли;
	Форма.Открыть();
КонецПроцедуры

Процедура ПередЗаписью(Отказ)	
	Если мОбновитьФайлОбработки Тогда
		Если ИмяФайлаВнешнейОбработки = "" Тогда
			Предупреждение("Не выбран файл внешней обработки");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		ХранилищеВнешнейОбработки = Новый ХранилищеЗначения(Новый ДвоичныеДанные(ИмяФайлаВнешнейОбработки));
	КонецЕсли;
КонецПроцедуры

Процедура ВыборФайлаНажатие(Элемент)
	СтандартнаяОбработка = Ложь;
	ДиалогФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогФайла.Фильтр = "Внещняя обработка(*.epf)|*.epf";
	ЕСли ДиалогФайла.Выбрать() Тогда
		ИмяФайлаВнешнейОбработки = ДиалогФайла.ПолноеИмяФайла;
		Попытка
			ВнешняяОбработка = ВнешниеОбработки.Создать(ИмяФайлаВнешнейОбработки);
			Если ЗначениеНеЗаполнено(Наименование) Тогда
				Наименование = ВнешняяОбработка.Метаданные().Синоним;
			КонецЕсли;
		Исключение
			Предупреждение("Выбранный файл не является внешней обработкой.
						   |Либо, данная обработка не предназначена для
						   |запуска в этой конфигурации.");
			ИмяФайлаВнешнейОбработки = "";
			Возврат
		КонецПопытки ;
		
		Если ВнешняяОбработка.Метаданные().Макеты.Найти("Параметры_Авторегистрации")<>Неопределено Тогда
		 	Макет = ВнешняяОбработка.ПолучитьМакет("Параметры_Авторегистрации");
			СписокПринадлежности = Новый СписокЗначений;
			Для индекс = 1 по Макет.ВысотаТаблицы Цикл
				Попытка
					ВидОбъектаСтр = Макет.Область(индекс, 1, индекс, 1).Текст;
					ЕСли Лев(ВидОбъектаСтр, Найти(ВидОбъектаСтр, ".")-1) = "Документы" Тогда
						Попытка
							СписокПринадлежности.Добавить(Документы[Сред(ВидОбъектаСтр, Найти(ВидОбъектаСтр, ".")+1)].ПустаяСсылка());
						Исключение
							
						КонецПопытки;
					ИначеЕсли Лев(ВидОбъектаСтр, Найти(ВидОбъектаСтр, ".")-1)  = "Справочники" Тогда
						Попытка
							СписокПринадлежности.Добавить(Справочники[Сред(ВидОбъектаСтр, Найти(ВидОбъектаСтр, ".")+1)].ПустаяСсылка());
						Исключение
						КонецПопытки;						
					КонецЕсли;
		
				Исключение					
				КонецПопытки;
			КонецЦикла;
			Если СписокПринадлежности.Количество() > 0 Тогда
				Ответ = Вопрос("Указанная внешняя печатная форма содержит параметры авторегистрации. Использовать их при регистрации?", РежимДиалогаВопрос.ДаНет);
				Если Ответ = КодВозвратаДиалога.Да Тогда
					Для Каждого ЭлементСписка Из СписокПринадлежности Цикл
						Если Принадлежность.Найти(ЭлементСписка.Значение, "СсылкаОбъекта") = Неопределено Тогда
							НоваяСтрока = Принадлежность.Добавить();			
							НоваяСтрока.СсылкаОбъекта = ЭлементСписка.Значение;
							МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипЗнч(ЭлементСписка.Значение));
							Если Метаданные.Документы.Содержит(МетаданныеОбъекта) Тогда
								НоваяСтрока.ПредставлениеОбъекта = "Документ """ + ?(МетаданныеОбъекта.Синоним = "", МетаданныеОбъекта.Имя, МетаданныеОбъекта.Синоним) + """";
							ИначеЕсли Метаданные.Справочники.Содержит(МетаданныеОбъекта) Тогда
								НоваяСтрока.ПредставлениеОбъекта = "Справочник """ + ?(МетаданныеОбъекта.Синоним = "", МетаданныеОбъекта.Имя, МетаданныеОбъекта.Синоним) + """";
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕСли;
		
		ЭлементыФормы.ВыборФайла.Заголовок = "Изменить файл внешней обработки";		
		ЭлементыФормы.ВыборФайла.ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
		мОбновитьФайлОбработки = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()	
	Если ЭтоНовый() Тогда
		ЭлементыФормы.ВыборФайла.Заголовок = "Укажите файл внешней обработки";		
		ЭлементыФормы.ВыборФайла.ЦветТекста = ЦветаСтиля.ТекстПредупреждающейНадписи;
		мОбновитьФайлОбработки = Истина;
	КонецЕсли;
	Если НЕ ЭтоНовый() Тогда
		
		УстановитьЭлементыИсторииКнопка(Ссылка, ЭтаФорма);
		
	КонецЕсли; 

	
КонецПроцедуры

Процедура ВыводИстории(Элемент)
	
	ВывестиИсторию(Ссылка, ЭтаФорма);
	
КонецПроцедуры
	
	

Процедура СохранитьВнешнийФайлНажатие(Элемент)
	ДвоичныеДанные = ХранилищеВнешнейОбработки.Получить();
	Если ДвоичныеДанные = Неопределено Тогда
		Предупреждение("Внешний файл отсутствует в хранилище"); 
		Возврат;
	КонецЕсли; 
	ДиалогФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогФайла.Фильтр = "Внешняя обработка(*.epf)|*.epf";
	ДиалогФайла.ПолноеИмяФайла = Наименование;
	Если ДиалогФайла.Выбрать() Тогда
		ИмяФайла = ДиалогФайла.ПолноеИмяФайла;
		Попытка
			ДвоичныеДанные.Записать(ИмяФайла);
		Исключение
			Предупреждение("Внешний файл не сохранен
			|"+ОписаниеОшибки()); 
		КонецПопытки; 
	КонецЕсли;
КонецПроцедуры

//Процедура ПринадлежностьПредставлениеОбъектаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
//	СтандартнаяОбработка = Ложь;
//	Если мТекущаяСтрока = Неопределено Тогда
//		Возврат;
//	КонецЕсли;		
//		
//КонецПроцедуры

мОбновитьФайлОбработки = Ложь;