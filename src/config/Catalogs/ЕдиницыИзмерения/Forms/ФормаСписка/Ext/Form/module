////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()

	ЕдиницаИзмеренияВеса = Константы.ЕдиницаИзмеренияВеса.Получить();

	Если ЗначениеНеЗаполнено(ЕдиницаИзмеренияВеса) Тогда
		ЭлементыФормы.СписокЕдиницыИзмерения.Колонки.Вес.ТекстШапки = "Вес (не назначена единица измерения)";
	Иначе
		ЭлементыФормы.СписокЕдиницыИзмерения.Колонки.Вес.ТекстШапки = "Вес (" + ЕдиницаИзмеренияВеса.Наименование +")";
	КонецЕсли;

	ЕдиницаИзмеренияОбъема = Константы.ЕдиницаИзмеренияОбъема.Получить();

	Если ЗначениеНеЗаполнено(ЕдиницаИзмеренияОбъема) Тогда
		ЭлементыФормы.СписокЕдиницыИзмерения.Колонки.Объем.ТекстШапки = "Объем (не назначена единица измерения)";
	Иначе
		ЭлементыФормы.СписокЕдиницыИзмерения.Колонки.Объем.ТекстШапки = "Объем (" + ЕдиницаИзмеренияОбъема.Наименование +")";
	КонецЕсли;

КонецПроцедуры

// Обработчик события ПриНачалеРедактирования элемента СписокЕдиницыИзмерения.
//
Процедура СписокЕдиницыИзмеренияПриНачалеРедактирования(Элемент, НоваяСтрока)

	Если НоваяСтрока Тогда

		Если Элемент.ТекущиеДанные.Коэффициент = 0 Тогда
			Элемент.ТекущиеДанные.Коэффициент = 1;
		КонецЕсли;

		Если ЗначениеНеЗаполнено(Элемент.ТекущиеДанные.ЕдиницаПоКлассификатору) Тогда
			Элемент.ТекущиеДанные.ЕдиницаПоКлассификатору = ПолучитьЗначениеПоУмолчанию(глТекущийПользователь, "ОсновнаяЕдиницаПоКлассификатору");
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

// Обработчик события ПриИзменении элемента СписокЕдиницыИзмерения.ЕдиницаПоКлассификатору.
//
Процедура СписокЕдиницыИзмеренияЕдиницаПоКлассификаторуПриИзменении(Элемент)

	ТекущиеДанные = ЭлементыФормы.СписокЕдиницыИзмерения.ТекущиеДанные;

	Если ПустаяСтрока(ТекущиеДанные.Наименование) Тогда
		ТекущиеДанные.Наименование = ТекущиеДанные.ЕдиницаПоКлассификатору.Наименование;
	КонецЕсли;

КонецПроцедуры

 // Обработчик события ПриВыводеСтроки элемента СписокЕдиницыИзмерения.
//
Процедура СписокЕдиницыИзмеренияПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Если ДанныеСтроки.Владелец = Неопределено Тогда
		ОформлениеСтроки.Ячейки.Коэффициент.ТолькоПросмотр = Ложь;
	Иначе
		ОформлениеСтроки.Ячейки.Коэффициент.ТолькоПросмотр = (ДанныеСтроки.Ссылка = ДанныеСтроки.Владелец.ЕдиницаХраненияОстатков);
	КонецЕсли;

КонецПроцедуры




