
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ


// Процедура заполняет заголовок элемента формы "ЕдиницаУсловия".
//
Процедура ЗаполнитьНадписьЕдиницаУсловия()

	Если Условие = Перечисления.УсловияСкидкиНаценки.ПоКоличествуТовара Тогда
		ЭлементыФормы.НадписьЕдиницаУсловия.Заголовок = "ед. хранения";
	ИначеЕсли Условие = Перечисления.УсловияСкидкиНаценки.ПоСуммеДокумента Тогда
		ЭлементыФормы.НадписьЕдиницаУсловия.Заголовок = "" + ?(Валюта = Справочники.Валюты.ПустаяСсылка(), "<валюта не выбрана>", Валюта);
	Иначе
		ЭлементыФормы.НадписьЕдиницаУсловия.Заголовок = "";
	КонецЕсли;

КонецПроцедуры

// Процедура заполняет время по табличной части "ВремяПоДнямНедели".
//
Процедура ИзменитьВремя(Элемент)
	Если ОбщееВремяНачала > ОбщееВремяОкончания Тогда
		Если Элемент.Данные = "ОбщееВремяОкончания" Тогда
			ОбщееВремяНачала = ОбщееВремяОкончания;
		Иначе
			ОбщееВремяОкончания = ОбщееВремяНачала;
		КонецЕсли;
	КонецЕсли;
	Для каждого СтрокаДня Из ВремяПоДнямНедели Цикл
		СтрокаДня.ВремяНачала = ОбщееВремяНачала;
		СтрокаДня.ВремяОкончания = ОбщееВремяОкончания;
		СтрокаДня.Выбран = Истина;
	КонецЦикла;
КонецПроцедуры

// Процедура изменяет тип элемента формы "ЗначениеУсловия".
//
Процедура ИзменитьТипЗначенияУсловия()

	Если Условие = Перечисления.УсловияСкидкиНаценки.ПоВидуОплаты Тогда
		ЭлементыФормы.ЗначениеУсловия.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ВидыОплатЧекаККМ");
	Иначе
		КвалификаторыЧисла = Новый КвалификаторыЧисла(15,2,ДопустимыйЗнак.Неотрицательный);
		ЭлементыФормы.ЗначениеУсловия.ТипЗначения = Новый ОписаниеТипов("Число",КвалификаторыЧисла);
	КонецЕсли;
	Если Условие = Перечисления.УсловияСкидкиНаценки.БезУсловий Тогда
		ЗначениеУсловия = 0;
		ЭлементыФормы.ЗначениеУсловия.Доступность = Ложь;
	Иначе
		ЭлементыФормы.ЗначениеУсловия.Доступность = Истина;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	ЭлементыФормы.Условие.СписокВыбора.Добавить(Перечисления.УсловияСкидкиНаценки.ПоСуммеДокумента);
	ЭлементыФормы.Условие.СписокВыбора.Добавить(Перечисления.УсловияСкидкиНаценки.ПоКоличествуТовара);
	ЭлементыФормы.Условие.СписокВыбора.Добавить(Перечисления.УсловияСкидкиНаценки.ПоВидуОплаты);
	ЭлементыФормы.Условие.СписокВыбора.Добавить(Перечисления.УсловияСкидкиНаценки.БезУсловий);
	ЭлементыФормы.Условие.Значение = Условие;

КонецПроцедуры

// Обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()

	Если ЭтоНовый() Тогда
		Для Сч=0 по 6 Цикл
			СтрокаДень = ВремяПоДнямНедели.Добавить();
			СтрокаДень.ДеньНедели = Перечисления.ДниНедели[Сч];
			СтрокаДень.ВремяНачала = Дата('00010101000000');
			СтрокаДень.ВремяОкончания = Дата('00010101235959');
		КонецЦикла;
		ОбщееВремяНачала = Дата('00010101000000');
		ОбщееВремяОкончания = Дата('00010101235959');
		Условие = Перечисления.УсловияСкидкиНаценки.ПоСуммеДокумента;
		ЭлементыФормы.Условие.Значение = Условие;
	КонецЕсли;

	Если Условие = Перечисления.УсловияСкидкиНаценки.ПоВидуОплаты Тогда
		ЭлементыФормы.ЗначениеУсловия.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ВидыОплатЧекаККМ");
	Иначе
		КвалификаторыЧисла = Новый КвалификаторыЧисла(15,2,ДопустимыйЗнак.Неотрицательный);
		ЭлементыФормы.ЗначениеУсловия.ТипЗначения = Новый ОписаниеТипов("Число",КвалификаторыЧисла);
	КонецЕсли;
	Если Условие = Перечисления.УсловияСкидкиНаценки.БезУсловий Тогда
		//ЗначениеУсловия = 0;
		ЭлементыФормы.ЗначениеУсловия.Доступность = Ложь;
	Иначе
		ЭлементыФормы.ЗначениеУсловия.Доступность = Истина;
	КонецЕсли;
	ЭлементыФормы.ЗначениеУсловия.Значение = ЗначениеУсловия;

	ЗаполнитьНадписьЕдиницаУсловия();

КонецПроцедуры

// Обработчик события "ПередЗаписью" формы.
//
Процедура ПередЗаписью(Отказ)

	ЗначениеУсловия = ЭлементыФормы.ЗначениеУсловия.Значение;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Обработчик события ПриИзменении элемента формы ОбщееВремяНачало.
//
Процедура ОбщееВремяНачалоПриИзменении(Элемент)
	ИзменитьВремя(Элемент);
КонецПроцедуры

// Обработчик события ПриИзменении элемента формы ОбщееВремяОкончание.
//
Процедура ОбщееВремяОкончаниеПриИзменении(Элемент)
	ИзменитьВремя(Элемент);
КонецПроцедуры

// Обработчик события ПриИзменении элемента формы Условие.
//
Процедура УсловиеПриИзменении(Элемент)

	ИзменитьТипЗначенияУсловия();
	ЗаполнитьНадписьЕдиницаУсловия();

КонецПроцедуры

// Обработчик события ПриИзменении элемента формы ВремяПоДнямНеделиВремяНачала.
//
Процедура ВремяПоДнямНеделиВремяНачалаПриИзменении(Элемент)
	ТекДень = ЭлементыФормы.ВремяПоДнямНедели.ТекущиеДанные;
	Если ТекДень.ВремяНачала>ТекДень.ВремяОкончания Тогда
		ТекДень.ВремяОкончания = ТекДень.ВремяНачала;
	КонецЕсли;
КонецПроцедуры

// Обработчик события ПриИзменении элемента формы ВремяПоДнямНеделиВремяОкончания.
//
Процедура ВремяПоДнямНеделиВремяОкончанияПриИзменении(Элемент)
	ТекДень = ЭлементыФормы.ВремяПоДнямНедели.ТекущиеДанные;
	Если ТекДень.ВремяНачала>ТекДень.ВремяОкончания Тогда
		ТекДень.ВремяНачала = ТекДень.ВремяОкончания;
	КонецЕсли;
КонецПроцедуры

// Обработчик события ПриИзменении элемента формы ВалютаПриИзменении.
//
Процедура ВалютаПриИзменении(Элемент)

	ЗаполнитьНадписьЕдиницаУсловия();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Обработчик события Нажатие элемента формы КоманднаяПанельВремяПоДнямНедели.ОтметитьВсе.
//
Процедура КоманднаяПанельВремяПоДнямНеделиДействиеОтметитьВсе(Кнопка)
	Для каждого День из ВремяПоДнямНедели Цикл
		День.Выбран = Истина;
	КонецЦикла;
КонецПроцедуры

// Обработчик события Нажатие элемента формы КоманднаяПанельВремяПоДнямНедели.СнятьВсе.
//
Процедура КоманднаяПанельВремяПоДнямНеделиДействиеСнятьВсе(Кнопка)
	Для каждого День из ВремяПоДнямНедели Цикл
		День.Выбран = Ложь;
	КонецЦикла;
КонецПроцедуры

Процедура ВремяПоДнямНеделиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

Процедура ВремяПоДнямНеделиПередНачаломДобавления(Элемент, Отказ, Копирование)
	Отказ = Истина;
КонецПроцедуры


















