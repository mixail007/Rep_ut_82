
////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура делает доступной кнопку удаления задачи непосредственно создателю задачи
//
Процедура ОбновитьКнопкуВыполнение()
	
	Если ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока <> Неопределено Тогда
		
		ЭлементыФормы.ДействияФормы.Кнопки.Выполнение.Доступность = Истина;
		ЭлементыФормы.ДействияФормы.Кнопки.Выполнение.Доступность = (ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока.Ссылка.Инициатор = ПараметрыСеанса.ТекущийПользователь) ИЛИ (ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока.Ссылка.Исполнитель = ПараметрыСеанса.ТекущийПользователь);
		Если ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока.Ссылка.Выполнена Тогда
			ЭлементыФормы.ДействияФормы.Кнопки.Выполнение.Подсказка = "Снять пометку выполнения с текущей задачи";
		Иначе
			ЭлементыФормы.ДействияФормы.Кнопки.Выполнение.Подсказка = "Пометить текущую задачу как выполненную";
		КонецЕсли;
		
	Иначе
		ЭлементыФормы.ДействияФормы.Кнопки.Выполнение.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Процедура управляет отображением списка задач:
// устанавлмивает отбор только активных задач или сбрасывает этот отбор
//
Процедура УстановитьОтборАктивных()
	
	Если ТолькоАктивные Тогда
 		ЭтаФорма.Отбор.Выполнена.Установить(Ложь);
  		ЭтаФорма.Отбор.ПометкаУдаления.Установить(Ложь); // удаленные - как выполненные!
 	Иначе //выключается отбор - выдны все!
		ЭтаФорма.Отбор.Выполнена.Использование = Ложь;
  		ЭтаФорма.Отбор.ПометкаУдаления.Использование = Ложь;
	КонецЕсли;

	ЭлементыФормы.ДействияФормы.Кнопки.ОтборАктивных.Пометка = ТолькоАктивные;
	
КонецПроцедуры

// Процедура управляет отображением списка задач:
// устанавливает отбор только задач, пставленных текущим пользователем или
// возвращает к отбору по исполнителю-текущему пользователю
//
Процедура УстановитьОтборПоставленных()
	
	Если ТолькоПоставленные Тогда
		
		ЭтаФорма.Отбор.Исполнитель.ВидСравнения  = ВидСравнения.Равно;
		ЭтаФорма.Отбор.Исполнитель.Использование = Истина;
		ЭтаФорма.Отбор.Исполнитель.Значение      = ПараметрыСеанса.ТекущийПользователь;
		
		ЭтаФорма.Отбор.Инициатор.Использование = ЛОЖЬ;
		
		ЭлементыФормы.ДействияФормы.Кнопки.ОтборПоставленных.Подсказка = "Отобрать задачи по мне как по исполнителю";
		
		ЭлементыФормы.ЗадачаСписок.Колонки.Исполнитель.Видимость = ложь;
		ЭлементыФормы.ЗадачаСписок.Колонки.Инициатор.Видимость = истина;
		
	Иначе
		//только другим!
		ЭтаФорма.Отбор.Исполнитель.ВидСравнения  = ВидСравнения.НЕРавно;
		ЭтаФорма.Отбор.Исполнитель.Значение      = ПараметрыСеанса.ТекущийПользователь;
		ЭтаФорма.Отбор.Исполнитель.Использование = Истина;
		
		ЭтаФорма.Отбор.Инициатор.ВидСравнения  = ВидСравнения.Равно;
		ЭтаФорма.Отбор.Инициатор.Использование = Истина;
		ЭтаФорма.Отбор.Инициатор.Значение      = ПараметрыСеанса.ТекущийПользователь;
				
		ЭлементыФормы.ДействияФормы.Кнопки.ОтборПоставленных.Подсказка = "Отобрать поставленные мной задачи для контроля прочих исполнителей";
		
		ЭлементыФормы.ЗадачаСписок.Колонки.Исполнитель.Видимость = истина;
		ЭлементыФормы.ЗадачаСписок.Колонки.Инициатор.Видимость = ложь;
		
	КонецЕсли;
	
	ЭлементыФормы.ДействияФормы.Кнопки.ОтборПоставленных.Пометка = ТолькоПоставленные;
	
КонецПроцедуры

// Процедура управляет отображением панели с деталями задачи
//
Процедура ПоказатьСпрятатьПанельДеталей()
	
	Если ПоказатьДетали Тогда
		
		ЭлементыФормы.ДействияФормы.Кнопки.Детали.Подсказка = "Скрыть детализацию по задаче";
		
		ЭлементыФормы.РазделительДеталей.Свертка = РежимСверткиЭлементаУправления.Нет;
		ЭлементыФормы.ПанельДеталей.Свертка      = РежимСверткиЭлементаУправления.Нет;
		
		Если ЭлементыФормы.ПанельДеталей.Высота = 0 Тогда
			
			ТекущийВерх = ЭлементыФормы.ПанельДеталей.Верх;
			
			ЭлементыФормы.ПанельДеталей.Верх   = ТекущийВерх - 155;
			ЭлементыФормы.ПанельДеталей.Высота = ТекущийВерх - ЭлементыФормы.ПанельДеталей.Верх;
			
		КонецЕсли;
		
		ЭлементыФормы.РазделительДеталей.УстановитьПривязку(ГраницаЭлементаУправления.Низ, Панель, ГраницаЭлементаУправления.Низ);
		ЭлементыФормы.ПанельДеталей.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ЭлементыФормы.РазделительДеталей, ГраницаЭлементаУправления.Низ);
		
		УстановитьТекстHTMLПоля();
		
	Иначе
		
		ЭлементыФормы.ДействияФормы.Кнопки.Детали.Подсказка = "Отобразить детализацию по задаче";
		
		ЭлементыФормы.ПанельДеталей.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ЭлементыФормы.ПанельДеталей, ГраницаЭлементаУправления.Низ);
		ЭлементыФормы.РазделительДеталей.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ЭлементыФормы.ПанельДеталей, ГраницаЭлементаУправления.Верх);
		
		ЭлементыФормы.ПанельДеталей.Свертка      = РежимСверткиЭлементаУправления.Низ;
		ЭлементыФормы.РазделительДеталей.Свертка = РежимСверткиЭлементаУправления.Низ;
		
	КонецЕсли;
	
	ЭлементыФормы.ДействияФормы.Кнопки.Детали.Пометка = ПоказатьДетали;
	
КонецПроцедуры

// Функция создает блок текста в HTML-формате.
// Созданный блок представляет собой некие сведения,
// которые озаглавлены строкой текста и могут содержать как строковое описание для чтения,
// так и сведения об объекте, переход к которому будет осуществлен по гиперссылке.
// Набор создаваемых блоков используется для отображения детализации.
//
// Параметры:
//	ЗаголовокБлока - строка с заглавием блока (только строка);
//	Содержимое     - собственно содержимое: текст, либо объект для открытия по гиперссылке;
//	ШрифтФейс      - название шрифта, которым будет отображен блок;
//	ШрифтСайз      - размер шрифта
//
Функция СформироватьHTMLБлок(ЗаголовокБлока, Содержимое, ШрифтФейс = "Verdana", ШрифтСайз = "2")
	
	БлокТекст = "<tr><td><b><font face=" + ШрифтФейс + " size=" + ШрифтСайз + ">" + ЗаголовокБлока + "</font></b></td></tr>";
	
	БлокТекст = БлокТекст + "
		|<table border=""0"">
		|<tr><td>&nbsp;&nbsp;</td>
		|<td><font face=" + ШрифтФейс + " size=" + ШрифтСайз + ">" + ?(ТипЗнч(Содержимое) = Тип("Строка"), СтрЗаменить(Содержимое, Символы.ПС, "<br>"), "<a id=" + ЗначениеВСтрокуВнутр(Содержимое) + " href=about:blank>" + Строка(Содержимое) + "</a>") + "</font></td>
		|<td>&nbsp;</td>
		|</table></div>";
		
	Возврат БлокТекст;
	
КонецФункции

// Процедура собирает необходимый текст в HTML-формате и устанавливает его текстом HTML-поля
//
Процедура УстановитьТекстHTMLПоля()
	
	ТекстПоля = "
	|<HTML>
	|<HEAD>
	|<META http-equiv=Content-Type content=""text/html; charset=utf-8"">
	|</HEAD>
	|<BODY>";
	
	Если ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока <> Неопределено Тогда
		
		Если ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока.Ссылка.Оповещение Тогда
			ТекстПоля = ТекстПоля + СформироватьHTMLБлок("Оповещение:", Формат(ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока.Ссылка.СрокОповещения, "ДФ=""дддд ',' дд ММММ гггг 'г.' ЧЧ:мм"""));
		КонецЕсли;
		
		Если СтрДлина(ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока.Ссылка.Описание) > 0 Тогда
			ТекстПоля = ТекстПоля + СформироватьHTMLБлок("Детальное описание:", ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока.Ссылка.Описание);
		КонецЕсли;
		
		Если ЭтаФорма.Отбор.Инициатор.Использование Тогда
			ТекстПоля = ТекстПоля + СформироватьHTMLБлок("Исполнитель задачи:", ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока.Ссылка.Исполнитель);
		Иначе
			ТекстПоля = ТекстПоля + СформироватьHTMLБлок("Автор задачи:", ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока.Ссылка.Инициатор);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока.Ссылка.Объект) Тогда
			ТекстПоля = ТекстПоля + СформироватьHTMLБлок("Основание задачи:", ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока.Ссылка.Объект);
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстПоля = ТекстПоля + "</BODY></HTML>";
	
	ЭлементыФормы.ПолеДеталей.УстановитьТекст(ТекстПоля);
	
КонецПроцедуры

// Процедура обновления деталей
//
Процедура ОбновлениеОтображенияДеталей()
	
	Если ПоказатьДетали Тогда
		УстановитьТекстHTMLПоля();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ СПИСКА

// Процедура-обработчик добавления новой задачи
//
Процедура ЗадачаСписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа)
	
	Если НЕ Копирование Тогда
		Отказ = ИСТИНА;
	
		НоваяЗадача = Задачи.ЗадачиПользователя.СоздатьЗадачу();
		Если (ЭтаФорма.Отбор.Объект.Использование = ИСТИНА)
			И (ЭтаФорма.Отбор.Объект.ВидСравнения = ВидСравнения.Равно)
			И (ЭтаФорма.Отбор.Объект.Значение <> Неопределено) Тогда
			НоваяЗадача.Объект = ЭтаФорма.Отбор.Объект.Значение;
		КонецЕсли;
	
		ФормаЗадачи = НоваяЗадача.ПолучитьФорму( , "НоваяЗадача");
		Если ФормаЗадачи.Открыта() Тогда
			ФормаЗадачи.Активизировать();
		Иначе
			ФормаЗадачи.Открыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура-обработчик события ПриАктивизацииСтроки
//
Процедура ЗадачаСписокПриАктивизацииСтроки(Элемент)
	
	ОбновитьКнопкуВыполнение();
	Если ПоказатьДетали Тогда
		УстановитьТекстHTMLПоля();
	КонецЕсли;
	
КонецПроцедуры

// Процедура-обработчик события ПриВыводеСтроки
//
Процедура ЗадачаСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	СтрокаПоздравления = "Поздравить с Днём рождения";
	
	ОформлениеСтроки.Ячейки.Картинка.ОтображатьФлажок   = Ложь;
	ОформлениеСтроки.Ячейки.Картинка.ОтображатьКартинку = Истина;
	
//	Если ДанныеСтроки.Ссылка.СрокИсполнения < ТекущаяДата() Тогда
	Если ДанныеСтроки.СрокИсполнения < ТекущаяДата() Тогда  //15.11.2016   так быстрее
		ОформлениеСтроки.ЦветТекста = WebЦвета.Красный;
	КонецЕсли;
	
	Если ДанныеСтроки.Ссылка.ПометкаУдаления тогда
		ОформлениеСтроки.ЦветТекста = WebЦвета.СветлоСерый;
	КонецЕсли;
	
	Если ДанныеСтроки.Ссылка.Выполнена Тогда
		ОформлениеСтроки.ЦветФона = WebЦвета.СветлоСерый;
		
		Если ДанныеСтроки.Ссылка.ПамятнаяДата Тогда 
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 7;
		Иначе
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 6;
		КонецЕсли;
		
	ИначеЕсли ДанныеСтроки.Ссылка.Оповещение Тогда
		
		Если ДанныеСтроки.Ссылка.СрокОповещения < ТекущаяДата() Тогда
			
			Если ДанныеСтроки.Ссылка.ПамятнаяДата Тогда
				ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 3;
			Иначе
				ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 2;
			КонецЕсли;
			
		Иначе
			
			Если ДанныеСтроки.Ссылка.ПамятнаяДата Тогда
				ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 5;
			Иначе
				ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 4;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Если ДанныеСтроки.Ссылка.ПамятнаяДата Тогда
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 1;
		Иначе
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 0;
		КонецЕсли;
		
	КонецЕсли;
	// миронычев
	Если глТекущийПользователь = Справочники.Пользователи.НайтиПоКоду("Фролов") Тогда
		Если ТипЗнч(ДанныеСтроки.Объект) = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваров") Тогда
		
		ЭлементыФормы.ЗадачаСписок.Колонки.Контрагент.Видимость = Истина;
		ЭлементыФормы.ЗадачаСписок.Колонки.Статус.Видимость = Истина;
        ЭлементыФормы.ЗадачаСписок.Колонки.НомЗаявки.Видимость = Истина;
		
		ОформлениеСтроки.Ячейки.Контрагент.ОтображатьТекст = Истина;
		ОформлениеСтроки.Ячейки.Контрагент.Текст = ?(ЗначениеЗаполнено(ДанныеСтроки.Объект), ДанныеСтроки.Объект.Контрагент, "");
		
		ОформлениеСтроки.Ячейки.Статус.ОтображатьТекст = Истина;
		т = Неопределено; 
        Т = ДанныеСтроки.Ссылка.Товары.Найти(Перечисления.СтатусыСтрокЗаказа.НаСогласовании, "Статус");
		Если т <> Неопределено тогда
        	ОформлениеСтроки.Ячейки.Статус.Текст = "Новая";
			ОформлениеСтроки.ЦветФона = WebЦвета.СветлоЖелтыйЗолотистый;
		Иначе
			Т = ДанныеСтроки.Ссылка.Товары.Найти(Перечисления.СтатусыСтрокЗаказа.Получен, "Статус");
			Если т <> Неопределено тогда
        		ОформлениеСтроки.Ячейки.Статус.Текст = "В работе";
				ОформлениеСтроки.ЦветФона = WebЦвета.Белый;

			Иначе
				Т = ДанныеСтроки.Ссылка.Товары.Найти(Перечисления.СтатусыСтрокЗаказа.Отменен, "Статус");
				Если т <> Неопределено тогда
        			ОформлениеСтроки.Ячейки.Статус.Текст = "Отменена";
					ОформлениеСтроки.ЦветФона = WebЦвета.СветлоРозовый;

				Иначе
               		Т = ДанныеСтроки.Ссылка.Товары.Найти(Перечисления.СтатусыСтрокЗаказа.Подтвержден, "Статус");
					Если т <> Неопределено тогда
        				ОформлениеСтроки.Ячейки.Статус.Текст = "Подтверждена";
						ОформлениеСтроки.ЦветФона = WebЦвета.СветлоЗеленый;

			        КонецЕсли;
                КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ОформлениеСтроки.Ячейки.НомЗаявки.ОтображатьТекст = Истина;
  		ОформлениеСтроки.Ячейки.НомЗаявки.Текст = ?(ЗначениеЗаполнено(ДанныеСтроки.Объект), ДанныеСтроки.Объект.Номер, "");
		 КонецЕсли;
    КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура-обработчик нажатия кнопки "Выполнение"
//
Процедура ДействияФормыВыполнение(Кнопка)
	
	Если ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока <> Неопределено Тогда
		ТекущаяЗадача = ЭлементыФормы.ЗадачаСписок.ТекущаяСтрока.Ссылка.ПолучитьОбъект();
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ТекущаяЗадача.Выполнена Тогда
		
		ТекущаяЗадача.ДатаИсполнения = ?(ТекущаяЗадача.ДатаИсполнения='00010101', ТекущаяДата(), ТекущаяЗадача.ДатаИсполнения); //+++ дата выполнения
				
		// Задачи в рамках бизнес-процессов с выполнения не снимаются.
		Если ЗначениеЗаполнено(ТекущаяЗадача.БизнесПроцесс) Тогда
			Возврат;
		КонецЕсли;

		ТекстВопроса = "Вы уверены, что следует снять пометку выполнения с текущей задачи?";

	Иначе

		ТекстВопроса = "Вы уверены, что текущую задачу следует считать выполненной?";
	КонецЕсли;
		
	Выполнение = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0);
	Если Выполнение = КодВозвратаДиалога.Да Тогда
		
		Если ЗначениеЗаполнено(ТекущаяЗадача.БизнесПроцесс) Тогда
			Попытка
				ТекущаяЗадача.ВыполнитьЗадачуИнтерактивно();
				
			Исключение
			КонецПопытки;
		Иначе
			ТекущаяЗадача.Выполнена = НЕ ТекущаяЗадача.Выполнена;
			ТекущаяЗадача.Записать();
		КонецЕсли;
		ОбновитьКнопкуВыполнение();
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура-обработчик нажатия кнопки "ОтборАктивных"
//
Процедура ДействияФормыОтборАктивных(Кнопка)
	
	ТолькоАктивные = НЕ ТолькоАктивные;
	УстановитьОтборАктивных();
	
КонецПроцедуры

// Процедура-обработчик события ПриОткрытии
//
Процедура ПриОткрытии()
	
	ПоказыватьПриЗапуске = ПолучитьЗначениеПоУмолчанию(глТекущийПользователь, "ПоказыватьСписокЗадачПриЗапуске");
	ТолькоАктивные       = Истина;
	ТолькоПоставленные   = Истина;
	
	ПоказатьДетали		 = Истина;//+++
	
	//+++ только свои задачи - всегда! +++ как и в списке задач!
	Если не ( РольДоступна("ПолныеПрава") или РольДоступна("МенеджерПоЗакупкам") 
		    или РольДоступна("яштФинДиректор")  ) тогда    //27.10.2015 - для Малышев И.И. - видны все задачи
		ЭлементыФормы.ЗадачаСписок.НастройкаОтбора["Исполнитель"].Доступность = Ложь;
		ЭлементыФормы.ДействияФормы.Кнопки.ОтборПоставленных.Доступность = ЛОЖЬ;
	КонецЕсли;

	УстановитьОтборАктивных();
	УстановитьОтборПоставленных();
	
	ЭлементыФормы.ЗадачаСписок.НастройкаПорядка.Наименование.Доступность = Ложь;
	ЭлементыФормы.ЗадачаСписок.Значение.Порядок.Установить("СрокИсполнения УБЫВ");
	ПоказатьСпрятатьПанельДеталей();
	 // миронычев
	Если глТекущийПользователь = Справочники.Пользователи.НайтиПоКоду("Фролов") или глТекущийПользователь = Справочники.Пользователи.НайтиПоКоду("Миронычев А.Е.") Тогда
		ЭлементыФормы.ДействияФормы.Кнопки.ОтборЗаявок.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Процедура-обработчик закрытия формы
//
Процедура ПриЗакрытии()
	
//	УстановитьЗначениеПоУмолчанию(глТекущийПользователь, "ПоказыватьСписокЗадачПриЗапуске", ПоказыватьПриЗапуске);
	
КонецПроцедуры

// Процедура-обработчик нажатия кнопки "Детали"
//
Процедура ДействияФормыДетали(Кнопка)
	
	ПоказатьДетали = НЕ ПоказатьДетали;
	ПоказатьСпрятатьПанельДеталей();
	
КонецПроцедуры

// Процедура-обработчик события onclick HTML-поля
//
Процедура ПолеДеталейonclick(Элемент, pEvtObj)
	
	Если СтрДлина(pEvtObj.srcElement.id) > 0 Тогда
		
		ОбъектЗадачи = ЗначениеИзСтрокиВнутр(pEvtObj.srcElement.id).ПолучитьОбъект();
		ОбъектЗадачи.ПолучитьФорму().Открыть();
		pEvtObj.returnValue = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура-обработчик нажатия кнопки ОтборПоставленных
//
Процедура ДействияФормыОтборПоставленных(Кнопка)
	
	ТолькоПоставленные = НЕ ТолькоПоставленные;
	УстановитьОтборПоставленных();
	
КонецПроцедуры

// Процедура-обработчик нажатия кнопки Обновить
//
Процедура ДействияФормыОбновить(Кнопка)
	
	ОбновлениеОтображенияДеталей();
	
КонецПроцедуры

// Процедура-обработчик записи нового объекта в подчиненной форме
//
Процедура ЗадачаСписокОбработкаЗаписиНовогоОбъекта(Элемент, Объект, СтандартнаяОбработка)
	
	ОбновлениеОтображенияДеталей();
	
КонецПроцедуры

Процедура ПоказыватьПриЗапускеПриИзменении(Элемент)
	попытка
		УстановитьЗначениеПоУмолчанию(глТекущийПользователь, "ПоказыватьСписокЗадачПриЗапуске", ПоказыватьПриЗапуске);
	исключение
		Предупреждение("У Вас недостаточно прав для изменения признака отображения задач!", 30);
	КонецПопытки;
КонецПроцедуры

Процедура ДействияФормыОтбор(Кнопка)
	Если Кнопка.имя = "ОтборЗаявок" тогда
		если ЭлементыФормы.ДействияФормы.Кнопки.ОтборЗаявок.Пометка = Ложь тогда
			ЭлементыФормы.ДействияФормы.Кнопки.ОтборЗаявок.Пометка = Истина;
			Отбор.Объект.ВидСравнения = ВидСравнения.НеРавно;
			Отбор.Объект.Значение  = Неопределено;
			Отбор.Объект.Использование = Истина;
		Иначе
			ЭлементыФормы.ДействияФормы.Кнопки.ОтборЗаявок.Пометка = Ложь;
			Отбор.Объект.Использование = Ложь;
		КонецЕсли;
	Иначе
		Список = Новый СписокЗначений;
		
		Если ЭлементыФормы.ДействияФормы.Кнопки.Действие3.Пометка = Ложь тогда
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	ЗадачиПользователяЗадачиПоИсполнителю.Объект,
			|	ЗадачиПользователяЗадачиПоИсполнителю.Ссылка
			|ИЗ
			|	Задача.ЗадачиПользователя.ЗадачиПоИсполнителю КАК ЗадачиПользователяЗадачиПоИсполнителю
			|ГДЕ
			|	ЗадачиПользователяЗадачиПоИсполнителю."+
			?(ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя = "Контрагент" , "Объект.Контрагент", 
			?(ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя = "Статус" , "Объект.ВиновникНедогруза",
			?(ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя = "НомЗаявки" ,"Объект.Номер" ,
			ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя)))+" = &ЗначОтбора";
		   
		    Запрос.УстановитьПараметр("ЗначОтбора", ?(ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя = "Контрагент" , ЭлементыФормы.ЗадачаСписок.ТекущиеДанные.Объект.Контрагент, 
			?(ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя = "Статус" , ЭлементыФормы.ЗадачаСписок.ТекущиеДанные.Объект.ВиновникНедогруза,
			?(ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя = "НомЗаявки" ,ЭлементыФормы.ЗадачаСписок.ТекущиеДанные.Объект.Номер,
			ЭлементыФормы.ЗадачаСписок.ТекущиеДанные[ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя]))));
			Для Каждого стр из Запрос.Выполнить().Выгрузить() цикл
				Список.Добавить(Стр.Объект);
			КонецЦикла;
			ЭлементыФормы.ДействияФормы.Кнопки.Действие3.Пометка = Истина;
			если ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя = "Контрагент" или 
				ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя = "Статус" или
				ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя = "НомЗаявки" тогда
				Отбор.Объект.ВидСравнения = ВидСравнения.ВСписке;
				Отбор.Объект.Значение  = Список;
				Отбор.Объект.Использование = Истина;
				
			Иначе
				От = Отбор.Найти(ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя);
				От.ВидСравнения = ВидСравнения.Равно;
				От.Значение  = ЭлементыФормы.ЗадачаСписок.ТекущиеДанные[ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя];
				От.Использование = Истина;
			КонецЕсли;
		Иначе
			ЭлементыФормы.ДействияФормы.Кнопки.Действие3.Пометка = Ложь;
			если ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя = "Контрагент" или 
				ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя = "Статус" или
				ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя = "НомЗаявки" тогда
				Отбор.Объект.Использование = Ложь;
				ЭлементыФормы.ДействияФормы.Кнопки.ОтборЗаявок.Пометка = Ложь;
			Иначе
				От = Отбор.Найти(ЭлементыФормы.ЗадачаСписок.ТекущаяКолонка.Имя);
				От.Использование = Ложь;
			КонецЕсли;

		КонецЕсли;
    КонецЕсли;
    
КонецПроцедуры


