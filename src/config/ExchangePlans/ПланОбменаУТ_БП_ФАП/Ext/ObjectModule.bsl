
Процедура ВыполнитьОбменУТФАП(Узел, ДокументДляВыгрузки = Неопределено) Экспорт
	
	НаименованиеОбработки = "УниверсальныйОбменДаннымиXMLv2";
	Если Метаданные.Обработки.Найти("УниверсальныйОбменДаннымиXMLv2") = Неопределено Тогда
		НаименованиеОбработки = "УниверсальныйОбменДаннымиXML";
	КонецЕсли;	
	
	Обработка = Обработки[НаименованиеОбработки].Создать();
	Обработка.РежимОбмена = "Выгрузка";
	
	УникальныйИдентификатор        = Новый УникальныйИдентификатор();
	ИмяВременногоФайлаПравилОбмена = КаталогВременныхФайлов() + УникальныйИдентификатор + ".xml";
		
	МакетПравилОбмена = ПолучитьМакет("ПравилаОбменаУТФАП");
	МакетПравилОбмена.Записать(ИмяВременногоФайлаПравилОбмена);
		
	Обработка.ИмяФайлаПравилОбмена = ИмяВременногоФайлаПравилОбмена;
	Обработка.ЗагружатьДанныеВРежимеОбмена = Истина;
	Обработка.ЗаписыватьРегистрыНаборамиЗаписей = Истина;
	Обработка.ЗапоминатьЗагруженныеОбъекты = Истина;
	Обработка.ИспользоватьОтборПоДатеДляВсехОбъектов = Истина;
	Обработка.ВыгружатьТолькоРазрешенные = Истина;
	Обработка.ВерсияПлатформыИнформационнойБазыДляПодключения = "V83";
	Обработка.ПользовательИнформационнойБазыДляПодключения = "Робот (центр - номенклатура)";
	Обработка.ПарольИнформационнойБазыДляПодключения = "09876";
	Обработка.АутентификацияWindowsИнформационнойБазыДляПодключения = Ложь;
	Обработка.НепосредственноеЧтениеВИБПриемнике = Истина;
	Обработка.ТипУдаленияРегистрацииИзмененийДляУзловОбменаПослеВыгрузки = 1; // 0 - не снимать регистрацию,
	
	РежимОтладки = Ложь;
		
	Если НЕ РежимОтладки Тогда
		Обработка.ИмяСервераИнформационнойБазыДляПодключения   = "delta:3041"; 
		Обработка.ИмяИнформационнойБазыНаСервереДляПодключения = "v83ib_formulaavtoplus_bp";
		Обработка.ТипИнформационнойБазыДляПодключения = Ложь;
	Иначе
		Обработка.КаталогИнформационнойБазыДляПодключения = "C:\1C copy bases\ФА+ БП";
		Обработка.ТипИнформационнойБазыДляПодключения     = Истина; // Файловая
	КонецЕсли;
		
	Обработка.ЗагрузитьПравилаОбмена();
	
	Список = Новый СписокЗначений();
	Если ДокументДляВыгрузки = Неопределено Тогда
		УстановитьУзелОбменаУСтрокДерева(Обработка.ТаблицаПравилВыгрузки.Строки, Узел);
        //выгружаются все зарегистрированные объекты для узла
        Результат = ПолучитьЗарегистрированныеДокументы(Узел);
		Пока Результат.Следующий() Цикл
			Список.Добавить(Результат.Ссылка);
		КонецЦикла;
	Иначе
		Список.Добавить(ДокументДляВыгрузки);
	КонецЕсли;
	Обработка.Параметры.Вставить("ОбъектДляВыгрузки", Список);
	
	Обработка.ВыполнитьВыгрузку();
	
КонецПроцедуры

Процедура УстановитьУзелОбменаУСтрокДерева(Дерево, Узел)
	
	Для Каждого Строка Из Дерево Цикл
		Если Строка.ЭтоГруппа Тогда
			УстановитьУзелОбменаУСтрокДерева(Строка.Строки, Узел);
		Иначе
			Строка.СсылкаНаУзелОбмена = Узел;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьЗарегистрированныеДокументы(Узел)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Узел", Узел);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПоступлениеТоваровУслугИзменения.Ссылка
	               |ИЗ
	               |	Документ.ПоступлениеТоваровУслуг.Изменения КАК ПоступлениеТоваровУслугИзменения
	               |ГДЕ
	               |	ПоступлениеТоваровУслугИзменения.Узел = &Узел";
	Результат = Запрос.Выполнить().Выбрать();
	
	Возврат Результат;
	
КонецФункции

 