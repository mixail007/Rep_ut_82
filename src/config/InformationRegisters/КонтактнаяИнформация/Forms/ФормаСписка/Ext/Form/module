
Процедура РегистрСведенийСписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	МенеджерЗаписи = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
	Для каждого Измерение Из Метаданные.РегистрыСведений.КонтактнаяИнформация.Измерения Цикл
		МенеджерЗаписи[Измерение.Имя] = Элемент.ТекущиеДанные[Измерение.Имя];
	КонецЦикла;
	МенеджерЗаписи.Прочитать();
	
	Обработки.РедактированиеКонтактнойИнформации.Создать().РедактироватьЗапись(МенеджерЗаписи, Истина);
	
КонецПроцедуры

Процедура РегистрСведенийСписокПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Отказ = Истина;
	
	МенеджерЗаписи = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
	Если Копирование Тогда
		Для каждого Измерение Из Метаданные.РегистрыСведений.КонтактнаяИнформация.Измерения Цикл
			МенеджерЗаписи[Измерение.Имя] = Элемент.ТекущиеДанные[Измерение.Имя];
		КонецЦикла;
		Для каждого Ресурс Из Метаданные.РегистрыСведений.КонтактнаяИнформация.Ресурсы Цикл
			МенеджерЗаписи[Ресурс.Имя] = Элемент.ТекущиеДанные[Ресурс.Имя];
		КонецЦикла;
		Для каждого Реквизит Из Метаданные.РегистрыСведений.КонтактнаяИнформация.Реквизиты Цикл
			МенеджерЗаписи[Реквизит.Имя] = Элемент.ТекущиеДанные[Реквизит.Имя];
		КонецЦикла;
	КонецЕсли;
	
	Если Отбор.Объект.Использование = Истина
	   И ТипЗнч(Отбор.Объект.Значение) <> Тип("СписокЗначений")
	   И НЕ ЗначениеНеЗаполнено(Отбор.Объект.Значение) Тогда
		МенеджерЗаписи.Объект = Отбор.Объект.Значение;
	КонецЕсли; 
	
	Если Отбор.Тип.Использование = Истина
	   И ТипЗнч(Отбор.Тип.Значение) <> Тип("СписокЗначений")
	   И НЕ ЗначениеНеЗаполнено(Отбор.Тип.Значение) Тогда
		МенеджерЗаписи.Тип = Отбор.Тип.Значение;
	КонецЕсли; 
	
	Если Отбор.Вид.Использование = Истина
	   И ТипЗнч(Отбор.Вид.Значение) = Тип("СправочникСсылка.ВидыКонтактнойИнформации")
	   И НЕ ЗначениеНеЗаполнено(Отбор.Вид.Значение) Тогда
		МенеджерЗаписи.Объект = ПолучитьПустоеЗначениеОбъектаКонтактнойИнформации(Отбор.Вид.Значение.ВидОбъектаКонтактнойИнформации);
		МенеджерЗаписи.Тип    = Отбор.Вид.Значение.Тип;
		МенеджерЗаписи.Вид    = Отбор.Вид.Значение;
	КонецЕсли; 
	
	Обработки.РедактированиеКонтактнойИнформации.Создать().РедактироватьЗапись(МенеджерЗаписи, Истина);
	
КонецПроцедуры

Процедура РегистрСведенийСписокВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)

	Если ЭтаФорма.РежимВыбора Тогда
		СтандартнаяОбработка = Ложь;
		
		рез = Элемент.ТекущиеДанные.Представление;
		//+++ 03.02.2016 для адресов доставок - добавляем Комментарий!
		Если найти( строка(Элемент.ТекущиеДанные.Вид), "доставки" )>0
			и Элемент.ТекущиеДанные.Комментарий<>"" Тогда
			рез = рез +", "+Элемент.ТекущиеДанные.Комментарий; 
		КонецЕсли;
		
		Если Элемент.ТекущиеДанные.Поле10 <> "" Тогда
		
		    рез = рез +",КРД:"+Элемент.ТекущиеДанные.Поле10;
			
		КонецЕсли;
		
		ЭтаФорма.Закрыть(рез);
	КонецЕсли; 
	
КонецПроцедуры

Для каждого Измерение Из Метаданные.РегистрыСведений.КонтактнаяИнформация.Измерения Цикл
	РегистрСведенийСписок.Колонки.Добавить(Измерение.Имя);
КонецЦикла;
Для каждого Ресурс Из Метаданные.РегистрыСведений.КонтактнаяИнформация.Ресурсы Цикл
	РегистрСведенийСписок.Колонки.Добавить(Ресурс.Имя);
КонецЦикла;
Для каждого Реквизит Из Метаданные.РегистрыСведений.КонтактнаяИнформация.Реквизиты Цикл
	РегистрСведенийСписок.Колонки.Добавить(Реквизит.Имя);
КонецЦикла;
