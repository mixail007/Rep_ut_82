
Процедура ОбновлениеОтображения()
	
	ЭлементыФормы.ИнфНадписьВалюты.Заголовок = СокрЛП(Валюта) + " ("+СокрЛП(Валюта.НаименованиеПолное)+")";
	
КонецПроцедуры

Процедура ОсновныеДействияФормыДействиеОК(Кнопка)

	// Заполним структуру возвращаемых параметров
	СтруктураВозвращаемыхЗначений = Новый Структура();
	СтруктураВозвращаемыхЗначений.Вставить("КратностьВалюты", КратностьВалюты);
	СтруктураВозвращаемыхЗначений.Вставить("КурсВалюты",      КурсВалюты);

	// Все сделали, можно закрывать форму
	Закрыть(СтруктураВозвращаемыхЗначений);

КонецПроцедуры

Процедура ОсновнаяПанельПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Элемент.Страницы.Получить(ТекущаяСтраница) = Элемент.Страницы.НаДату Тогда
		Если КурсыВалюты.Количество() = 0 Тогда
			ЗапросДляПолученияКурсов = Новый Запрос(
				"ВЫБРАТЬ
				|	КурсыВалют.Период,
				|	КурсыВалют.Курс,
				|	КурсыВалют.Кратность
				|ИЗ
				|	РегистрСведений.КурсыВалют КАК КурсыВалют
				|
				|ГДЕ
				|	КурсыВалют.Валюта = &Валюта");
				
			ЗапросДляПолученияКурсов.УстановитьПараметр("Валюта", Валюта);
			КурсыВалюты = ЗапросДляПолученияКурсов.Выполнить().Выгрузить();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	КурсыВалюты.Очистить();
	
КонецПроцедуры

Процедура КнопкаЗаполнитьНаДатуНажатие(Элемент)
	
	// Выберем дату курса
	Если ВвестиДату(ДатаУстановкиКурсаИКратности, "Выберите дату установки курса и кратности.", ЧастиДаты.Дата) Тогда
		
		// Если дата введена, заполним значение курса и кратности
		СтруктураВалюты = ПолучитьКурсВалюты(Валюта, ДатаУстановкиКурсаИКратности);
		КурсВалюты      = СтруктураВалюты.Курс;
		КратностьВалюты = СтруктураВалюты.Кратность;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура КурсыВалютыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	// Заполним структуру возвращаемых параметров
	СтруктураВозвращаемыхЗначений = Новый Структура();
	СтруктураВозвращаемыхЗначений.Вставить("КратностьВалюты", ВыбраннаяСтрока.Кратность);
	СтруктураВозвращаемыхЗначений.Вставить("КурсВалюты",      ВыбраннаяСтрока.Курс);
	
	// Все сделали, можно закрывать форму
	Закрыть(СтруктураВозвращаемыхЗначений);
	
КонецПроцедуры



