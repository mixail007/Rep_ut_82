
Процедура ПриОткрытии()
	Если не РольДоступна("ПолныеПрава")тогда 
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДоговорыКонтрагентов.Владелец КАК Контрагент
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.ТипДоговора = ЗНАЧЕНИЕ(Справочник.типыДоговоров.ПеремещениеОТХ)
		|	И ДоговорыКонтрагентов.ОтветственноеЛицо = &ОтветственноеЛицо";
		
		Запрос.УстановитьПараметр("ОтветственноеЛицо", глТекущийПользователь);
		
		Результат = Запрос.Выполнить().Выгрузить();
		
		СписокКонтрагентов  = Результат.ВыгрузитьКолонку("Контрагент");
		списокЗ = Новый СписокЗначений;
		СписокЗ.ЗагрузитьЗначения(СписокКонтрагентов);
		Список.Отбор["Контрагент"].Использование = Истина;
		Список.Отбор["Контрагент"].ВидСравнения = ВидСравнения.ВСписке;
		Список.Отбор["Контрагент"].Значение = СписокЗ;
		ЭлементыФормы.Список.НастройкаОтбора["Контрагент"].Доступность = Ложь;
	конецесли;	
КонецПроцедуры

Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	Если ЭлементыФормы.Список.ТекущиеДанные = Неопределено тогда
		Возврат
	КонецЕсли;
	
	ПоказатьСтруктуруПодчиненностиДокумента(ЭтаФорма.ЭлементыФормы.Список.ТекущаяСтрока);

КонецПроцедуры

Процедура СписокПриПолученииДанных(Элемент, ОформленияСтрок)
	массивОТХ = новый массив;
	Для Каждого Строка Из ОформленияСтрок Цикл
		массивОТХ.Добавить(Строка.ДанныеСтроки.Ссылка); 
	КонецЦикла;
	
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОтчетПоОТХСформированныеДокументы.Ссылка КАК ОтчетПоОТХ,
	               |	СУММА(ВЫРАЗИТЬ(ОтчетПоОТХСформированныеДокументы.Документ КАК Документ.РеализацияТоваровУслуг).СуммаДокумента) КАК СуммаРеализации
	               |ИЗ
	               |	Документ.ОтчетПоОТХ.СформированныеДокументы КАК ОтчетПоОТХСформированныеДокументы
	               |ГДЕ
	               |	ОтчетПоОТХСформированныеДокументы.Ссылка В(&СписокОтчетовОТХ)
	               |	И ОтчетПоОТХСформированныеДокументы.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОтчетПоОТХСформированныеДокументы.Ссылка";
	Запрос.УстановитьПараметр("СписокОтчетовОТХ",массивОТХ);
	Рез = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из ОформленияСтрок Цикл
		найденнаяСтрока = Рез.Найти(Строка.ДанныеСтроки.Ссылка,"ОтчетПоОТХ");
		Если найденнаяСтрока<>Неопределено тогда
			Строка.Ячейки.СуммаРеализаций.Значение = найденнаяСтрока.СуммаРеализации;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
