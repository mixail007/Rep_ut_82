
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ДатаНач = НачалоМесяца(Дата);
	ДатаКон = КонецМесяца(Дата);
	
	// Распределение за месяц
	ВыполнитьРаспределениеДопРасходов(НачалоМесяца(Дата), КонецМесяца(Дата), , Ссылка, Дата);
	
	// Усреднение стоимости списания
	ТаблицаТоваров = Новый ТаблицаЗначений;
	
	текСтатусПартии = Перечисления.СтатусыПартийТоваров.ПустаяСсылка();
	текНоменклатураНаименование = "";
	текДокументОприходованияДата = '00010101';
	текДокументОприходования = Неопределено;
	
	// Возьмем движения списания по 100
	КоличествоСтрокЗаПроход=100;
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДатаНач", ДатаНач);
	Запрос.УстановитьПараметр("ДатаКон", ДатаКон);
	
	Пока Истина Цикл
		МассивТаблиц= Новый Массив;
		
		Запрос.УстановитьПараметр("ВидДвижения", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("СтатусПартии", текСтатусПартии);
		Запрос.УстановитьПараметр("НоменклатураНаименование", текНоменклатураНаименование);
		Запрос.УстановитьПараметр("ДокументОприходованияДата", текДокументОприходованияДата);
		Запрос.УстановитьПараметр("ДокументОприходования", текДокументОприходования);
		
		ТекстЗапросаСклад = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
		|	НаСкладах.Номенклатура КАК Номенклатура,
		|	НаСкладах.Номенклатура.Наименование КАК НоменклатураНаименование,
		|	НаСкладах.СтатусПартии КАК СтатусПартии,
		|	НаСкладах.ДокументОприходования КАК ДокументОприходования,
		|	НаСкладах.ДокументОприходования.Дата КАК ДокументОприходованияДата
		|ИЗ
		|	РегистрНакопления.ПартииТоваровНаСкладах КАК НаСкладах
		|
		|ГДЕ
		|	НаСкладах.Период МЕЖДУ &ДатаНач И &ДатаКон И
		|	(НаСкладах.Номенклатура.Наименование > &НоменклатураНаименование ИЛИ НаСкладах.Номенклатура.Наименование = &НоменклатураНаименование И НаСкладах.СтатусПартии > &СтатусПартии ИЛИ НаСкладах.Номенклатура.Наименование = &НоменклатураНаименование И НаСкладах.СтатусПартии = &СтатусПартии И НаСкладах.ДокументОприходования.Дата > &ДокументОприходованияДата ИЛИ НаСкладах.Номенклатура.Наименование = &НоменклатураНаименование И НаСкладах.СтатусПартии = &СтатусПартии И НаСкладах.ДокументОприходования.Дата = &ДокументОприходованияДата И НаСкладах.ДокументОприходования > &ДокументОприходования) И
		|	НаСкладах.ВидДвижения = &ВидДвижения
		|
		|УПОРЯДОЧИТЬ ПО
		|	НоменклатураНаименование,
		|	СтатусПартии,
		|	ДокументОприходованияДата,
		|	ДокументОприходования
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.Текст = ТекстЗапросаСклад;
		
		МассивТаблиц.Добавить(Запрос.Выполнить().Выгрузить());
		
		ТекстЗапросаПереданные = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
		|	Переданные.Номенклатура КАК Номенклатура,
		|	Переданные.Номенклатура.Наименование КАК НоменклатураНаименование,
		|	Переданные.СтатусПартии КАК СтатусПартии,
		|	Переданные.ДокументОприходования КАК ДокументОприходования,
		|	Переданные.ДокументОприходования.Дата КАК ДокументОприходованияДата
		|ИЗ
		|	РегистрНакопления.ПартииТоваровПереданные КАК Переданные
		|
		|ГДЕ
		|	Переданные.Период МЕЖДУ &ДатаНач И &ДатаКон И
		|	(Переданные.Номенклатура.Наименование > &НоменклатураНаименование ИЛИ Переданные.Номенклатура.Наименование = &НоменклатураНаименование И Переданные.СтатусПартии > &СтатусПартии ИЛИ Переданные.Номенклатура.Наименование = &НоменклатураНаименование И Переданные.СтатусПартии = &СтатусПартии И Переданные.ДокументОприходования.Дата > &ДокументОприходованияДата ИЛИ Переданные.Номенклатура.Наименование = &НоменклатураНаименование И Переданные.СтатусПартии = &СтатусПартии И Переданные.ДокументОприходования.Дата = &ДокументОприходованияДата И Переданные.ДокументОприходования > &ДокументОприходования) И
		|	Переданные.ВидДвижения = &ВидДвижения
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура.Наименование,
		|	СтатусПартии,
		|	ДокументОприходованияДата,
		|	ДокументОприходования
		|";
		
		Запрос.Текст = ТекстЗапросаПереданные;
		
		МассивТаблиц.Добавить(Запрос.Выполнить().Выгрузить());
		
		Таблица = Новый ТаблицаЗначений;
		
		// После упорядочивания по датам строки должны располагаться в том же порядке, что и в исходном запросе
		// для этого в служебной колонке запомним порядок запроса (таблицы в массиве) и положение строки в запросе в виде 
		// ПорядокТаблицыВМассиве*10^N + ПорядокВТаблице, где N - количество десятичных разрядов в числе КоличествоСтрокЗаПроход.
		СдвигРазряда = Pow(10,Цел(Log10(КоличествоСтрокЗаПроход)+1));
		
		Таблица.Колонки.Добавить("Ключ"); // Исходный индекс строки
		
		// Создаем колонки
		Для Каждого Колонка Из МассивТаблиц[0].Колонки Цикл
			Таблица.Колонки.Добавить(Колонка.Имя);
		КонецЦикла;
		
		// Загрузим значения в одну таблицу
		Сч=0;
		Для Каждого Элемент Из МассивТаблиц Цикл
			
			Сч=Сч+1;
			
			Для Каждого Строка Из Элемент Цикл
				
				НоваяСтрока = Таблица.Добавить();
				
				Для Каждого Колонка Из Элемент.Колонки Цикл
					Если ПустаяСтрока(Колонка.Имя) ИЛИ Колонка.Имя = "QuieryId" Тогда
						Продолжить;
					КонецЕсли; 
					
					НоваяСтрока[Колонка.Имя] = Строка[Колонка.Имя];
				КонецЦикла;
				
				НоваяСтрока.Ключ=Сч*СдвигРазряда+Элемент.Индекс(Строка);
			КонецЦикла;
		КонецЦикла; 
		
		Таблица.Колонки.Добавить("МоментВремени", Новый ОписаниеТипов("МоментВремени"));
		Для каждого Строка Из Таблица Цикл
			Строка.МоментВремени = Новый МоментВремени(Строка.ДокументОприходованияДата, Строка.ДокументОприходования);
		КонецЦикла;
		
		Таблица.Сортировать("НоменклатураНаименование Возр, СтатусПартии Возр, МоментВремени Возр, Ключ Возр");
		Таблица.Свернуть("Номенклатура, СтатусПартии, ДокументОприходования, ДокументОприходованияДата, НоменклатураНаименование");
		
		// От таблицы оставим только первые КоличествоСтрокЗаПроход, таким образом получим ограничение
		// обрабатываемого количества строк числом КоличествоСтрокЗаПроход и ограничение выбираемых из  
		// базы данных строк (не более КоличествоСтрокЗаПроход*МассивТаблиц.Количество())
		
		КоличествоЛишнихСтрокСнизу = Таблица.Количество()-КоличествоСтрокЗаПроход;
		
		Если КоличествоЛишнихСтрокСнизу>0 Тогда
			Для Сч=1 По КоличествоЛишнихСтрокСнизу Цикл
				Таблица.Удалить(Таблица[КоличествоСтрокЗаПроход]);
			КонецЦикла;
		КонецЕсли;
		
		КоличествоСтрок = Таблица.Количество();
		// Если строки кончились, прерываем цикл
		Если КоличествоСтрок = 0 Тогда
			Прервать;
		КонецЕсли;
		
		текСтатусПартии = Таблица[КоличествоСтрок-1].СтатусПартии;
		текНоменклатураНаименование = Таблица[КоличествоСтрок-1].НоменклатураНаименование;
		текДокументОприходования = Таблица[КоличествоСтрок-1].ДокументОприходования;
		текДокументОприходованияДата = Таблица[КоличествоСтрок-1].ДокументОприходованияДата;
		
		Таблица.Свернуть("Номенклатура, СтатусПартии, ДокументОприходования");
		
		// Корректировка списания по-средней
		СкорректироватьСписание(ДатаНач, ДатаКон, Таблица, Ссылка);

	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Дата = КонецМесяца(Дата);
	
	Если НЕ Отказ Тогда
	
		обЗаписатьПротоколИзменений(ЭтотОбъект);
	
	КонецЕсли; 

КонецПроцедуры


