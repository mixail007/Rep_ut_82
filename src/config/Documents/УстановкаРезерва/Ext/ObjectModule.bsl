
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если (Ответственный = Справочники.Пользователи.ПустаяСсылка()) Тогда 
		Ответственный = глТекущийПользователь;
	КонецЕсли;
	
	Если (Отдел = Справочники.Подразделения.ПустаяСсылка()) Тогда 
		Отдел = ПолучитьЗначениеПоУмолчанию(глТекущийПользователь,"ОсновноеПодразделение");	
	КонецЕсли;
	
	ВыполнитьДвиженияПоРезерву(Отказ);
	
КонецПроцедуры

Процедура ВыполнитьДвиженияПоРезерву(Отказ)
	
	ДвиженияРезерв = Движения.Резерв;
	
	Для Каждого Стр из Резервирование Цикл
		
		Если (Стр.ВидДвижения=Перечисления.ВидУстановкиРезерва.Установка) Тогда 
			ДвижениеРезерв 					= ДвиженияРезерв.Добавить();
			ДвижениеРезерв.ВидДвижения 		= ВидДвиженияНакопления.Приход;
			ДвижениеРезерв.Номенклатура 	= Стр.Номенклатура;
			ДвижениеРезерв.Количество		= Стр.Количество;
			ДвижениеРезерв.Период			= Дата;			
			ДвижениеРезерв.ДокументРезерва 	= Ссылка;
		ИначеЕсли (Стр.ДокументРезерва=Неопределено) Тогда 
			// Списываем по фИФО
			Запрос = новый Запрос;
			Запрос.УстановитьПараметр("Документ",Ссылка);
			Запрос.УстановитьПараметр("Номенклатура",Стр.Номенклатура);
			Запрос.УстановитьПараметр("Дата",Дата);
			Запрос.УстановитьПараметр("ДатаРез",НачалоДня(Дата));
			Запрос.УстановитьПараметр("Подразделение",Отдел);
			Запрос.Текст = "
				|	Выбрать
				|	Номенклатура,
				|	ДокументРезерва,
				|	КоличествоОстаток
				|Из РегистрНакопления.Резерв.Остатки(&Дата,Номенклатура = &Номенклатура)
				|Где 
				|	ДобавитьКДате(ДокументРезерва.Дата,ДЕНЬ,ДокументРезерва.ДлинаРезерва)>=&ДатаРез И ДокументРезерва<>&Документ "
				+ ?(Отдел<>Справочники.Подразделения.ПустаяСсылка(),"И ДокументРезерва.Отдел=&подразделение","") + "
				|Упорядочить по ДокументРезерва.Дата";
			ВыбТабл = Запрос.Выполнить().Выгрузить();
			СуммаКоличества = ВыбТабл.Итог("КоличествоОстаток");
			Если (СуммаКоличества<Стр.Количество) Тогда 
				Сообщить("Необходимо еще " + Строка(Стр.Количество - СуммаКоличества) + " единиц  номенклатуры " + Стр.Номенклатура + " в резерве." ,СтатусСообщения.Важное);
				Отказ = Истина;
			Иначе 
				КоличествоДляСписания = Стр.Количество;
				Для Каждого СтрТ из ВыбТабл Цикл
					ДвижениеРезерв 					= ДвиженияРезерв.Добавить();
					ДвижениеРезерв.ВидДвижения 		= ВидДвиженияНакопления.Расход;
					ДвижениеРезерв.Номенклатура 	= Стр.Номенклатура;
					ДвижениеРезерв.Количество		= Мин(СтрТ.КоличествоОстаток,КоличествоДляСписания);
					ДвижениеРезерв.Период			= Дата;			
					ДвижениеРезерв.ДокументРезерва 	= СтрТ.ДокументРезерва;	
					КоличествоДляСписания 			= КоличествоДляСписания - СтрТ.КоличествоОстаток;
					Если (КоличествоДляСписания<=0) Тогда 
						Прервать;
					Конецесли;
				КонецЦикла;
			КонецЕсли;			
		Иначе 
			// Устанавливаем документ явно
			// С проверкой остатков
			КоличествоПоДокументу = КоличествоВРезервеПоДокументу(Стр.Номенклатура,Стр.ДокументРезерва);
			Если (КоличествоПоДокументу>=Стр.Количество) Тогда 
				ДвижениеРезерв 					= ДвиженияРезерв.Добавить();
				ДвижениеРезерв.ВидДвижения 		= ВидДвиженияНакопления.Расход;
				ДвижениеРезерв.Номенклатура 	= Стр.Номенклатура;
				ДвижениеРезерв.Количество		= Стр.Количество;
				ДвижениеРезерв.Период			= Дата;			
				ДвижениеРезерв.ДокументРезерва 	= Стр.ДокументРезерва;			
			Иначе 
				Сообщить("По документу " + Стр.ДокументРезерва + " всего " + КоличествоПоДокументу + " " + Стр.Номенклатура + " требуется еще" + Строка(Стр.Количество - КоличествоПоДокументу) + " единиц.",СтатусСообщения.Важное);
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ДвиженияРезерв.Записать();
	
КонецПроцедуры

Функция КоличествоВРезервеПоДокументу(Номенклатура,ДокументРезерва)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ",ДокументРезерва);
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
	Запрос.УстановитьПараметр("Дата",ТекущаяДата());
	Запрос.УстановитьПараметр("ДатаРез",НачалоДня(ТекущаяДата()));
	Запрос.ТЕкст = "
	|	Выбрать 
	|	Номенклатура,
	|  	Сумма(КоличествоОстаток) как КоличествоОстаток
	|  		ИЗ
	|	(
	|	Выбрать
	|	Номенклатура,
	|	ДокументРезерва,
	|	КоличествоОстаток
	|Из РегистрНакопления.Резерв.Остатки(&Дата,Номенклатура = &Номенклатура)
	|Где 
	|	ДобавитьКДате(ДокументРезерва.Дата,ДЕНЬ,ДокументРезерва.ДлинаРезерва)>=&ДатаРез И ДокументРезерва=&Документ)
	| как ПЗ
	|Сгруппировать по 
	|Номенклатура";
	
	Выб = Запрос.Выполнить().Выбрать();
	
	Если (Выб.Следующий()) Тогда 
		Возврат Выб.КоличествоОстаток;
	Иначе 
		Возврат 0;
	КонецЕсли;
	
Конецфункции

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если НЕ Отказ Тогда
	
		обЗаписатьПротоколИзменений(ЭтотОбъект);
	
	КонецЕсли; 

КонецПроцедуры
