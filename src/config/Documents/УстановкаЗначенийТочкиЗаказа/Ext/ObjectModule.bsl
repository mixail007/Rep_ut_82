////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//  Отказ 					- флаг отказа в проведении.
//
Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура();

	// Теперь позовем общую процедуру проверки.
	ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения строк табличной части "Товары".
//
// Параметры:
// Параметры: 
//  ТаблицаПоТоварам        - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  СтруктураШапкиДокумента - структура, содержащая рексвизиты шапки документа и результаты запроса по шапке
//  Отказ                   - флаг отказа в проведении.
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиТовары(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок)

	Для каждого СтрокаТаблицы Из ТаблицаПоТоварам Цикл

		СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) + """ ";

		// Номенклатура.
		Если ЗначениеНеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
		
			ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "не заполнена Номенклатура.", Отказ, Заголовок);
		
		КонецЕсли;

		// СпособОпределенияЗначенияТочкиЗаказа.
		Если ЗначениеНеЗаполнено(СтрокаТаблицы.СпособОпределенияЗначенияТочкиЗаказа) Тогда
		
			ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "не заполнен cпособ определения значения точки заказа.", Отказ, Заголовок);
		
		Иначе
			
			Если СтрокаТаблицы.СпособОпределенияЗначенияТочкиЗаказа = Перечисления.СпособыОпределенияЗначенияТочкиЗаказа.Фиксированная Тогда
				
				// ЗначениеТочкиЗаказа.
				Если ЗначениеНеЗаполнено(СтрокаТаблицы.ЗначениеТочкиЗаказа) Тогда
				
					ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "не заполнено значение точки заказа.", Отказ, Заголовок);
				
				КонецЕсли;
				
			ИначеЕсли СтрокаТаблицы.СпособОпределенияЗначенияТочкиЗаказа = Перечисления.СпособыОпределенияЗначенияТочкиЗаказа.СреднийРазмерПартии Тогда
				
				// ДатаНач.
				Если ЗначениеНеЗаполнено(СтрокаТаблицы.ДатаНач) Тогда
				
					ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "не заполнено значение даты начала периода.", Отказ, Заголовок);
				
				КонецЕсли;
				
				// ДатаКон.
				Если ЗначениеНеЗаполнено(СтрокаТаблицы.ДатаКон) Тогда
				
					ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "не заполнено значение даты окончания периода.", Отказ, Заголовок);
				
				КонецЕсли;
				
				// ПроцентнаяСтавкаЗначенияТочкиЗаказа.
				Если ЗначениеНеЗаполнено(СтрокаТаблицы.ПроцентнаяСтавкаЗначенияТочкиЗаказа) Тогда
				
					ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "не заполнено значение процентной ставка значения точки заказа.", Отказ, Заголовок);
				
				КонецЕсли;
				
			ИначеЕсли СтрокаТаблицы.СпособОпределенияЗначенияТочкиЗаказа = Перечисления.СпособыОпределенияЗначенияТочкиЗаказа.ОптимальныйРазмерЗаказа Тогда
				
				// ДатаНач.
				Если ЗначениеНеЗаполнено(СтрокаТаблицы.ДатаНач) Тогда
				
					ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "не заполнено значение даты начала периода.", Отказ, Заголовок);
				
				КонецЕсли;
				
				// ДатаКон.
				Если ЗначениеНеЗаполнено(СтрокаТаблицы.ДатаКон) Тогда
				
					ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "не заполнено значение даты окончания периода.", Отказ, Заголовок);
				
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// Выполняет движения по регистрам 
//
Процедура ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок)
	
	НаборДвижений   = Движения.ЗначенияТочкиЗаказа;
	ТаблицаДвижений = НаборДвижений.Выгрузить();
	
	// Заполним таблицу движений.
	ЗагрузитьВТаблицуЗначений(ТаблицаПоТоварам, ТаблицаДвижений);
	
	НаборДвижений.мПериод          = Дата;
	НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;

	Если ЗначениеЗаполнено(Подразделение) Тогда
		ТаблицаДвижений.ЗаполнитьЗначения(Подразделение,"Подразделение");
		ТаблицаДвижений.ЗаполнитьЗначения(Контрагент,"Контрагент");
	КонецЕсли;

	Если Не Отказ Тогда
		
		Движения.ЗначенияТочкиЗаказа.ВыполнитьДвижения();
		
	КонецЕсли;

КонецПроцедуры // ДвиженияПоРегистрам

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ПредставлениеДокументаПриПроведении(Ссылка);

	// Сформируем структуру реквизитов шапки документа
	СтруктураШапкиДокумента = СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);

	// Заполним по шапке документа дерево параметров, нужных при проведении.
	ДеревоПолейЗапросаПоШапке = СформироватьДеревоПолейЗапросаПоШапке();
	
	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = СформироватьЗапросПоДеревуПолей(ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШапкиДокумента, "");

	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("Номенклатура", "Номенклатура");
	СтруктураПолей.Вставить("ХарактеристикаНоменклатуры", "ХарактеристикаНоменклатуры");
	СтруктураПолей.Вставить("Склад", "Склад");
	СтруктураПолей.Вставить("СпособОпределенияЗначенияТочкиЗаказа", "СпособОпределенияЗначенияТочкиЗаказа");
	СтруктураПолей.Вставить("ЗначениеТочкиЗаказа", "ЗначениеТочкиЗаказа");
	СтруктураПолей.Вставить("МинимальныйСтраховойЗапас", "МинимальныйСтраховойЗапас");
	СтруктураПолей.Вставить("ПроцентнаяСтавкаЗначенияТочкиЗаказа", "ПроцентнаяСтавкаЗначенияТочкиЗаказа");
	СтруктураПолей.Вставить("ПроцентнаяСтавкаМинимальногоСтраховогоЗапаса", "ПроцентнаяСтавкаМинимальногоСтраховогоЗапаса");
	СтруктураПолей.Вставить("ДатаНач", "ДатаНач");
	СтруктураПолей.Вставить("ДатаКон", "ДатаКон");
	
	РезультатЗапросаПоТоварам = СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Товары", СтруктураПолей);
	ТаблицаПоТоварам          = РезультатЗапросаПоТоварам.Выгрузить();

	ПроверитьЗаполнениеТабличнойЧастиТовары(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок);

	Если Не Отказ Тогда
		
		ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок);
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если НЕ Отказ Тогда
	
		обЗаписатьПротоколИзменений(ЭтотОбъект);
	
	КонецЕсли; 

КонецПроцедуры

