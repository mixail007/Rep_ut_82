Процедура УстановитьДоступность()

	Если ЗначениеНеЗаполнено(ДисконтнаяКарта) Тогда
	
		ЭлементыФормы.НадписьСостояние.Заголовок = "Ожидание чтения VIP-карты...";
		ЭлементыФормы.НадписьСостояние.ЦветТекста = WebЦвета.Красный;
		//ЭлементыФормы.НадписьФИО.Видимость = Ложь;
		//ЭлементыФормы.КонтактФИО.Видимость = Ложь;
	
	Иначе
		
		ЭлементыФормы.НадписьСостояние.Заголовок = "VIP-карта прочитана. Переспросить рег. данные";
		ЭлементыФормы.НадписьСостояние.ЦветТекста = WebЦвета.Синий;
		//ЭлементыФормы.НадписьФИО.Видимость = Истина;
		//ЭлементыФормы.КонтактФИО.Видимость = Истина;
		
	КонецЕсли; 
	
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ОК.Доступность = Не ЗначениеНеЗаполнено(ДисконтнаяКарта);

КонецПроцедуры

Процедура ПриИзмененииДисконтнойКарты()

	//
	Если ЗначениеНеЗаполнено(ДисконтнаяКарта) Тогда
		;
	ИначеЕсли ДисконтнаяКарта.ТипКарты <> Перечисления.ТипыИнформационныхКарт.Дисконтная Тогда
		Предупреждение("Считанная карта не является дисконтной!");
		ДисконтнаяКарта = Справочники.ИнформационныеКарты.ПустаяСсылка();
	ИначеЕсли ДисконтнаяКарта.ВидКарты <> Перечисления.ВидыИнформационныхКарт.Магнитная Тогда
		Предупреждение("Считанная карта не является магнитной!");
		ДисконтнаяКарта = Справочники.ИнформационныеКарты.ПустаяСсылка();
	ИначеЕсли ДисконтнаяКарта.ВидСкидки <> Перечисления.ВидыСкидокИнформационныхКарт.Фиксированная Тогда
		Предупреждение("Считанная карта не является VIP-картой!");
		ДисконтнаяКарта = Справочники.ИнформационныеКарты.ПустаяСсылка();
	ИначеЕсли Не (ДисконтнаяКарта.СтатусДисконтнойКарты = Перечисления.СтатусыДисконтнойКарты.Неактивна 
		ИЛИ ДисконтнаяКарта.Наименование = " (VIP)") Тогда
		Предупреждение("Считанная карта уже активна или аннулирована!");
		ДисконтнаяКарта = Справочники.ИнформационныеКарты.ПустаяСсылка();
	ИначеЕсли Не ЗначениеНеЗаполнено(ДисконтнаяКарта.Основание) Тогда
		Предупреждение("По считанной карте уже зарегистрировано основание!");
		ДисконтнаяКарта = Справочники.ИнформационныеКарты.ПустаяСсылка();
	КонецЕсли; 
	
	УстановитьДоступность();

КонецПроцедуры
 
Процедура КартаVIPПриИзменении(Элемент)
	
	ПриИзмененииДисконтнойКарты();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	КонтактФИО				= ДисконтнаяКартаОснование.КонтактФИО;
	КонтактДатаРождения     = ДисконтнаяКартаОснование.КонтактДатаРождения;
	КонтактДомашнийАдрес    = ДисконтнаяКартаОснование.КонтактДомашнийАдрес;
	КонтактДомашнийТелефон  = ДисконтнаяКартаОснование.КонтактДомашнийТелефон;
	КонтактСотовыйТелефон   = ДисконтнаяКартаОснование.КонтактСотовыйТелефон;
	
	УстановитьДоступность();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если СтрДлина(КонтактФИО) < 6 Тогда
		Предупреждение("Необходимо ввести как минимум ФИО!");
		Возврат;
	КонецЕсли; 
		
	врОб = ДисконтнаяКарта.ПолучитьОбъект();
	
	врОб.Наименование = КонтактФИО + " (VIP)";
	врОб.СтатусДисконтнойКарты = Перечисления.СтатусыДисконтнойКарты.Активна;
	врОб.ПроцентСкидки = Константы.ПроцентСкидкиVIPКарты.Получить();
	врОб.Основание = ДисконтнаяКартаОснование;
	
    врОб.КонтактФИО              = КонтактФИО;
    врОб.КонтактДатаРождения     = КонтактДатаРождения;
    врОб.КонтактДомашнийАдрес    = КонтактДомашнийАдрес;
    врОб.КонтактДомашнийТелефон  = КонтактДомашнийТелефон;
    врОб.КонтактСотовыйТелефон   = КонтактСотовыйТелефон;
    врОб.КонтактПаспорт   		 = КонтактПаспорт;
	
	врОшибка = Ложь;
	Попытка
		врОб.Записать();
	Исключение
		врОшибка = Истина;
	КонецПопытки; 
	
	Если врОшибка Тогда
		Предупреждение("Не удалось зарегистрировать информацию по VIP-карте. Попробуйте другую карту и обратитесь к администратору!");
	Иначе
		Предупреждение("VIP-карта на лицо """ + КонтактФИО + """ зарегистрирована!");
		ЭтаФорма.Закрыть(ДисконтнаяКарта);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	ЭлементыФормы.ИнформационнаяНадпись.Заголовок = ИнформационнаяНадпись;
	
КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Не ВводДоступен() Тогда
		Возврат;
	КонецЕсли;

	Если глТорговоеОборудование <> Неопределено
		И Событие = "MagneticStripeCardValue" Тогда

		ТекДанные = глТорговоеОборудование.ПолучитьДанныеОтРидера(Данные);
		врСс = глТорговоеОборудование.НайтиМагнитнуюКарту(ТекДанные);
		Если врСс <> Неопределено Тогда
			ДисконтнаяКарта = врСс;
		Иначе
			ДисконтнаяКарта = Справочники.ИнформационныеКарты.ПустаяСсылка();
		КонецЕсли; 
		
		ПриИзмененииДисконтнойКарты();
			
	КонецЕсли;

КонецПроцедуры
