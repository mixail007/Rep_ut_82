
// Процедура вызывается из процедуры "Обработка проведения".
// Основная задача - на основании движений по регистру НДСПокупки, сделанных документом
// реализации по ставке 0%, заполнить параметры конкретного движения показывающего
// подтверждение или неподтверждение ставки 0%.
//
Процедура СформироватьДвижениеНДСПокупкиПоОтражениюСтавки0(СтруктураШапкиДокумента, ТекСтрокаСостав, ТаблицаДвиженийПокупки)

	// Выбрать все записи регистра НДСПокупки, у которого:
	// Регистратор = ТекСтрокаСостав.СчетФактура (документ отгрузки)
	// Событие = Перечисления.СобытияПоНДСПокупки.ПредполагаетсяСтавка0
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
					|	НДСПокупки.Регистратор,
					|	НДСПокупки.ВидЦенности,
					|	НДСПокупки.Поставщик,
					|	НДСПокупки.СчетФактура,
					|	НДСПокупки.СтавкаНДС,
					|	СУММА(НДСПокупки.СуммаБезНДС) КАК СуммаБезНДС,
					|	СУММА(НДСПокупки.НДС) КАК НДС
					|ИЗ
					|	РегистрНакопления.НДСПокупки КАК НДСПокупки

					|ГДЕ
					|	НДСПокупки.Регистратор = &Регистратор И
					|	НДСПокупки.Событие = &Событие

					|СГРУППИРОВАТЬ ПО
					|	НДСПокупки.ВидЦенности,
					|	НДСПокупки.Поставщик,
					|	НДСПокупки.СчетФактура,
					|	НДСПокупки.СтавкаНДС,
					|	НДСПокупки.Регистратор";
	
	Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("Событие", 	 Перечисления.СобытияПоНДСПокупки.ПредполагаетсяСтавка0);
	Запрос.УстановитьПараметр("Регистратор", ТекСтрокаСостав.СчетФактура);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		// регистр НДСПокупки
		СтрокаДвижения = ТаблицаДвиженийПокупки.Добавить();
		СтрокаДвижения.Организация = СтруктураШапкиДокумента.Организация;
		СтрокаДвижения.ВидЦенности = Выборка.ВидЦенности;
		СтрокаДвижения.Поставщик =   Выборка.Поставщик;
		СтрокаДвижения.СчетФактура = Выборка.СчетФактура;
		
		Если ТекСтрокаСостав.Событие = Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0 Тогда
			СтрокаДвижения.Событие = Перечисления.СобытияПоНДСПокупки.ПодтвержденаСтавка0;
		Иначе
			СтрокаДвижения.Событие = Перечисления.СобытияПоНДСПокупки.НеПодтвержденаСтавка0;
		КонецЕсли;
		
		СтрокаДвижения.СтавкаНДС = 	 Выборка.СтавкаНДС;
		СтрокаДвижения.СуммаБезНДС = Выборка.СуммаБезНДС;
		СтрокаДвижения.НДС = 		 Выборка.НДС;
		
		// Дополнительные колонки
		СтрокаДвижения.ДокРеализации = 	  ТекСтрокаСостав.СчетФактура;
		СтрокаДвижения.СтавкаРеализации = ТекСтрокаСостав.СтавкаНДС;
		
	КонецЦикла;
		
КонецПроцедуры // СформироватьДвижениеНДСПокупкиПоОтражениюСтавки0()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	СтруктураШапкиДокумента = Новый Структура("Дата, Организация", Дата, Организация);

	// Движения по книге покупок
	ТаблицаДвиженийПокупки = Движения.НДСПокупки.Выгрузить();
	ТаблицаДвиженийПокупки.Очистить();
	// Создаем дополнительные колонки для формирования проводок
	// Перед непосредственной записью в движения текущего документа данные колонки
	// будут удалены
	ТаблицаДвиженийПокупки.Колонки.Добавить("ДокРеализации");
	ТаблицаДвиженийПокупки.Колонки.Добавить("СтавкаРеализации");
	
	Для Каждого ТекСтрокаСостав Из Состав Цикл
		// Движение по регистру НДСПродажи, отражающие подтверждение или неподтверждение ставки 0 
		Движение = Движения.НДСПродажи.Добавить();
		Движение.Период = Дата;
		Движение.Организация = 	Организация;
		Движение.ВидЦенности = 	ТекСтрокаСостав.ВидЦенности;
		Движение.Покупатель = 	ТекСтрокаСостав.Покупатель;
		Движение.СчетФактура = 	ТекСтрокаСостав.СчетФактура;
		Движение.Событие = 		ТекСтрокаСостав.Событие;
		
		Если ТекСтрокаСостав.Событие = Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0 Тогда
			Движение.СтавкаНДС = ТекСтрокаСостав.СтавкаНДС;
		Иначе			
			Движение.СтавкаНДС = Перечисления.СтавкиНДС.НДС0;
		КонецЕсли;
		
		Движение.СуммаБезНДС = ТекСтрокаСостав.ПродажиСНДС0;
		Движение.НДС = 0;

		// Движения по регистру НДСПокупки, отражающие подтверждение или неподтверждение ставки 0
		// для всех поступлений МПЗ, связанных с данной реализацией
		СформироватьДвижениеНДСПокупкиПоОтражениюСтавки0(СтруктураШапкиДокумента, ТекСтрокаСостав, ТаблицаДвиженийПокупки);
		
	КонецЦикла;
	
	// записываем движения регистров
	Движения.НДСПродажи.Записать();
	
	// Удаляем дополнительные колонки
	ТаблицаДвиженийПокупки.Колонки.Удалить("ДокРеализации");
	ТаблицаДвиженийПокупки.Колонки.Удалить("СтавкаРеализации");
	
	Если ТаблицаДвиженийПокупки.Количество() <> 0 Тогда
		Движения.НДСПокупки.мПериод            = Дата;
		Движения.НДСПокупки.мТаблицаДвижений   = ТаблицаДвиженийПокупки;
		Движения.НДСПокупки.ДобавитьДвижение();
		Движения.НДСПокупки.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если НЕ Отказ Тогда
	
		обЗаписатьПротоколИзменений(ЭтотОбъект);
	
	КонецЕсли; 

КонецПроцедуры
