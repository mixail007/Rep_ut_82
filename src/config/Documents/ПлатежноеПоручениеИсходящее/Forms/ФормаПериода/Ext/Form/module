// Устанавливает номер периода при выборе переодичности
// налога. Также задает предельные значения для
// элемента формы номер периода.
//
// Параметры
//  ВыбранноеЗнач - Строка - значение реквизита "Периодичность налога"
//
Процедура УстановитьНомерПериода(ВыбранноеЗнач)
	
	Если НЕ(ПериодичностьНалога=Неопределено) Тогда
		Если (ВыбранноеЗнач = "Д1") или (ВыбранноеЗнач = "Д2") 
				или (ВыбранноеЗнач = "Д3") или (ВыбранноеЗнач = "МС") Тогда
			НомерПериода=1;	
			ЭлементУпр = ЭлементыФормы.НомерПериода;
			ЭлементУпр.МинимальноеЗначение = 1;
			ЭлементУпр.МаксимальноеЗначение = 12;
			ЭлементУпр.Доступность = Истина;
			тНомерПериода="Месяц периода:";
		ИначеЕсли ВыбранноеЗнач = "КВ" Тогда
			НомерПериода=1;	
			ЭлементУпр = ЭлементыФормы.НомерПериода;
			ЭлементУпр.МинимальноеЗначение = 1;
			ЭлементУпр.МаксимальноеЗначение = 4;
			ЭлементУпр.Доступность = Истина;
			тНомерПериода="Квартал периода:";
		ИначеЕсли ВыбранноеЗнач = "ПЛ" Тогда
			НомерПериода=1;	
			ЭлементУпр = ЭлементыФормы.НомерПериода;
			ЭлементУпр.МинимальноеЗначение = 1;
			ЭлементУпр.МаксимальноеЗначение = 2;
			ЭлементУпр.Доступность = Истина;
			тНомерПериода="Полугодие периода:";
		ИначеЕсли ВыбранноеЗнач = "ГД" Тогда
			НомерПериода=0;	
			ЭлементУпр = ЭлементыФормы.НомерПериода;
			ЭлементУпр.МинимальноеЗначение = 0;
			ЭлементУпр.МаксимальноеЗначение = 1;
			ЭлементУпр.Доступность = Ложь;
			тНомерПериода="";
		КонецЕсли;		
	КонецЕсли;	
	
КонецПроцедуры // УстановитьНомерПериода()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	СписокВыбора = ЭлементыФормы.ПериодичностьНалога.СписокВыбора;
	СписокВыбора.Добавить("Д1","Д1, Платеж за первую декаду месяца");
	СписокВыбора.Добавить("Д2","Д2, Платеж за вторую декаду месяца");
	СписокВыбора.Добавить("Д3","Д3, Платеж за третью декаду месяца");
	СписокВыбора.Добавить("МС","МС, Месячные платежи");
	СписокВыбора.Добавить("КВ","КВ, Квартальные платежи");
	СписокВыбора.Добавить("ПЛ","ПЛ, Полугодовые платежи");
	СписокВыбора.Добавить("ГД","ГД, Годовые платежи");
	ГодПериода = Год(ТекущаяДата());
	
КонецПроцедуры // ПриОткрытии()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ, ВЫЗЫВАЕМЫЕ ИЗ ЭЛЕМЕНТОВ ФОРМЫ

// Обработчик события "НачалоВыбора" реквизита "ПериодичностьНалога"
Процедура ПериодичностьНалогаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СписокВыбора = ЭлементыФормы.ПериодичностьНалога.СписокВыбора;
	ВыбранныйЭлемент = СписокВыбора.ВыбратьЭлемент();
	Если НЕ(ВыбранныйЭлемент = Неопределено) Тогда
		ЭлементыФормы.ПериодичностьНалога.Значение = ВыбранныйЭлемент.Значение;
		УстановитьНомерПериода(ВыбранныйЭлемент.Значение);
		Обновить();
	КонецЕсли;	
	
КонецПроцедуры // ПериодичностьНалогаНачалоВыбора()
 
// Обработчик события "Нажатие" кнопки "КнопкаПринять"
Процедура КнопкаПринятьНажатие(Элемент)
	
	ФормаВл = ВладелецФормы;
	Если НЕ (ФормаВл = Неопределено) Тогда
		СтрНомерПериода = ?(СтрДлина(Строка(НомерПериода)) = 1,"0" + НомерПериода,НомерПериода);
		СтрГода         = СтрЗаменить(Строка(ГодПериода),Символы.НПП,"");
		Стр             = "" + ПериодичностьНалога + "." + СтрНомерПериода + "." + СтрГода;
		ФормаВл.ДокументОбъект.ПоказательПериода = Стр;
		ФормаВл.Обновить();
		Закрыть();
	КонецЕсли;	
	
КонецПроцедуры // КнопкаПринятьНажатие()






