
Процедура ОбработкаПроведения(Отказ, Режим)
	 
	Движения.ПениИШтрафыПоставщикам.Записывать = Истина;
	Движения.ПениИШтрафыПоставщикам.Очистить();
	Для Каждого ТекСтрокаРасшифровка Из Расшифровка Цикл
		Если ТекСтрокаРасшифровка.ЗаказПоставщикуСезонный.Дата < Константы.ДатаНачалаРасчетаШтрафовПоСезоннымЗаказамПоставщикам.Получить() Тогда
			Продолжить;
		Иначе
			Движение											 = Движения.ПениИШтрафыПоставщикам.Добавить();
			//Движение.ВидДвижения								 = ВидДвиженияНакопления.Приход;
			Движение.Период										 = Дата;
			Движение.Контрагент									 = ТекСтрокаРасшифровка.ЗаказПоставщикуСезонный.Контрагент;
			Движение.ЗаказПоставщикуСезонный					 = ТекСтрокаРасшифровка.ЗаказПоставщикуСезонный;
			Движение.Номенклатура								 = ТекСтрокаРасшифровка.Номенклатура;
			Движение.Количество									 = ТекСтрокаРасшифровка.Количество;
			Движение.Штраф										 = ТекСтрокаРасшифровка.Штраф;
			Движение.ШтрафЗаНевыполнениеМинимальногоОбъемаЗаказа = ТекСтрокаРасшифровка.ШтрафЗаНевыполнениеМинимальногоОбъемаЗаказа;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДокумент() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказыПоставщикамСезонныеОстаткиИОбороты.ЗаказПоставщикуСезонный,
	|	ЗаказыПоставщикамСезонныеОстаткиИОбороты.Номенклатура,
	|	ЗаказыПоставщикамСезонныеОстаткиИОбороты.КоличествоПриход,
	|	ЗаказыПоставщикамСезонныеОстаткиИОбороты.КоличествоКонечныйОстаток
	|ПОМЕСТИТЬ Недогрузы
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикамСезонные.ОстаткиИОбороты(
	|			,
	|			,
	|			,
	|			,
	|			ЗаказПоставщикуСезонный.НеРассчитыватьПени = ЛОЖЬ
	|				И ЗаказПоставщикуСезонный.ДатаДействияПо < &Дата
	|				И ЕСТЬNULL(Подразделение.ВЭД, ЛОЖЬ) = ЛОЖЬ) КАК ЗаказыПоставщикамСезонныеОстаткиИОбороты
	|ГДЕ
	|	ЗаказыПоставщикамСезонныеОстаткиИОбороты.КоличествоКонечныйОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Недогрузы.ЗаказПоставщикуСезонный,
	|	Недогрузы.Номенклатура,
	|	ЗаказПоставщикуСезонныйТовары.Цена,
	|	Недогрузы.КоличествоПриход КАК КоличествоВЗаказе,
	|	Недогрузы.КоличествоКонечныйОстаток
	|ИЗ
	|	Недогрузы КАК Недогрузы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщикуСезонный.Товары КАК ЗаказПоставщикуСезонныйТовары
	|		ПО Недогрузы.ЗаказПоставщикуСезонный = ЗаказПоставщикуСезонныйТовары.Ссылка
	|			И Недогрузы.Номенклатура = ЗаказПоставщикуСезонныйТовары.Номенклатура
	|ГДЕ
	|	Недогрузы.КоличествоКонечныйОстаток > 0";
	
	Запрос.УстановитьПараметр("Дата", НачалоДня(Дата)-30*24*60*60);
	
	Результат = Запрос.Выполнить();
	Если не результат.Пустой() Тогда
		//найдем параметры для начисления
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПараметрыДляНачисленияПениПоставщикам.Контрагент КАК Контрагент,
		|	ПараметрыДляНачисленияПениПоставщикам.КоличествоВСезонномЗаказе КАК КоличествоВСезонномЗаказе,
		|	ПараметрыДляНачисленияПениПоставщикам.ПроцентНедогруза,
		|	ПараметрыДляНачисленияПениПоставщикам.ПроцентПерегруза,
		|	ПараметрыДляНачисленияПениПоставщикам.ДеньНачисленияПени,
		|	ПараметрыДляНачисленияПениПоставщикам.ДеньШтрафа,
		|	ПараметрыДляНачисленияПениПоставщикам.РазмерПени,
		|	ПараметрыДляНачисленияПениПоставщикам.РазмерШтрафа
		|ИЗ
		|	РегистрСведений.ПараметрыДляНачисленияПениПоставщикам КАК ПараметрыДляНачисленияПениПоставщикам
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагент,
		|	КоличествоВСезонномЗаказе";
		
		
		ПараметрыНачисленияПеней = Запрос.Выполнить().Выгрузить();
		
		Выборка=результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НужныеПараметры  =Параметрыначисленияпеней.НайтиСтроки(Новый Структура("Контрагент",Выборка.ЗаказПоставщикуСезонный.контрагент));
			Если НужныеПараметры.Количество()=0 Тогда //Если персональных нет, тогда по общим
			НужныеПараметры  =Параметрыначисленияпеней.НайтиСтроки(Новый Структура("Контрагент",Справочники.Контрагенты.ПустаяСсылка()));
			КонецЕсли;

			РазрешенныйНедогруз =0;
			ШтрафЗаНедогруз = 0;
			Для каждого стр из НужныеПараметры Цикл
				Если Выборка.КоличествоВЗаказе> стр.КоличествоВСезонномЗаказе Тогда
				 РазрешенныйНедогруз = Окр(Выборка.КоличествоВЗаказе*стр.ПроцентНедогруза/100,0);
				 штрафЗаНедогруз = стр.РазмерШтрафа;
			    иначе
				прервать; 
			    конецЕсли;
			конецЦикла;
			Если Выборка.КоличествоКонечныйОстаток>РазрешенныйНедогруз Тогда //начислим штраф
				нстр = Расшифровка.Добавить();
			    Нстр.ЗаказПоставщикуСезонный = Выборка.ЗаказпоставщикуСезонный;
				нстр.Номенклатура = Выборка.номенклатура;
				нстр.КоличествоВЗаказе = Выборка.КоличествоВзаказе;
				нстр.Количество = Выборка.КоличествоКонечныйОстаток-РазрешенныйНедогруз;
				Нстр.Цена =  выборка.цена;
				нстр.Штраф = нстр.Количество*Нстр.Цена*штрафЗаНедогруз/100;
			
			конецЕсли;
		КонецЦикла;
		
		
	конецЕсли;
	
	
	
	
	
	
конецПроцедуры	

//***2017.12
Процедура ЗаполнитьДокументШтрафамиЗаНевыполнениеМинимальногоОбъемаСезонногоЗаказа() Экспорт
	
	//на следующий день после указанной в сезонных заказах поставщику даты выгрузки минимального объема заказа считается штраф
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПараметрыДляНачисленияПениПоставщикам.Контрагент КАК Контрагент,
	               |	ПараметрыДляНачисленияПениПоставщикам.КоличествоВСезонномЗаказе КАК КоличествоВСезонномЗаказе,
	               |	ПараметрыДляНачисленияПениПоставщикам.РазмерШтрафаНевыполнениеМинимальногоОбъемаЗаказа
	               |ИЗ
	               |	РегистрСведений.ПараметрыДляНачисленияПениПоставщикам КАК ПараметрыДляНачисленияПениПоставщикам
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Контрагент,
	               |	КоличествоВСезонномЗаказе";
	
	ПараметрыНачисленияШтрафов = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонецПериода", НачалоДня(Дата)-1);
	Запрос.УстановитьПараметр("КонтрольнаяДата", НачалоДня(НачалоДня(Дата)-1));
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказыПоставщикамСезонныеОстаткиИОбороты.ЗаказПоставщикуСезонный,
	               |	ЗаказыПоставщикамСезонныеОстаткиИОбороты.Номенклатура,
	               |	ЗаказыПоставщикамСезонныеОстаткиИОбороты.КоличествоПриход КАК КоличествоЗаказано,
	               |	ЗаказыПоставщикамСезонныеОстаткиИОбороты.КоличествоКонечныйОстаток КАК КоличествоКонечныйОстаток
	               |ПОМЕСТИТЬ ВТ_Недогрузы
	               |ИЗ
	               |	РегистрНакопления.ЗаказыПоставщикамСезонные.ОстаткиИОбороты(
	               |			,
	               |			&КонецПериода,
	               |			,
	               |			,
	               |			ЕСТЬNULL(Подразделение.ВЭД, ЛОЖЬ) = ЛОЖЬ
	               |				И НАЧАЛОПЕРИОДА(ЗаказПоставщикуСезонный.ДатаВыгрузкиМинимальногоОбъемаЗаказа, ДЕНЬ) = &КонтрольнаяДата) КАК ЗаказыПоставщикамСезонныеОстаткиИОбороты
	               |ГДЕ
	               |	ЗаказыПоставщикамСезонныеОстаткиИОбороты.КоличествоРасход / ЗаказыПоставщикамСезонныеОстаткиИОбороты.КоличествоПриход * 100 < ЗаказыПоставщикамСезонныеОстаткиИОбороты.ЗаказПоставщикуСезонный.РазмерМинимальногоОбъемаЗаказа
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Недогрузы.ЗаказПоставщикуСезонный,
	               |	ВТ_Недогрузы.Номенклатура,
	               |	ЗаказПоставщикуСезонныйТовары.Цена,
	               |	ВТ_Недогрузы.КоличествоЗаказано,
	               |	ВТ_Недогрузы.КоличествоКонечныйОстаток
	               |ИЗ
	               |	ВТ_Недогрузы КАК ВТ_Недогрузы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщикуСезонный.Товары КАК ЗаказПоставщикуСезонныйТовары
	               |		ПО ВТ_Недогрузы.ЗаказПоставщикуСезонный = ЗаказПоставщикуСезонныйТовары.Ссылка
	               |			И ВТ_Недогрузы.Номенклатура = ЗаказПоставщикуСезонныйТовары.Номенклатура
	               |ГДЕ
	               |	ВТ_Недогрузы.КоличествоКонечныйОстаток > 0";
				   
	Результат = Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл
		
		НужныеПараметры = ПараметрыНачисленияШтрафов.НайтиСтроки(Новый Структура("Контрагент", Результат.ЗаказПоставщикуСезонный.Контрагент));
		Если НужныеПараметры.Количество() = 0 Тогда //Если персональных нет, тогда по общим
			НужныеПараметры = ПараметрыНачисленияШтрафов.НайтиСтроки(Новый Структура("Контрагент", Справочники.Контрагенты.ПустаяСсылка()));
		КонецЕсли;
		
		Для каждого стр из НужныеПараметры Цикл
			Если Результат.КоличествоЗаказано > стр.КоличествоВСезонномЗаказе Тогда
				ШтрафЗаНевыполнениеМинимальногоОбъемаЗаказа = стр.РазмерШтрафаНевыполнениеМинимальногоОбъемаЗаказа;
			Иначе
				Прервать; 
			КонецЕсли;
		КонецЦикла;
		
		СтрТЧ = Расшифровка.Добавить();
		СтрТЧ.ЗаказПоставщикуСезонный						 = Результат.ЗаказпоставщикуСезонный;
		СтрТЧ.Номенклатура									 = Результат.номенклатура;
		СтрТЧ.КоличествоВЗаказе								 = Результат.КоличествоЗаказано;
		СтрТЧ.Количество									 = Результат.КоличествоКонечныйОстаток;
		СтрТЧ.Цена											 = Результат.Цена;
		СтрТЧ.ШтрафЗаНевыполнениеМинимальногоОбъемаЗаказа	 = СтрТЧ.Количество * СтрТЧ.Цена * ШтрафЗаНевыполнениеМинимальногоОбъемаЗаказа / 100;  
			
	КонецЦикла;
	
КонецПроцедуры