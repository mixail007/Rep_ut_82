////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит текущую дату документа - для проверки перехода документа в другой период установки номера
Перем мТекущаяДатаДокумента; 

// Хранит текущее значение валюты документа.
Перем мТекущаяВалютаДокумента;

// Хранит валюту взаиморасчетов, установленную в текущем договоре взаиморасчетов,
// используется для определения необходимости пересчетов при изменении договора.
Перем мТекущаяВалютаВзаиморасчетов; 

Перем мКолонкиТовары;

Перем мПечатнаяФормаПоУмолчанию;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ 

// Заполняет текст про счет-фактуру в форме документа.
//   Вызывается из обработчика ПриОткрытии этой формы и из обработчика ПослеЗаписи
// формы счета-фактуры.
//
// Параметры:
//  Нет.
//
Процедура ЗаполнитьТекстПроСчетФактуру() Экспорт

	ЭлементыФормы.ГиперссылкаСчетФактура.Заголовок = ПолучитьТекстСчетаФактуры(
	                                                 НайтиПодчиненныйДокумент(Ссылка, "СчетФактураПолученный"));

КонецПроцедуры // ЗаполнитьТекстПроСчетФактуру()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура рассчитывает сумму документа и сумму НДС 
// 
Процедура РассчитатьСуммыДокумента()

	СуммаНДС       = РассчитатьСуммуНДС(Сумма, УчитыватьНДС, СуммаВключаетНДС, ПолучитьСтавкуНДС(СтавкаНДС));
	СуммаДокумента = Товары.Итог("Сумма") + Сумма + ?(СуммаВключаетНДС, 0, (Товары.Итог("СуммаНДС") + СуммаНДС));

КонецПроцедуры

// Функция формирует список запросов для передачи в форму подбора.
//
// Параметры:
//  ТабличнаяЧасть - табличная часть, для подбора в которую формируется список запросов.
//
// Возвращаемое значение:
//  Список значений - список запросов.
//
Функция СформироватьСписокЗапросовДляПодбора(ТабличнаяЧасть)

	СписокЗапросов = Новый СписокЗначений();
	СписокЗапросов.Добавить(,"По справочнику");
	СписокЗапросов.Добавить("ПриходНоменклатураКонтрагента", "По номенклатуре контрагента");

	Возврат СписокЗапросов;

КонецФункции // СформироватьСписокЗапросовДляПодбора()

// Процедура обновляет информационную надпись.
//
Процедура ОбновитьИнфНадписьТовары()

	ВременнаяСтрока = "";

	Если (НЕ ЗначениеНеЗаполнено(ЭтотОбъект.ВалютаДокумента))
	   И (ЭтотОбъект.ВалютаДокумента <> мВалютаРегламентированногоУчета) Тогда
	
		Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДопРасходов.ВнутреннийРасход Тогда
			СтруктураКурсаВалюты  = ПолучитьКурсВалюты(ВалютаДокумента, Дата);

			ВременнаяСтрока = ВременнаяСтрока + ПолучитьИнформациюКурсаВалютыСтрокой(ВалютаДокумента,
			                                    СтруктураКурсаВалюты.Курс,
			                                    СтруктураКурсаВалюты.Кратность,
			                                    мВалютаРегламентированногоУчета)
		Иначе
			ВременнаяСтрока = ВременнаяСтрока + ПолучитьИнформациюКурсаВалютыСтрокой(ВалютаДокумента, 
			                                    КурсДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета),
			                                    КратностьДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета),
			                                    мВалютаРегламентированногоУчета);
		КонецЕсли;
	КонецЕсли;

	ЭлементыФормы.ИнфНадписьТоварыИтоги.Заголовок = ВременнаяСтрока;

КонецПроцедуры // ОбновитьИнфНадписьТовары()

// Процедура обновляет параметры в форме подбора, если она открыта.
//
// Параметры:
//  Реквизит - измененный реквизит.
//
Процедура ОбновитьФормуПодбора(Реквизит)

	ОбновитьПараметрыИФормуПодбора(ЭтотОбъект, ЭтаФорма, Реквизит);

КонецПроцедуры // ОбновитьФормуПодбора()

// Процедура вызывает сервисный механизм для подбора номеклатуры в табличную часть.
//
// Параметры:
//  ТабличнаяЧасть - табличная часть, в которую осуществляется подбор.
//
Процедура ДействиеПодбор(ТабличнаяЧасть)

	Перем Команда, Валюта;

	ЕстьЦена  = Ложь;
	ЕстьСерия = Ложь;

	Команда = "ПодборВТабличнуюЧастьТовары";
	Валюта  = ВалютаДокумента;
	ИмяТабличнойЧасти = "Товары";

	СписокВидовПодбора = СформироватьСписокЗапросовДляПодбора(ТабличнаяЧасть);
	ПредставлениеДок = Метаданные().Представление();

	СтруктураПараметровПодбора = Новый Структура();
	СтруктураПараметровПодбора.Вставить("Команда"            , Команда);
	СтруктураПараметровПодбора.Вставить("СписокВидовПодбора" , СписокВидовПодбора);
	
	// Параметры запросов.
	ВременнаяДатаРасчетов = ?(НачалоДня(Дата) = НачалоДня(ТекущаяДата()), Неопределено, Дата);
	СтруктураПараметровПодбора.Вставить("ДатаРасчетов",       ВременнаяДатаРасчетов);
	СтруктураПараметровПодбора.Вставить("Контрагент",         Контрагент);
	СтруктураПараметровПодбора.Вставить("ДоговорКонтрагента", ДоговорКонтрагента);
	СтруктураПараметровПодбора.Вставить("Организация",        Организация);
	СтруктураПараметровПодбора.Вставить("ТипЦен",             Неопределено);
	СтруктураПараметровПодбора.Вставить("Склад",              Неопределено);
	СтруктураПараметровПодбора.Вставить("ЕстьЦена" ,          ЕстьЦена);
	СтруктураПараметровПодбора.Вставить("ЕстьСерия",          ЕстьСерия);
	СтруктураПараметровПодбора.Вставить("ВалютаДокумента",    Валюта);
	СтруктураПараметровПодбора.Вставить("Заголовок", "Подбор номенклатуры в документ " + 
	                                    ПредставлениеДок + " № " + Номер + " (" + ИмяТабличнойЧасти + ")");

	ОткрытьПодборНоменклатуры(ЭтаФорма, СтруктураПараметровПодбора);

КонецПроцедуры // ДействиеПодбор()

// Производит заполнение документа переданными из формы подбора данными.
//
// Параметры:
//  ТабличнаяЧасть    - табличная часть, в которую надо добавлять подобранную позицию номенклатуры;
//  ЗначениеВыбора    - структура, содержащая параметры подбора.
//
Процедура ОбработкаПодбора(ТабличнаяЧасть, ЗначениеВыбора) Экспорт

	Перем СпособЗаполненияЦен, ВалютаЦены;
	Перем Номенклатура, ЕдиницаИзмерения, Количество, Цена, Характеристика, Серия;

	// Получим параметры подбора из структуры подбора.
	ЗначениеВыбора.Свойство("СпособЗаполненияЦен", СпособЗаполненияЦен);
	ЗначениеВыбора.Свойство("ВалютаЦены"         , ВалютаЦены);

	ЗначениеВыбора.Свойство("Номенклатура"    , Номенклатура);
	ЗначениеВыбора.Свойство("ЕдиницаИзмерения", ЕдиницаИзмерения);
	ЗначениеВыбора.Свойство("Количество"      , Количество);
	ЗначениеВыбора.Свойство("Цена"            , Цена);
	ЗначениеВыбора.Свойство("Характеристика"  , Характеристика);
	ЗначениеВыбора.Свойство("Серия"           , Серия);
	
	Если Номенклатура.Услуга Тогда
		Предупреждение("В данном контексте услуги не подбираются!");
		Возврат;
	КонецЕсли;

	// Ищем выбранную позицию в таблице подобранной номенклатуры.
	//  Если найдем - увеличим количество; не найдем - добавим новую строку.
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	СтруктураОтбора.Вставить( "ХарактеристикаНоменклатуры", Характеристика);
	СтруктураОтбора.Вставить( "СерияНоменклатуры"         , Серия);
	
	ИмяТабЧасти = "Товары";

	СтрокаТабличнойЧасти = НайтиСтрокуТабЧасти(ТабличнаяЧасть, СтруктураОтбора);
	Если СтрокаТабличнойЧасти <> Неопределено Тогда

		// Нашли, увеличиваем количество в первой найденной строке.
		СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество + Количество;

	Иначе

		// Не нашли - добавляем новую строку.
		СтрокаТабличнойЧасти = ТабличнаяЧасть.Добавить();
		СтрокаТабличнойЧасти.Номенклатура     = Номенклатура;
		СтрокаТабличнойЧасти.Количество       = Количество;
		СтрокаТабличнойЧасти.ЕдиницаИзмерения = ЕдиницаИзмерения;
		СтрокаТабличнойЧасти.Коэффициент      = СтрокаТабличнойЧасти.ЕдиницаИзмерения.Коэффициент;
		СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры = Характеристика;
		СтрокаТабличнойЧасти.СерияНоменклатуры          = Серия;

	КонецЕсли;

	ЭлементыФормы[ИмяТабЧасти].ТекущаяСтрока  = СтрокаТабличнойЧасти;
	ЭлементыФормы[ИмяТабЧасти].ТекущаяКолонка = ЭлементыФормы[ИмяТабЧасти].Колонки["Количество"];

КонецПроцедуры // ОбработкаПодбора()

// Пересчитывает суммы в шапке и табличной части документа из одной валюты в другую.
//
// Параметры:
// СтараяВалютаДокумента - старая валюта всех сумм
// НоваяВалютаДокумента - валюта, в которую надо пересчитать суммы
//
Процедура ПересчетСуммДокумента(СтараяВалютаДокумента, НоваяВалютаДокумента)

	Если СтараяВалютаДокумента = НоваяВалютаДокумента Тогда
		Возврат;
	КонецЕсли;

	СтруктураКурсаДокумента = ПолучитьКурсВалюты(НоваяВалютаДокумента, Дата);
	НовыйКурсДокумента      = СтруктураКурсаДокумента.Курс;
	НовыйКратностьДокумента = СтруктураКурсаДокумента.Кратность;

	Для каждого СтрокаТабличнойЧасти Из Товары Цикл

		СтрокаТабличнойЧасти.Сумма = ПересчитатьИзВалютыВВалюту(СтрокаТабличнойЧасти.Сумма,
												СтараяВалютаДокумента, 
												НоваяВалютаДокумента,
												КурсДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета), 
												НовыйКурсДокумента,
												КратностьДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета), 
												НовыйКратностьДокумента);

		СтрокаТабличнойЧасти.СуммаТовара = ПересчитатьИзВалютыВВалюту(СтрокаТабличнойЧасти.СуммаТовара,
													  СтараяВалютаДокумента,
													  НоваяВалютаДокумента,
													  КурсДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета),
													  НовыйКурсДокумента,
													  КратностьДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета),
													  НовыйКратностьДокумента);

		РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);

	КонецЦикла;

	Сумма = ПересчитатьИзВалютыВВалюту(Сумма,
					   СтараяВалютаДокумента, 
					   НоваяВалютаДокумента,
					   КурсДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета), 
					   НовыйКурсДокумента,
					   КратностьДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета), 
					   НовыйКратностьДокумента);

	РассчитатьСуммыДокумента();

КонецПроцедуры // ПересчетСуммДокумента()

// Процедура выполняет необходимые действия при изменении договора взаиморасчетов
// с контрагентом.
//
Процедура ПриИзмененииДоговора()

	ЭтотОбъект.Сделка = Неопределено; // Для сделки нет значения по умолчанию в договоре

	Если ЗначениеНеЗаполнено(ДоговорКонтрагента) Тогда

		КурсВзаиморасчетов      = 0;
		КратностьВзаиморасчетов = 0;

		Возврат;

	КонецЕсли;

	Если Контрагент <> ДоговорКонтрагента.Владелец Тогда
		Контрагент = ДоговорКонтрагента.Владелец;
	КонецЕсли;

	Если НЕ ((Товары.Итог("Сумма") = 0) И (Товары.Итог("СуммаТовара") = 0) И (Сумма = 0)) Тогда

		// Валюта документа
		Если НЕ ЗначениеНеЗаполнено(ДоговорКонтрагента.ВалютаВзаиморасчетов) Тогда

			НоваяВалютаДокумента = ДоговорКонтрагента.ВалютаВзаиморасчетов;

			Если ВалютаДокумента <> НоваяВалютаДокумента Тогда
				ТекстВопроса = "Договор с контрагентом предусматривает параметры взаиморасчетов,
				               |отличные от установленных в документе.
				               |Валюта документа: """ +
				               ?(ЗначениеНеЗаполнено(ВалютаДокумента), "не задана",СокрЛП(ВалютаДокумента)) +
				               """, договора: """ +  СокрЛП(НоваяВалютаДокумента) + """.
				               |Пересчитать документ в соответствии с договором?";
				Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);

				Если Ответ = КодВозвратаДиалога.Да Тогда
					ПересчетСуммДокумента(ВалютаДокумента, НоваяВалютаДокумента);
				КонецЕсли;
			КонецЕсли;

		КонецЕсли;
	КонецЕсли;

	ВалютаДокумента              = ДоговорКонтрагента.ВалютаВзаиморасчетов;
	СтруктураКурсаНовый          = ПолучитьКурсВалюты(ВалютаДокумента, Дата);
	КурсВзаиморасчетов           = СтруктураКурсаНовый.Курс;
	КратностьВзаиморасчетов      = СтруктураКурсаНовый.Кратность;
	КурсДокумента                = СтруктураКурсаНовый.Курс;
	КратностьДокумента           = СтруктураКурсаНовый.Кратность;
	мТекущаяВалютаДокумента      = ВалютаДокумента;
	мТекущаяВалютаВзаиморасчетов = ДоговорКонтрагента.ВалютаВзаиморасчетов;

	СформироватьНадписьДолга(ДоговорКонтрагента, Сделка, ЭлементыФормы.ИнфНадписьДолга, , Истина);
	УстановитьЗаголовкиПоДоговору();

	УстановитьВидимость();
	ОбновитьИнфНадписьТовары();
	УстановитьАвтоотметку();

КонецПроцедуры // ПриИзмененииДоговора()

// Процедура выполняет копирование табличной части заказа поставщику в документ.
//
// Параметры:
//  ТабличнаяЧасть - табличная часть, которую необходимо заполнить.
//
Процедура ЗаполнитьТабличнуюЧастьПоПоступлению(ТабЧасть, СпособЗаполнения)

	ФормаПоступления = Документы.ПоступлениеТоваровУслуг.ПолучитьФормуВыбора();
	ФормаПоступления.Заголовок = "Выберите документ поступления для заполнения поступления доп. расходов";
	ФормаПоступления.РежимВыбора = Истина;

	Поступление = ФормаПоступления.ОткрытьМодально();

	Если ЗначениеНеЗаполнено(Поступление) Тогда
		Возврат;
	КонецЕсли;

	Если ТабЧасть.Количество() > 0 И СпособЗаполнения = "Заполнить" Тогда
		
		ТекстВопроса = "Перед заполнением табличная часть будет очищена. Заполнить?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Метаданные().Имя);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли; 
		
		ТабЧасть.Очистить();
	КонецЕсли;

	ЗаполнитьТоварыПоПоступлениюТоваров(Поступление, ТабЧасть);

КонецПроцедуры // СкопироватьТабличнуюЧасть()

// Производит заполнение и установку необходимых полей при изменении товара в табличной части.
// Вызывается из:
//  ТоварыНоменклатураПриИзменении()
//  ВнешнееСобытие()
//
Процедура ПриИзмененииНоменклатурыТоваров(СтрокаТабличнойЧасти)

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

// Процедура устанавливает видимость для тех колонок в табличной части 
// "Товары", видимость которых определяется реквизитами документа.
//
// Параметры:
//  Нет.
//
Процедура УстановитьВидимость()

	ДоступностьКонтрагента = НЕ(ВидОперации = Перечисления.ВидыОперацийПоступлениеДопРасходов.ВнутреннийРасход);

	Если НЕ (ДоступностьКонтрагента = ЭлементыФормы.Контрагент.Доступность) Тогда
		ЭлементыФормы.Контрагент.Доступность        = ДоступностьКонтрагента;
		ЭлементыФормы.НадписьКонтрагент.Доступность = ДоступностьКонтрагента;
	КонецЕсли;

	Если НЕ (ДоступностьКонтрагента = ЭлементыФормы.ДоговорКонтрагента.Доступность) Тогда
		ЭлементыФормы.ДоговорКонтрагента.Доступность = ДоступностьКонтрагента;
		ЭлементыФормы.НадписьДоговор.Доступность     = ДоступностьКонтрагента;
	КонецЕсли;

	Если НЕ (ДоступностьКонтрагента = ЭлементыФормы.Сделка.Доступность) Тогда
		ЭлементыФормы.Сделка.Доступность        = ДоступностьКонтрагента;
		ЭлементыФормы.НадписьСделка.Доступность = ДоступностьКонтрагента;
	КонецЕсли;

	ЭлементыФормы.ГиперссылкаСчетФактура.Видимость = ДоступностьКонтрагента;
	ЭлементыФормы.НадписьСчетФактура.Видимость     = ДоступностьКонтрагента;

	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДопРасходов.ВнутреннийРасход Тогда
		ЭлементыФормы.ОтражатьВНалоговомУчете.Доступность     = Ложь;
		ЭлементыФормы.ОтражатьВБухгалтерскомУчете.Доступность = Ложь;
		ЭлементыФормы.НадписьВсегоНДС.Видимость               = Ложь;
		ЭлементыФормы.ВсегоНДС.Видимость                      = Ложь;
		ЭлементыФормы.НадписьСтавкаНДС.Видимость              = Ложь;
		ЭлементыФормы.СтавкаНДС.Видимость                     = Ложь;
		ЭлементыФормы.НадписьСуммаНДС.Видимость               = Ложь;
		ЭлементыФормы.СуммаНДС.Видимость                      = Ложь;
		ЭлементыФормы.ИнфНадписьДолга.Видимость               = Ложь;
		ЭлементыФормы.КнопкаПерерасчитатьДолг.Видимость       = Ложь;

		УстановитьВидимостьКолонкиТабЧасти(мКолонкиТовары.СтавкаНДС, Ложь);
		УстановитьВидимостьКолонкиТабЧасти(мКолонкиТовары.СуммаНДС,  Ложь);
	Иначе
		ЭлементыФормы.ОтражатьВБухгалтерскомУчете.Доступность = Истина;
		ЭлементыФормы.ОтражатьВНалоговомУчете.Доступность     = ОтражатьВБухгалтерскомУчете 
		                                                        И (ДоговорКонтрагента.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
		ЭлементыФормы.ИнфНадписьДолга.Видимость               = Истина;
		ЭлементыФормы.КнопкаПерерасчитатьДолг.Видимость       = Истина;
		ЭлементыФормы.НадписьСтавкаНДС.Видимость              = УчитыватьНДС;
		ЭлементыФормы.СтавкаНДС.Видимость                     = УчитыватьНДС;
		ЭлементыФормы.НадписьСуммаНДС.Видимость               = УчитыватьНДС;
		ЭлементыФормы.СуммаНДС.Видимость                      = УчитыватьНДС;
		ЭлементыФормы.НадписьВсегоНДС.Видимость               = УчитыватьНДС;
		ЭлементыФормы.ВсегоНДС.Видимость                      = УчитыватьНДС;

		УстановитьВидимостьКолонкиТабЧасти( мКолонкиТовары.СтавкаНДС, УчитыватьНДС);
		УстановитьВидимостьКолонкиТабЧасти(мКолонкиТовары.СуммаНДС,   УчитыватьНДС);
	КонецЕсли;

	ТекущаяСтраница = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДопРасходов.ВнутреннийРасход Тогда
		ЭлементыФормы.ОсновнаяПанель.Страницы.Дополнительно.Видимость                  = Ложь;
		ЭлементыФормы.ОсновнаяПанель.Страницы.ДополнительноБезВзаиморасчетов.Видимость = Истина;

		Если ТекущаяСтраница = ЭлементыФормы.ОсновнаяПанель.Страницы.Дополнительно Тогда
			ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница = ЭлементыФормы.ОсновнаяПанель.Страницы.ДополнительноБезВзаиморасчетов;
		КонецЕсли;

	Иначе
		ЭлементыФормы.ОсновнаяПанель.Страницы.Дополнительно.Видимость                  = Истина;
		ЭлементыФормы.ОсновнаяПанель.Страницы.ДополнительноБезВзаиморасчетов.Видимость = Ложь;

		Если ТекущаяСтраница = ЭлементыФормы.ОсновнаяПанель.Страницы.ДополнительноБезВзаиморасчетов Тогда
			ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница = ЭлементыФормы.ОсновнаяПанель.Страницы.Дополнительно;
		КонецЕсли;

	КонецЕсли;

	ВидимостьРеквизитаПроект(ЭтотОбъект, ЭлементыФормы, "Товары.Проект");

КонецПроцедуры // УстановитьВидимость()

// Процедура устанавливает автоотметку незаполненного для тех колонок в табличной части 
// "Товары", обязательность заполнения которых определяется реквизитами документа.
//
// Параметры:
//  Нет.
//
Процедура УстановитьАвтоотметку()

	// Сделка должна заполняться, если ведение взаиморасчетов - по заказам
	Автоотметка = Ложь;

	Если НЕ ЗначениеНеЗаполнено(ДоговорКонтрагента) Тогда
		Если ДоговорКонтрагента.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказам
		 ИЛИ ДоговорКонтрагента.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоСчетам Тогда
			АвтоОтметка = Истина;
		КонецЕсли;
	КонецЕсли;

	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДопРасходов.ВнутреннийРасход Тогда
		ЭлементыФормы.Контрагент.АвтоОтметкаНезаполненного         = Ложь;
		ЭлементыФормы.Контрагент.ОтметкаНезаполненного             = Ложь;
		ЭлементыФормы.ДоговорКонтрагента.АвтоОтметкаНезаполненного = Ложь;
		ЭлементыФормы.ДоговорКонтрагента.ОтметкаНезаполненного     = Ложь;
	Иначе
		ЭлементыФормы.Контрагент.АвтоОтметкаНезаполненного         = Истина;
		ЭлементыФормы.ДоговорКонтрагента.АвтоОтметкаНезаполненного = Истина;
	КонецЕсли;
	
	Если ЭлементыФормы.Сделка.АвтоОтметкаНезаполненного <> Автоотметка Тогда
		ЭлементыФормы.Сделка.АвтоОтметкаНезаполненного = Автоотметка;
	КонецЕсли;

	Если ЭлементыФормы.Сделка.ОтметкаНезаполненного <> Автоотметка И ЗначениеНеЗаполнено(Сделка) Тогда
		ЭлементыФормы.Сделка.ОтметкаНезаполненного = Автоотметка;
	КонецЕсли;

КонецПроцедуры // УстановитьАвтоотментку()

// Процедура устанавливает заголовки по выбранному договору для:
// - колонок табличной части "Возвратная тара";
// - элементов формы НадписьСделки, НадписьСуммаВзаиморасчетов.
//
// Параметры:
//  Нет.
//
Процедура УстановитьЗаголовкиПоДоговору()

	УстановитьНадписьСделки(ЭтотОбъект, ЭтаФорма, Ложь);
	УстановитьНадписьСуммыВзаиморасчетов(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры // УстановитьЗаголовкиПоДоговору()

// Процедура формирует текст в информационной надписи об итогах документа.
//
// Параметры:
//  Нет.
//
Процедура ОбновитьПодвал()

	НДСДокумента      = Товары.Итог("СуммаНДС") + СуммаНДС;
	ПромежуточныйИтог = Товары.Итог("Сумма") + Сумма + ?(СуммаВключаетНДС, 0, НДСДокумента);

	ЭлементыФормы.Всего.Значение    = ФорматСумм(ПромежуточныйИтог);
	ЭлементыФормы.ВсегоНДС.Значение = ФорматСумм(НДСДокумента);

	Если ЗначениеНеЗаполнено(ВалютаДокумента) Тогда
		ЭлементыФормы.НадписьВсего.Заголовок = "Всего (<>):";
	Иначе
		ЭлементыФормы.НадписьВсего.Заголовок = "Всего (" + СокрЛП(ВалютаДокумента) +"):";
	КонецЕсли;

	Если СуммаВключаетНДС Тогда
		ЭлементыФормы.НадписьВсегоНДС.Заголовок = "НДС (в т. ч.):";
	Иначе
		ЭлементыФормы.НадписьВсегоНДС.Заголовок = "НДС (сверху):";
	КонецЕсли;

КонецПроцедуры // ОбновитьПодвал()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеНеЗаполнено(ПараметрОснование) Тогда

		Если ТипЗнч(ПараметрОснование) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
		 ИЛИ ТипЗнч(ПараметрОснование) = Тип("ДокументСсылка.ПоступлениеТоваровУслугВНеавтоматизированнуюТорговуюТочку") Тогда
		 Если ПараметрОснование.ДоговорКонтрагента.ВидДоговора  = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда
				Предупреждение("Поступление доп. расходов не должно вводиться на основании документа приема товаров на комиссию!");
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()

	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ

		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнитьШапкуДокумента(ЭтотОбъект, глТекущийПользователь, мВалютаРегламентированногоУчета, "Покупка");
		УстановитьНомерДокумента(ЭтотОбъект);

	КонецЕсли;

	СтруктураКолонок = Новый Структура();

	// Установить колонки, видимостью которых пользователь управлять не может.
	СтруктураКолонок.Вставить("Номенклатура");
	СтруктураКолонок.Вставить("Количество");
	СтруктураКолонок.Вставить("Сумма");
	СтруктураКолонок.Вставить("СтавкаНДС");
	СтруктураКолонок.Вставить("СуммаНДС");
	СтруктураКолонок.Вставить("ЕдиницаИзмерения");

	УстановитьИзменятьВидимостьКолонокТабЧасти(ЭлементыФормы.Товары.Колонки, СтруктураКолонок);

	// Заполняем подменю, вызываемое нажатием кнопки "Операция" командной панели 
	// формы, значениями перечисления "Вид операции" данного вида документа.
	// В качестве обработки выбора вида операции назначается процедура 
	// ДействияФормыДействиеУстановитьОперацию модуля формы.
	УстановитьПодменюВыбораВидаОперации(ЭлементыФормы.ДействияФормы.Кнопки.ПодменюВидаОперации,
	                                      ВидОперации.Метаданные().ЗначенияПеречисления,
	                                      Новый Действие("ДействияФормыДействиеУстановитьОперацию"));

	// Заполнить подменю выбора печатных форм.
	СписокМакетов = ЭтотОбъект.ПолучитьСписокПечатныхФорм();
	УстановитьПодменюВыбораПечатнойФормы(ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ПодменюПечати,
	                                       СписокМакетов,
	                                       Новый Действие("ДействияФормыДействиеВыбратьПечатнуюФормы"));

	// Установить печатную форму по умолчанию.
	УстановитьКнопкуПечати(ЭтотОбъект.Метаданные().Имя, ЭтаФорма, СписокМакетов, мПечатнаяФормаПоУмолчанию);

	// Вывести в заголовке формы вид операции.
	УстановитьЗаголовокФормыДокумента(Строка(ВидОперации), ЭтотОбъект, ЭтаФорма);

	// Определить счет-фактуру, введенный на основании этого документа, если такой 
	// счет-фактура существует.
	ЗаполнитьТекстПроСчетФактуру();

	// Обновить содержание информационных строк.
	СформироватьНадписьДолга(ДоговорКонтрагента, Сделка, ЭлементыФормы.ИнфНадписьДолга, , Истина);
	УстановитьЗаголовкиПоДоговору();

	// Запомнить текущие значения реквизитов формы.
	мТекущаяДатаДокумента        = Дата;
	мТекущаяВалютаДокумента      = ВалютаДокумента;
	мТекущаяВалютаВзаиморасчетов = ДоговорКонтрагента.ВалютаВзаиморасчетов;

	// Установить видимость колонок "ХарактеристикаНоменклатуры" и "СерияНоменклатуры"
	УстановитьВидимостьХарактеристикиНоменклатуры(мКолонкиТовары);
	УстановитьВидимостьСерииНоменклатуры(мКолонкиТовары);

	// Установить видимость реквизитов и заголовков колонок.
	УстановитьВидимость();

	// Установить автоотметку незаполненного
	УстановитьАвтоотметку();

	ОбновитьИнфНадписьТовары();

	// Установить активный реквизит.
	АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма);
	Если (мИзКомплектации=Истина) Тогда 
		ЭлементыФормы.СпособРаспределения.Доступность = Ложь;
	КонецЕсли;
    
	//БАЛАНС (04.12.2007)                       
	//
	мПроведениеИзФормы = Истина;
	
	Если НЕ ЭтоНовый() Тогда
		
		УстановитьЭлементыИсторииКнопка(Ссылка, ЭтаФорма);
		
	КонецЕсли; 

	
КонецПроцедуры

Процедура ВыводИстории(Элемент)
	
	ВывестиИсторию(Ссылка, ЭтаФорма);
	
КонецПроцедуры
	
	

// Процедура - обработчик события "ОбновлениеОтображения" формы.
//
Процедура ОбновлениеОтображения()

	// Пересчитаем сумму взаиморасчетов.
	Если КурсВзаиморасчетов = 0 Тогда
		ЭлементыФормы.СуммаВзаиморасчетов.Значение = 0;
	Иначе
		ЭлементыФормы.СуммаВзаиморасчетов.Значение = ФорматСумм(ПересчитатьИзВалютыВВалюту(Сумма + Товары.Итог("Сумма"),  
													ВалютаДокумента, мТекущаяВалютаВзаиморасчетов,
													КурсДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета), КурсВзаиморасчетов,
													КратностьДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета) , КратностьВзаиморасчетов));
	КонецЕсли;

	ЭлементыФормы.ИнфНадписьКурса.Заголовок = ПолучитьИнформациюКурсаВалютыСтрокой(мТекущаяВалютаВзаиморасчетов, 
	                                                                               КурсВзаиморасчетов,
	                                                                               КратностьВзаиморасчетов,
	                                                                               мВалютаРегламентированногоУчета,
	                                                                               Истина);

	ОбновитьПодвал();

	// Подсчитаем количество строк в табличных частях.
	Для каждого СтраницаПанели из ЭлементыФормы.ОсновнаяПанель.Страницы Цикл
		Если СтраницаПанели.Имя = "Товары" Тогда
			СтраницаПанели.Заголовок = "Товары (" + ЭтотОбъект.Товары.Количество() + " поз.)";
		КонецЕсли;
	КонецЦикла;

	ВременнаяСтрока = "Сумма" + Символы.ПС + "расхода (";

	Если ЗначениеНеЗаполнено(ВалютаДокумента) Тогда
		ЭлементыФормы.НадписьСуммаРасхода.Заголовок = ВременнаяСтрока + "<>):";
	Иначе
		ЭлементыФормы.НадписьСуммаРасхода.Заголовок = ВременнаяСтрока + СокрЛП(ЭтотОбъект.ВалютаДокумента) +"):";
	КонецЕсли;

	Если Сумма = 0 Тогда
		ЭлементыФормы.СпособРаспределения.АвтоОтметкаНезаполненного = Ложь;
		ЭлементыФормы.СпособРаспределения.ОтметкаНезаполненного     = Ложь;
	Иначе
		ЭлементыФормы.СпособРаспределения.АвтоОтметкаНезаполненного = Истина;
	КонецЕсли;

КонецПроцедуры // ОбновлениеОтображения()

// Процедура - обработчик события "ОбработкаВыбора" формы.
//
Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)

	Перем Команда;

	Если ТипЗнч(ЗначениеВыбора) = Тип("Структура") Тогда
		ЗначениеВыбора.Свойство("Команда", Команда);
		Если Команда = "ПодборВТабличнуюЧастьТовары" Тогда
			ОбработкаПодбора(Товары, ЗначениеВыбора);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ОбработкаВыбора()

// Процедура - обработчик события "ПослеЗаписи" формы.
//
Процедура ПослеЗаписи()

	// Вывести в заголовке формы вид операции и статус документа (новый, не проведен, проведен).
	УстановитьЗаголовокФормыДокумента(Строка(ВидОперации), ЭтотОбъект, ЭтаФорма);

КонецПроцедуры // ПослеЗаписи()

// Обработчик внешнего события.
//
Процедура ВнешнееСобытие(Источник, Событие, Данные)

	Если Не ВводДоступен() Тогда
		Возврат;
	КонецЕсли;

	Если глТорговоеОборудование <> Неопределено Тогда
		Если Событие = "BarCodeValue" Тогда

			Команда         = "ПодборВТабличнуюЧастьТовары";
			ИмяТабличнойЧасти = "Товары";

			ВременнаяДатаРасчетов = ?(НачалоДня(Дата) = НачалоДня(ТекущаяДата()), Неопределено, Дата);

			СтруктураПараметров = Новый Структура();
			СтруктураПараметров.Вставить("Команда"              , Команда);
			СтруктураПараметров.Вставить("ДатаРасчетов"         , ВременнаяДатаРасчетов);
			СтруктураПараметров.Вставить("ИмяТабличнойЧасти"    , ИмяТабличнойЧасти);

			глТорговоеОборудование.ОбработатьВнешнееСобытиеОтСканераДляФормы(ЭтаФорма, ЭтотОбъект, СтруктураПараметров, Данные);

		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при выборе пункта меню "Заполнить по остаткам" кнопки "Заполнить"
// командной панели табличного поля "Товары",
// вызывает сервисный механизм для заполнения табличной части.
//
Процедура КоманднаяПанельТоварыДействиеЗаполнитьПоПоступлению(Кнопка)

	ЗаполнитьТабличнуюЧастьПоПоступлению( Товары, "Заполнить");

КонецПроцедуры // КоманднаяПанельТоварыДействиеЗаполнитьПоОстаткам()

// Процедура вызывается при выборе пункта меню "Скопировать состав" кнопки "Заполнить"
// командной панели табличного поля "Товары",
// вызывает сервисный механизм для заполнения табличной части.
//
Процедура КоманднаяПанельТоварыДействиеДобавитьИзПоступления(Кнопка)

	ЗаполнитьТабличнуюЧастьПоПоступлению( Товары, "Добавить");

КонецПроцедуры // КоманднаяПанельТоварыДействиеСкопироватьСостав()

// Процедура вызывается при нажатии кнопки "Подбор" командной панели
// табличного поля "Товары", вызывает сервисный механизм для
// подбора номеклатуры в табличную часть "Товары".
//
Процедура КоманднаяПанельТоварыДействиеПодбор(Кнопка)

	ДействиеПодбор(Товары);

КонецПроцедуры // КоманднаяПанельТоварыДействиеПодбор()

// Процедура вызывается при нажатии кнопки "ЦеныВалюта" командной панели
// табличного поля "Товары", вызывает сервисный механизм для изменения
// общих для всей табличной части "Товары" реквизитов - таких, например,
// как тип цен, валюта и т. д.
//
Процедура КоманднаяПанельТоварыДействиеЦеныВалюта(Кнопка)

	СпособЗаполненияЦен = Перечисления.СпособыЗаполненияЦен.ПоЦенамНоменклатурыКонтрагентов;

	// Задать набор реквизитов для редактирования.
	СтруктураРеквизитовДокумента = ПолучитьСтруктуруРеквизитовДокументаДляЦенообразования(ЭтотОбъект);
	СтруктураРеквизитовДокумента.Вставить("НДСВключенВСтоимость");
	СтруктураЗапрещенных = Неопределено;

	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДопРасходов.ВнутреннийРасход Тогда
		СтруктураРеквизитовДокумента.Вставить("БезКонтроляВалютыВзаиморасчетов");
		СтруктураЗапрещенных = Новый Структура("НДСВключенВСтоимость, УчитыватьНДС, СуммаВключаетНДС, КурсВзаиморасчетов");
	КонецЕсли;

	// Вызов общей формы "Цены и валюта" и пересчеты по результатам выбора в этой форме.
	СтруктураЗначений = ОткрытьФормуЦеныИВалюта(ДокументОбъект,
	                                            СтруктураРеквизитовДокумента,
	                                            мВалютаРегламентированногоУчета,
	                                            СтруктураЗапрещенных,
	                                            "Товары");

	Если СтруктураЗначений <> Неопределено Тогда
		Если СтруктураЗначений.НовыйВалютаДокумента <> СтруктураЗначений.ТекущийВалютаДокумента Тогда
			Сумма = ПересчитатьИзВалютыВВалюту(
					Сумма,
					СтруктураЗначений.ТекущийВалютаДокумента,
					СтруктураЗначений.НовыйВалютаДокумента,
					СтруктураЗначений.ТекущийКурсДокумента,
					СтруктураЗначений.НовыйКурсДокумента,
					СтруктураЗначений.ТекущийКратностьДокумента,
					СтруктураЗначений.НовыйКратностьДокумента);
		КонецЕсли;

		ИзменитьЦеныВалюту(ЭтотОбъект, СпособЗаполненияЦен, СтруктураРеквизитовДокумента, СтруктураЗапрещенных, "Товары", мВалютаРегламентированногоУчета,, СтруктураЗначений);

		Для каждого СтрокаТабличнойЧасти Из Товары Цикл
			РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
		КонецЦикла;

		РассчитатьСуммыДокумента();

	КонецЕсли;

	УстановитьВидимость();
	ОбновитьИнфНадписьТовары();

КонецПроцедуры // КоманднаяПанельТоварыКнопкаЦеныВалюта()

// Процедура вызывается при нажатии кнопки "Печать" командной панели формы,
// вызывает печать по умолчанию для формы документа.
//
Процедура ОсновныеДействияФормыДействиеПечать(Кнопка)

	НапечататьДокументПоУмолчанию(ЭтотОбъект, глТекущийПользователь, мПечатнаяФормаПоУмолчанию);

КонецПроцедуры // ОсновныеДействияФормыДействиеПечать()

// Процедура вызывается при выборе пункта подменю "ПодменюВидаОперации" командной панели
// формы. Процедура устанавливает значение реквизита ВидОперации.
//
Процедура ДействияФормыДействиеУстановитьОперацию(Кнопка)

	Если Кнопка <> Неопределено Тогда // найти новое значение вида операции
		ВидОперации = Перечисления.ВидыОперацийПоступлениеДопРасходов[Кнопка.Имя];
	КонецЕсли;

	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДопРасходов.ВнутреннийРасход Тогда
		Контрагент                  = Неопределено;
		ДоговорКонтрагента          = Неопределено;
		Сделка                      = Неопределено;
		КратностьВзаиморасчетов     = 0;
		КурсВзаиморасчетов          = 0;
		ОтражатьВБухгалтерскомУчете = Ложь;
		ОтражатьВНалоговомУчете     = Ложь;

		УчитыватьНДС                = Ложь;
		СуммаВключаетНДС            = Ложь;
		НДСВключенВСтоимость        = Ложь;
		СтавкаНДС                   = Неопределено;
		СуммаНДС                    = 0;

		Для каждого СтрокаТабличнойЧасти Из Товары Цикл
			СтрокаТабличнойЧасти.СтавкаНДС = Неопределено;
			СтрокаТабличнойЧасти.СуммаНДС  = 0;
		КонецЦикла;
	Иначе
		ОтражатьВБухгалтерскомУчете = НЕ ПолучитьЗначениеПоУмолчанию(глТекущийПользователь, "НеОтражатьДокументыВБухгалтерскомУчете");
		ОтражатьВНалоговомУчете     = НЕ ПолучитьЗначениеПоУмолчанию(глТекущийПользователь, "НеОтражатьДокументыВНалоговомУчете");
	КонецЕсли;

	// Отобразить в заголовке формы вид операции.
	УстановитьЗаголовокФормыДокумента(Строка(ВидОперации), ЭтотОбъект, ЭтаФорма);

	// Установить видимость реквизитов по виду операции.
	УстановитьВидимость();

	ОбновитьИнфНадписьТовары();

	// Установить автоотметку незаполненного
	УстановитьАвтоотметку();

КонецПроцедуры // ДействияФормыДействиеУстановитьОперацию()

// Процедура вызывается при выборе пункта подменю "Печать" командной панели
// формы. Процедура отрабатывает выбор печатной формы.
//
Процедура ДействияФормыДействиеВыбратьПечатнуюФормы(Кнопка)

	Если Кнопка <> Неопределено Тогда // найти новое значение вида операции
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ДействиеПечать.Текст = Кнопка.Текст;
		СтруктураМакета = Новый Структура("Макет, ПредставлениеМакета");
		СписокМакетов = ПолучитьСписокПечатныхФорм();
		СтрокаМакетаВСписке = СписокМакетов.НайтиПоЗначению(Кнопка.Имя);
		Если СтрокаМакетаВСписке <> Неопределено Тогда
			Печать(СтрокаМакетаВСписке.Значение);
			СтруктураМакета.Макет = СтрокаМакетаВСписке.Значение;
			СтруктураМакета.ПредставлениеМакета = СтрокаМакетаВСписке.Представление;			
			
		Иначе
			СсылкаВнешнейФормы = Справочники.ДополнительныеПечатныеФормы.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрЗаменить(Кнопка.Имя, "_", "-")));
			Печать(СсылкаВнешнейФормы);
			СтруктураМакета.Макет = СсылкаВнешнейФормы;
			СтруктураМакета.ПредставлениеМакета = Кнопка.Текст;
		КонецЕсли;		
		
		мПечатнаяФормаПоУмолчанию = СтруктураМакета.Макет;
		СохранитьТекущуюКнопкуПечати(ЭтотОбъект.Метаданные().Имя, СтруктураМакета);
	КонецЕсли;

КонецПроцедуры // ДействияФормыДействиеВыбратьПечатнуюФормы()

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

// Процедура выполняет открытие формы работы со свойствами документа
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура выполняет открытие формы работы с категориями документа
//
Процедура ДействияФормыДействиеОткрытьКатегории(Кнопка)

	ОткрытьКатегорииДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Процедура - обработчик события "ПриИзменении" поля ввода даты документа.
//
Процедура ДатаПриИзменении(Элемент)

	ПроверитьНомерДокумента(ЭтотОбъект, мТекущаяДатаДокумента);
	ПриИзмененииЗначенияДатыДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета);

	мТекущаяДатаДокумента = Дата; // запомним текущую дату документа для контроля номера документа

КонецПроцедуры // ДатаПриИзменении()

// Процедура - обработчик события "ПриИзменении" флага отражения
// документа в регламентированном учете.
//
Процедура ОтражатьВБухгалтерскомУчетеПриИзменении(Элемент)

	Если ДоговорКонтрагента.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда
		ОтражатьВНалоговомУчете = Элемент.Значение;
	КонецЕсли;

	УстановитьВидимость();

КонецПроцедуры // ОтражатьВБухгалтерскомУчетеПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода организации.
//
Процедура ОрганизацияПриИзменении(Элемент)

	УстановитьНомерДокумента(ЭтотОбъект);

КонецПроцедуры // ОрганизацияПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода контрагента.
//
Процедура КонтрагентПриИзменении(Элемент)

	// Выполняем общие действия для всех документов при изменении Контрагент.
	ПриИзмененииЗначенияКонтрагента(ЭтотОбъект);

	// Могли поменять договор.
	ПриИзмененииДоговора();

КонецПроцедуры // КонтрагентПриИзменении()

// Процедура - обработчик события "НачалоВыбора" поля ввода ДоговорКонтрагента
//
Процедура ДоговорКонтрагентаНачалоВыбора(Элемент, СтандартнаяОбработка)

	// сохраним договор контрагента, возможно понадобится к нему вернуться
	мТекущийДоговорКонтрагента = ДоговорКонтрагента;

	СписокВидовДоговоров = Новый СписокЗначений;
	СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);

	НачалоВыбораЗначенияДоговораКонтрагента(ЭтотОбъект, ЭтаФорма, Элемент, Контрагент, ДоговорКонтрагента,
	                                        СписокВидовДоговоров, СтандартнаяОбработка);

КонецПроцедуры // ДоговорКонтрагентаНачалоВыбора()

// Процедура - обработчик события "ПриИзменении" поля ввода
// "ДоговорКонтрагента". Процедура выполняет действия при изменении договора
// взаиморасчетов с контрагентом.
//
Процедура ДоговорКонтрагентаПриИзменении(Элемент)

	ПриИзмененииДоговора();

КонецПроцедуры // ДоговорКонтрагентаПриИзменении()

// Процедура - обработчик события "Нажатие" надписи о счете-фактуре.
//   Процедура выполняет либо ввод нового счета-фактуры, либо открывает
// форму уже существующего счета-фактуры, если тот был введен на основании
// текущего документа.
//
Процедура ГиперссылкаСчетФактураНажатие(Элемент)

	ВвестиСчетФактуру(ЭтотОбъект, ЭтаФорма, "СчетФактураПолученный");

КонецПроцедуры // ГиперссылкаСчетФактураНажатие()

// Процедура - обработчик события "НачалоВыбора" поля ввода "КурсВзаиморасчетов".
//
Процедура КурсВзаиморасчетовНачалоВыбора(Элемент, СтандартнаяОбработка)

	// Отменить стандартную обработку.
	СтандартнаяОбработка = Ложь;

	// Выберать дату курса и заполнить курс взаиморасчетов.
	ВыбратьКурсВзаиморасчетов(ЭтотОбъект);

КонецПроцедуры // КурсВзаиморасчетовНачалоВыбора()

// Процедура - обработчик события "НачалоВыбора" поля ввода "Сделка".
//
Процедура СделкаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	// Данный документ выполняет расход по регистру взаиморасчетов.
	НачалоВыбораЗначенияСделки(ЭтотОбъект, ЭтаФорма, Элемент, ДоговорКонтрагента, Сделка, СтандартнаяОбработка, 
	                     "Расход", "ЗаказПоставщику");

КонецПроцедуры // СделкаНачалоВыбора()

// Процедура - обработчик события "ПриИзменении" поля ввода Сумма
//
Процедура СуммаПриИзменении(Элемент)

	РассчитатьСуммыДокумента();

КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ввода СтавкаНДС
//
Процедура СтавкаНДСПриИзменении(Элемент)

	РассчитатьСуммыДокумента();

КонецПроцедуры

// Процедура - обработчик события "Нажатие" кнопки "КнопкаПерерасчитатьДолг".
// Процедура вызывает сервисный механизм пересчета долга контрагента 
// по выбранному в форме договору взаиморасчетов и сделке.
//
Процедура КнопкаПерерасчитатьДолгНажатие(Элемент)

	СформироватьНадписьДолга(ДоговорКонтрагента, Сделка, ЭлементыФормы.ИнфНадписьДолга, Ссылка);

КонецПроцедуры // КнопкаПерерасчитатьАвансДоговораНажатие()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ ТОВАРЫ

// Процедура - обработчик события "ПриВыводеСтроки" табличной части
// "Товары". Формирует данные в колонке "Вес".
//
Процедура ТоварыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Если ЭлементыФормы.Товары.Колонки.Вес.Видимость Тогда
		ОбщийВесТовара = ДанныеСтроки.ЕдиницаИзмерения.Вес * ДанныеСтроки.Количество; 
		ОформлениеСтроки.Ячейки.Вес.УстановитьТекст(Формат(ОбщийВесТовара, "ЧДЦ=3"));
	КонецЕсли;

	// Установим автоотметку незаполненого для документа партии
	// Она должна быть установлена, если вид операции - внутренний расход.
	Автоотметка = Ложь;
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДопРасходов.ВнутреннийРасход Тогда
		Автоотметка = Истина;
	КонецЕсли;

	Если ОформлениеСтроки.Ячейки.ДокументПартии.ОтметкаНезаполненного <> Автоотметка И ЗначениеНеЗаполнено(ДанныеСтроки.ДокументПартии) Тогда
		ОформлениеСтроки.Ячейки.ДокументПартии.ОтметкаНезаполненного = Автоотметка 
	КонецЕсли;

	ПоказатьКодАртикул(мКолонкиТовары, ОформлениеСтроки.Ячейки, ДанныеСтроки.Номенклатура);

КонецПроцедуры // ТоварыПриВыводеСтроки()

// Процедура - обработчик события "ПриИзменении" поля ввода номенклатуры
// в строке табличной части "Товары".
//
Процедура ТоварыНоменклатураПриИзменении(Элемент)

	СтрокаТЧ = ЭлементыФормы.Товары.ТекущиеДанные;

	СтрокаТЧ.ЕдиницаИзмерения = СтрокаТЧ.Номенклатура.ЕдиницаХраненияОстатков;
	СтрокаТЧ.Коэффициент      = СтрокаТЧ.ЕдиницаИзмерения.Коэффициент; 

	РассчитатьСуммуНДСТабЧасти(СтрокаТЧ, ЭтотОбъект);

	ПриИзмененииНоменклатурыТоваров(СтрокаТЧ);

КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ввода единицы
// в строке табличной части "Товары".
//
Процедура ТоварыЕдиницаПриИзменении(Элемент)

	// Выполнить общие действия для всех документов при изменении Единица.
	ПриИзмененииЕдиницыТабЧасти(ЭлементыФормы.Товары.ТекущиеДанные, ЭтотОбъект);

КонецПроцедуры // ТоварыЕдиницаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода единицы
// в строке табличной части "Товары".
//
Процедура ТоварыЗаказПокупателяНачалоВыбора(Элемент, СтандартнаяОбработка)

	НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, Новый Структура(), "Товары");

КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ввода Сумма
// в строке табличной части "Товары".
//
Процедура ТоварыСуммаПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Товары.ТекущиеДанные;

	РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
	РассчитатьСуммыДокумента();

КонецПроцедуры

Процедура ТоварыСтавкаНДСПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Товары.ТекущиеДанные;

	РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);

КонецПроцедуры

Процедура ТоварыДокументПартииНачалоВыбора(Элемент, СтандартнаяОбработка)
	НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, Новый Структура(), "Товары");
КонецПроцедуры

// Процедура вызова структуры подчиненности документа
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ПЕРЕТАСКИВАНИЕ НОМЕНКЛАТУРЫ

// Процедура - обработчик события "ПроверкаПеретаскивания" табличной части "Товары".
//
Процедура ТоварыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)

	Если ЭтоПеретаскиваниеИзПодбора(ПараметрыПеретаскивания) Тогда
		ПроверкаПеретаскиванияИзПодбора(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Ссылка, "Товары");
	Иначе
		ПеретаскиваниеОтменить(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
	КонецЕсли;

КонецПроцедуры // ТоварыПроверкаПеретаскивания()

// Процедура - обработчик события "Перетаскивание" табличной части "Товары".
//
Процедура ТоварыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)

	Если ЭтоПеретаскиваниеИзПодбора(ПараметрыПеретаскивания) Тогда
		ПеретаскиваниеИзПодбора(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
	Иначе
		ПеретаскиваниеОтменить(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
	КонецЕсли;

КонецПроцедуры // ТоварыПеретаскивание()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	яштСменаПользователя(ЭтотОбъект, ЭтаФорма, глТекущийПользователь);
КонецПроцедуры

Процедура КоманднаяПанельТоварыДействиеУвеличитьСтоимостьНаСтоимостьКрышек(Кнопка)
	Если Вопрос("Для выполнения операции необходимо записать документ. Записать?",РежимДиалогаВопрос.ДаНет)=КодВозвратаДиалога.Да тогда
		ЭтотОбъект.Записать();
		Если ТипЗнч(Сделка)<>Тип("ДокументСсылка.ЗаказПоставщику") или Сделка=Документы.ЗаказПоставщику.ПустаяСсылка() Тогда
			Предупреждение("Для увеличения стоимости дисков должен быть заполнен заказ поставщику!!!");	
		Иначе
			Если ВалютаДокумента<>Сделка.ВалютаДокумента тогда
				Предупреждение("Валюты поступления доп. расходов и заказа поставщику не совпадают.");	
			иначе
				Запрос = новый запрос;
				Запрос.Текст="ВЫБРАТЬ
				             |	ПоступлениеДопРасходовТовары.Номенклатура
				             |ПОМЕСТИТЬ втНоменклатураДопРасходов
				             |ИЗ
				             |	Документ.ПоступлениеДопРасходов.Товары КАК ПоступлениеДопРасходовТовары
				             |ГДЕ
				             |	ПоступлениеДопРасходовТовары.Ссылка = &ДокДопРасходов
				             |
				             |СГРУППИРОВАТЬ ПО
				             |	ПоступлениеДопРасходовТовары.Номенклатура
				             |;
				             |
				             |////////////////////////////////////////////////////////////////////////////////
				             |ВЫБРАТЬ РАЗЛИЧНЫЕ
				             |	втНоменклатураДопРасходов.Номенклатура,
				             |	КомплектующиеНоменклатуры.Комплектующая
				             |ПОМЕСТИТЬ втДискиСКрышками
				             |ИЗ
				             |	втНоменклатураДопРасходов КАК втНоменклатураДопРасходов
				             |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КомплектующиеНоменклатуры КАК КомплектующиеНоменклатуры
				             |		ПО втНоменклатураДопРасходов.Номенклатура = КомплектующиеНоменклатуры.Номенклатура
				             |;
				             |
				             |////////////////////////////////////////////////////////////////////////////////
				             |ВЫБРАТЬ
				             |	втДискиСКрышками.Номенклатура,
				             |	А.Цена,
				             |	втДискиСКрышками.Комплектующая
				             |ИЗ
				             |	втДискиСКрышками КАК втДискиСКрышками
				             |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				             |			ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
				             |			МАКСИМУМ(ЗаказПоставщикуТовары.Цена) КАК Цена
				             |		ИЗ
				             |			Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
				             |		ГДЕ
				             |			ЗаказПоставщикуТовары.Ссылка = &ЗаказПоставщику
				             |		
				             |		СГРУППИРОВАТЬ ПО
				             |			ЗаказПоставщикуТовары.Номенклатура) КАК А
				             |		ПО втДискиСКрышками.Комплектующая = А.Номенклатура";
				Запрос.УстановитьПараметр("ДокДопРасходов",ЭтотОбъект.Ссылка);
				Запрос.УстановитьПараметр("ЗаказПоставщику",Сделка);
				Запрос.УстановитьПараметр("Крышки",Справочники.Номенклатура.НайтиПоКоду("0080004"));
				Коэфф=1.1;//крышки заказываем с 10% запасом, стоимость диска увеличиваем на стоимость крышки +10%
				Рез=Запрос.Выполнить().Выгрузить();
				Для каждого стр из Товары цикл
					стрКрышка=Рез.Найти(стр.Номенклатура,"Номенклатура");
					Если стрКрышка<>Неопределено тогда
						стр.Сумма=Стр.Количество*стрКрышка.Цена*Коэфф;
						Если найти(нрег(стр.Номенклатура.Наименование),"legeartis")>0 и стрКрышка.Комплектующая=неопределено тогда
							Сообщить("Для номенклатуры в строке № "+стр.НомерСтроки+" не установлена комплектующая или цена комплектующей.");
						КонецЕсли;	
					КонецЕсли;
				КонецЦикла	 
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мКолонкиТовары = ЭлементыФормы.Товары.Колонки;
