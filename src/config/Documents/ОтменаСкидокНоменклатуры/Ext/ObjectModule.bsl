
Процедура ЗаполнитьПоОснованию(Основание) Экспорт
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.УстановкаСкидокНоменклатуры") Тогда

		Если Документы.Найти(Основание) = Неопределено Тогда
			СтрокаДокумента = Документы.Добавить();
			СтрокаДокумента.УстановкаСкидокНоменклатуры = Основание;
		КонецЕсли;

	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция ПодготовитьТаблицуДвижений(СписокДокументов)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистраторы", СписокДокументов);

	ТекстЗапроса = "ВЫБРАТЬ
	|	СкидкиНаценкиНоменклатуры.ПолучательСкидки,
	|	СкидкиНаценкиНоменклатуры.Номенклатура,
	|	СкидкиНаценкиНоменклатуры.ХарактеристикаНоменклатуры,
	|	СкидкиНаценкиНоменклатуры.Качество,
	|	СкидкиНаценкиНоменклатуры.Условие,
	|	СкидкиНаценкиНоменклатуры.ЗначениеУсловия,
	|	СкидкиНаценкиНоменклатуры.ОграничениеСкидкиНаценки,
	|	СкидкиНаценкиНоменклатуры.Валюта
	|ИЗ
	|	РегистрСведений.СкидкиНаценкиНоменклатуры КАК СкидкиНаценкиНоменклатуры
	|
	|ГДЕ
	|	СкидкиНаценкиНоменклатуры.Регистратор В(&Регистраторы)";

	Запрос.Текст = ТекстЗапроса;
	ТаблицаДвижений = Запрос.Выполнить().Выгрузить();

	Возврат ТаблицаДвижений;

КонецФункции

 // Выполняет движения по регистрам 
//
Процедура ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаПоДокументам, Отказ, Заголовок)

	НаборДвижений   = Движения.СкидкиНаценкиНоменклатуры;
	ТаблицаДвижений = НаборДвижений.Выгрузить();

	// Заполним таблицу движений.
	ЗагрузитьВТаблицуЗначений(ТаблицаПоДокументам, ТаблицаДвижений);

	НаборДвижений.мПериод          = СтруктураШапкиДокумента.Дата;
	НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;

	Если (Не Отказ) Тогда
		Движения.СкидкиНаценкиНоменклатуры.ВыполнитьДвижения();
	КонецЕсли;

КонецПроцедуры // ДвиженияПоРегистрам

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ПредставлениеДокументаПриПроведении(Ссылка);

	// Сформируем структуру реквизитов шапки документа
	СтруктураШапкиДокумента = СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);

	// Заполним по шапке документа дерево параметров, нужных при проведении.
	ДеревоПолейЗапросаПоШапке = СформироватьДеревоПолейЗапросаПоШапке();

	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = СформироватьЗапросПоДеревуПолей(ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШапкиДокумента, "");

	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("УстановкаСкидокНоменклатуры"                     , "УстановкаСкидокНоменклатуры");

	РезультатЗапросаПоДокументам = СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Документы", СтруктураПолей);
	СписокПоДокументам = Новый СписокЗначений;
	СписокПоДокументам.ЗагрузитьЗначения(РезультатЗапросаПоДокументам.Выгрузить().ВыгрузитьКолонку("УстановкаСкидокНоменклатуры"));
	ТаблицаПоДокументам          = ПодготовитьТаблицуДвижений(СписокПоДокументам);

	Если Не Отказ Тогда
		ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаПоДокументам, Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры	// ОбработкаПроведения

Процедура ОбработкаЗаполнения(Основание)

	ЗаполнитьПоОснованию(Основание);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если НЕ Отказ Тогда
	
		обЗаписатьПротоколИзменений(ЭтотОбъект);
	
	КонецЕсли; 

КонецПроцедуры
