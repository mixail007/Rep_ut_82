
процедура ПроверитьСуммуГотПрод(Отказ)
	delta = ГотоваяПродукция.Итог("Сумма") - ( Материалы.Итог("Сумма") + ДопЗатраты.Итог("Сумма") );
	Если delta тогда
		Отказ = Истина;
		#Если Клиент тогда
		Сообщить("Сумма готовой продукции: "+строка(ГотоваяПродукция.Итог("Сумма"))
		+"р. отличается от суммы Материалов: "+строка(Материалы.Итог("Сумма"))
		+" и Доп.Затрат: "+строка(ДопЗатраты.Итог("Сумма"))+" на "+строка(delta)+"р.!", СтатусСообщения.Внимание);
		#КонецЕсли	
	КонецЕсли;	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	КачествоГотовойПродукции = справочники.Качество.Новый;
	
	ПроверитьСуммуГотПрод(Отказ);
	
	Если Отказ тогда
		возврат;
	КонецЕсли;
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//1) регистр НезавершенноеПроизводство Расход
	Движения.НезавершенноеПроизводство.Записывать = Истина;
	Движения.НезавершенноеПроизводство.Очистить();
	Для Каждого ТекСтрокаМатериалы Из Материалы Цикл
		Движение = Движения.НезавершенноеПроизводство.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаМатериалы.Номенклатура;
		Движение.Количество   = ТекСтрокаМатериалы.Количество;
		Движение.Сумма        = ТекСтрокаМатериалы.Сумма;     // сумма вторична!
	КонецЦикла;

	//2) регистр Затраты  - СТОРНО!
	Движения.Затраты.Записывать = Истина;
	Движения.Затраты.Очистить();
		Движение = Движения.Затраты.Добавить();
		Движение.Период = Дата;
		Движение.Подразделение = Подразделение;
		Движение.СтатьяЗатрат 	 = СтатьяМатЗатрат;
		Движение.СтатьяЗатратУпр = СтатьяМатЗатратУПР;
		Движение.НоменклатурнаяГруппа = НомГруппаЗатрат;
		Движение.Сумма = -Материалы.Итог("Сумма");
		
	//+++ доп.затраты - СТОРНО! +++	
	Для Каждого ТекСтрокаДопЗатрат Из ДопЗатраты Цикл
		Движение = Движения.Затраты.Добавить();
		Движение.Период = Дата;
		Движение.Подразделение = Подразделение;
		Движение.СтатьяЗатрат 	 = ТекСтрокаДопЗатрат.СтатьяЗатрат;
		Движение.СтатьяЗатратУпр = ТекСтрокаДопЗатрат.СтатьяЗатратУПР;
		Движение.НоменклатурнаяГруппа = НомГруппаЗатрат;
		Движение.Сумма = -ТекСтрокаДопЗатрат.Сумма;
	КонецЦикла;	
		
	//3) регистр ПартииТоваровНаСкладах Приход
	// сумма = сумма мат. + доп. затрат !
	Движения.ПартииТоваровНаСкладах.Записывать = Истина;
	Движения.ПартииТоваровНаСкладах.Очистить();
	Для Каждого ТекСтрокаГотоваяПродукция Из ГотоваяПродукция Цикл
		Движение = Движения.ПартииТоваровНаСкладах.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Склад  = СкладГотовойПродукции;
		Движение.Качество     = КачествоГотовойПродукции;
		Движение.Номенклатура = ТекСтрокаГотоваяПродукция.Номенклатура;
		Движение.Количество = ТекСтрокаГотоваяПродукция.Количество;
		Движение.Стоимость = ТекСтрокаГотоваяПродукция.Сумма;
	КонецЦикла;
    	
	//4) регистр ТоварыНаСкладах Приход
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.ТоварыНаСкладах.Очистить();
	Для Каждого ТекСтрокаГотоваяПродукция Из ГотоваяПродукция Цикл
		Движение = Движения.ТоварыНаСкладах.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Склад  = СкладГотовойПродукции;
		Движение.Качество     = КачествоГотовойПродукции;
		Движение.Номенклатура = ТекСтрокаГотоваяПродукция.Номенклатура;
		Движение.Количество   = ТекСтрокаГотоваяПродукция.Количество;
	КонецЦикла;

	//5) регистр ТоварыОрганизаций Приход
	Движения.ТоварыОрганизаций.Записывать = Истина;
	Движения.ТоварыОрганизаций.Очистить();
	Для Каждого ТекСтрокаГотоваяПродукция Из ГотоваяПродукция Цикл
		Движение = Движения.ТоварыОрганизаций.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Качество     = КачествоГотовойПродукции;
		Движение.Номенклатура = ТекСтрокаГотоваяПродукция.Номенклатура;
		Движение.Количество   = ТекСтрокаГотоваяПродукция.Количество;
	КонецЦикла;

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры

//при вводе на основании -> ТребованиеНакладная
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ТребованиеНакладная") Тогда
		// Заполнение шапки
		Комментарий = "По тр.накладной № "+ДанныеЗаполнения.Номер+" от "+формат(ДанныеЗаполнения.Дата,"ДЛФ=D");
		Организация   = ДанныеЗаполнения.Организация;
		Подразделение = ДанныеЗаполнения.Подразделение;
		
		СкладГотовойПродукции = ДанныеЗаполнения.Склад; //куда всё скинули...
		
		Для Каждого ТекСтрокаПродукция Из ДанныеЗаполнения.Продукция Цикл
			НоваяСтрока = ГотоваяПродукция.Добавить();
			НоваяСтрока.Количество   = ТекСтрокаПродукция.Количество;
			НоваяСтрока.Номенклатура = ТекСтрокаПродукция.Номенклатура;
			НоваяСтрока.Сумма        = ТекСтрокаПродукция.ПлановаяСтоимость;
		КонецЦикла;
		
		Запрос = новый Запрос; // сумма незавершёнки только в регистре! 
		запрос.Текст = "ВЫБРАТЬ
		               |	НезавершенноеПроизводствоОбороты.Номенклатура,
		               |	НезавершенноеПроизводствоОбороты.КоличествоОборот как Количество,
		               |	НезавершенноеПроизводствоОбороты.СуммаОборот как Сумма
		               |ИЗ
		               |	РегистрНакопления.НезавершенноеПроизводство.Обороты(&НачДата, &КонДата, Регистратор, ) КАК НезавершенноеПроизводствоОбороты
		               |ГДЕ
		               |	НезавершенноеПроизводствоОбороты.Регистратор = &Регистратор";
		запрос.УстановитьПараметр("Регистратор", ДанныеЗаполнения);
		запрос.УстановитьПараметр("НачДата", ДанныеЗаполнения.Дата);
		запрос.УстановитьПараметр("КонДата", ДанныеЗаполнения.Дата+1);
		результат = запрос.Выполнить();
		Если результат.Пустой() тогда
			Для каждого выборка из ДанныеЗаполнения.Материалы цикл  // прямо из ТЧ.
			НоваяСтрока = Материалы.Добавить();
			НоваяСтрока.Номенклатура = выборка.Номенклатура;
			НоваяСтрока.Количество   = выборка.Количество; 
			//суммы НЕТ!
			КонецЦикла;	
		Иначе	
			выборка = результат.Выбрать(); 
			пока выборка.Следующий() цикл
			НоваяСтрока = Материалы.Добавить();
			НоваяСтрока.Номенклатура = выборка.Номенклатура;
			НоваяСтрока.Количество   = выборка.Количество;
			НоваяСтрока.Сумма        = выборка.Сумма;
			КонецЦикла;
		КонецЕсли;
		
		//по первой строке (все статьи должны быть одинаковые!
		СтатьяМатЗатрат    = ДанныеЗаполнения.Материалы[0].статьяЗатрат;
		СтатьяМатЗатратУПР = ДанныеЗаполнения.Материалы[0].статьяЗатратУПР;
		
	СпособРаспределенияДопЗатрат = перечисления.СпособыРаспределенияДопРасходов.ПоСумме;
		
	КонецЕсли;
	
	//{{__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	//}}__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
КонецПроцедуры

Процедура мРасчетСебестоимостиГотовойПродукции() ЭКСПОРТ
	
	//Для контроля количества материалов...
	таблМат = Материалы.Выгрузить();
	
	//1) расчет мат.стоимости
	Для каждого стрГ из ГотоваяПродукция цикл
		суммаГ = 0;
		Для каждого стрК из стрГ.Спецификация.Состав цикл //сборка материалов и проверка их достаточности...
			КолНадо = стрК.Количество * стрГ.Количество;
			стрМ = таблМат.Найти( стрК.Комплектующая, "Номенклатура");
			Если стрМ = Неопределено тогда
				КолЕсть = 0;
			Иначе КолЕсть = стрМ.Количество;
			КонецЕсли;	
			
			НеХватает = КолНадо - КолЕсть;
			Если  НеХватает>0 тогда
				#Если Клиент тогда
					Сообщить(строка(стрГ.НомерСтроки)+") Недостаточно "+строка(НеХватает)+" шт. "+строка(стрК.Комплектующая)+" в таблице материалов!", СтатусСообщения.Внимание);
				#КонецЕсли	
			КонецЕсли;
			
			Если стрМ <> Неопределено тогда
				сумма1 = КолНадо * стрМ.Сумма / стрМ.Количество; //сумма по Спецификации
				суммаГ = суммаГ + сумма1;
				стрМ.Количество = ?(НеХватает>0, 0, стрМ.Количество - КолНадо);
				стрМ.Сумма      = ?(НеХватает>0, 0, стрМ.Сумма      - Сумма1 );  
			КонецЕсли;
		КонецЦикла;	
		стрГ.Сумма = суммаГ;
	КонецЦикла;
	
	//2) распределение всех затрат на готовую продукцию --------------------------------------------
	СуммаДопЗатрат = ДопЗатраты.Итог("Сумма");
	Если СуммаДопЗатрат=0 тогда
		возврат;
	КонецЕсли;
	
	Если СпособРаспределенияДопЗатрат = перечисления.СпособыРаспределенияДопРасходов.ПоСумме тогда
		ВсяГотПродукция =ГотоваяПродукция.Итог("Сумма");
	ИначеЕсли СпособРаспределенияДопЗатрат = перечисления.СпособыРаспределенияДопРасходов.ПоКоличеству тогда
		ВсяГотПродукция =ГотоваяПродукция.Итог("Количество");
	ИначеЕсли СпособРаспределенияДопЗатрат = перечисления.СпособыРаспределенияДопРасходов.ПоВесу тогда
		ВсяГотПродукция = 0;
		Для каждого стрГ из ГотоваяПродукция цикл
			ВсяГотПродукция = ВсяГотПродукция + стрГ.Номенклатура.ЕдиницаХраненияОстатков.Вес * стрГ.Количество;
		КонецЦикла;
	КонецЕсли;
	
	макс = 0; стрМакс = неопределено;СуммаДопЗатратГ = 0; // Для исправления округления - остаток на макс. сумму1
	Стоимость1 = СуммаДопЗатрат/ВсяГотПродукция;
	Для каждого стрГ из ГотоваяПродукция цикл
		
		Если СпособРаспределенияДопЗатрат = перечисления.СпособыРаспределенияДопРасходов.ПоСумме тогда
			сумма1 =  стрГ.Сумма * Стоимость1;   	
		ИначеЕсли СпособРаспределенияДопЗатрат = перечисления.СпособыРаспределенияДопРасходов.ПоКоличеству тогда
			сумма1 =  стрГ.Количество * Стоимость1;   	
		ИначеЕсли СпособРаспределенияДопЗатрат = перечисления.СпособыРаспределенияДопРасходов.ПоВесу тогда
			сумма1 =  стрГ.Номенклатура.ЕдиницаХраненияОстатков.Вес * стрГ.Количество * Стоимость1;   	
		КонецЕсли;
		
		Если сумма1>=макс тогда
			макс = сумма1; стрМакс = стрГ;
		КонецЕсли;	
		
		СуммаДопЗатратГ = СуммаДопЗатратГ + Окр(сумма1,2); // Для контроля копеек
		стрГ.Сумма = стрГ.Сумма + Окр(сумма1,2);
	КонецЦикла;
	
	Если СуммаДопЗатратГ<>СуммаДопЗатрат тогда   //исправление ошибок округления
		 стрМакс.Сумма = стрМакс.Сумма + (СуммаДопЗатрат - СуммаДопЗатратГ);
		 #Если Клиент тогда
		 сообщить("Разность "+строка(СуммаДопЗатрат - СуммаДопЗатратГ)+"р. записана в строку № "+строка(стрМакс.НомерСтроки));
		 #КонецЕсли	 
	КонецЕсли;
	
КонецПроцедуры	
