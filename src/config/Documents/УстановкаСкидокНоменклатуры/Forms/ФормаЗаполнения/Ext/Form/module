Перем мСтруктураПараметров Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить(ВидСравнения.ВИерархии);
	СписокВыбора.Добавить(ВидСравнения.ВСпискеПоИерархии);
	ЭлементыФормы.ВидСравненияГрупп.СписокВыбора = СписокВыбора;
	ЭлементыФормы.ВидСравненияГрупп.Значение     = ВидСравнения.ВИерархии;
	ВидСравненияГруппПриИзменении(ЭлементыФормы.ВидСравненияГрупп);

КонецПроцедуры

Процедура ПриЗакрытии()
	// Вставить содержимое обработчика.
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура вызывается при нажатии кнопки "ОК",
// заполняет табличную часть документа.
//
Процедура ОКНажатие(Элемент)

	ТекстЗапроса = "ВЫБРАТЬ
	               |	Номенклатура.Ссылка КАК Номенклатура,
	               |	&ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
	               |	&ОграничениеСкидкиНаценки КАК ОграничениеСкидкиНаценки
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |
	               |ГДЕ
	               |	(НЕ(Номенклатура.Ссылка.ЭтоГруппа)) И
	               |	(НЕ(Номенклатура.Ссылка.Набор)) И
	               |	Номенклатура.Ссылка В ИЕРАРХИИ(&Группы)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	КомплектующиеНоменклатуры.Комплектующая,
	               |	&ПроцентСкидкиНаценки,
	               |	&ОграничениеСкидкиНаценки
	               |ИЗ
	               |	РегистрСведений.КомплектующиеНоменклатуры КАК КомплектующиеНоменклатуры
	               |
	               |ГДЕ
	               |	(НЕ(КомплектующиеНоменклатуры.Номенклатура.Ссылка.ЭтоГруппа)) И
	               |	(НЕ(КомплектующиеНоменклатуры.Номенклатура.Ссылка.Набор)) И
	               |	(КомплектующиеНоменклатуры.Номенклатура.Ссылка В ИЕРАРХИИ (&Группы))";

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Группы", Группы);
	Запрос.УстановитьПараметр("ПроцентСкидкиНаценки", ПроцентСкидкиНаценки);
	Запрос.УстановитьПараметр("ОграничениеСкидкиНаценки", ОграничениеСкидкиНаценки);

	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();

	Добавить = Ложь;
	мСтруктураПараметров.Свойство("Добавить",                        Добавить);
	КачествоПустой = Справочники.Качество.ПустаяСсылка();
	Если Добавить = Неопределено Или НЕ Добавить Тогда

		ТаблицаТоваров.Колонки.Добавить("Качество");
		ТаблицаТоваров.ЗаполнитьЗначения(КачествоПустой, "Качество");
		Товары.Загрузить(ТаблицаТоваров);
	Иначе
		ТаблицаПроверкиДублей = Товары.Выгрузить();
		Для Каждого СтрокаДобавления ИЗ ТаблицаТоваров Цикл
			Если ТаблицаПроверкиДублей.Найти(СтрокаДобавления.Номенклатура, "Номенклатура") = Неопределено Тогда
				СтрокаТовара = Товары.Добавить();
				СтрокаТовара.Номенклатура = СтрокаДобавления.Номенклатура;
				СтрокаТовара.Качество = КачествоПустой;
				СтрокаТовара.ПроцентСкидкиНаценки = ПроцентСкидкиНаценки;
				СтрокаТовара.ОграничениеСкидкиНаценки = ОграничениеСкидкиНаценки;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля "ВидСравненияГрупп".
//
Процедура ВидСравненияГруппПриИзменении(Элемент)

	Если Элемент.Значение = ВидСравнения.ВСпискеПоИерархии Тогда
		Значение = Неопределено;
		Если ЭлементыФормы.Группы.ОграничениеТипа <> Новый ОписаниеТипов("СписокЗначений")Тогда
			Значение = ЭлементыФормы.Группы.Значение;
		КонецЕсли;
		ЭлементыФормы.Группы.ОграничениеТипа = Новый ОписаниеТипов("СписокЗначений");
		ЭлементыФормы.Группы.ТипЗначенияСписка = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
		Группы = Новый СписокЗначений;
		Если Значение <> Неопределено
		   И Значение <> Справочники.Номенклатура.ПустаяСсылка() Тогда
			Группы.Добавить(Значение);
		КонецЕсли;

	ИначеЕсли Элемент.Значение = ВидСравнения.ВИерархии Тогда
		Значение = Неопределено;
		Если ЭлементыФормы.Группы.ОграничениеТипа = Новый ОписаниеТипов("СписокЗначений")Тогда
			Если Группы.Количество()>0 Тогда
				Значение = Группы[0].Значение;
			КонецЕсли;
		КонецЕсли;
		ЭлементыФормы.Группы.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
		Группы = Справочники.Номенклатура.ПустаяСсылка();
		Если Значение <> Неопределено Тогда
			Группы = Значение;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры
