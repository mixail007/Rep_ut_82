Процедура УстановитьДоступность()

	Если ЗначениеНеЗаполнено(ДисконтнаяКарта) Тогда
	
		ЭлементыФормы.НадписьСостояние.Заголовок = "Ожидание чтения накопительной карты...";
		ЭлементыФормы.НадписьСостояние.ЦветТекста = WebЦвета.Красный;
		//ЭлементыФормы.НадписьФИО.Видимость = Ложь;
		//ЭлементыФормы.КонтактФИО.Видимость = Ложь;
	
	Иначе
		
		ЭлементыФормы.НадписьСостояние.Заголовок = "Накопительная карта прочитана. Заполнить рег. данные";
		ЭлементыФормы.НадписьСостояние.ЦветТекста = WebЦвета.Синий;
		//ЭлементыФормы.НадписьФИО.Видимость = Истина;
		//ЭлементыФормы.КонтактФИО.Видимость = Истина;
		
	КонецЕсли; 
	
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ОК.Доступность = Не ЗначениеНеЗаполнено(ДисконтнаяКарта);

КонецПроцедуры

Процедура ПриИзмененииДисконтнойКарты()

	//
	Если ЗначениеНеЗаполнено(ДисконтнаяКарта) Тогда
		;
	ИначеЕсли ДисконтнаяКарта.ТипКарты <> Перечисления.ТипыИнформационныхКарт.Дисконтная Тогда
		Предупреждение("Считанная карта не является дисконтной!");
		ДисконтнаяКарта = Справочники.ИнформационныеКарты.ПустаяСсылка();
	ИначеЕсли ДисконтнаяКарта.ВидКарты <> Перечисления.ВидыИнформационныхКарт.Магнитная Тогда
		Предупреждение("Считанная карта не является магнитной!");
		ДисконтнаяКарта = Справочники.ИнформационныеКарты.ПустаяСсылка();
	ИначеЕсли ДисконтнаяКарта.ВидСкидки <> Перечисления.ВидыСкидокИнформационныхКарт.Накопительная Тогда
		Предупреждение("Считанная карта не является накопительной картой!");
		ДисконтнаяКарта = Справочники.ИнформационныеКарты.ПустаяСсылка();
	ИначеЕсли ДисконтнаяКарта.СтатусДисконтнойКарты <> Перечисления.СтатусыДисконтнойКарты.Неактивна Тогда
		Предупреждение("Считанная карта уже активна или аннулирована!");
		ДисконтнаяКарта = Справочники.ИнформационныеКарты.ПустаяСсылка();
	КонецЕсли; 
	
	УстановитьДоступность();

КонецПроцедуры
 
Процедура КартаVIPПриИзменении(Элемент)
	
	ПриИзмененииДисконтнойКарты();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	УстановитьДоступность();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если СтрДлина(КонтактФИО) < 6 Тогда
		Предупреждение("Необходимо ввести как минимум ФИО!");
		Возврат;
	КонецЕсли; 
		
	врОб = ДисконтнаяКарта.ПолучитьОбъект();
	
	врОб.Наименование = КонтактФИО + " (накоп.)";
	врОб.СтатусДисконтнойКарты = Перечисления.СтатусыДисконтнойКарты.Активна;
	
    врОб.КонтактФИО              = КонтактФИО;
    врОб.КонтактДатаРождения     = КонтактДатаРождения;
    врОб.КонтактДомашнийАдрес    = КонтактДомашнийАдрес;
    врОб.КонтактДомашнийТелефон  = КонтактДомашнийТелефон;
    врОб.КонтактСотовыйТелефон   = КонтактСотовыйТелефон;
	
	врОшибка = Ложь;
	Попытка
		врОб.Записать();
	Исключение
		вр = ОписаниеОшибки();
		врОшибка = Истина;
	КонецПопытки; 
	
	Если врОшибка Тогда
		Предупреждение("Не удалось зарегистрировать информацию по накопительной карте. Попробуйте другую карту и обратитесь к администратору!");
	Иначе
		Предупреждение("Накопительная карта на лицо """ + КонтактФИО + """ зарегистрирована!");
		ЭтаФорма.Закрыть(ДисконтнаяКарта);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Не ВводДоступен() Тогда
		Возврат;
	КонецЕсли;

	Если глТорговоеОборудование <> Неопределено
		И Событие = "MagneticStripeCardValue" Тогда

		ТекДанные = глТорговоеОборудование.ПолучитьДанныеОтРидера(Данные);
		врСс = глТорговоеОборудование.НайтиМагнитнуюКарту(ТекДанные);
		Если врСс <> Неопределено Тогда
			ДисконтнаяКарта = врСс;
		Иначе
			ДисконтнаяКарта = Справочники.ИнформационныеКарты.ПустаяСсылка();
		КонецЕсли; 
		
		ПриИзмененииДисконтнойКарты();
			
	КонецЕсли;

КонецПроцедуры
