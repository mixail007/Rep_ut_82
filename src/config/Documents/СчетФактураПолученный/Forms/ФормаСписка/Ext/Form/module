//  Процедура печатает выбранный документ 
// Печатается та форма, которая была отпечатана при нажатии в документе кнопки
// печати по умолчанию
//
Процедура ДействияФормыДействиеПечать(Кнопка)

	Если ЭлементыФормы.ДокументСписок.ТекущаяСтрока = Неопределено тогда
		Возврат
	КонецЕсли;

	НапечататьДокументИзФормыСписка(ЭлементыФормы.ДокументСписок.ТекущаяСтрока.ПолучитьОбъект());

КонецПроцедуры // ДействиеПечать()

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	Если ЭлементыФормы.ДокументСписок.ТекущаяСтрока = Неопределено тогда
		Возврат
	КонецЕсли;

	НапечататьДвиженияДокумента(ЭлементыФормы.ДокументСписок.ТекущиеДанные.Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

// Процедура управляет подстановкой значений в вычисляемые колонки списка
// документов
Процедура СписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Если ДанныеСтроки <> Неопределено Тогда

		Колонка = ОформлениеСтроки.Ячейки;

		Попытка

			Если ДанныеСтроки.ДокументОснование.Метаданные().Имя = "АвансовыйОтчет" Тогда

				Сумма     = 0;
				СуммаНДС  = 0;
				Поставщик = "";

				РасчетСуммыПоСчетуФактуре(ДанныеСтроки.Ссылка, Сумма, СуммаНДС, Поставщик);

				Колонка.Контрагент.УстановитьТекст(Поставщик);
				Колонка.Сумма.УстановитьТекст(Сумма + СуммаНДС);
				Колонка.ВалютаДокумента.УстановитьТекст(ДанныеСтроки.ДокументОснование.ВалютаДокумента);

			Иначе
				Колонка.Контрагент.УстановитьТекст(ДанныеСтроки.ДокументОснование.Контрагент);
				Колонка.Сумма.УстановитьТекст(ДанныеСтроки.ДокументОснование.СуммаДокумента);
				Колонка.ВалютаДокумента.УстановитьТекст(ДанныеСтроки.ДокументОснование.ВалютаДокумента);

			КонецЕсли;

		Исключение

		КонецПопытки;

	КонецЕсли;

КонецПроцедуры // СписокПриВыводеСтроки()

// Функция вызывается из тела процедуры "СписокПриВыводеСтроки"
// Выполняет расчет общей суммы и суммы НДС по счету-фактуре для 
// тех случаев, когда документа-основания это "Авансовый отчет"
Процедура РасчетСуммыПоСчетуФактуре(СчетФактура, Сумма, СуммаНДС, Поставщик)

	Если ТипЗнч(СчетФактура) <> Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
					|	АвансовыйОтчетТовары.Поставщик,
					|	АвансовыйОтчетТовары.СчетФактура,
					|	АвансовыйОтчетТовары.СтавкаНДС,
					|	СУММА(АвансовыйОтчетТовары.СуммаНДС) КАК СуммаНДС,
					|	СУММА(ВЫБОР КОГДА АвансовыйОтчетТовары.Ссылка.СуммаВключаетНДС ТОГДА АвансовыйОтчетТовары.Сумма - АвансовыйОтчетТовары.СуммаНДС ИНАЧЕ АвансовыйОтчетТовары.Сумма КОНЕЦ) КАК Сумма
					|ИЗ
					|	Документ.АвансовыйОтчет.Товары КАК АвансовыйОтчетТовары

					|ГДЕ
					|	АвансовыйОтчетТовары.СчетФактура = &СчетФактура И
					|	АвансовыйОтчетТовары.Ссылка = &АвансовыйОтчет

					|СГРУППИРОВАТЬ ПО
					|	АвансовыйОтчетТовары.Поставщик,
					|	АвансовыйОтчетТовары.СчетФактура,
					|	АвансовыйОтчетТовары.СтавкаНДС

					|ОБЪЕДИНИТЬ ВСЕ

					|ВЫБРАТЬ
					|	АвансовыйОтчетПрочее.Поставщик,
					|	АвансовыйОтчетПрочее.СчетФактура,
					|	АвансовыйОтчетПрочее.СтавкаНДС,
					|	СУММА(АвансовыйОтчетПрочее.СуммаНДС),
					|	СУММА(ВЫБОР КОГДА АвансовыйОтчетПрочее.Ссылка.СуммаВключаетНДС ТОГДА АвансовыйОтчетПрочее.Сумма - АвансовыйОтчетПрочее.СуммаНДС ИНАЧЕ АвансовыйОтчетПрочее.Сумма КОНЕЦ)
					|ИЗ
					|	Документ.АвансовыйОтчет.Прочее КАК АвансовыйОтчетПрочее

					|ГДЕ
					|	АвансовыйОтчетПрочее.СчетФактура = &СчетФактура И
					|	АвансовыйОтчетПрочее.Ссылка = &АвансовыйОтчет

					|СГРУППИРОВАТЬ ПО
					|	АвансовыйОтчетПрочее.Поставщик,
					|	АвансовыйОтчетПрочее.СчетФактура,
					|	АвансовыйОтчетПрочее.СтавкаНДС";
		
	Запрос.УстановитьПараметр("СчетФактура",	СчетФактура);
	Запрос.УстановитьПараметр("АвансовыйОтчет", СчетФактура.ДокументОснование);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Поставщик = Выборка.Поставщик;
		Сумма = 	Сумма + Выборка.Сумма;
		СуммаНДС =  СуммаНДС + Выборка.СуммаНДС;
	КонецЦикла;
	
КонецПроцедуры // РасчетСуммыПоСчетуФактуре()

// Процедура вызывается при выборе пункта подменю "Структура подчиненности" меню "Перейти".
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	Если ЭлементыФормы.ДокументСписок.ТекущиеДанные = Неопределено тогда
		Возврат
	КонецЕсли;
	
	ПоказатьСтруктуруПодчиненностиДокумента(ЭтаФорма.ЭлементыФормы.ДокументСписок.ТекущаяСтрока);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКИЧИ СОБЫТИЙ ФОРМЫ

