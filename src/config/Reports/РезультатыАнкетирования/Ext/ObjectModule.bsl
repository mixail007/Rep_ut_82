////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Возвращает основную форму отчета, связанную с данным экземпляром отчета
//
// Параметры
//	Нет
//
Функция ПолучитьОсновнуюФорму() Экспорт
	
	ОснФорма = ПолучитьФорму();
	ОснФорма.ОбщийОтчет = ОбщийОтчет;
	ОснФорма.ЭтотОтчет = ЭтотОбъект;
	Возврат ОснФорма;
	
КонецФункции // ПолучитьОсновнуюФорму()

// Читает свойство Построитель отчета
//
// Параметры
//	Нет
//
Функция ПолучитьПостроительОтчета() Экспорт

	Возврат ОбщийОтчет.ПолучитьПостроительОтчета();

КонецФункции // ПолучитьПостроительОтчета()

// Настраивает отчет по переданной структуре параметров
//
// Параметры:
//	Нет.
//
Процедура Настроить(Параметры) Экспорт

	ОбщийОтчет.Настроить(Параметры, ЭтотОбъект);

КонецПроцедуры

// Выполняет настройку отчета по умолчанию для заданного вида отчета
//
// Параметры: 
// 
Процедура ЗаполнитьНачальныеНастройки(ВосстанавливаемНастройку = Ложь) Экспорт
	
	ОбщийОтчет.ИмяРегистра = "-";
	ОбщийОтчет.мНазваниеОтчета = "Результаты анкетирования";
	
	ПостроительОтчета = ОбщийОтчет.ПостроительОтчета;
	ОбщийОтчет.СтруктураФорматаПолей.Вставить("ТиповойОтвет", "БЛ=Нет; БИ=Да");
	
	// структура представлений полей
	СтруктураПредставлениеПолей = Новый Структура();
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	РезультатыАнкетирования.Ссылка.ОпрашиваемоеЛицо КАК ФизЛицо,
	|	РезультатыАнкетирования.ТиповойОтвет КАК ТиповойОтвет,
	|	ВариантыОтветовОпросов.Владелец.ВесВопроса * ВариантыОтветовОпросов.ОценкаОтвета КАК АбсолютнаяОценка
	|
	|{ВЫБРАТЬ
	|	РезультатыАнкетирования.Ссылка.ОпрашиваемоеЛицо КАК ФизЛицо,
	|	РезультатыАнкетирования.Ссылка.ТиповаяАнкета.* КАК Анкета,
	|	РезультатыАнкетирования.Ссылка.Дата КАК ДатаАнкетирования,	
	|	РезультатыАнкетирования.Вопрос.* КАК Вопрос,
	|	РезультатыАнкетирования.Ответ КАК Ответ,
	|	РезультатыАнкетирования.ТиповойОтвет.* КАК ТиповойОтвет,
	|	РезультатыАнкетирования.Ссылка КАК Опрос,
	|	ВариантыОтветовОпросов.ОценкаОтвета КАК ОценкаОтвета,
	|	ВариантыОтветовОпросов.Владелец.ВесВопроса КАК ВесВопроса
	|
	|	//СВОЙСТВА
	|}
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОпросВопросы.Вопрос КАК Вопрос,
	|		ОпросВопросы.Ответ КАК Ответ,
	|		ОпросВопросы.ТиповойОтвет КАК ТиповойОтвет,
	|		ОпросВопросы.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.Опрос.Вопросы КАК ОпросВопросы
	|	
	|	ГДЕ
	|		(ОпросВопросы.ТиповойОтвет <> НЕОПРЕДЕЛЕНО) и (ОпросВопросы.ТиповойОтвет <> &ПустойОтвет)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	//|		ВЫБОР КОГДА ОпросСоставнойОтвет.Вопрос = &ПустойВопрос ТОГДА ОпросСоставнойОтвет.ВопросВладелец ИНАЧЕ ОпросСоставнойОтвет.Вопрос КОНЕЦ,
	|		ОпросСоставнойОтвет.ВопросВладелец как Вопрос,
	|		ОпросСоставнойОтвет.Ответ,
	|		ОпросСоставнойОтвет.ТиповойОтвет,
	|		ОпросСоставнойОтвет.Ссылка
	|	ИЗ
	|		Документ.Опрос.СоставнойОтвет КАК ОпросСоставнойОтвет) КАК РезультатыАнкетирования
	|
	|		//СОЕДИНЕНИЯ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтветовОпросов КАК ВариантыОтветовОпросов
	|			ПО РезультатыАнкетирования.ТиповойОтвет = ВариантыОтветовОпросов.Ссылка
	|
	|{ГДЕ
	|	РезультатыАнкетирования.Ссылка.ОпрашиваемоеЛицо КАК ФизЛицо,
	|	РезультатыАнкетирования.Ссылка КАК Опрос,
	|	РезультатыАнкетирования.Ссылка.ТиповаяАнкета КАК Анкета,
	|	РезультатыАнкетирования.Ссылка.Дата КАК ДатаАнкетирования,	
	|	РезультатыАнкетирования.Вопрос.* КАК Вопрос,
	|	РезультатыАнкетирования.Ответ КАК Ответ,
	|	РезультатыАнкетирования.ТиповойОтвет КАК ТиповойОтвет
	|
	|	//СВОЙСТВА
	|	//КАТЕГОРИИ
	|}
	|
	|{УПОРЯДОЧИТЬ ПО
	|	РезультатыАнкетирования.Ссылка.ОпрашиваемоеЛицо КАК ФизЛицо,
	|	РезультатыАнкетирования.Ссылка КАК Опрос,
	|	РезультатыАнкетирования.Ссылка.ТиповаяАнкета КАК Анкета,
	|	РезультатыАнкетирования.Ссылка.Дата КАК ДатаАнкетирования,	
	|	РезультатыАнкетирования.Вопрос КАК Вопрос,
	|	РезультатыАнкетирования.Ответ КАК Ответ,
	|	РезультатыАнкетирования.ТиповойОтвет КАК ТиповойОтвет
	|
	|	//СВОЙСТВА
	|}
	|
	|ИТОГИ СУММА(АбсолютнаяОценка) ПО ОБЩИЕ
	|
	|{ИТОГИ ПО
	|	РезультатыАнкетирования.Ссылка.ОпрашиваемоеЛицо КАК ФизЛицо,
	|	РезультатыАнкетирования.Ссылка КАК Опрос,
	|	РезультатыАнкетирования.Ссылка.ТиповаяАнкета КАК Анкета,
	|	РезультатыАнкетирования.Вопрос КАК Вопрос,
	|	РезультатыАнкетирования.Ответ КАК Ответ,
	|	РезультатыАнкетирования.ТиповойОтвет КАК ТиповойОтвет
	| 	//ДАННЫЕ О ФИЗЛИЦЕ: ПОЛЯ
	|	//СВОЙСТВА
	|}
	|";

	// При использовании свойств и категорий в текст запроса добавляются дополнительные поля
	Если ОбщийОтчет.ИспользоватьСвойстваИКатегории Тогда

		// Свойства и категории, назначаемые пользователем:

		ТаблицаПолей = Новый ТаблицаЗначений;
		ТаблицаПолей.Колонки.Добавить("ПутьКДанным");  // описание поля запроса поля, для которого добавляются свойства и категории. Используется в условии соединения с регистром сведений, хранящим значения свойств или категорий
		ТаблицаПолей.Колонки.Добавить("Представление");// представление поля, для которого добавляются свойства и категории. 
		ТаблицаПолей.Колонки.Добавить("Назначение");   // назначение свойств/категорий объектов для данного поля
		ТаблицаПолей.Колонки.Добавить("ТипЗначения");  // тип значения поля, для которого добавляются свойства и категории. Используется, если не установлено назначение
		ТаблицаПолей.Колонки.Добавить("НетКатегорий"); // признак НЕиспользования категорий для объекта

		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "РезультатыАнкетирования.Ссылка.ОпрашиваемоеЛицо";
		НоваяСтрока.Представление = "Опрашиваемое лицо";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ФизическиеЛица;

		// Добавим строки запроса, необходимые для использования свойств и категорий
		ДобавитьВТекстСвойстваИКатегории(ТаблицаПолей, ТекстЗапроса, СтруктураПредставлениеПолей, ОбщийОтчет.мСоответствиеНазначений, ОбщийОтчет.ПостроительОтчета.Параметры);
	КонецЕсли;	
	
	ПостроительОтчета.Текст = ТекстЗапроса;							  
	
	// представление полей запроса
	СтруктураПредставлениеПолей.Вставить("ФизЛицо", "Опрашиваемое лицо");
	СтруктураПредставлениеПолей.Вставить("ДатаАнкетирования", "Дата опроса");
	СтруктураПредставлениеПолей.Вставить("ТиповойОтвет", "Типовой ответ");
	СтруктураПредставлениеПолей.Вставить("ОценкаОтвета", "Оценка ответа");
	СтруктураПредставлениеПолей.Вставить("ВесВопроса", "Вес вопроса");
	СтруктураПредставлениеПолей.Вставить("АбсолютнаяОценка", "Абсолютная оценка");
	ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей, ПостроительОтчета);
	
	// формат полей
	ОбщийОтчет.СтруктураФорматаПолей.Вставить("ДатаАнкетирования", "ДФ=dd.MM.yyyy");
	ОбщийОтчет.СтруктураФорматаПолей.Вставить("ТиповойОтвет", "ЧЦ=15; ЧДЦ=2; ДФ=dd.MM.yyyy; БЛ=Нет; БИ=Да");
	
	Если ОбщийОтчет.Показатели.Найти("АбсолютнаяОценка","Имя") = Неопределено Тогда
	
		Показатель = ОбщийОтчет.Показатели.Добавить();
		Показатель.Имя = "АбсолютнаяОценка";
		Показатель.Представление = "Абс.оценка";
		Показатель.Использование = Истина;
		
		Показатель = ОбщийОтчет.мТаблицаПоказатели.Добавить();
		Показатель.ИмяПоля = "АбсолютнаяОценка";
		Показатель.ПредставлениеПоля = "Абс.оценка";
		Показатель.ФорматнаяСтрока = "ЧЦ=15; ЧДЦ=2";
	
	КонецЕсли;
	
    Если Не ВосстанавливаемНастройку Тогда
		
		// группировки по умолчанию
		ПостроительОтчета.ИзмеренияСтроки.Добавить("ФизЛицо");
		ПостроительОтчета.ИзмеренияСтроки.Добавить("Вопрос");
		
		// отборы по умолчанию
		МассивОтбора = Новый Массив;
		МассивОтбора.Добавить("ФизЛицо");
		ЗаполнитьОтбор(МассивОтбора, ПостроительОтчета);
		
		// поля по умолчанию
		ПостроительОтчета.ВыбранныеПоля.Очистить();
		ПостроительОтчета.ВыбранныеПоля.Добавить("ТиповойОтвет");
		ПостроительОтчета.ВыбранныеПоля.Добавить("Ответ");
	
		// настройки отчета
		ОбщийОтчет.РаскрашиватьИзмерения = Истина;
		ОбщийОтчет.ВыводитьИтогиПоВсемУровням = Истина;
		ОбщийОтчет.ВыводитьПоказателиВСтроку = Истина;
		ОбщийОтчет.мРежимВводаПериода = 1; // Дата
	КонецЕсли;
	
КонецПроцедуры

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом,
//	ЕстьОшибки - флаг того, что при формировании произошли ошибки
//
//Процедура СформироватьОтчет(ДокументРезультат, ЕстьОшибки = Ложь) Экспорт
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок = Ложь, ВысотаЗаголовка = 0, ТолькоЗаголовок = Ложь) Экспорт

	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ПустойВопрос", ПланыВидовХарактеристик.ВопросыДляАнкетирования.ПустаяСсылка());
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ПустойОтвет", Справочники.ВариантыОтветовОпросов.ПустаяСсылка());
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ДатаАктуальности", ОбщийОтчет.ДатаКон);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ДатаАктуальности_Год", Год(ОбщийОтчет.ДатаКон));
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ДатаАктуальности_Месяц", Месяц(ОбщийОтчет.ДатаКон));
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ДатаАктуальности_День", День(ОбщийОтчет.ДатаКон));
	ОбщийОтчет.СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок);

КонецПроцедуры

// Возвращает форму настройки 
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	
//
Функция ПолучитьФормуНастройки() Экспорт
	
	ФормаНастройки = ОбщийОтчет.ПолучитьФорму("ФормаНастройка");
	Возврат ФормаНастройки;
	
КонецФункции // ПолучитьФормуНастройки()

// Процедура обработки расшифровки
//
// Параметры:
//	Нет.
//
Процедура ОбработкаРасшифровки(РасшифровкаСтроки, ПолеТД, ВысотаЗаголовка, СтандартнаяОбработка) Экспорт
	
	// Добавление расшифровки из колонки
	Если ТипЗнч(РасшифровкаСтроки) = Тип("Структура") Тогда
		
		// Расшифровка колонки находится в заголовке колонки
		РасшифровкаКолонки = ПолеТД.Область(ВысотаЗаголовка+2, ПолеТД.ТекущаяОбласть.Лево).Расшифровка;

		Расшифровка = Новый Структура;

		Для каждого Элемент Из РасшифровкаСтроки Цикл
			Расшифровка.Вставить(Элемент.Ключ, Элемент.Значение);
		КонецЦикла;

		Если ТипЗнч(РасшифровкаКолонки) = Тип("Структура") Тогда

			Для каждого Элемент Из РасшифровкаКолонки Цикл
				Расшифровка.Вставить(Элемент.Ключ, Элемент.Значение);
			КонецЦикла;

		КонецЕсли; 

		ОбщийОтчет.ОбработкаРасшифровкиСтандартногоОтчета(Расшифровка, СтандартнаяОбработка, ЭтотОбъект);

	КонецЕсли;
	
КонецПроцедуры // ОбработкаРасшифровки()

// Формирует структуру, в которую складываются настройки
//
Функция СформироватьСтруктуруДляСохраненияНастроек(ПоказыватьЗаголовок) Экспорт
	
	СтруктураНастроек = Новый Структура;
	
	ОбщийОтчет.СформироватьСтруктуруДляСохраненияНастроек(СтруктураНастроек, ПоказыватьЗаголовок);
	
	Возврат СтруктураНастроек;
	
КонецФункции

// Заполняет настройки из структуры - кроме состояния панели "Отбор"
//
Процедура ВосстановитьНастройкиИзСтруктуры(СохраненныеНастройки, ПоказыватьЗаголовок) Экспорт

	ОбщийОтчет.ВосстановитьНастройкиИзСтруктуры(СохраненныеНастройки, ПоказыватьЗаголовок);
	
КонецПроцедуры
