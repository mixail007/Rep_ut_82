///////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 

Перем мСтруктураНастройки Экспорт;
Перем мВосстановлениеНастройки Экспорт;
Перем мНаименованиеТекущейНастройки;
Перем мНаименованиеСкопированнойНастройки;

Перем мИмяТаблицыНастроек Экспорт;

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

Процедура УправлениеОтображениемФормы()
	
	Если мВосстановлениеНастройки = Истина Тогда
		
		Если мИмяТаблицыНастроек = "ДревовидныйЖурналДокументов_ТаблицаНастроекТП" Тогда
			Заголовок = "Восстановление настройки табличных полей";
		Иначе
			Заголовок = "Восстановление настройки журнала";
		КонецЕсли;
		
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ДействиеДобавить);
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ДействиеСкопировать);
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ДействиеИзменить);
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ДействиеУдалить);
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ДействиеЗакончитьРедактирование);
		ЭлементыФормы.ТабличноеПолеСписокНастроек.ТолькоПросмотр = Истина;
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ДействиеНастройкиВсехПользователей.Пометка = НастройкиВсехПользователей;
		
	Иначе
		
		Если мИмяТаблицыНастроек = "ДревовидныйЖурналДокументов_ТаблицаНастроекТП" Тогда
			Заголовок = "Сохранение настройки табличных полей";
		Иначе
			Заголовок = "Сохранение настройки журнала";
		КонецЕсли;
		
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ДействиеНастройкиВсехПользователей);
		
	КонецЕсли;
	
КонецПроцедуры // УправлениеОтображениемФормы()

Функция ПолучитьНовоеНаименованиеНастройки(Наименование)
	
	Результат = Наименование;
	
	Индекс = 0;
	
	Пока СписокНастроек.Найти(Результат, "НаименованиеНастройки") <> Неопределено Цикл
		
		Индекс = Индекс + 1;
		Результат = Наименование + " " + Формат(Индекс, "ЧГ=0");
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ПолучитьНовоеНаименованиеНастройки()

Процедура ТабличноеПолеСписокНастроекПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для каждого ОформлениеСтроки из ОформленияСтрок Цикл
		
		Если ОформлениеСтроки.ДанныеСтроки <> Неопределено И ОформлениеСтроки.ДанныеСтроки.ИспользоватьПриОткрытии <> Неопределено Тогда
		
			Если ОформлениеСтроки.ДанныеСтроки.ИспользоватьПриОткрытии Тогда
			
				ОформлениеСтроки.Шрифт = Новый Шрифт(ОформлениеСтроки.Шрифт,,, Истина);
			
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура ТабличноеПолеСписокНастроекПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		Если Копирование Тогда
			
			мНаименованиеСкопированнойНастройки = Элемент.ТекущиеДанные.НаименованиеНастройки;
			Элемент.ТекущиеДанные.НаименованиеНастройки = ПолучитьНовоеНаименованиеНастройки(Элемент.ТекущиеДанные.НаименованиеНастройки);
			Элемент.ТекущиеДанные.ИспользоватьПриОткрытии = Ложь;
			
		Иначе
			
			Элемент.ТекущиеДанные.НаименованиеНастройки = ПолучитьНовоеНаименованиеНастройки("Основная");
			
			Если мСтруктураНастройки.Свойство("Пользователь") = Ложь ИЛИ НЕ ЗначениеЗаполнено(мСтруктураНастройки.Пользователь) Тогда
				Элемент.ТекущиеДанные.Пользователь = ""
			Иначе
				Элемент.ТекущиеДанные.Пользователь = мСтруктураНастройки.Пользователь;
			КонецЕсли;
			
			Элемент.ТекущиеДанные.ИспользоватьПриОткрытии = Ложь;
			Элемент.ТекущиеДанные.СохранятьАвтоматически = Ложь;
			
		КонецЕсли;
		
	Иначе
		
		мНаименованиеТекущейНастройки = Элемент.ТекущиеДанные.НаименованиеНастройки;
		
	КонецЕсли;
	
КонецПроцедуры // ТабличноеПолеСписокНастроекПриНачалеРедактирования()

Процедура ТабличноеПолеСписокНастроекПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ПустаяСтрока(Элемент.ТекущиеДанные.НаименованиеНастройки) Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	Если СписокНастроек.НайтиСтроки(Новый Структура("НаименованиеНастройки", Элемент.ТекущиеДанные.НаименованиеНастройки)).Количество() > 1 Тогда
		
		Предупреждение("Настройка с таким наименованием уже существует. Укажите уникальное наименование настройки.");
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	СтруктураНастройки = Новый Структура;
	СтруктураНастройки.Вставить("Пользователь", Элемент.ТекущиеДанные.Пользователь);
	СтруктураНастройки.Вставить("ИмяОбъекта", мСтруктураНастройки.ИмяОбъекта);
	СтруктураНастройки.Вставить("НаименованиеНастройки", Элемент.ТекущиеДанные.НаименованиеНастройки);
	СтруктураНастройки.Вставить("СохраненнаяНастройка", мСтруктураНастройки.СохраненнаяНастройка);
	СтруктураНастройки.Вставить("ИспользоватьПриОткрытии", Элемент.ТекущиеДанные.ИспользоватьПриОткрытии);
	СтруктураНастройки.Вставить("СохранятьАвтоматически", Элемент.ТекущиеДанные.СохранятьАвтоматически);
	
	Если НоваяСтрока Тогда
		
		СтруктураСкопированнойНастройки = Новый Структура;
		
		СтруктураСкопированнойНастройки.Вставить("Пользователь", Элемент.ТекущиеДанные.Пользователь);
		СтруктураСкопированнойНастройки.Вставить("ИмяОбъекта", мСтруктураНастройки.ИмяОбъекта);
		СтруктураСкопированнойНастройки.Вставить("НаименованиеНастройки", мНаименованиеСкопированнойНастройки);
		
		Если ОбработкаОбъект.ПолучитьНастройку(СтруктураСкопированнойНастройки, мИмяТаблицыНастроек) Тогда
			
			СтруктураСкопированнойНастройки.НаименованиеНастройки = СтруктураНастройки.НаименованиеНастройки;
			СтруктураСкопированнойНастройки.ИспользоватьПриОткрытии = СтруктураНастройки.ИспользоватьПриОткрытии;
			СтруктураСкопированнойНастройки.СохранятьАвтоматически = СтруктураНастройки.СохранятьАвтоматически;
			
			ОбработкаОбъект.СохранитьНастройку(СтруктураСкопированнойНастройки, мИмяТаблицыНастроек);
			
		Иначе	
			
			ОбработкаОбъект.СохранитьНастройку(СтруктураНастройки, мИмяТаблицыНастроек);
			
		КонецЕсли;
		
	Иначе
		
		СтруктураЗаменяемойНастройки = Новый Структура;
		
		СтруктураЗаменяемойНастройки.Вставить("Пользователь", Элемент.ТекущиеДанные.Пользователь);
		СтруктураЗаменяемойНастройки.Вставить("ИмяОбъекта", мСтруктураНастройки.ИмяОбъекта);
		СтруктураЗаменяемойНастройки.Вставить("НаименованиеНастройки", мНаименованиеТекущейНастройки);
		
		Если ОбработкаОбъект.СохранитьНастройку(СтруктураНастройки, мИмяТаблицыНастроек, СтруктураЗаменяемойНастройки) Тогда
		
			Если мСтруктураНастройки.НаименованиеНастройки = мНаименованиеТекущейНастройки Тогда
				
				мСтруктураНастройки.НаименованиеНастройки = СтруктураНастройки.НаименованиеНастройки;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	мНаименованиеТекущейНастройки = Неопределено;
	мНаименованиеСкопированнойНастройки = Неопределено;
	
КонецПроцедуры // ТабличноеПолеСписокНастроекПередОкончаниемРедактирования()

Процедура ТабличноеПолеСписокНастроекПередУдалением(Элемент, Отказ)
	
	Если ЭлементыФормы.ТабличноеПолеСписокНастроек.ТекущаяСтрока = Неопределено Тогда
			
		Предупреждение("Не выбрана сохраняемая настройка.");
		Возврат;
			
	КонецЕсли;
		
	СтруктураНастройки = Новый Структура;
	СтруктураНастройки.Вставить("Пользователь", ЭлементыФормы.ТабличноеПолеСписокНастроек.ТекущиеДанные.Пользователь);
	СтруктураНастройки.Вставить("ИмяОбъекта", мСтруктураНастройки.ИмяОбъекта);
	СтруктураНастройки.Вставить("НаименованиеНастройки", ЭлементыФормы.ТабличноеПолеСписокНастроек.ТекущиеДанные.НаименованиеНастройки);
	
	Если ОбработкаОбъект.УдалитьНастройку(СтруктураНастройки, мИмяТаблицыНастроек) Тогда
		
		Если мСтруктураНастройки.НаименованиеНастройки = ЭлементыФормы.ТабличноеПолеСписокНастроек.ТекущиеДанные.НаименованиеНастройки Тогда
			
			мСтруктураНастройки.НаименованиеНастройки = Неопределено;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ТабличноеПолеСписокНастроекПередУдалением()

Процедура ТабличноеПолеСписокНастроекПриИзмененииФлажка(Элемент, Колонка)
	
	Если Колонка.Имя = "ИспользоватьПриОткрытии" Тогда
		
		Если Элемент.ТекущиеДанные.ИспользоватьПриОткрытии Тогда
			
			Для каждого Настройка из СписокНастроек Цикл
				
				Если Настройка.НаименованиеНастройки <> Элемент.ТекущиеДанные.НаименованиеНастройки Тогда
					
					Настройка.ИспользоватьПриОткрытии = Ложь;
					
				КонецЕсли;
				
				Если мСтруктураНастройки.НаименованиеНастройки = Настройка.НаименованиеНастройки Тогда
					
					мСтруктураНастройки.ИспользоватьПриОткрытии = Настройка.ИспользоватьПриОткрытии;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли Колонка.Имя = "СохранятьАвтоматически" Тогда
		
		Если мСтруктураНастройки.НаименованиеНастройки = Элемент.ТекущиеДанные.НаименованиеНастройки Тогда
					
			мСтруктураНастройки.СохранятьАвтоматически = Элемент.ТекущиеДанные.СохранятьАвтоматически;
					
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ТабличноеПолеСписокНастроекВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	КоманднаяПанельФормыДействиеВыбрать(ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ДействиеВыбрать);
	
КонецПроцедуры // ТабличноеПолеСписокНастроекВыбор()

Процедура ЗаполнитьНастройки()
	
	СтруктураНастройки = Новый Структура;
		
	Если мСтруктураНастройки.Свойство("Пользователь") = Ложь ИЛИ НЕ ЗначениеЗаполнено(мСтруктураНастройки.Пользователь) Тогда
		СтруктураНастройки.Вставить("Пользователь", "");
	Иначе
		СтруктураНастройки.Вставить("Пользователь", мСтруктураНастройки.Пользователь);
	КонецЕсли;
		
	СтруктураНастройки.Вставить("ИмяОбъекта", мСтруктураНастройки.ИмяОбъекта);
	
	СписокНастроек = ОбработкаОбъект.ПолучитьНастройки(СтруктураНастройки, мИмяТаблицыНастроек, НастройкиВсехПользователей, мВосстановлениеНастройки, мВосстановлениеНастройки);
	
	НайденнаяСтрока = СписокНастроек.Найти(мСтруктураНастройки.НаименованиеНастройки, "НаименованиеНастройки");
	
	Если НайденнаяСтрока <> Неопределено Тогда
		
		ЭлементыФормы.ТабличноеПолеСписокНастроек.ТекущаяСтрока = НайденнаяСтрока;
		
	КонецЕсли;

КонецПроцедуры // ЗаполнитьНастройки()

Процедура КоманднаяПанельФормыДействиеВыбрать(Кнопка)
		
	Если мВосстановлениеНастройки Тогда
		
		Если ЭлементыФормы.ТабличноеПолеСписокНастроек.ТекущаяСтрока = Неопределено Тогда
				
			Предупреждение("Не выбрана восстанавливаемая настройка.");
			Возврат;
				
		КонецЕсли;
		
		СтруктураНастройки = Новый Структура;
		СтруктураНастройки.Вставить("Пользователь", ЭлементыФормы.ТабличноеПолеСписокНастроек.ТекущиеДанные.Пользователь);
		СтруктураНастройки.Вставить("ИмяОбъекта", мСтруктураНастройки.ИмяОбъекта);
		СтруктураНастройки.Вставить("НаименованиеНастройки", ЭлементыФормы.ТабличноеПолеСписокНастроек.ТекущиеДанные.НаименованиеНастройки);
		
		Если ОбработкаОбъект.ПолучитьНастройку(СтруктураНастройки, мИмяТаблицыНастроек) Тогда
			
			Закрыть(СтруктураНастройки);
			
		Иначе
			
			Закрыть();
			
		КонецЕсли;
		
	Иначе
		
		Если ЭлементыФормы.ТабличноеПолеСписокНастроек.ТекущаяСтрока = Неопределено Тогда
				
			Предупреждение("Не выбрана сохраняемая настройка.");
			Возврат;
				
		КонецЕсли;
		
		СтруктураНастройки = Новый Структура;
		СтруктураНастройки.Вставить("Пользователь", ЭлементыФормы.ТабличноеПолеСписокНастроек.ТекущиеДанные.Пользователь);
		СтруктураНастройки.Вставить("ИмяОбъекта", мСтруктураНастройки.ИмяОбъекта);
		СтруктураНастройки.Вставить("НаименованиеНастройки", ЭлементыФормы.ТабличноеПолеСписокНастроек.ТекущиеДанные.НаименованиеНастройки);
		СтруктураНастройки.Вставить("СохраненнаяНастройка", мСтруктураНастройки.СохраненнаяНастройка);
		СтруктураНастройки.Вставить("ИспользоватьПриОткрытии", ЭлементыФормы.ТабличноеПолеСписокНастроек.ТекущиеДанные.ИспользоватьПриОткрытии);
		СтруктураНастройки.Вставить("СохранятьАвтоматически", ЭлементыФормы.ТабличноеПолеСписокНастроек.ТекущиеДанные.СохранятьАвтоматически);
		
		Если ОбработкаОбъект.СохранитьНастройку(СтруктураНастройки, мИмяТаблицыНастроек) Тогда
			
			Закрыть(СтруктураНастройки);
			
		Иначе
			
			Закрыть();
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // КоманднаяПанельФормыДействиеВыбрать()

Процедура КоманднаяПанельФормыДействиеНастройкиВсехПользователей(Кнопка)
	
	НастройкиВсехПользователей = НЕ НастройкиВсехПользователей;
	ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ДействиеНастройкиВсехПользователей.Пометка = НастройкиВсехПользователей;
	
	ЗаполнитьНастройки();

КонецПроцедуры // КоманднаяПанельФормыДействиеНастройкиВсехПользователей()

Процедура КоманднаяПанельФормыВосстановитьНастройкиИзФайла(Кнопка)
	
	СтруктураНастройки = Новый Структура;
	Если мСтруктураНастройки.Свойство("Пользователь") = Ложь ИЛИ НЕ ЗначениеЗаполнено(мСтруктураНастройки.Пользователь) Тогда
		СтруктураНастройки.Вставить("Пользователь", "");
	Иначе
		СтруктураНастройки.Вставить("Пользователь", мСтруктураНастройки.Пользователь);
	КонецЕсли;
	СтруктураНастройки.Вставить("ИмяОбъекта", мСтруктураНастройки.ИмяОбъекта);
	
	ТаблицаНастроек = ОбработкаОбъект.ПолучитьНастройки(СтруктураНастройки, мИмяТаблицыНастроек, Истина, Истина, Истина);
	Если (ТаблицаНастроек = Неопределено или ТипЗнч(ТаблицаНастроек) <> Тип("ТаблицаЗначений"))
		и Вопрос("Текущий список настроек не пустой и будет замещён. Продолжить?", РежимДиалогаВопрос.ДаНет, 90, КодВозвратаДиалога.Нет) = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Форма = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Форма.Заголовок = "Выберите файл для загрузки настроек:";
	Если мИмяТаблицыНастроек = "ДревовидныйЖурналДокументов_ТаблицаНастроекТП" Тогда
		Форма.Фильтр = "Файл настроек табличных полей (*.ujt)|*.ujt";
		Форма.Расширение = "ujt";
	Иначе
		Форма.Фильтр = "Файл настроек универсального журнала (*.ujd)|*.ujd";
		Форма.Расширение = "ujd";
	КонецЕсли;
	Форма.МножественныйВыбор = Ложь;
	
	Если Не Форма.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Файл = Новый ТекстовыйДокумент;
		Файл.Прочитать(Форма.ПолноеИмяФайла, КодировкаТекста.ANSI);
		НоваяТаблицаНастроек = ЗначениеИзСтрокиВнутр(Файл.ПолучитьТекст());
	Исключение
		НоваяТаблицаНастроек = Неопределено;
	КонецПопытки;
	
	Если НоваяТаблицаНастроек = Неопределено или ТипЗнч(НоваяТаблицаНастроек) <> Тип("ТаблицаЗначений") Тогда
		Предупреждение("Файл настроек имеет неверный формат или повреждён!");
		Возврат;
	КонецЕсли;
	
	//ТаблицаНастроек = ОбработкаОбъект.ПолучитьТаблицуНастроек(мИмяТаблицыНастроек);
	Для каждого СтрокаНастройки Из НоваяТаблицаНастроек Цикл
		СтруктураНастройки.Вставить("НаименованиеНастройки", СтрокаНастройки.НаименованиеНастройки);
		СтруктураНастройки.Вставить("СохраненнаяНастройка", СтрокаНастройки.СохраненнаяНастройка.Получить());
		СтруктураНастройки.Вставить("ИспользоватьПриОткрытии", СтрокаНастройки.ИспользоватьПриОткрытии);
		СтруктураНастройки.Вставить("СохранятьАвтоматически", СтрокаНастройки.СохранятьАвтоматически);
		ОбработкаОбъект.СохранитьНастройку(СтруктураНастройки, мИмяТаблицыНастроек);
	КонецЦикла;
	
	ЗаполнитьНастройки();
	
КонецПроцедуры

Процедура КоманднаяПанельФормыСохранитьНастройкиВФайл(Кнопка)
	
	СтруктураНастройки = Новый Структура;
	Если мСтруктураНастройки.Свойство("Пользователь") = Ложь ИЛИ НЕ ЗначениеЗаполнено(мСтруктураНастройки.Пользователь) Тогда
		СтруктураНастройки.Вставить("Пользователь", "");
	Иначе
		СтруктураНастройки.Вставить("Пользователь", мСтруктураНастройки.Пользователь);
	КонецЕсли;
	СтруктураНастройки.Вставить("ИмяОбъекта", мСтруктураНастройки.ИмяОбъекта);
	
	ТаблицаНастроек = ОбработкаОбъект.ПолучитьНастройки(СтруктураНастройки, мИмяТаблицыНастроек, Истина, Истина, Истина);
	Если ТаблицаНастроек = Неопределено или ТипЗнч(ТаблицаНастроек) <> Тип("ТаблицаЗначений") или ТаблицаНастроек.Количество() = 0 Тогда
		Предупреждение("Список настроек пустой.", 90);
		Возврат;
	КонецЕсли;
	
	Форма = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Форма.Заголовок = "Выберите каталог для выгрузки настроек:";
	Если мИмяТаблицыНастроек = "ДревовидныйЖурналДокументов_ТаблицаНастроекТП" Тогда
		Форма.Фильтр = "Файл настроек табличных полей (*.ujt)|*.ujt";
		Форма.Расширение = "ujt";
	Иначе
		Форма.Фильтр = "Файл настроек универсального журнала (*.ujd)|*.ujd";
		Форма.Расширение = "ujd";
	КонецЕсли;
	Форма.МножественныйВыбор = Ложь;
	
	Если Не Форма.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	Файл = Новый ТекстовыйДокумент;
	Файл.УстановитьТекст(ЗначениеВСтрокуВнутр(ТаблицаНастроек));
	Файл.Записать(Форма.ПолноеИмяФайла, КодировкаТекста.ANSI);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ МОДУЛЯ
//

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если ТипЗнч(мСтруктураНастройки) <> Тип("Структура") Тогда
			
		Предупреждение("Не заполнена структура настроек.");
		Отказ = Истина;
		Возврат;
			
	КонецЕсли;
		
	Если мСтруктураНастройки.Свойство("ИмяОбъекта") = Ложь ИЛИ ПустаяСтрока(мСтруктураНастройки.ИмяОбъекта) Тогда
			
		Предупреждение("Не заполнено имя объекта в структуре настроек.");
		Отказ = Истина;
		Возврат;
			
	КонецЕсли;

КонецПроцедуры // ПередОткрытием()

Процедура ПриОткрытии()
	
	ЗаполнитьНастройки();
	УправлениеОтображениемФормы();

КонецПроцедуры // ПриОткрытии()

///////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//

мВосстановлениеНастройки = Ложь;