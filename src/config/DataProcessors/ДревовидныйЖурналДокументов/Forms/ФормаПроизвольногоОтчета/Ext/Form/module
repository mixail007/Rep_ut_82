///////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
Перем ЭлементовГруппировокСтрок;
Перем ЭлементовГруппировокКолонок;

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

Процедура СгенерироватьКнопкиУправленияГруппировкой()
	
	ТабличныйДокумент = ЭлементыФормы.Результат;
	
	КоличествоГруппировокСтрок = ТабличныйДокумент.КоличествоУровнейГруппировокСтрок();
	КоличествоГруппировокКолонок = ТабличныйДокумент.КоличествоУровнейГруппировокКолонок();
	
	Если ЭлементовГруппировокСтрок = КоличествоГруппировокСтрок
		и ЭлементовГруппировокКолонок = КоличествоГруппировокКолонок Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТабличногоДокумента = ТабличныйДокумент.Имя;
	ЗакладкаРезультат = ЭлементыФормы.ПанельСКД;
	ЗакладкаРезультат.ТекущаяСтраница = ЗакладкаРезультат.Страницы.РезультатСКД;
	
	// Привязка к верхнему левому углу ПоляТабличногоДокумента
	НачалоВерх = ТабличныйДокумент.Верх + ?(КоличествоГруппировокКолонок, 5 + КоличествоГруппировокКолонок * 13, 2);
	НачалоЛево = ТабличныйДокумент.Лево + 2;
	
	// Удалим лишние кнопки
	УдалитьВсеКнопкиСтрок = ЭлементовГруппировокКолонок <> КоличествоГруппировокКолонок;
	Для Индекс = ?(УдалитьВсеКнопкиСтрок, 1, КоличествоГруппировокСтрок + 1) По ЭлементовГруппировокСтрок Цикл
		ИмяКнопки = "КнопкаГруппировкиСтрок_" + Формат(Индекс, "ЧЦ=3; ЧН=; ЧВН=") + "_" + ИмяТабличногоДокумента;
		ТекЭлемент = ЭлементыФормы.Найти(ИмяКнопки);
		Если ТекЭлемент <> Неопределено Тогда
			ЭлементыФормы.Удалить(ТекЭлемент);
		КонецЕсли;
	КонецЦикла;
	
	// Добавим новые кнопки по количеству группировок
	Для Индекс = 1 По КоличествоГруппировокСтрок Цикл
		ИмяКнопки = "КнопкаГруппировкиСтрок_" + Формат(Индекс, "ЧЦ=3; ЧН=; ЧВН=") + "_" + ИмяТабличногоДокумента;
		Если ЭлементыФормы.Найти(ИмяКнопки) = Неопределено Тогда
			НоваяКнопка = ЭлементыФормы.Добавить(Тип("Кнопка"), ИмяКнопки,, ЗакладкаРезультат);
			НоваяКнопка.Верх      = НачалоВерх;
			НоваяКнопка.Лево      = НачалоЛево + (13 * (Индекс - 1));
			НоваяКнопка.Высота    = 12;
			НоваяКнопка.Ширина    = 12;
			НоваяКнопка.Шрифт     = Новый Шрифт("Arial", 7);
			НоваяКнопка.Заголовок = "" + Индекс;
			Если Индекс = 1 Тогда
				НоваяКнопка.Подсказка = "Показать группировки верхнего уровня";
			ИначеЕсли Индекс = КоличествоГруппировокСтрок Тогда
				НоваяКнопка.Подсказка = "Показать все группировки";
			Иначе
				НоваяКнопка.Подсказка = "Показать группировки до уровня " + Индекс;
			КонецЕсли;
			НоваяКнопка.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
			Попытка
				НоваяКнопка.УстановитьДействие("Нажатие", Новый Действие("УправлениеОтображениемЭлементовФормы"));
			Исключение
			КонецПопытки;
			НоваяКнопка.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ТабличныйДокумент, ГраницаЭлементаУправления.Верх);
			НоваяКнопка.УстановитьПривязку(ГраницаЭлементаУправления.Низ, НоваяКнопка, ГраницаЭлементаУправления.Верх);
			НоваяКнопка.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ТабличныйДокумент, ГраницаЭлементаУправления.Лево);
			НоваяКнопка.УстановитьПривязку(ГраницаЭлементаУправления.Право, НоваяКнопка, ГраницаЭлементаУправления.Лево);
		КонецЕсли;
	КонецЦикла;
	
	// Привязка к верхнему левому углу ПоляТабличногоДокумента
	НачалоВерх = ТабличныйДокумент.Верх + 2 ;
	НачалоЛево = ТабличныйДокумент.Лево + ?(КоличествоГруппировокСтрок, 15 + КоличествоГруппировокСтрок * 13, 2);
	
	// Удалим лишние кнопки
	УдалитьВсеКнопкиКолонок = ЭлементовГруппировокСтрок <> КоличествоГруппировокСтрок;
	Для Индекс = ?(УдалитьВсеКнопкиКолонок, 1, КоличествоГруппировокКолонок + 1) По ЭлементовГруппировокКолонок Цикл
		ИмяКнопки = "КнопкаГруппировкиКолонок_" + Формат(Индекс, "ЧЦ=3; ЧН=; ЧВН=") + "_" + ИмяТабличногоДокумента;
		ТекЭлемент = ЭлементыФормы.Найти(ИмяКнопки);
		Если ТекЭлемент <> Неопределено Тогда
			ЭлементыФормы.Удалить(ТекЭлемент);
		КонецЕсли;
	КонецЦикла;
	
	// Добавим новые кнопки по количеству группировок
	Для Индекс = 1 По КоличествоГруппировокКолонок Цикл
		ИмяКнопки = "КнопкаГруппировкиКолонок_" + Формат(Индекс, "ЧЦ=3; ЧН=; ЧВН=") + "_" + ИмяТабличногоДокумента;
		Если ЭлементыФормы.Найти(ИмяКнопки) = Неопределено Тогда
			НоваяКнопка = ЭлементыФормы.Добавить(Тип("Кнопка"), ИмяКнопки,, ЗакладкаРезультат);
			НоваяКнопка.Верх      = НачалоВерх + (13 * (Индекс - 1));
			НоваяКнопка.Лево      = НачалоЛево;
			НоваяКнопка.Высота    = 12;
			НоваяКнопка.Ширина    = 12;
			НоваяКнопка.Шрифт     = Новый Шрифт("Arial", 7);
			НоваяКнопка.Заголовок = "" + Индекс;
			Если Индекс = 1 Тогда
				НоваяКнопка.Подсказка = "Показать группировки верхнего уровня";
			ИначеЕсли Индекс = КоличествоГруппировокКолонок Тогда
				НоваяКнопка.Подсказка = "Показать все группировки";
			Иначе
				НоваяКнопка.Подсказка = "Показать группировки до уровня " + Индекс;
			КонецЕсли;
			НоваяКнопка.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
			Попытка
				НоваяКнопка.УстановитьДействие("Нажатие", Новый Действие("УправлениеОтображениемЭлементовФормы"));
			Исключение
			КонецПопытки;
			НоваяКнопка.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ТабличныйДокумент, ГраницаЭлементаУправления.Верх);
			НоваяКнопка.УстановитьПривязку(ГраницаЭлементаУправления.Низ, НоваяКнопка, ГраницаЭлементаУправления.Верх);
			НоваяКнопка.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ТабличныйДокумент, ГраницаЭлементаУправления.Лево);
			НоваяКнопка.УстановитьПривязку(ГраницаЭлементаУправления.Право, НоваяКнопка, ГраницаЭлементаУправления.Лево);
		КонецЕсли;
	КонецЦикла;
	
	ЭлементовГруппировокСтрок = КоличествоГруппировокСтрок;
	ЭлементовГруппировокКолонок = КоличествоГруппировокКолонок;
	
КонецПроцедуры // СгенерироватьКнопкиУправленияГруппировкой()

Процедура УправлениеОтображениемЭлементовФормы(Кнопка)
	
	КнопкаИмя = Кнопка.Имя;
	
	Если Лев(КнопкаИмя, 23) = "КнопкаГруппировкиСтрок_" Тогда
		ТабличныйДокумент = ЭлементыФормы.Результат;
		ТекущийЭлемент = ТабличныйДокумент;
		Уровень = Число(ЭлементыФормы[КнопкаИмя].Заголовок); // здесь записан нужный уровень
		ТабличныйДокумент.ПоказатьУровеньГруппировокСтрок(Уровень - 1);
		
	ИначеЕсли Лев(КнопкаИмя, 25) = "КнопкаГруппировкиКолонок_" Тогда
		ТабличныйДокумент = ЭлементыФормы.Результат;
		ТекущийЭлемент = ТабличныйДокумент;
		Уровень = Число(ЭлементыФормы[КнопкаИмя].Заголовок); // здесь записан нужный уровень
		ТабличныйДокумент.ПоказатьУровеньГруппировокКолонок(Уровень - 1);
	КонецЕсли;
	
КонецПроцедуры // УправлениеОтображениемЭлементовФормы()

Процедура ОсновныеДействияФормыСформировать(Кнопка)
	
	СформироватьОтчет();
	
КонецПроцедуры //ОсновныеДействияФормыСформировать()

Процедура СформироватьОтчет() Экспорт
	
	ТабличныйДокумент = ЭлементыФормы.Результат;
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроекСКД.Настройки, ДанныеРасшифровки);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки,, ДанныеРасшифровки);
	ТабличныйДокумент.Очистить();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабличныйДокумент);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ВерхнийКолонтитул = ТабличныйДокумент.ВерхнийКолонтитул;
	ВерхнийКолонтитул.Выводить = Истина;
	ВерхнийКолонтитул.НачальнаяСтраница = 2;
	ВерхнийКолонтитул.ТекстСправа = "Страница [&НомерСтраницы] из [&СтраницВсего]";
	
	ВыводПараметров = ДанныеРасшифровки.Настройки.ПараметрыВывода.Элементы.Найти("DataParametersOutput");
	ПараметрыВОтчете = Не ВыводПараметров.Использование или ВыводПараметров.Значение <> ТипВыводаТекстаКомпоновкиДанных.НеВыводить;
	ШапкаНачало = ?(ПараметрыВОтчете, 7, 4);
	ДобавитьСтрок = 0;
	СтруктураНастроек = КомпоновщикНастроекСКД.Настройки.Структура[0];
	Если ТипЗнч(СтруктураНастроек) = Тип("ТаблицаКомпоновкиДанных")
		и СтруктураНастроек.Строки.Количество()
		и СтруктураНастроек.Строки[0].Структура.Количество() Тогда
		ДобавитьСтрок = ?(СтруктураНастроек.Строки[0].Структура[0].Структура.Количество(), 2, 1);
	ИначеЕсли ТипЗнч(СтруктураНастроек) = Тип("ГруппировкаКомпоновкиДанных")
		и СтруктураНастроек.Структура.Количество() Тогда
		ДобавитьСтрок = ?(СтруктураНастроек.Структура[0].Структура.Количество(), 2, 1);
	КонецЕсли;
	ШапкаОкончение = ?(ПараметрыВОтчете, 7 + ДобавитьСтрок, 4); // Здесь должен быть расчет
	
  	ТабличныйДокумент.ПовторятьПриПечатиСтроки = ТабличныйДокумент.Область(ШапкаНачало,, ШапкаОкончение,);
	//ТабличныйДокумент.Защита = Истина;
	ТабличныйДокумент.АвтоМасштаб = Истина;
 	ТабличныйДокумент.ФиксацияСверху = ШапкаОкончение;

	ЭтаФорма.ТекущийЭлемент = ТабличныйДокумент;
	СгенерироватьКнопкиУправленияГруппировкой();
	
КонецПроцедуры // СформироватьОтчет()

///////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ МОДУЛЯ
//

Процедура ПриОткрытии()
	
	ЭлементовГруппировокСтрок = 0;
	ЭлементовГруппировокКолонок = 0;
	
КонецПроцедуры // ПриОткрытии()

///////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//