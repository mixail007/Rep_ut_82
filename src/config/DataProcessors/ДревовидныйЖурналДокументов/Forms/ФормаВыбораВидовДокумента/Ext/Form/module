///////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

Процедура КоманднаяПанельУстановитьФлажки(Кнопка)
	
	СписокВсехДокументов.ЗаполнитьПометки(Истина);
	
КонецПроцедуры

Процедура КоманднаяПанельСнятьФлажки(Кнопка)
	
	СписокВсехДокументов.ЗаполнитьПометки(Ложь);
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	КлючУникальности.Очистить();
	Для каждого ЭлементСписка Из СписокВсехДокументов Цикл Если ЭлементСписка.Пометка Тогда КлючУникальности.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление) КонецЕсли КонецЦикла;
	
	Закрыть(Истина);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ МОДУЛЯ
//

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()
	
	СписокВсехДокументов.Очистить();
	
	Картинка = БиблиотекаКартинок.ДокументОбъект;
	МетаданныеДокументы = Метаданные.Документы;
	МассивМетаданныеДокументы = Новый Массив;
	Если ТолькоСуществующие Тогда
		ТекстЗапроса = "";
		Для каждого МетаданныеДокумента Из МетаданныеДокументы Цикл
			ИмяДокумента = МетаданныеДокумента.Имя;
			ТекстЗапроса = ?(ПустаяСтрока(ТекстЗапроса), "ВЫБРАТЬ РАЗРЕШЕННЫЕ ", ТекстЗапроса + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ") + " ПЕРВЫЕ 1
			|	""" + ИмяДокумента + """ КАК ИмяДокумента
			|ИЗ Документ." + ИмяДокумента + " КАК __Документ
			|";
		КонецЦикла;
		Запрос = Новый Запрос(ТекстЗапроса);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл ИмяДокумента = Выборка.ИмяДокумента; МетаданныеДокумента = МетаданныеДокументы[ИмяДокумента]; МассивМетаданныеДокументы.Добавить(МетаданныеДокумента); КонецЦикла;
	Иначе
		Для каждого МетаданныеДокумента Из МетаданныеДокументы Цикл МассивМетаданныеДокументы.Добавить(МетаданныеДокумента); КонецЦикла;
	КонецЕсли;
	
	Для каждого Документ Из МассивМетаданныеДокументы Цикл Если ПравоДоступа("Чтение", Документ) Тогда СписокВсехДокументов.Добавить(Документ.Имя, Документ.Представление(), Не КлючУникальности.НайтиПоЗначению(Документ.Имя) = Неопределено, Картинка) КонецЕсли КонецЦикла;
	
	СписокВсехДокументов.СортироватьПоПредставлению();
	
КонецПроцедуры

// Процедура - обработчик события "ОбработкаОповещения" формы
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗавершитьРаботуСистемы" Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОповещения()

///////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
