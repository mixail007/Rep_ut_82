// Установление соединения с ФР
//
// Параметры:
//  Объект       - возвращаемый объект подключенного торгового оборудования
//  Пароль       - пароль пользователя
//  НомерЛУ      - номер логического устройства с который связан Объект
//
// Возвращаемое значение:
//  строка с представлением ошибки если она произошла, иначе пустая строка
//
Функция ПодключитьсяКФР(Объект, Пароль, НомерЛУ)

	Объект.CurrentDeviceNumber = НомерЛУ;
	Если Объект.DeviceEnabled = 1 Тогда
		Возврат "";
	КонецЕсли;

	Объект.DeviceEnabled = 1;
	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Объект.GetStatus();
	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		Объект.DeviceEnabled = 0; // освободим порт
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Возврат "";

КонецФункции // ПодключитьсяКФР()

// Отключение от ФР
//
// Параметры:
//  Объект       - возвращаемый объект подключенного торгового оборудования
//  НомерЛУ      - номер логического устройства с который связан Объект
//
// Возвращаемое значение:
//  строка с представлением ошибки если она произошла, иначе пустая строка
//
Функция ОтключитьсяОтФР(Объект, НомерЛУ)

	// пеключим ФР нерабочий режим
	Объект.CurrentDeviceNumber = НомерЛУ;
	Объект.Mode                = 0;
	Объект.Password            = "0";
	Объект.SetMode();

	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Объект.DeviceEnabled = 0;
	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Возврат "";

КонецФункции

// Анулирование чека на ФР
//
// Параметры:
//  Объект       - объект фискальный регистратор
//  Пароль       - пароль пользователя
//  НомерЛУ      - номер логического устройства с который связан Объект
//  Подключаться - булево, признак необходимости предварительно подключиться 
//
Функция АннулироватьЧек(Объект, Пароль, НомерЛУ = 1, Подключаться = Ложь);

	Если Подключаться Тогда

		Если Объект = Неопределено Тогда
			ОписаниеРезультата = "устройство не подключено";
			Возврат ОписаниеРезультата;
		КонецЕсли;

		ОписаниеРезультата = ПодключитьсяКФР(Объект, Пароль, НомерЛУ);
		Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда
			Возврат ОписаниеРезультата;
		КонецЕсли;

	КонецЕсли;

	ОписаниеРезультата = "";
	Попытка
		Объект.CancelCheck();
	Исключение
		Если Объект.ResultCode <> 0 Тогда
			ОписаниеРезультата = Объект.ResultDescription;
			ОтключитьсяОтФР(Объект, НомерЛУ);
			Возврат ОписаниеРезультата;
		Иначе
			ОписаниеРезультата = "Неизвестная ошибка";
			Возврат ОписаниеРезультата;
		КонецЕсли;
	КонецПопытки;

	Возврат ОтключитьсяОтФР(Объект, НомерЛУ);

КонецФункции // АннулироватьЧек()

// Проверить состояние чека на ФР
//
// Параметры:
//  Объект       - объект фискальный регистратор
//  Пароль       - пароль пользователя
//  ТаблицаТоваров - таблица товаров, их цен и количества
//  Скидка       - скидка на чек
//  ПризнВозврата - признак возврата товара
//  НомерСекции  - номер секции, в которой печатается чек
//  НомерЧека    - возвращаемый номер чека
//  НомерЛУ      - номер логического устройства с который связан Объект
//
// Возвращаемое значение:
//  строка с представлением ошибки если она произошла, иначе пустая строка
//
Функция ПроверитьЧекНаФР(Объект, Пароль, НомерЛУ)

	Если Объект = Неопределено Тогда
		ОписаниеРезультата = "устройство не подключено";
		Возврат 0;
	КонецЕсли;

	ОписаниеРезультата = ПодключитьсяКФР(Объект, Пароль, НомерЛУ);
	Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда
		Возврат ОписаниеРезультата;
	КонецЕсли;

	// Проверка состояния чека
	Объект.GetStatus();
	Если Объект.СостояниеЧека <> 0 Тогда
		Ответ = Вопрос("Предыдущий чек не закрыт!! Отменить предыдущий чек?", РежимДиалогаВопрос.ДаНет ,, 
			  	КодВозвратаДиалога.Да, "Ошибка фискального регистратора");
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Объект.CancelCheck();
			Если Объект.ResultCode <> 0 Тогда
				ОписаниеРезультата = Объект.ResultDescription;
				ОтключитьсяОтФР(Объект, НомерЛУ);
				Возврат ОписаниеРезультата;
			КонецЕсли;
			Объект.GetStatus();
		Иначе
			ОписаниеРезультата = "Предыдущий чек не закрыт!!";
			Возврат ОписаниеРезультата;
		КонецЕсли;		
	КонецЕсли;		

	Возврат ОтключитьсяОтФР(Объект, НомерЛУ);

КонецФункции // ПроверитьЧекНаФР()

// Открыть ящик ФР
//
// Параметры:
//  Объект       - объект фискальный регистратор
//  Пароль       - пароль пользователя
//
Функция ОткрытьЯщик(Объект, Пароль, НомерЛУ = 1);

	Если Объект = Неопределено Тогда
		ОписаниеРезультата = "устройство не подключено";
		Возврат ОписаниеРезультата;
	КонецЕсли;

	ОписаниеРезультата = ПодключитьсяКФР(Объект, Пароль, НомерЛУ);
	Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда
		Возврат ОписаниеРезультата;
	КонецЕсли;

	ОписаниеРезультата = "";
	Попытка

		Объект.OpenDrawer();

	Исключение
		
		Если Объект.ResultCode <> 0 Тогда
			ОписаниеРезультата = Объект.ResultDescription;
			ОтключитьсяОтФР(Объект, НомерЛУ);
			Возврат ОписаниеРезультата;
		Иначе
			ОписаниеРезультата = "Неизвестная ошибка";
			Возврат ОписаниеРезультата;
		КонецЕсли;
	КонецПопытки;

	Возврат ОтключитьсяОтФР(Объект, НомерЛУ);

КонецФункции // ОткрытьЯщик()

// Внести сумму в ящик ФР
//
// Параметры:
//  Объект       - объект фискальный регистратор
//  Пароль       - пароль пользователя
//
Функция ВнестиСумму(Объект, Пароль, Сумма, НомерЛУ = 1);

	Если Объект = Неопределено Тогда
		ОписаниеРезультата = "устройство не подключено";
		Возврат ОписаниеРезультата;
	КонецЕсли;

	ОписаниеРезультата = ПодключитьсяКФР(Объект, Пароль, НомерЛУ);
	Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда
		Возврат ОписаниеРезультата;
	КонецЕсли;

	ОписаниеРезультата = "";
	Попытка
		Объект.Mode     = 1;        // режим регистрации чеков
		Объект.Password = Пароль;   // пароль на режим регистрации
		Объект.SetMode();
		Если Объект.ResultCode <> 0 Тогда
			ОписаниеРезультата = Объект.ResultDescription;
			ОтключитьсяОтФР(Объект, НомерЛУ);
			Возврат ОписаниеРезультата;
		КонецЕсли;

		Объект.Summ = Сумма;
		Объект.CashIncome();

	Исключение
		Если Объект.ResultCode <> 0 Тогда
			ОписаниеРезультата = Объект.ResultDescription;
			ОтключитьсяОтФР(Объект, НомерЛУ);
			Возврат ОписаниеРезультата;
		Иначе
			ОписаниеРезультата = "Неизвестная ошибка";
			Возврат ОписаниеРезультата;
		КонецЕсли;
	КонецПопытки;

	Возврат ОтключитьсяОтФР(Объект, НомерЛУ);
	
КонецФункции // ВнестиСумму()

// Изъять сумму из ящика ФР
//
// Параметры:
//  Объект       - объект фискальный регистратор
//  Пароль       - пароль пользователя
//
Функция ИзъятьСумму(Объект, Пароль, Сумма, НомерЛУ = 1);

	Если Объект = Неопределено Тогда
		ОписаниеРезультата = "устройство не подключено";
		Возврат ОписаниеРезультата;
	КонецЕсли;

	ОписаниеРезультата = ПодключитьсяКФР(Объект, Пароль, НомерЛУ);
	Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда
		Возврат ОписаниеРезультата;
	КонецЕсли;

	ОписаниеРезультата = "";
	Попытка
		Объект.Mode     = 1;        // режим регистрации чеков
		Объект.Password = Пароль;   // пароль на режим регистрации
		Объект.SetMode();
		Если Объект.ResultCode <> 0 Тогда
			ОписаниеРезультата = Объект.ResultDescription;
			ОтключитьсяОтФР(Объект, НомерЛУ);
			Возврат ОписаниеРезультата;
		КонецЕсли;

		Объект.Password=Пароль;
		Объект.Summ = Сумма;
		Объект.CashOutcome();

	Исключение
		Если Объект.ResultCode <> 0 Тогда
			ОписаниеРезультата = Объект.ResultDescription;
			ОтключитьсяОтФР(Объект, НомерЛУ);
			Возврат ОписаниеРезультата;
		Иначе
			ОписаниеРезультата = "Неизвестная ошибка";
			Возврат ОписаниеРезультата;
		КонецЕсли;
	КонецПопытки;

	Возврат ОтключитьсяОтФР(Объект, НомерЛУ);

КонецФункции // ИзъятьСумму()

// Напечатать Х-отчет ФР
//
// Параметры:
//  Объект       - объект фискальный регистратор
//  Пароль       - пароль пользователя
//
Функция ХОтчет(Объект, Пароль, НомерЛУ = 1);

	Если Объект = Неопределено Тогда
		ОписаниеРезультата = "устройство не подключено";
		Возврат ОписаниеРезультата;
	КонецЕсли;

	ОписаниеРезультата = ПодключитьсяКФР(Объект, Пароль, НомерЛУ);
	Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда
		Возврат ОписаниеРезультата;
	КонецЕсли;

	ОписаниеРезультата = "";
	Попытка
		Объект.Mode     = 2;        // режим печати отчетов
		Объект.Password = Пароль;   // пароль на режим печати отчетов
		Объект.SetMode();
		Если Объект.ResultCode <> 0 Тогда
			ОписаниеРезультата = Объект.ResultDescription;
			ОтключитьсяОтФР(Объект, НомерЛУ);
			Возврат ОписаниеРезультата;
		КонецЕсли;

		Объект.Password=Пароль;
		Объект.ReportType = 2;
		Объект.Report();

	Исключение
		Если Объект.ResultCode <> 0 Тогда
			ОписаниеРезультата = Объект.ResultDescription;
			ОтключитьсяОтФР(Объект, НомерЛУ);
			Возврат ОписаниеРезультата;
		Иначе
			ОписаниеРезультата = "Неизвестная ошибка";
			Возврат ОписаниеРезультата;
		КонецЕсли;
	КонецПопытки;

	Возврат ОтключитьсяОтФР(Объект, НомерЛУ);

КонецФункции // ХОтчет()

// Печать чека на ФР
//
// Параметры:
//  Объект       - объект фискальный регистратор
//  Пароль       - пароль пользователя
//  ТаблицаТоваров - таблица товаров, их цен и количества
//  Скидка       - скидка на чек
//  ПризнВозврата - признак возврата товара
//  НомерСекции  - номер секции, в которой печатается чек
//  НомерЧека    - возвращаемый номер чека
//  НомерЛУ      - номер логического устройства с который связан Объект
//
// Возвращаемое значение:
//  строка с представлением ошибки если она произошла, иначе пустая строка
//
Функция ПечататьЧек(Объект, Пароль, ТаблицаТоваров, Скидка, ПризнВозврата, НомерСекции, НомерЧека,НомерСмены, НомерЛУ)

	Если Объект = Неопределено Тогда
		ОписаниеРезультата = "устройство не подключено";
		Возврат 0;
	КонецЕсли;

	ОписаниеРезультата = ПроверитьЧекНаФР(Объект, Пароль, НомерЛУ);
	Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда
		Возврат ОписаниеРезультата;
	КонецЕсли;
	
	ОписаниеРезультата = ПодключитьсяКФР(Объект, Пароль, НомерЛУ);
	Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда
		Возврат ОписаниеРезультата;
	КонецЕсли;
	Объект.Mode     = 1;        // режим регистрации чеков
	Объект.Password = Пароль;   // пароль на режим регистрации
	Объект.SetMode();
	
	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		ОтключитьсяОтФР(Объект, НомерЛУ);
		Возврат ОписаниеРезультата;
	КонецЕсли;

	// регистрация товаров
	ДлинаНаименования = Объект.CharLineLength; // зависит от модели ФР
	Для Каждого СтрокаИзСписка Из ТаблицаТоваров Цикл
		Объект.Name     = Лев(СтрокаИзСписка.Товар, ДлинаНаименования);
		Объект.Price    = СтрокаИзСписка.Цена;
		Объект.Quantity = СтрокаИзСписка.Колво;

		Если ПризнВозврата Тогда
			Объект.Return();
		Иначе
			Объект.Registration();
		КонецЕсли;

		Если Объект.ResultCode <> 0 Тогда
			ОписаниеРезультата = Объект.ResultDescription;
			Объект.CancelCheck();
			ОтключитьсяОтФР(Объект, НомерЛУ);
			Возврат ОписаниеРезультата;
		КонецЕсли;

	КонецЦикла;

	Если Объект.ResultCode <> 0 Тогда		
		ОписаниеРезультата = Объект.ResultDescription;
		Объект.Percents    = 0;
		Объект.CancelCheck();
		ОтключитьсяОтФР(Объект, НомерЛУ);
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Если Скидка < 0 Тогда
		// надбавка
		Объект.Destination = 0;
		Объект.Summ = -Скидка;
		Объект.SummCharge();
	ИначеЕсли Скидка > 0 Тогда
		// Скидка
		Объект.Destination = 0;
		Объект.Summ = Скидка;
		Объект.SummDiscount();
	КонецЕсли;
	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		Объект.CancelCheck();
		ОтключитьсяОтФР(Объект, НомерЛУ);
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Объект.Department = НомерСекции;
	Объект.CloseCheck();

	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		Объект.CancelCheck();
		ОтключитьсяОтФР(Объект, НомерЛУ);
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Объект.GetStatus();
	НомерЧека = Объект.CheckNumber-1;
	НомерСмены = Объект.Session+1;

	Возврат ОтключитьсяОтФР(Объект, НомерЛУ);

КонецФункции // ПечататьЧек()

// Печать строку чека на ФР
//
// Параметры:
//  Объект       - объект фискальный регистратор
//  Пароль       - пароль пользователя
//  ТаблицаТоваров - таблица товаров, их цен и количества
//  Скидка       - скидка на чек
//  ПризнВозврата - признак возврата товара
//  НомерСекции  - номер секции, в которой печатается чек
//  НомерЧека    - возвращаемый номер чека
//  НомерЛУ      - номер логического устройства с который связан Объект
//
// Возвращаемое значение:
//  строка с представлением ошибки если она произошла, иначе пустая строка
//
Функция ПечататьСтрокуЧека(Объект, Пароль, СтрокаТаблицыТоваров, НомерЧека, НомерСмены, НомерЛУ)

	Если Объект = Неопределено Тогда
		ОписаниеРезультата = "устройство не подключено";
		Возврат ОписаниеРезультата;
	КонецЕсли;

	ОписаниеРезультата = ПодключитьсяКФР(Объект, Пароль, НомерЛУ);
	Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда

		Если Объект.ResultCode <> 0 Тогда

			ОписаниеРезультата = Объект.ОписаниеРезультата;
			АннулироватьЧек(Объект, Пароль);
			ОтключитьсяОтФР(Объект, НомерЛУ);

		КонецЕсли;

		Возврат ОписаниеРезультата;
	КонецЕсли;

	Если Объект.Mode <> 1 Тогда
		Объект.Mode     = 1;        // режим регистрации чеков
		Объект.Password = Пароль;   // пароль на режим регистрации
		Объект.SetMode();
	КонецЕсли;

	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		ОтключитьсяОтФР(Объект, НомерЛУ);
		Возврат ОписаниеРезультата;
	КонецЕсли;


	// регистрация товаров
	ДлинаНаименования = Объект.CharLineLength; // зависит от модели ФР
	Объект.Name     = Лев(СтрокаТаблицыТоваров.Номенклатура.Наименование, ДлинаНаименования);
	Объект.Price             = СтрокаТаблицыТоваров.Цена;
	Объект.Quantity          = СтрокаТаблицыТоваров.Количество;

	Объект.Registration();

	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		АннулироватьЧек(Объект, Пароль);
		ОтключитьсяОтФР(Объект, НомерЛУ);

		Возврат ОписаниеРезультата;
	КонецЕсли;


	Объект.GetStatus();

	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		ОтключитьсяОтФР(Объект, НомерЛУ);

		Возврат ОписаниеРезультата;
	КонецЕсли;

	НомерЧека = Объект.CheckNumber;
	НомерСмены = Объект.Session+1;

	Возврат Неопределено //ОтключитьсяОтФР(Объект, НомерЛУ);

КонецФункции // ПечататьСтрокуЧека()

// Закрыть чек на ФР
//
// Параметры:
//  Объект       - объект фискальный регистратор
//  Пароль       - пароль пользователя
//  ТаблицаТоваров - таблица товаров, их цен и количества
//  Скидка       - скидка на чек
//  ПризнВозврата - признак возврата товара
//  НомерСекции  - номер секции, в которой печатается чек
//  НомерЧека    - возвращаемый номер чека
//  НомерЛУ      - номер логического устройства с который связан Объект
//
// Возвращаемое значение:
//  строка с представлением ошибки если она произошла, иначе пустая строка
//
Функция ЗакрытьЧек(Объект, Пароль, Получено, ПолученоНал,ПолученоБезНал, Скидка, НомерСекции, НомерЧека, НомерСмены, НомерЛУ)

	Если Объект = Неопределено Тогда
		ОписаниеРезультата = "устройство не подключено";
		Возврат ОписаниеРезультата;
	КонецЕсли;

	ОписаниеРезультата = ПодключитьсяКФР(Объект, Пароль, НомерЛУ);
	Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Если Объект.Mode <> 1 Тогда
		Объект.Mode     = 1;        // режим регистрации чеков
		Объект.Password = Пароль;   // пароль на режим регистрации
		Объект.SetMode();
	КонецЕсли;

	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Если Скидка < 0 Тогда
			// надбавка
		Объект.Destination = 0;
		Объект.Summ = -Скидка;
		Объект.SummCharge();
	ИначеЕсли Скидка > 0 Тогда
		// Скидка
		Объект.Destination = 0;
		Объект.Summ = Скидка;
		Объект.SummDiscount();
	КонецЕсли;
	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		Объект.CancelCheck();
		ОтключитьсяОтФР(Объект, НомерЛУ);
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Если ПолученоНал > 0 Тогда
		Объект.Summ = ПолученоНал;
		Объект.TypeClose = 0;
		Объект.Payment();
		Если Объект.ResultCode <> 0 Тогда
			ОписаниеРезультата = Объект.ResultDescription;
			Объект.CancelCheck();
			ОтключитьсяОтФР(Объект, НомерЛУ);
			Возврат ОписаниеРезультата;
		КонецЕсли;
	КонецЕсли;

	Если ПолученоБезНал > 0 Тогда
		Объект.Summ = ПолученоБезНал;
		Объект.TypeClose = 1;
		Объект.Payment();
		Если Объект.ResultCode <> 0 Тогда
			ОписаниеРезультата = Объект.ResultDescription;
			Объект.CancelCheck();
			ОтключитьсяОтФР(Объект, НомерЛУ);
			Возврат ОписаниеРезультата;
		КонецЕсли;
	КонецЕсли;

	Объект.Department = НомерСекции;
	Объект.CloseCheck();

	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Объект.GetStatus();

	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		ОтключитьсяОтФР(Объект, НомерЛУ);

		Возврат ОписаниеРезультата;
	КонецЕсли;

	НомерЧека = Объект.CheckNumber-1;
	НомерСмены = Объект.Session+1;

	Возврат ОтключитьсяОтФР(Объект, НомерЛУ);

КонецФункции // ЗакрытьЧек()

// Печать чека для ПКО на ФР
//
// Параметры:
//  Объект       - объект фискальный регистратор
//  Пароль       - пароль пользователя
//  ТаблицаТоваров - таблица товаров, их цен и количества
//  ПризнВозврата - признак возврата товара
//  НомерСекции  - номер секции, в которой печатается чек
//  НомерЧека    - возвращаемый номер чека
//  НомерЛУ      - номер логического устройства с который связан Объект
//
// Возвращаемое значение:
//  строка с представлением ошибки если она произошла, иначе пустая строка
//
Функция ПечататьЧекДляПКО(Объект, Пароль, ТаблицаТоваров, Получено, ПризнВозврата, НомерСекции, НомерЧека, НомерЛУ)

	Если Объект = Неопределено Тогда
		ОписаниеРезультата = "устройство не подключено";
		Возврат 0;
	КонецЕсли;

	ОписаниеРезультата = ПодключитьсяКФР(Объект, Пароль, НомерЛУ);
	Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Объект.Mode     = 1;        // режим регистрации чеков
	Объект.Password = Пароль;   // пароль на режим регистрации
	Объект.SetMode();
	
	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		ОтключитьсяОтФР(Объект, НомерЛУ);
		Возврат ОписаниеРезультата;
	КонецЕсли;

	// регистрация товаров
	ДлинаНаименования = Объект.CharLineLength; // зависит от модели ФР
	Для Каждого СтрокаИзСписка Из ТаблицаТоваров Цикл
		Объект.Name     = "";
		Объект.Price    = СтрокаИзСписка.Цена;
		Объект.Quantity = СтрокаИзСписка.Колво;

		// В настройках ФР надо настроить ставки:
		// Налог 1 = 18% - НДС 18%
		// Налог 2 = 10% - НДС 10%
		// Налог 3 = 20% - НДС 20%
		// Налог 4 =  0% - без НДС

		Если СтрокаИзСписка.НДС = 18 Тогда
			Объект.Department = 1;
		ИначеЕсли СтрокаИзСписка.НДС = 10 Тогда
			Объект.Department = 2;
		ИначеЕсли СтрокаИзСписка.НДС = 20 Тогда
			Объект.Department = 3;
		Иначе
			Объект.Department = 4;
		КонецЕсли;

		Если ПризнВозврата Тогда
			Объект.Return();
		Иначе
			Объект.Registration();
		КонецЕсли;

		СтрокаДляПечати = СтрокаИзСписка.Товар;
		Пока СтрДлина(СтрокаДляПечати)>0 Цикл
			Объект.Caption = Лев(СтрокаДляПечати,ДлинаНаименования);
			Объект.PrintString();
			СтрокаДляПечати = СокрЛП(Сред(СтрокаДляПечати,ДлинаНаименования+1));
		КонецЦикла;

		Если Объект.ResultCode <> 0 Тогда
			ОписаниеРезультата = Объект.ResultDescription;
			ОтключитьсяОтФР(Объект, НомерЛУ);
			Возврат ОписаниеРезультата;
		КонецЕсли;
	КонецЦикла;

	Объект.Department = НомерСекции;
	Объект.CloseCheck();

	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		ОтключитьсяОтФР(Объект, НомерЛУ);
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Объект.GetStatus();
	НомерЧека = Объект.CheckNumber-1;

	Возврат ОтключитьсяОтФР(Объект, НомерЛУ);

КонецФункции // ПечататьЧекДляПКО()

// Подключение ФР
//
// Параметры:
//  Компонента   - внешняя компонента
//  ПрограммныйИдентификатор - пароль пользователя
//  Объект       - возвращаемый объект подключенного торгового оборудования
//  НомерЛУ      - номер логического устройства с который связан Объект
//
// Возвращаемое значение:
//  строка с представлением ошибки если она произошла, иначе пустая строка
//
Функция Подключить(Компонента, ПрограммныйИдентификатор, Объект, НомерЛУ)

	Попытка
		ЗагрузитьВнешнююКомпоненту(Компонента);
	Исключение
		ОписаниеРезультата = "не удалось загрузить внешнюю компоненту """ + Компонента + """";
		Возврат ОписаниеРезультата;
	КонецПопытки;

	Попытка
		Объект = Новый("AddIn." + ПрограммныйИдентификатор);
	Исключение
		ОписаниеРезультата = "не удалось создать объект внешней компоненты с программым идентификатром AddIn." + ПрограммныйИдентификатор;
		Возврат ОписаниеРезультата;
	КонецПопытки;

	Возврат "";

КонецФункции // Подключить()

// Отключение ФР
//
// Параметры:
//  Объект       - объект подключенного торгового оборудования
//  НомерЛУ      - номер логического устройства с который связан Объект
//
// Возвращаемое значение:
//  строка с представлением ошибки если она произошла, иначе пустая строка
//
Функция Отключить(Объект, НомерЛУ)

	// если нет объекта, то ничего не нужно отключать
	Если Объект = Неопределено Тогда
		Возврат "";
	КонецЕсли;

	ОтключитьсяОтФР(Объект, НомерЛУ);

	Возврат "";

КонецФункции // Отключить()

// Закрытие смены на ФР
// на ФР печатается Z-отчет
//
// Параметры:
//  Объект       - объект подключенного торгового оборудования
//  Пароль       - пароль администратора
//  НомерЛенты   - возвращаемый номер ленты
//  НомерЛУ      - номер логического устройства с который связан Объект
//
// Возвращаемое значение:
//  строка с представлением ошибки если она произошла, иначе пустая строка
//
Функция ЗакрытьСмену(Объект, Пароль, НомерЛенты, НомерЛУ)

	Если Объект = Неопределено Тогда
		ОписаниеРезультата = "устройство не подключено";
		Возврат ОписаниеРезультата;
	КонецЕсли;

	ОписаниеРезультата = ПодключитьсяКФР(Объект, Пароль, НомерЛУ);
	Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда
		Возврат ОписаниеРезультата;
	КонецЕсли;

	// переключение в режим снятия отчета (нужен пароль администратора)
	Объект.CurrentDeviceNumber = НомерЛУ;
	Объект.Mode                = 3;
	Объект.Password            = Пароль;
	Объект.SetMode();

	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		ОтключитьсяОтФР(Объект, НомерЛУ);
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Объект.ReportType = 1;
	Объект.Report();

	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		ОтключитьсяОтФР(Объект, НомерЛУ);
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Объект.GetStatus();
	НомерЛенты = Объект.Session;

	Возврат ОтключитьсяОтФР(Объект, НомерЛУ);

КонецФункции // ЗакрытьСмену()

Функция ПолучитьНомерЧекаСмены(Объект, Пароль, НомерЧека, НомерСмены, НомерЛУ)

	Если Объект = Неопределено Тогда
		ОписаниеРезультата = "устройство не подключено";
		Возврат ОписаниеРезультата;
	КонецЕсли;

	ОписаниеРезультата = ПодключитьсяКФР(Объект, Пароль, НомерЛУ);
	Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда
		Возврат ОписаниеРезультата;
	КонецЕсли;

	Объект.GetStatus();

	Если Объект.ResultCode <> 0 Тогда
		ОписаниеРезультата = Объект.ResultDescription;
		ОтключитьсяОтФР(Объект, НомерЛУ);

		Возврат ОписаниеРезультата;
	КонецЕсли;

	//Если Объект.CheckState <> 0 Тогда // Предыдущий чек не закрыт, надо аннулировать.
	//	АннулироватьЧек(Объект, Пароль);
	//КонецЕсли;

	//Если Объект.ResultCode <> 0 Тогда
	//	ОписаниеРезультата = Объект.ResultDescription;
	//	ОтключитьсяОтФР(Объект, НомерЛУ);

	//	Возврат ОписаниеРезультата;
	//КонецЕсли;

	//ОписаниеРезультата = ПодключитьсяКФР(Объект, Пароль, НомерЛУ);
	//Если НЕ ПустаяСтрока(ОписаниеРезультата) Тогда
	//	Возврат ОписаниеРезультата;
	//КонецЕсли;

	//Объект.GetStatus();

	//Если Объект.ResultCode <> 0 Тогда
	//	ОписаниеРезультата = Объект.ResultDescription;
	//	ОтключитьсяОтФР(Объект, НомерЛУ);

	//	Возврат ОписаниеРезультата;
	//КонецЕсли;

	НомерЧека = Объект.CheckNumber;
	НомерСмены = Объект.Session+1;

	Возврат Неопределено; //ОтключитьсяОтФР(Объект, НомерЛУ);

КонецФункции

Функция ПроверкаПароля (Пароль) Экспорт
	Если ПустаяСтрока(Пароль) Тогда
		Ответ = "неправильно указан пароль пользователя / администратора";
		Возврат Ответ;
	Иначе
		Попытка
			ПарольЧислом = Число(Пароль);
		Исключение
			Ответ = "неправильно указан пароль пользователя / администратора";
			Возврат Ответ;
		КонецПопытки;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Обработка вызова операции на ФР
//
// Параметры:
//  ИмяОперации  - имя выполняемой операции
//  Параметры    - структура с параметрами торгового оборудования
//  Ответ        - возвращаемая строка с представлением ошибки если она произошла, иначе пустая строка
//
Процедура ВыполнитьОперацию(ИмяОперации = "", Параметры, Ответ) Экспорт
	Перем Пароль, Модель, Объект, НомерЛУ;
	Перем ТаблицаТоваров, Получено, Скидка, ЧекНаВозврат, НомерСекции, НомерЧека, НомерСмены;
	Перем СтрокаТовара, Сумма, ПолученоНал, ПолученоБезНал;
	Перем НомерЛенты;

	Параметры.Свойство("Пароль" , Пароль );
	Параметры.Свойство("Объект" , Объект );
	Параметры.Свойство("НомерЛУ", НомерЛУ);

	Если ЗначениеНеЗаполнено(НомерЛУ) Тогда
		НомерЛУ = 1;
	КонецЕсли;

	Если ИмяОперации = "Подключить" Тогда

		//Ответ = ПроверкаПароля (Пароль);
		//Если Ответ <> Неопределено Тогда
		//	Возврат;
		//КонецЕсли;

		Параметры.Свойство("Модель", Модель);

		Ответ = Подключить(Модель.ВнешняяКомпонента, Модель.ПрограммныйИдентификатор, Объект, НомерЛУ);

		Параметры.Вставить("Объект", Объект);

	ИначеЕсли ИмяОперации = "Отключить" Тогда

		//Ответ = ПроверкаПароля (Пароль);
		//Если Ответ <> Неопределено Тогда
		//	Возврат;
		//КонецЕсли;

		Ответ = Отключить(Объект, НомерЛУ);

	ИначеЕсли ИмяОперации = "ПечататьЧек" Тогда

		Ответ = ПроверкаПароля (Пароль);
		Если Ответ <> Неопределено Тогда
			Возврат;
		КонецЕсли;

		Параметры.Свойство("СписокТоваров", ТаблицаТоваров);
		Параметры.Свойство("Скидка"       , Скидка        );
		Параметры.Свойство("ЧекНаВозврат" , ЧекНаВозврат  );
		Параметры.Свойство("НомерСекции"  , НомерСекции   );

		Ответ = ПечататьЧек(Объект, Пароль, ТаблицаТоваров, Скидка, ЧекНаВозврат, НомерСекции, НомерЧека, НомерСмены, НомерЛУ);

		Параметры.Вставить("НомерЧека", НомерЧека);
		Параметры.Вставить("НомерСмены", НомерСмены );

	ИначеЕсли ИмяОперации = "ПечататьЧекДляПКО" Тогда

		Параметры.Свойство("СписокТоваров", ТаблицаТоваров );
		Параметры.Свойство("Получено"     , Получено       );
		Параметры.Свойство("ЧекНаВозврат" , ЧекНаВозврат   );
		Параметры.Свойство("НомерСекции"  , НомерСекции    );

		Ответ = ПечататьЧекДляПКО(Объект, Пароль, ТаблицаТоваров, Получено, ЧекНаВозврат, НомерСекции, НомерЧека, НомерЛУ);

		Параметры.Вставить("НомерЧека", НомерЧека );

	ИначеЕсли ИмяОперации = "ЗакрытьСмену" Тогда

		Ответ = ЗакрытьСмену(Объект, Пароль, НомерЛенты, НомерЛУ);

		Параметры.Вставить("НомерЛенты", НомерЛенты);

	ИначеЕсли ИмяОперации = "ПечататьСтрокуЧека" Тогда

		Ответ = ПроверкаПароля (Пароль);
		Если Ответ <> Неопределено Тогда
			Возврат;
		КонецЕсли;
		Параметры.Свойство("СтрокаТовара", СтрокаТовара );
		Параметры.Свойство("НомерСекции"  , НомерСекции    );

		Ответ = ПечататьСтрокуЧека(Объект, Пароль, СтрокаТовара, НомерЧека, НомерСмены, НомерЛУ);

		Параметры.Вставить("НомерЧека", НомерЧека );
		Параметры.Вставить("НомерСмены", НомерСмены );

	ИначеЕсли ИмяОперации = "ЗакрытьЧек" Тогда

		Ответ = ПроверкаПароля (Пароль);
		Если Ответ <> Неопределено Тогда
			Возврат;
		КонецЕсли;
		Параметры.Свойство("Получено"     , Получено       );
		Параметры.Свойство("ПолученоНал"     , ПолученоНал);
		Параметры.Свойство("ПолученоБезНал"     , ПолученоБезНал);
		Параметры.Свойство("Скидка"       , Скидка       );
		Параметры.Свойство("НомерСекции"  , НомерСекции    );

		Ответ = ЗакрытьЧек(Объект, Пароль, Получено, ПолученоНал, ПолученоБезНал, Скидка, НомерСекции, НомерЧека, НомерСмены, НомерЛУ);

		Параметры.Вставить("НомерЧека", НомерЧека );
		Параметры.Вставить("НомерСмены", НомерСмены );

	ИначеЕсли ИмяОперации = "АннулироватьЧек" Тогда

		Ответ = ПроверкаПароля (Пароль);
		Если Ответ <> Неопределено Тогда
			Возврат;
		КонецЕсли;

		Ответ = АннулироватьЧек(Объект, Пароль, НомерЛУ, Истина);

	ИначеЕсли ИмяОперации = "Внести" Тогда

		Параметры.Свойство("Сумма", Сумма);

		Ответ = ПроверкаПароля (Пароль);
		Если Ответ <> Неопределено Тогда
			Возврат;
		КонецЕсли;

		Ответ = ВнестиСумму(Объект, Пароль, Сумма, НомерЛУ);

	ИначеЕсли ИмяОперации = "Изъять" Тогда

		Параметры.Свойство("Сумма", Сумма);
		Ответ = ПроверкаПароля (Пароль);
		Если Ответ <> Неопределено Тогда
			Возврат;
		КонецЕсли;

		Ответ = ИзъятьСумму(Объект, Пароль, Сумма, НомерЛУ);

	ИначеЕсли ИмяОперации = "ОткрытьЯщик" Тогда

		Ответ = ПроверкаПароля (Пароль);
		Если Ответ <> Неопределено Тогда
			Возврат;
		КонецЕсли;

		Ответ = ОткрытьЯщик(Объект, Пароль, НомерЛУ);
		
	ИначеЕсли ИмяОперации = "ХОтчет" Тогда

		Ответ = ПроверкаПароля (Пароль);
		Если Ответ <> Неопределено Тогда
			Возврат;
		КонецЕсли;

		Ответ = ХОтчет(Объект, Пароль, НомерЛУ);
		
	ИначеЕсли ИмяОперации = "ПолучитьНомерЧекаСмены" Тогда

		Ответ = ПроверкаПароля (Пароль);
		Если Ответ <> Неопределено Тогда
			Возврат;
		КонецЕсли;

		Ответ = ПолучитьНомерЧекаСмены(Объект, Пароль, НомерЧека, НомерСмены, НомерЛУ);

		Параметры.Вставить("НомерЧека", НомерЧека );
		Параметры.Вставить("НомерСмены", НомерСмены );

	Иначе

		Ответ = "неизвестная команда";

	КонецЕсли;

КонецПроцедуры // ВыполнитьОперацию()

// возвращает параметры для текущий обработки
//
Процедура ПолучитьПараметры(Вид, ВнешняяКомпонента, ПрограммныйИдентификатор, Модели) Экспорт

	Вид                      = Перечисления.ВидыТорговогоОборудования.ФискальныйРегистратор;
	ВнешняяКомпонента        = "FprnM1C.dll";
	ПрограммныйИдентификатор = "FprnM45";

	Модели = Новый СписокЗначений;
	Модели.Добавить("ФРТРМ","Триум-Ф");
	Модели.Добавить("ФРФКС","Феликс-Р Ф");
	Модели.Добавить("ФРШ01","Штрих-ФР-Ф (v.01,02)");
	Модели.Добавить("ФРЭ01","Элвес-Мини-ФР-Ф (v.01)");

КонецПроцедуры
