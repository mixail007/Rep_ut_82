
Функция ПолучитьВалютуРегламентированногоУчета()
	СкладПоУмолчанию = ПолучитьЗначениепоУмолчанию(глТекущийПользователь,"ОсновнойСклад");
	ТипЦеныПоУмолчанию = ?(СкладПоУмолчанию=Справочники.Склады.ПустаяСсылка(),Справочники.ТипыЦенНоменклатуры.ПустаяСсылка(),СкладПоУмолчанию.ТипЦенРозничнойТорговли);
	возврат ?(ТипЦеныПоУмолчанию = Справочники.ТипыЦенНоменклатуры.ПустаяСсылка(),Справочники.Валюты.ПустаяСсылка(),ТипЦеныПоУмолчанию.ВалютаЦены);
КонецФункции
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	//ВладелецФормы.ПерезаполнятьДисплейПокупателя = Ложь;
	Заголовок = "Оплата чека";
	ДокументОбъект = Этаформа.ВладелецФормы.ДокументОбъект;
	мВалютаРегламентированногоУчета = ПолучитьВалютуРегламентированногоУчета();
	ЭлементыФормы.НадписьСуммаДокумента.Заголовок = "" + ФорматСумм(ДокументОбъект.Товары.Итог("Сумма"), мВалютаРегламентированногоУчета, "0.00");
	ДокументОбъект.СуммаНаличнойОплаты = ДокументОбъект.Товары.Итог("Сумма") - ДокументОбъект.СуммаБезналичнойОплаты;

КонецПроцедуры

// Процедура - обработчик события "ОбновлениеОтображения" формы.
//
Процедура ОбновлениеОтображения()
	мВалютаРегламентированногоУчета = ПолучитьВалютуРегламентированногоУчета();
	Сдача = (ДокументОбъект.СуммаБезналичнойОплаты + ДокументОбъект.СуммаНаличнойОплаты) - ДокументОбъект.Товары.Итог("Сумма");
	ЭлементыФормы.НадписьСдача.Заголовок = "" + ФорматСумм(Сдача, мВалютаРегламентированногоУчета, "0.00");
                               
	ВладелецФормы.ВывестиИнформациюНаДисплейПокупателя("ОтобразитьИтог", Сдача);

КонецПроцедуры

// Процедура - обработчик события "ПередЗакрытием" формы.
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	СтандартнаяОбработка                         = Ложь;
	ВладелецФормы.ПерезаполнятьДисплейПокупателя = Истина;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при выборе пункта "ДействиеОк" командной панели
// формы. Процедура отрабатывает выбор печатной формы.
//

Процедура НапечататьТоварныйЧек()

	КоличествоСтрокТоварногоЧека = 8;
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Чек_Накладная";
    
	Макет = ПолучитьМакет("ТоварныйЧек");
	
	// Выводим шапку чека

	ОбластьШапки 		= Макет.ПолучитьОбласть("Шапка");
	ОбластьСтроки		= Макет.ПолучитьОбласть("Строка");
	ОбластьПодвала		= Макет.ПолучитьОбласть("Подвал");
	ОбластьПустойСтроки	= Макет.ПолучитьОбласть("ПустаяСтрока");
	
	ОбластьШапки.Параметры.ДатаДокумента	= Формат(Дата, "ДЛФ=DD");
	ОбластьПодвала.Параметры.СуммаПрописью	= СформироватьСуммуПрописью(Товары.Итог("Сумма"), ТипЦен.ВалютаЦены.Ссылка);
	ТабДокумент.Вывести(ОбластьШапки);

	СчСтрок = 0;
	
	Для каждого Сч Из Товары Цикл
	
		ОбластьСтроки.Параметры.Ном				= Сч.НомерСтроки;
		ОбластьСтроки.Параметры.Наименование	= Сч.Номенклатура;
		ОбластьСтроки.Параметры.Количество		= Формат(Сч.Количество, "ЧДЦ=3");
		ОбластьСтроки.Параметры.Цена			= Формат(Сч.Цена, "ЧДЦ=2");
		ОбластьСтроки.Параметры.Сумма			= Формат(Сч.Сумма, "ЧДЦ=2");
		ТабДокумент.Вывести(ОбластьСтроки);
		СчСтрок = СчСтрок + 1;
		
	КонецЦикла;
	
	Если КоличествоСтрокТоварногоЧека - СчСтрок > 0 Тогда
		
		Для СчСтр = 1 По КоличествоСтрокТоварногоЧека - СчСтрок Цикл
		
			ТабДокумент.Вывести(ОбластьПустойСтроки);
		
		КонецЦикла; 
		
	
	КонецЕсли; 
	
	ТабДокумент.Вывести(ОбластьПодвала);
	
	ТабДокумент.Напечатать();

КонецПроцедуры

Процедура НапечататьЗаказНаряд()

	КоличествоСтрокРабот = 7;
	КоличествоСтрокТовара = 4;
	
	ТабДокумент = Новый ТабличныйДокумент;
	//ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Чек_Накладная";

	Макет = ПолучитьОбщийМакет("ЗаказНарядМойка");
	
	// Выводим шапку чека

	ОбластьШапки 					= Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкиТовары 				= Макет.ПолучитьОбласть("Шапка_ТЧ_Материалы");
	ОбластьШапкиПовреждения 		= Макет.ПолучитьОбласть("Шапка_ТЧ_Повреждения");
	
	ОбластьСтроки			= Макет.ПолучитьОбласть("СтрокаТЧ");
	ОбластьПодвала			= Макет.ПолучитьОбласть("Подвал");
	ОбластьДоговорЗаявка	= Макет.ПолучитьОбласть("Договор_Заявка");
	
	Для Счет = 1 По 2 Цикл
		
		
		ОбластьШапки.Параметры.Дата		= Формат(ДокументОбъект.Дата, "ДЛФ=DD");
		ОбластьШапки.Параметры.Номер	= ДокументОбъект.Номер;
		Если НЕ ЗначениеНеЗаполнено(МаркаИНомер) Тогда
		
			ОбластьШапки.Параметры.МаркаАМ = МаркаИНомер.Наименование;	
			ОбластьШапки.Параметры.НомерАМ = МаркаИНомер.НомерАвто;	
		
		КонецЕсли; 
		ТабДокумент.Вывести(ОбластьШапки);
		
		СчСтрок = 0;
		
		Для каждого Сч Из ДокументОбъект.Товары Цикл
			
			Если Сч.Номенклатура.Услуга Тогда
				
				ОбластьСтроки.Параметры.Номенклатура	= Сч.Номенклатура;
				ОбластьСтроки.Параметры.Количество		= Формат(Сч.Количество, "ЧДЦ=3");
				ОбластьСтроки.Параметры.Цена			= Формат(Сч.Цена, "ЧДЦ=2");
				ОбластьСтроки.Параметры.Сумма			= Формат(Сч.Сумма, "ЧДЦ=2");
				ТабДокумент.Вывести(ОбластьСтроки);
				СчСтрок = СчСтрок + 1;
				
			КонецЕсли; 
			
		КонецЦикла;
		
		Если КоличествоСтрокРабот - СчСтрок > 0 Тогда
			
			Для СчСтр = 1 По КоличествоСтрокРабот - СчСтрок Цикл
				
				ОбластьСтроки.Параметры.Номенклатура	= "";
				ОбластьСтроки.Параметры.Количество		= "";
				ОбластьСтроки.Параметры.Цена			= "";
				ОбластьСтроки.Параметры.Сумма			= "";
				ТабДокумент.Вывести(ОбластьСтроки);
				
			КонецЦикла; 
			
			
		КонецЕсли;
		
		СчСтрок = 0;
		
		ТабДокумент.Вывести(ОбластьШапкиТовары);
		
		Для каждого Сч Из ДокументОбъект.Товары Цикл
			
			Если НЕ Сч.Номенклатура.Услуга Тогда
				
				ОбластьСтроки.Параметры.Номенклатура	= Сч.Номенклатура;
				ОбластьСтроки.Параметры.Количество		= Формат(Сч.Количество, "ЧДЦ=3");
				ОбластьСтроки.Параметры.Цена			= Формат(Сч.Цена, "ЧДЦ=2");
				ОбластьСтроки.Параметры.Сумма			= Формат(Сч.Сумма, "ЧДЦ=2");
				ТабДокумент.Вывести(ОбластьСтроки);
				СчСтрок = СчСтрок + 1;
				
			КонецЕсли; 
			
		КонецЦикла;
		
		Если КоличествоСтрокТовара - СчСтрок > 0 Тогда
			
			Для СчСтр = 1 По КоличествоСтрокТовара - СчСтрок Цикл
				
				ОбластьСтроки.Параметры.Номенклатура	= "";
				ОбластьСтроки.Параметры.Количество		= "";
				ОбластьСтроки.Параметры.Цена			= "";
				ОбластьСтроки.Параметры.Сумма			= "";
				ТабДокумент.Вывести(ОбластьСтроки);
				
			КонецЦикла; 
			
			
		КонецЕсли; 
		
		ТабДокумент.Вывести(ОбластьШапкиТовары);
		ОбластьСтроки.Параметры.Номенклатура	= "";
		ОбластьСтроки.Параметры.Количество		= "";
		ОбластьСтроки.Параметры.Цена			= "";
		ОбластьСтроки.Параметры.Сумма			= "";
		ТабДокумент.Вывести(ОбластьСтроки);
		
		ТабДокумент.Вывести(ОбластьПодвала);
		
	КонецЦикла; 
	
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	Если НЕ ЗначениеНеЗаполнено(МаркаИНомер) Тогда
		
		ОбластьДоговорЗаявка.Параметры.МаркаАМ = МаркаИНомер.Наименование;	
		ОбластьДоговорЗаявка.Параметры.НомерАМ = МаркаИНомер.НомерАвто;	
		
	КонецЕсли; 
	
	ТабДокумент.Вывести(ОбластьДоговорЗаявка);
	
	ТабДокумент.Напечатать();

КонецПроцедуры


Процедура ДействияФормыДействиеОк(Кнопка)

	Сдача = (СуммаБезналичнойОплаты + СуммаНаличнойОплаты) - Товары.Итог("Сумма");

	Если Сдача < 0 Тогда
		Предупреждение("Суммы недостаточно для оплаты чека!");
		Возврат;
	КонецЕсли;
	
	Если ЭлементыФормы.ПечатьТоварногоЧека.Значение Тогда
	
		НапечататьТоварныйЧек();
	
	КонецЕсли;
	
	Если ЭлементыФормы.ПечатьЗаказаНаряда.Значение Тогда
	
		НапечататьЗаказНаряд();
	
	КонецЕсли; 


	Закрыть("Закрыть чек");

КонецПроцедуры

// Процедура вызывается при выборе пункта "ДействиеОтмена" командной панели
// формы. Процедура отрабатывает выбор печатной формы.
//
Процедура ДействияФормыДействиеОтмена(Кнопка)
	Закрыть("Отменить");
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Процедура - обработчик события "ПриИзменении" поля ввода "СуммаБезналичнойОплаты".
//
Процедура СуммаБезналичнойОплатыПриИзменении(Элемент)

	Если Элемент.Значение = 0 Тогда
		ЭлементыФормы.БезналичнаяОплата.Доступность = Ложь;
	Иначе
		ЭлементыФормы.БезналичнаяОплата.Доступность = Истина;
	КонецЕсли;

КонецПроцедуры

Процедура БезналичнаяОплатаПриИзменении(Элемент)

	Если ВладелецФормы.РассчитыватьАвтоматическиеСкидки() Тогда
		//Расчет скидок

		ВладелецФормы.мСуммаДокументаБезСкидок = ПолучитьСуммуДокументаБезСкидки(Товары);
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ВидРеализации" ,                       Перечисления.ВидыСкидок.Розничная);
		СтруктураПараметров.Вставить("СуммаДокумента",                       ВладелецФормы.мСуммаДокументаБезСкидок);
		СтруктураПараметров.Вставить("ВидОплаты"     ,                       БезналичнаяОплата);
		СтруктураПараметров.Вставить("Карта",                                ДисконтнаяКарта);

		РассчитатьСкидкиПриПродаже(ЭтотОбъект, Товары, СтруктураПараметров, ВладелецФормы.мМинимальныеЦены, ВладелецФормы.мСписокФильтров);

		ВладелецФормы.СуммаРучныхСкидок = 0;
		ВладелецФормы.СуммаАвтоматическихСкидок = 0;
		Для каждого СтрокаТовара ИЗ Товары Цикл

			СуммаСтроки = СтрокаТовара.Цена * СтрокаТовара.Количество;
			СуммаРучныхСкидокСтроки = СуммаСтроки * СтрокаТовара.ПроцентСкидкиНаценки / 100;
			СуммаАвтоматическихСкидокСтроки = СуммаСтроки * СтрокаТовара.ПроцентАвтоматическихСкидок / 100;

			Разница = СуммаСтроки - СуммаРучныхСкидокСтроки - СуммаАвтоматическихСкидокСтроки - СтрокаТовара.Сумма;

			Если Разница <> 0 Тогда
				Если СуммаРучныхСкидокСтроки <> 0 Тогда
					СуммаРучныхСкидокСтроки = СуммаРучныхСкидокСтроки + Разница;
				Иначе
					СуммаАвтоматическихСкидокСтроки = СуммаАвтоматическихСкидокСтроки + Разница;
				КонецЕсли;
			КонецЕсли;

			ВладелецФормы.СуммаРучныхСкидок = ВладелецФормы.СуммаРучныхСкидок + СуммаРучныхСкидокСтроки;
			ВладелецФормы.СуммаАвтоматическихСкидок = ВладелецФормы.СуммаАвтоматическихСкидок + СуммаАвтоматическихСкидокСтроки;

		КонецЦикла;
		ВладелецФормы.ИтогоСуммаСкидок = ВладелецФормы.СуммаРучныхСкидок + ВладелецФормы.СуммаАвтоматическихСкидок;

	КонецЕсли;

КонецПроцедуры

Процедура БезналичнаяОплатаНачалоВыбора(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ФормаВыбора = Справочники.ВидыОплатЧекаККМ.ПолучитьФормуВыбора();
	ФормаВыбора.СправочникСписок.Отбор.ВидДенежныхСредств.Значение = Перечисления.ВидыДенежныхСредств.Наличные;
	ФормаВыбора.СправочникСписок.Отбор.ВидДенежныхСредств.Использование = Истина;
	ФормаВыбора.СправочникСписок.Отбор.ВидДенежныхСредств.ВидСравнения = ВидСравнения.НеРавно;
	ФормаВыбора.ЭлементыФормы.СправочникСписок.НастройкаОтбора.ВидДенежныхСредств.Доступность = Ложь;
	Элемент.Значение = ФормаВыбора.ОткрытьМодально();

КонецПроцедуры



