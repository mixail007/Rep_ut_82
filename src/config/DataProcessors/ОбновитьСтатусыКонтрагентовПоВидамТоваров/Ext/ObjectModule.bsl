
Функция ПолучитьТекстЗапросаСтатусы()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗначенияСвойствОбъектов.Объект КАК Контрагент,
	|	ЗначенияСвойствОбъектов.Значение КАК РабГруппа
	|ПОМЕСТИТЬ РабГруппы
	|ИЗ
	|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|ГДЕ
	|	ЗначенияСвойствОбъектов.Свойство = &СвойствоРабГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыТоваров.Шины) КАК ВидТовара
	|ПОМЕСТИТЬ втВидыТоваров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыТоваров.Диски)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыТоваров.Акб)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыТоваров.Аксессуары)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Контрагент,
	|	ЕСТЬNULL(РабГруппы.РабГруппа, Контрагенты.Ссылка) КАК РабГруппа,
	|	втВидыТоваров.ВидТовара
	|ПОМЕСТИТЬ Покупатели
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РабГруппы КАК РабГруппы
	|		ПО Контрагенты.Ссылка = РабГруппы.Контрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ втВидыТоваров КАК втВидыТоваров
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Контрагенты.Покупатель = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ПродажиОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг) КАК ДокументПродажи,
	|	ЕСТЬNULL(РабГруппы.РабГруппа, ПродажиОбороты.Регистратор.Контрагент) КАК РабГруппа,
	|	ПродажиОбороты.Номенклатура.ВидТовара КАК ВидТовара
	|ПОМЕСТИТЬ ДокументыПродажи
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(, , Регистратор, ) КАК ПродажиОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РабГруппы КАК РабГруппы
	|		ПО ПродажиОбороты.ДоговорКонтрагента.Владелец = РабГруппы.Контрагент
	|ГДЕ
	|	ПродажиОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Покупатели.РабГруппа КАК РабГруппа,
	|	Покупатели.Контрагент КАК Контрагент,
	|	Покупатели.ВидТовара КАК ВидТовара,
	|	МАКСИМУМ(ДокументыПродажи.ДокументПродажи.Дата) КАК ДокПродажиДата,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументыПродажи.ДокументПродажи) КАК КоличПродаж
	|ПОМЕСТИТЬ СДатойРеализации
	|ИЗ
	|	Покупатели КАК Покупатели
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыПродажи КАК ДокументыПродажи
	|		ПО Покупатели.РабГруппа = ДокументыПродажи.РабГруппа
	|			И Покупатели.ВидТовара = ДокументыПродажи.ВидТовара
	|
	|СГРУППИРОВАТЬ ПО
	|	Покупатели.Контрагент,
	|	Покупатели.РабГруппа,
	|	Покупатели.ВидТовара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СДатойРеализации.Контрагент КАК Контрагент,
	|	СДатойРеализации.ВидТовара КАК ВидТовара,
	|	ВЫБОР
	|		КОГДА СтатусыКонтрагентовПоТоварнымГруппам.Статус = ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.Закрылись)
	|				ИЛИ СтатусыКонтрагентовПоТоварнымГруппам.Статус = ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.ВременноНеРаботаем)
	|				ИЛИ СтатусыКонтрагентовПоТоварнымГруппам.Статус = ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.Разобраться)
	|			ТОГДА СтатусыКонтрагентовПоТоварнымГруппам.Статус
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СДатойРеализации.ДокПродажиДата ЕСТЬ NULL 
	|					ТОГДА ВЫБОР
	|							КОГДА Авторизация.Ссылка ЕСТЬ NULL 
	|								ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.Потенциальные)
	|							ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.ВРазработке)
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА СДатойРеализации.ДокПродажиДата < ДОБАВИТЬКДАТЕ(&ТекДата, МЕСЯЦ, -12)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.НеРаботающиеБольшеГода)
	|						КОГДА СДатойРеализации.КоличПродаж = 1
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.ПерваяПродажа)
	|						ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.Работающие)
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК НовСтатус,
	|	СтатусыКонтрагентовПоТоварнымГруппам.Статус КАК ТекСтатус,
	|	СДатойРеализации.ДокПродажиДата,
	|	СтатусыКонтрагентовПоТоварнымГруппам.Ответственный
	|ИЗ
	|	СДатойРеализации КАК СДатойРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Авторизация КАК Авторизация
	|		ПО СДатойРеализации.Контрагент = Авторизация.Владелец
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыКонтрагентовПоТоварнымГруппам КАК СтатусыКонтрагентовПоТоварнымГруппам
	|		ПО СтатусыКонтрагентовПоТоварнымГруппам.Контрагент = СДатойРеализации.Контрагент
	|			И СтатусыКонтрагентовПоТоварнымГруппам.ТоварнаяГруппа = СДатойРеализации.ВидТовара
	|ГДЕ
	|	(СтатусыКонтрагентовПоТоварнымГруппам.Статус ЕСТЬ NULL 
	|			ИЛИ НЕ СтатусыКонтрагентовПоТоварнымГруппам.Статус = ВЫБОР
	|					КОГДА СтатусыКонтрагентовПоТоварнымГруппам.Статус = ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.Закрылись)
	|							ИЛИ СтатусыКонтрагентовПоТоварнымГруппам.Статус = ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.ВременноНеРаботаем)
	|							ИЛИ СтатусыКонтрагентовПоТоварнымГруппам.Статус = ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.Разобраться)
	|						ТОГДА СтатусыКонтрагентовПоТоварнымГруппам.Статус
	|					ИНАЧЕ ВЫБОР
	|							КОГДА СДатойРеализации.ДокПродажиДата ЕСТЬ NULL 
	|								ТОГДА ВЫБОР
	|										КОГДА Авторизация.Ссылка ЕСТЬ NULL 
	|											ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.Потенциальные)
	|										ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.ВРазработке)
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА СДатойРеализации.ДокПродажиДата < ДОБАВИТЬКДАТЕ(&ТекДата, МЕСЯЦ, -12)
	|										ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.НеРаботающиеБольшеГода)
	|									КОГДА СДатойРеализации.КоличПродаж = 1
	|										ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.ПерваяПродажа)
	|									ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.КатегорииКонтрагентов.Работающие)
	|								КОНЕЦ
	|						КОНЕЦ
	|				КОНЕЦ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидТовара
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапросаСтатусы()


Процедура ВыполнитьОбновлениеСтатусовКонтрагентовПоНоменклГруппам() Экспорт
	
	#Если Клиент Тогда
		Сообщить("Начало обработки: " + ТекущаяДата());
	#КонецЕсли
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаСтатусы();
	
	ТекДата = ТекущаяДата();
	СвойствоРабГруппа = ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоКоду("90230");
	
	Запрос.УстановитьПараметр("СвойствоРабГруппа", СвойствоРабГруппа);
	Запрос.УстановитьПараметр("ТекДата"          , ТекДата);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Для каждого стр из Результат Цикл
		Запись = РегистрыСведений.СтатусыКонтрагентовПоТоварнымГруппам.СоздатьМенеджерЗаписи();
		Запись.Контрагент = Стр.Контрагент;
		Запись.ТоварнаяГруппа = стр.ВидТовара;
		Запись.Ответственный = стр.Ответственный;
		Запись.Статус = Стр.НовСтатус;
		Запись.Записать(Истина);	
	конецЦикла;
	
	#Если Клиент Тогда
		Сообщить("Окончание обработки: " + ТекущаяДата());
	#КонецЕсли
	
КонецПроцедуры // ВыполнитьОбновлениеСтатусовКонтрагентовПоНоменклГруппам()