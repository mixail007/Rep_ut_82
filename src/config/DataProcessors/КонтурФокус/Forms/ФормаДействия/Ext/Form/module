
Процедура УправлениеВидимостью()
	
	ЭлементыФормы.ПанельНастройкиФильтра.Страницы.Маркеры.Доступность = НЕ ЛюбойМаркер;
	ЭлементыФормы.СписокМаркеров.Колонки.Пометка.Видимость = НЕ ЛюбойМаркер;
	ЭлементыФормы.ПанельНастройкиФильтра.Страницы.Контрагенты.Доступность = НЕ ЛюбойКонтрагент;
	
КонецПроцедуры

Функция ЗаписатьЭлемент()
	
	СтруктураЭлемента = Новый Структура;
	СтруктураЭлемента.Вставить("Наименование",		Наименование);
	СтруктураЭлемента.Вставить("Включен",			Включен);
	СтруктураЭлемента.Вставить("ЛюбойМаркер",		ЛюбойМаркер);
	СтруктураЭлемента.Вставить("ЛюбойКонтрагент",	ЛюбойКонтрагент);
	СтруктураЭлемента.Вставить("СписокМаркеров",	СписокМаркеров);
	СтруктураЭлемента.Вставить("СписокКонтрагентов",СписокКонтрагентов);
	СтруктураЭлемента.Вставить("ВыполняемыйКод",	ЭлементыФормы.ВыполняемыйКод.ПолучитьТекст());
	
	Фокус_ХранениеДанных_СохранитьЭлементСправочника("Действия",Ссылка,СтруктураЭлемента);
	
КонецФункции

Процедура ОсновныеДействияФормыОсновныеДействияФормыЗаписатьИЗакрыть(Кнопка)
	
	ЗаписатьЭлемент();
	
	Оповестить("Фокус_ИзмененоДействие");
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	СохраненныйСписокМаркеров = Неопределено;
			   
	Если ЗначениеЗаполнено(Ссылка) Тогда
		
		СтруктураЭлемента = Фокус_ХранениеДанных_ПолучитьЭлементСправочника("Действия", Ссылка);
		Наименование	= СтруктураЭлемента.Наименование;
		Включен			= СтруктураЭлемента.Включен;
		ЛюбойМаркер		= СтруктураЭлемента.ЛюбойМаркер;
		ЛюбойКонтрагент	= СтруктураЭлемента.ЛюбойКонтрагент;
		
		СохраненныйСписокМаркеров = СтруктураЭлемента.СписокМаркеров;
		Для Каждого Стр Из СтруктураЭлемента.СписокКонтрагентов Цикл
			ЗаполнитьЗначенияСвойств(СписокКонтрагентов.Добавить(),Стр);
		КонецЦикла;
		
		ЭлементыФормы.ВыполняемыйКод.УстановитьТекст(СтруктураЭлемента.ВыполняемыйКод);
		
	Иначе
		
		ЛюбойКонтрагент = Истина;
		ЛюбойМаркер = Истина;
		Включен = Истина;
		
		ЭлементыФормы.ВыполняемыйКод.УстановитьТекст("//ЗаголовокТекстаПисьма="""";
		|// Фокус_УведомитьОбИзмененияхПоПочте(""ваш e-mail"",СписокМаркеров,СписокКонтрагентов,ЗаголовокТекстаПисьма);");
		
	КонецЕсли;
	
	СписокВсехМаркеров = Фокус_ХранениеДанных_ПолучитьСписокМаркеров();
	Если ТипЗнч(СписокВсехМаркеров) = Тип("ТаблицаЗначений") Тогда
		Для Каждого Эл ИЗ СписокВсехМаркеров Цикл
			НоваяСтрока = СписокМаркеров.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Эл);
			Если НЕ СохраненныйСписокМаркеров = Неопределено Тогда
				НайденноеЗначение = СохраненныйСписокМаркеров.Найти(Эл.ID,"ID");
				Если НЕ НайденноеЗначение = Неопределено Тогда
					НоваяСтрока.Включен = НайденноеЗначение.Включен;
				КонецЕсли;
			КонецЕсли;		
		КонецЦикла;
	КонецЕсли;
	СписокШаблонов=Фокус_Наблюдение_ПолучитьСписокШаблонов();
	УправлениеВидимостью();
	
КонецПроцедуры

Процедура СписокМаркеровПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.Пометка.УстановитьФлажок(ДанныеСтроки.Включен);
	
	Если ДанныеСтроки.Категория = "Красный" Тогда
		ОформлениеСтроки.Ячейки.Наименование.УстановитьКартинку(ЭлементыФормы.Красный.Картинка);
	ИначеЕсли ДанныеСтроки.Категория = "Желтый" Тогда
		ОформлениеСтроки.Ячейки.Наименование.УстановитьКартинку(ЭлементыФормы.Желтый.Картинка);
	ИначеЕсли ДанныеСтроки.Категория = "Зеленый" Тогда
		ОформлениеСтроки.Ячейки.Наименование.УстановитьКартинку(ЭлементыФормы.Зеленый.Картинка);
	Иначе	
		ОформлениеСтроки.Ячейки.Наименование.УстановитьКартинку(ЭлементыФормы.Пустой.Картинка);
	КонецЕсли;

КонецПроцедуры

Процедура СписокМаркеровПриИзмененииФлажка(Элемент, Колонка)
	
	ТекСтрока = ЭлементыФормы.СписокМаркеров.ТекущиеДанные;
	Если НЕ ТекСтрока = Неопределено Тогда
		ТекСтрока.Включен = НЕ ТекСтрока.Включен;
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельСпискаМаркеровВключитьВсеМаркеры(Кнопка)

	СписокМаркеров.ЗаполнитьЗначения(Истина,"Включен");
	
КонецПроцедуры

Процедура КоманднаяПанельСпискаМаркеровСнятьВсеМаркеры(Кнопка)

	СписокМаркеров.ЗаполнитьЗначения(Ложь,"Включен");
	
КонецПроцедуры

