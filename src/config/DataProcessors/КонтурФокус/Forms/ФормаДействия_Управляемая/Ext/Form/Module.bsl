
&НаСервере
Процедура ЗаписатьЭлементНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	СтруктураЭлемента = Новый Структура;
	СтруктураЭлемента.Вставить("Наименование",		Наименование);
	СтруктураЭлемента.Вставить("Включен",			Включен);
	СтруктураЭлемента.Вставить("ЛюбойМаркер",		ЛюбойМаркер);
	СтруктураЭлемента.Вставить("ЛюбойКонтрагент",	ЛюбойКонтрагент);
	СтруктураЭлемента.Вставить("СписокМаркеров",	СписокМаркеров.Выгрузить());
	СтруктураЭлемента.Вставить("СписокКонтрагентов",СписокКонтрагентов.Выгрузить());
	СтруктураЭлемента.Вставить("ВыполняемыйКод",	ВыполняемыйКод);
	
	ОбработкаОбъект.Фокус_ХранениеДанных_СохранитьЭлементСправочника("Действия",Параметры.Ссылка,СтруктураЭлемента);

КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьЭлементНаСервере();
	Оповестить("Фокус_ИзмененоДействие");
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура СписокМаркеровУстановитьСнятьФлажки(Пометка)
	
	Для Каждого Стр Из СписокМаркеров Цикл
		Стр.Включен = Пометка;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВсеМаркеры(Команда)
	СписокМаркеровУстановитьСнятьФлажки(Истина);	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеМаркеры(Команда)
	СписокМаркеровУстановитьСнятьФлажки(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура УправлениеДоступностью()
	
	Элементы.СтраницаМаркеры.Доступность = НЕ ЛюбойМаркер;
	Элементы.СтраницаКонтрагенты.Доступность = НЕ ЛюбойКонтрагент;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УправлениеДоступностью();
КонецПроцедуры

&НаКлиенте
Процедура ЛюбойМаркерПриИзменении(Элемент)
	УправлениеДоступностью();
КонецПроцедуры

&НаКлиенте
Процедура ЛюбойКонтрагентПриИзменении(Элемент)
	УправлениеДоступностью();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	СохраненныйСписокМаркеров = Неопределено;
	
	Если ЗначениеЗаполнено(Параметры.Ссылка) Тогда
		
		СтруктураЭлемента = ОбработкаОбъект.Фокус_ХранениеДанных_ПолучитьЭлементСправочника("Действия", Параметры.Ссылка);
		
		Наименование	= СтруктураЭлемента.Наименование;
		Включен			= СтруктураЭлемента.Включен;
		ЛюбойМаркер		= СтруктураЭлемента.ЛюбойМаркер;
		ЛюбойКонтрагент	= СтруктураЭлемента.ЛюбойКонтрагент;
		
		СохраненныйСписокМаркеров = СтруктураЭлемента.СписокМаркеров;
		Для Каждого Стр Из СтруктураЭлемента.СписокКонтрагентов Цикл
			ЗаполнитьЗначенияСвойств(СписокКонтрагентов.Добавить(),Стр);
		КонецЦикла;
			
		ВыполняемыйКод = СтруктураЭлемента.ВыполняемыйКод;
		
	Иначе
		
		ЛюбойКонтрагент	= Истина;
		ЛюбойМаркер		= Истина;
		Включен			= Истина;
		
		ВыполняемыйКод = "//ЗаголовокТекстаПисьма="""";
		|// Фокус_УведомитьОбИзмененияхПоПочте(""ваш e-mail"",СписокМаркеров,СписокКонтрагентов,ЗаголовокТекстаПисьма);";
		
	КонецЕсли;
	
	СписокВсехМаркеров = ОбработкаОбъект.Фокус_ХранениеДанных_ПолучитьСписокМаркеров();
	Если ТипЗнч(СписокВсехМаркеров) = Тип("ТаблицаЗначений") Тогда
		Для Каждого Эл ИЗ СписокВсехМаркеров Цикл
			НоваяСтрока = СписокМаркеров.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Эл);
			Если НЕ СохраненныйСписокМаркеров = Неопределено Тогда
				НайденноеЗначение = СохраненныйСписокМаркеров.Найти(Эл.ID,"ID");
				Если НЕ НайденноеЗначение = Неопределено Тогда
					НоваяСтрока.Включен = НайденноеЗначение.Включен;
				КонецЕсли;
			КонецЕсли;		
		КонецЦикла;
	КонецЕсли;
	СписокШаблонов=ОбработкаОбъект.Фокус_Наблюдение_ПолучитьСписокШаблонов();
	
КонецПроцедуры
