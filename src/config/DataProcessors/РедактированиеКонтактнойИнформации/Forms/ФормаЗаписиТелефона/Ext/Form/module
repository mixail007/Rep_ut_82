
Перем мОбработкаТайпинга;
Перем мТекстТайпинга;
Перем мПоследнееЗначениеЭлементаТайпинга;

Перем мСтруктураИзмерений Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
//

// Процедура вызывается при открытии формы.
//
Процедура ПриОткрытии()

	ЭлементыФормы.Объект.ТолькоПросмотр     = НЕ ДоступностьОбъекта;
	
	Если Вид = Неопределено Тогда
		Вид = Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
	КонецЕсли;
	
	Если ЗначениеНеЗаполнено(Объект) И ДоступностьОбъекта Тогда
		ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.Объект;
	ИначеЕсли ЗначениеНеЗаполнено(Вид) Тогда
		ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.Вид;
	Иначе
		ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.Поле3;
	КонецЕсли; 
	
	мПоследнееЗначениеЭлементаТайпинга = Вид;
	
КонецПроцедуры // ПриОткрытии()

// Процедура вызывается при ОбновлениеОтображения формы.
//
Процедура ОбновлениеОтображения()
	
	ЭтаФорма.ЭлементыФормы.Вид.ТолькоПросмотр = (Объект = Неопределено);

	ОбновлениеОтображенияВФормеПриТайпинге(ЭтаФорма, ЭтаФорма.ЭлементыФормы.Вид, мОбработкаТайпинга, мТекстТайпинга);
	
КонецПроцедуры

Процедура ОсновныеДействияФормыЗаписать(Кнопка)
	
	Записать();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если Записать() = Истина Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Отказ = ЗакрыватьФормуРедактирования();
	КонецЕсли; 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
//

// Процедура вызывается при очистке значения элемента формы Объект.
//
Процедура ОбъектОчистка(Элемент, СтандартнаяОбработка)
	
	Если ТипЗнч(Вид) = Тип("СправочникСсылка.ВидыКонтактнойИнформации") Тогда
		Вид = Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
	КонецЕсли; 
	
КонецПроцедуры

// Процедура вызывается при изменении реквизита Поле3 - собственно номер телефона.
Процедура Поле3ПриИзменении(Элемент)

	Элемент.Значение = ПривестиНомерТелефонаКШаблону(Элемент.Значение);
	СформироватьПредставлениеТелефона(ЭтотОбъект);
	
	//asm 09.01.14 в поле5 записываем номер без след.символов и по нему ищем контрагента
	Поле5=Представление;
	Поле5=СтрЗаменить(Поле5,")","");
	Поле5=СтрЗаменить(Поле5,"(","");
	Поле5=СтрЗаменить(Поле5,"-","");
	Поле5=СтрЗаменить(Поле5," ","");
	Поле5=СтрЗаменить(Поле5,"+7","8");
	Поле5=СтрЗаменить(Поле5,"+","");
	Если СтрДлина(Поле5)=6 тогда
	 Поле5="4852"+Поле5;
	КонецЕсли;
	//-asm
КонецПроцедуры // Поле3ПриИзменении()

// Процедура вызывается при изменении реквизита Поле4 - добавочный номер телефона.
Процедура Поле4ПриИзменении(Элемент)
	
	Элемент.Значение = ПривестиНомерТелефонаКШаблону(Элемент.Значение);
	СформироватьПредставлениеТелефона(ЭтотОбъект);
	
КонецПроцедуры // Поле4ПриИзменении()

// Процедура вызывается при изменении реквизита Поле1 - код страны.
Процедура Поле1ПриИзменении(Элемент)
	
	Если Лев(СокрЛ(Поле1), 1) <> "+" И НЕ ПустаяСтрока(Поле1) Тогда
		Поле1 = СокрЛП(Поле1);
		Пока Лев(Поле1, 1) = "0" Цикл
			Поле1 = Сред(Поле1, 2);
		КонецЦикла;
		Если НЕ ПустаяСтрока(Поле1) Тогда
			Поле1 = "+" + Поле1;
		КонецЕсли; 
	КонецЕсли; 
	
	СформироватьПредставлениеТелефона(ЭтотОбъект);
	
КонецПроцедуры // Поле1ПриИзменении()

// Процедура вызывается при изменении реквизита Поле2 - код города.
Процедура Поле2ПриИзменении(Элемент)
	
	СформироватьПредставлениеТелефона(ЭтотОбъект);
	
КонецПроцедуры // Поле2ПриИзменении()

// Процедура перехватывает момент начала выбора вида контактной информации.
//
// Параметры
//  Элемент - элемент формы, выбор значения которого должен произойти
//  СтандартнаяОбработка - булево, флаг стандартной обработки выбора.
Процедура ВидНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	Если Объект = Неопределено ИЛИ Объект.Ссылка.Пустая() Тогда
		Сообщить("Выберите объект.");
		Возврат;
	КонецЕсли;

	ОткрытьФормуВыбораВидаКИ(Истина, Элемент, Тип, ВидОбъектаКИ(Объект));
	
КонецПроцедуры // ВидНачалоВыбора()

// Процедура перехватывает момент начала выбора Объекта.
//
// Параметры
//  Элемент - элемент формы, выбор значения которого должен произойти
//  СтандартнаяОбработка - булево, флаг стандартной обработки выбора.
Процедура ОбъектНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = НачалоВыбораОбъектаКИ(ЭтаФорма, Элемент, глТекущийПользователь);

КонецПроцедуры

// Процедура обработчик события АвтоПодборТекста элемента формы Вид.
//
Процедура ВидАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	
	АвтоПодборТекстаВЭлементеУправления(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка, Новый Структура("Тип, ВидОбъектаКонтактнойИнформации", Тип, ВидОбъектаКИ(Объект)), Тип("СправочникСсылка.ВидыКонтактнойИнформации"));
	
КонецПроцедуры

// Процедура обработчик события ОкончаниеВводаТекста элемента формы Вид.
//
Процедура ВидОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	ОкончаниеВводаТекстаВЭлементеУправления(Элемент, Текст, Значение, СтандартнаяОбработка, Новый Структура("Тип, ВидОбъектаКонтактнойИнформации", Тип, ВидОбъектаКИ(Объект)), ЭтаФорма, Тип("СправочникСсылка.ВидыКонтактнойИнформации"), мОбработкаТайпинга, мТекстТайпинга, мПоследнееЗначениеЭлементаТайпинга);
	
КонецПроцедуры

// Процедура обработчик события ПриИзменении элемента формы Вид.
//
Процедура ВидПриИзменении(Элемент)
	
	мПоследнееЗначениеЭлементаТайпинга = Элемент.Значение;
	
КонецПроцедуры

мОбработкаТайпинга                 = Ложь;
мТекстТайпинга                     = "";
мПоследнееЗначениеЭлементаТайпинга = Неопределено;
мСтруктураИзмерений                = Неопределено;
