Перем мПериод          Экспорт; // Период движений
Перем мТаблицаДвижений Экспорт; // Таблица движений

// Выполняет приход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьПриход() Экспорт

	ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Приход);

КонецПроцедуры // ВыполнитьПриход()

// Выполняет расход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьРасход() Экспорт

	ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Расход);

КонецПроцедуры // ВыполнитьРасход()

// Выполняет движения по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьДвижения() Экспорт

	Загрузить(мТаблицаДвижений);

КонецПроцедуры // ВыполнитьДвижения()

Процедура ПередЗаписью(Отказ, Замещение)
		
	ПроверкаПериодаЗаписей(ЭтотОбъект, Отказ);
	
КонецПроцедуры


Процедура КонтрольНаСоответствиеОснованию(ДокументОбъект, ИмяТабличнойЧасти, СтруктураШапкиДокумента, Отказ, Заголовок) Экспорт
	   Запрос=Новый Запрос;
	   
	   Запрос.Текст="
| ВЫБРАТЬ
| ДвиженияПоОснованию.Контрагент Контрагент,
| ДвиженияПоОснованию.ДоговорКонтрагента ДоговорКонтрагента,
| ДвиженияПоОснованию.ВидОперации ВидОперации,
| ДвиженияПоОснованию.Номенклатура Номенклатура,
| ЕстьNULL(ОснованиеТовары.Количество,0) КоличествоПоОснованию,
| ЕстьNULL(ДвиженияПоОснованию.Количество,0) ТребуемоеКоличество
|  
|	ИЗ
| 
| (ВЫБРАТЬ
|	Ссылка.Контрагент Контрагент,
|	Ссылка.ДоговорКонтрагента ДоговорКонтрагента,
|	Ссылка.ВидОперации ВидОперации,
|	Номенклатура,
|	Количество
|ИЗ
|	Документ.ЗаказПоОтветственномуХранению.Товары 
|	ГДЕ Ссылка = &Основание И Ссылка.Проведен) ОснованиеТовары
|	ПРАВОЕ СОЕДИНЕНИЕ(
|	ВЫБРАТЬ
|	А.Контрагент,
|	А.ДоговорКонтрагента,
|	А.ВидОперации,
|	А.Номенклатура,
|	СУММА(А.Количество) Количество ИЗ
|	( ВЫБРАТЬ Ссылка.Контрагент Контрагент,
|	Ссылка.ДоговорКонтрагента ДоговорКонтрагента,
|	Ссылка.ВидОперации ВидОперации,
|	Номенклатура,
|	Количество
|ИЗ
|	Документ.ОперацияПоОтветственномуХранению.Товары 
|ГДЕ
|	Ссылка.ДокументОснование =  &Основание
|	И Ссылка.Проведен и Ссылка <>  &Ссылка
|	ОБЪЕДИНИТЬ ВСЕ
|	ВЫБРАТЬ Ссылка.Контрагент Контрагент,
|	Ссылка.ДоговорКонтрагента ДоговорКонтрагента,
|	Ссылка.ВидОперации ВидОперации,
|	Номенклатура,
|	Количество
|ИЗ
|	Документ.ОперацияПоОтветственномуХранению.Товары 
|ГДЕ
|	Ссылка =  &Ссылка) А
|	СГРУППИРОВАТЬ ПО А.Контрагент,А.ДоговорКонтрагента,
|	А.ВидОперации, А.Номенклатура
|	) ДвиженияПоОснованию
|	ПО ОснованиеТовары.Контрагент=ДвиженияПоОснованию.Контрагент
|	И ОснованиеТовары.ДоговорКонтрагента=ДвиженияПоОснованию.ДоговорКонтрагента
|	И ОснованиеТовары.ВидОперации=ДвиженияПоОснованию.ВидОперации
|	И ОснованиеТовары.Номенклатура=ДвиженияПоОснованию.Номенклатура

|	
|ГДЕ	  ЕстьNULL(ДвиженияПоОснованию.Количество,0)-  ЕстьNULL(ОснованиеТовары.Количество,0) >0 ";

Запрос.УстановитьПараметр("Основание",ДокументОбъект.ДокументОснование); 
Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);

Выборка=Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда    
		СтрокаСообщения = "Количество  по номенклатуре "+СокрЛП(Выборка.Номенклатура.Наименование) + " превышает количество по заказу "+ 
			Строка(ДокументОбъект.ДокументОснование);
			
			ОшибкаНетОстатка(СтрокаСообщения, Выборка.КоличествоПоОснованию, Выборка.ТребуемоеКоличество,
			Выборка.Номенклатура.БазоваяЕдиницаИзмерения, Отказ, "");

			//Сообщить(СтрокаСообщения,СтатусСообщения.Важное);
			//Отказ=Истина;
	КонецЕсли;	

КонецПроцедуры	

// Процедура контролирует остаток по данному регистру по переданному документу
// и его табличной части. В случае недостатка товаров выставляется флаг отказа и 
// выдается сообщегние.
//
// Параметры:
//  ДокументОбъект    - объект проводимого документа, 
//  ИмяТабличнойЧасти - строка, имя табличной части, которая проводится по регистру, 
//  СтруктураШапкиДокумента - структура, содержащая значения "через точку" ссылочных реквизитов по шапке документа,
//  Отказ             - флаг отказа в проведении,
//  Заголовок         - строка, заголовок сообщения об ошибке проведения.
//
Процедура КонтрольОстатков(ДокументОбъект, ИмяТабличнойЧасти, СтруктураШапкиДокумента, Отказ, Заголовок) Экспорт

	МетаданныеДокумента = ДокументОбъект.Метаданные();
	ИмяДокумента        = МетаданныеДокумента.Имя;
	
	ИмяТаблицы          = ИмяДокумента + "." + СокрЛП(ИмяТабличнойЧасти);
	
	Запрос =Новый Запрос;
	Запрос.Текст= "ВЫБРАТЬ
|	ОперацияПоОтвХранению.Контрагент Контрагент,
|	ОперацияПоОтвХранению.ДоговорКонтрагента ДоговорКонтрагента,
|	ОперацияПоОтвХранению.Склад Склад,
|	ОперацияПоОтвХранению.Номенклатура Номенклатура,
|	ЕстьNULL(ОтветственноеХранениеОстатки.КоличествоОстаток,0)  КоличествоОстаток,
|	ЕстьNULL(ОперацияПоОтвХранению.ДокументКоличество,0)  ДокументКоличество,
|	ЕстьNULL(ОперацияПоОтвХранению.ДокументКоличество,0)-ЕстьNULL(ОтветственноеХранениеОстатки.КоличествоОстаток,0) ПревышениеОстатка
|	ИЗ
|	(ВЫБРАТЬ
|	Ссылка.Контрагент Контрагент,
|   Ссылка.ДоговорКонтрагента ДоговорКонтрагента,
|	// Ссылка.Склад Склад,
|	 Склад,
|	Номенклатура,
|	СУММА(Количество) ДокументКоличество
|	ИЗ	Документ.ОперацияПоОтветственномуХранению.Товары 
|	ГДЕ Ссылка = &Ссылка
|	СГРУППИРОВАТЬ ПО Ссылка.Контрагент, Ссылка.ДоговорКонтрагента,Склад,Номенклатура) ОперацияПоОтвХранению
|	ЛЕВОЕ СОЕДИНЕНИЕ
|	(ВЫБРАТЬ 
|	Контрагент,
|	ДоговорКонтрагента,
|	Склад,
|	Номенклатура,
|	ЕстьNULL(КоличествоОстаток,0)  КоличествоОстаток
|	ИЗ
|	РегистрНакопления.ТоварыНаОтветственномХранении.Остатки(&ДатаОстатков,	Контрагент = &Контрагент 
|)) КАК ОтветственноеХранениеОстатки
|	ПО  ОперацияПоОтвХранению.Контрагент=ОтветственноеХранениеОстатки.Контрагент
|   //ТекстДоговора
|	И ОперацияПоОтвХранению.Склад=ОтветственноеХранениеОстатки.Склад
|	И ОперацияПоОтвХранению.Номенклатура=ОтветственноеХранениеОстатки.Номенклатура
|ГДЕ	ЕстьNULL(ОперацияПоОтвХранению.ДокументКоличество,0)-ЕстьNULL(ОтветственноеХранениеОстатки.КоличествоОстаток,0)>0";	
	
	Запрос.УстановитьПараметр("Контрагент",СтруктураШапкиДокумента.Контрагент);
	Запрос.УстановитьПараметр("Ссылка",ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДатаОстатков",ДокументОбъект.Дата);
	
	Если не ЗначениеНеЗаполнено(СтруктураШапкиДокумента.ДоговорКонтрагента) Тогда // если в операции выбран договор, тогда
		Запрос.Текст=СтрЗаменить(Запрос.Текст,"//ТекстДоговора", " И ОперацияПоОтвХранению.ДоговорКонтрагента=ОтветственноеХранениеОстатки.ДоговорКонтрагента ");
	КонецЕсли;	
	
	Выборка=Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда    
		Если Выборка.ПревышениеОстатка>0 Тогда
			СтрокаСообщения = "Остатка товара на ответственном хранении по контрагенту:"+СокрЛП(Выборка.Контрагент.Наименование)+
			" по складу: "+СокрЛП(Выборка.Склад.Наименование)+ " по номенклатуре :"+СокрЛП(Выборка.Номенклатура.Наименование) + " не достаточно.";
			ОшибкаНетОстатка(СтрокаСообщения, Выборка.КоличествоОстаток, Выборка.ДокументКоличество,
			Выборка.Номенклатура.БазоваяЕдиницаИзмерения, Отказ, "");
			
		КонецЕсли;	
	КонецЕсли;	
	
	
КонецПроцедуры // КонтрольОстатков()
