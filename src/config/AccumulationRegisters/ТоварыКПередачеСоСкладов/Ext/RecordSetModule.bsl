Перем мПериод          Экспорт; // Период движений
Перем мТаблицаДвижений Экспорт; // Таблица движений

// Выполняет приход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьПриход() Экспорт

	ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Приход);

КонецПроцедуры // ВыполнитьПриход()

// Выполняет расход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьРасход() Экспорт

	ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Расход);

КонецПроцедуры // ВыполнитьРасход()

// Выполняет движения по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьДвижения() Экспорт

	Загрузить(мТаблицаДвижений);

КонецПроцедуры // ВыполнитьДвижения()

// Процедура контролирует остаток по данному регистру по переданному документу
// и его табличной части. В случае недостатка товаров выставляется флаг отказа и 
// выдается сообщегние.
//
// Параметры:
//  ДокументОбъект    - объект проводимого документа, 
//  ИмяТабличнойЧасти - строка, имя табличной части, которая проводится по регистру, 
//  СтруктураШапкиДокумента - структура, содержащая значения "через точку" ссылочных реквизитов по шапке документа,
//  Отказ             - флаг отказа в проведении,
//  Заголовок         - строка, заголовок сообщения об ошибке проведения.
//
Процедура КонтрольОстатков(ДокументОбъект, ИмяТабличнойЧасти, СтруктураШапкиДокумента, Отказ, Заголовок) Экспорт

	МетаданныеДокумента   = ДокументОбъект.Метаданные();
	ИмяДокумента          = МетаданныеДокумента.Имя;
	ИмяТаблицы            = ИмяДокумента + "." + СокрЛП(ИмяТабличнойЧасти);
	ЕстьХарактеристика    = ЕстьРеквизитТабЧастиДокумента("ХарактеристикаНоменклатуры", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьДокументПередачи  = ЕстьРеквизитДокумента("ДокументПередачи", МетаданныеДокумента);
	ЕстьСерия             = ЕстьРеквизитТабЧастиДокумента("СерияНоменклатуры", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьКачество          = ЕстьРеквизитТабЧастиДокумента("Качество", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьФлагУказанияСерий = ЕстьРеквизитТабЧастиДокумента("СерияУказываетсяПриОтпускеСоСклада", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьКоэффициент       = ЕстьРеквизитТабЧастиДокумента("Коэффициент", МетаданныеДокумента, ИмяТабличнойЧасти);
	
	// Текст вложенного запроса, ограничивающего номенклатуру при получении остатков
	ТекстЗапросаСписокНоменклатуры = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура 
	|ИЗ
	|	Документ." + ИмяТаблицы +"
	|ГДЕ Ссылка = &ДокументСсылка";


	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", СтруктураШапкиДокумента.Ссылка);
	Запрос.УстановитьПараметр("Склад",          СтруктураШапкиДокумента.Склад);
	Запрос.УстановитьПараметр("ПустаяСерия",    Справочники.СерииНоменклатуры.ПустаяСсылка());	
	Если ЕстьДокументПередачи Тогда
		Запрос.УстановитьПараметр("ДокументПередачи",        СтруктураШапкиДокумента.ДокументПередачи);
	КонецЕсли;

	Запрос.Текст = "
	|ВЫБРАТЬ // Запрос, контролирующий остатки на складах
	|	Док.Номенклатура.Представление                         КАК НоменклатураПредставление,
	|	Док.Номенклатура.ЕдиницаХраненияОстатков.Представление КАК ЕдиницаХраненияОстатковПредставление,"
	+ ?(ЕстьХарактеристика, "
	|	Док.ХарактеристикаНоменклатуры.Представление           КАК ХарактеристикаНоменклатурыПредставление,"
	,"")
	+ ?(ЕстьСерия, "
	|	Док.СерияНоменклатуры.Представление                    КАК СерияНоменклатурыПредставление,"
	,"")
	+ ?(ЕстьКачество, "
	|	Док.Качество.Представление                             КАК КачествоПредставление,"
	,"")
	+ ?(ЕстьДокументПередачи, "
	|	&ДокументПередачи                                      КАК ДокументПередачи,", "
	|   Док.ДокументРезерва                                    КАК ДокументПередачи,")
	+ ?(ЕстьКоэффициент, "
	|	СУММА(Док.Количество * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК ДокументКоличество,", "
	|	СУММА(Док.Количество)                                  КАК ДокументКоличество,") + "
	|	МАКСИМУМ(Остатки.КоличествоОстаток)                    КАК ОстатокКоличество
	|ИЗ 
	|	Документ." + ИмяТаблицы + " КАК Док
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(,
	|		Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")
	|	И Склад            = &Склад"
	+ ?(ЕстьДокументПередачи, "
	|   И ДокументПередачи = &ДокументПередачи","") + "
	|	) КАК Остатки
	|ПО 
	|	Док.Номенклатура                = Остатки.Номенклатура"
	+ ?(ЕстьХарактеристика, "
	| И Док.ХарактеристикаНоменклатуры  = Остатки.ХарактеристикаНоменклатуры"
	,"")
	+ ?(ЕстьСерия, "
	| И "+ ?(ЕстьФлагУказанияСерий, "((Док.СерияНоменклатуры = Остатки.СерияНоменклатуры
	| И НЕ Док.СерияУказываетсяПриОтпускеСоСклада) ИЛИ (Остатки.СерияНоменклатуры = &ПустаяСерия И Док.СерияУказываетсяПриОтпускеСоСклада))", "Док.СерияНоменклатуры = Остатки.СерияНоменклатуры"), "")
	+ ?(ЕстьКачество, "
	| И Док.Качество                    = Остатки.Качество", "")
	+ ?(ЕстьДокументПередачи, "", "
	|   И ДокументПередачи = Док.ДокументРезерва") + "
	|
	|ГДЕ
	|	Док.Ссылка  =  &ДокументСсылка "
	+ ?(ЕстьДокументПередачи, "", "
	|   И Док.ДокументРезерва <> Неопределено") + "
	|
	|СГРУППИРОВАТЬ ПО
	|
	|	Док.Номенклатура"
	+ ?(ЕстьХарактеристика, ",
	|	Док.ХарактеристикаНоменклатуры "
	,"")
	+ ?(ЕстьДокументПередачи, "", ",
	|   Док.ДокументРезерва")
	+ ?(ЕстьСерия, ",
	|	Док.СерияНоменклатуры ", "")
	+ ?(ЕстьКачество, ",
	|	Док.Качество "	,"") + "
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки // Блокирующие чтение таблицы остатков регистра для разрешения коллизий многопользовательской работы
	|";

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		КоличествоОстаток = ?(Выборка.ОстатокКоличество = NULL, 0, Выборка.ОстатокКоличество);

		Если КоличествоОстаток < Выборка.ДокументКоличество Тогда

			СтрокаСообщения = "Остатка " + 
			ПредставлениеНоменклатуры(Выборка.НоменклатураПредставление, 
			                          ?(ЕстьХарактеристика, Выборка.ХарактеристикаНоменклатурыПредставление, ""), ?(ЕстьСерия, Выборка.СерияНоменклатурыПредставление, "")) +
			" к передаче со склада " + СокрЛП(СтруктураШапкиДокумента.Склад) + " по документу " + Выборка.ДокументПередачи +
			?(ЕстьКачество, " с качеством " + Выборка.КачествоПредставление, "") + " недостаточно.";

			ОшибкаНетОстатка(СтрокаСообщения, КоличествоОстаток, Выборка.ДокументКоличество,
			Выборка.ЕдиницаХраненияОстатковПредставление, Отказ, Заголовок);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // КонтрольОстатков()

// Процедура контролирует остаток по данному регистру по переданному документу
// и его табличной части. В случае недостатка товаров выставляется флаг отказа и 
// выдается сообщегние.
//
// Параметры:
//  ДокументОбъект    - объект проводимого документа, 
//  ИмяТабличнойЧасти - строка, имя табличной части, которая проводится по регистру, 
//  СтруктураШапкиДокумента - структура, содержащая значения "через точку" ссылочных реквизитов по шапке документа,
//  Отказ             - флаг отказа в проведении,
//  Заголовок         - строка, заголовок сообщения об ошибке проведения.
//
Процедура КонтрольСвободныхОстатков(ДокументОбъект, ИмяТабличнойЧасти, СтруктураШапкиДокумента, Отказ, Заголовок) Экспорт

	Если РазрешеноПревышениеОстаткаТоваровНаСкладе() Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	ИмяДокумента        = МетаданныеДокумента.Имя;
	ИмяТаблицы          = ИмяДокумента + "." + СокрЛП(ИмяТабличнойЧасти);
	ЕстьХарактеристика  = ЕстьРеквизитТабЧастиДокумента("ХарактеристикаНоменклатуры", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьСделка          = ЕстьРеквизитДокумента("Сделка", МетаданныеДокумента);
	ЕстьЗаказ           = ЕстьРеквизитДокумента("Заказ", МетаданныеДокумента);
	ЕстьСклад           = ЕстьРеквизитДокумента("Склад", МетаданныеДокумента);
	ЕстьСерия           = ЕстьРеквизитТабЧастиДокумента("СерияНоменклатуры", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьКачество        = ЕстьРеквизитТабЧастиДокумента("Качество", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьДокументРезерва = ЕстьРеквизитТабЧастиДокумента("ДокументРезерва", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьКоэффициент     = ЕстьРеквизитТабЧастиДокумента("Коэффициент", МетаданныеДокумента, ИмяТабличнойЧасти);
	ЕстьСкладВТабЧасти  = ЕстьРеквизитТабЧастиДокумента("Склад", МетаданныеДокумента, ИмяТабличнойЧасти);
	
	// Текст вложенного запроса, ограничивающего номенклатуру при получении остатков
	ТекстЗапросаСписокНоменклатуры = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура 
	|ИЗ
	|	Документ." + ИмяТаблицы +"
	|ГДЕ Ссылка = &ДокументСсылка";


	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", СтруктураШапкиДокумента.Ссылка);
	Запрос.УстановитьПараметр("ПустаяСерия"   , Справочники.СерииНоменклатуры.ПустаяСсылка());	
	Запрос.УстановитьПараметр("ПустойОрдер", Документы.ПриходныйОрдерНаТовары.ПустаяСсылка()); 
	
	Если ЕстьСделка Тогда
		Если ЗначениеНеЗаполнено(СтруктураШапкиДокумента.Сделка) Тогда
			Запрос.УстановитьПараметр("Сделка", Неопределено);
		Иначе
			Запрос.УстановитьПараметр("Сделка", СтруктураШапкиДокумента.Сделка); 
		КонецЕсли;
	ИначеЕсли ЕстьЗаказ Тогда
		Запрос.УстановитьПараметр("Сделка", СтруктураШапкиДокумента.Заказ); 
	Иначе
		Запрос.УстановитьПараметр("Сделка", Неопределено); 
	КонецЕсли;

	Запрос.Текст = "
	|ВЫБРАТЬ // Запрос, контролирующий остатки на складах
	|	Док.Номенклатура.Представление                         КАК НоменклатураПредставление,
	|	Док.Номенклатура.ЕдиницаХраненияОстатков.Представление КАК ЕдиницаХраненияОстатковПредставление,
	|	NULL                                                   КАК СерияНоменклатурыПредставление,"
	+ ?(ЕстьХарактеристика, "
	|	Док.ХарактеристикаНоменклатуры.Представление           КАК ХарактеристикаНоменклатурыПредставление,"
	,"")
	+ ?(ЕстьКачество, "
	|	Док.Качество.Представление                             КАК КачествоПредставление,"
	,"")
	+ ?(ЕстьСкладВТабЧасти, "
	|   Док.Склад                                              КАК Склад,", ?(ЕстьСклад, "
	|	Док.Ссылка.Склад                                       КАК Склад,", "
	|	Док.Ссылка.СкладОтправитель                            КАК Склад,"))
	+ ?(ЕстьКоэффициент, "
	|	СУММА(Док.Количество * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК ДокументКоличество,", "
	|	СУММА(Док.Количество)                                  КАК ДокументКоличество,") + "
	|	МАКСИМУМ(Резервы.КоличествоОстаток)                    КАК РезервыКоличество,
	|	МАКСИМУМ(РезервыПоСделке.КоличествоОстаток)            КАК РезервыПоСделкеКоличество,"
	+? (ИмяДокумента = "ПеремещениеТоваров", "
	|	МАКСИМУМ(КПолучению.КоличествоОстаток)                 КАК КПолучению,", "
	|	0                                                      КАК КПолучению,") + "
	|	МАКСИМУМ(Остатки.КоличествоОстаток)                    КАК ОстаткиКоличество,
	|	МАКСИМУМ(КПередаче.КоличествоОстаток)                  КАК КПередачеКоличество
	|ИЗ 
	|	Документ." + ИмяТаблицы + " КАК Док
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(,
	|		Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")) КАК Остатки
	|ПО 
	|	Док.Номенклатура                = Остатки.Номенклатура"
	+ ?(ЕстьСкладВТабЧасти, "
	|   И Док.Склад                     = Остатки.Склад ", ?(ЕстьСклад, "
	|   И Док.Ссылка.Склад              = Остатки.Склад ", "
	|   И Док.Ссылка.СкладОтправитель   = Остатки.Склад ")) 
	+ ?(ЕстьХарактеристика, "
	| И Док.ХарактеристикаНоменклатуры  = Остатки.ХарактеристикаНоменклатуры"
	,"")
	+ ?(ЕстьКачество, "
	| И Док.Качество                    = Остатки.Качество", "") + "
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(,
	|		Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")) КАК Резервы
	|ПО 
	|	Док.Номенклатура                = Резервы.Номенклатура"
	+ ?(ЕстьСкладВТабЧасти, "
	|   И Док.Склад                     = Резервы.Склад ", ?(ЕстьСклад, "
	|   И Док.Ссылка.Склад              = Резервы.Склад", "
	|   И Док.Ссылка.СкладОтправитель   = Резервы.Склад"))
	+ ?(ЕстьХарактеристика, "
	| И Док.ХарактеристикаНоменклатуры  = Резервы.ХарактеристикаНоменклатуры", "") + "
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(,
	|		Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")) КАК РезервыПоСделке
	|ПО 
	|	Док.Номенклатура                = РезервыПоСделке.Номенклатура"
	+ ?(ЕстьСкладВТабЧасти, "
	|   И Док.Склад                     = РезервыПоСделке.Склад ", ?(ЕстьСклад, "
	|   И Док.Ссылка.Склад              = РезервыПоСделке.Склад","
	|   И Док.Ссылка.СкладОтправитель   = РезервыПоСделке.Склад")) + " 
	|   И РезервыПоСделке.ДокументРезерва       = &Сделка "
	+ ?(ЕстьДокументРезерва, " ИЛИ РезервыПоСделке.ДокументРезерва = Док.ДокументРезерва", "")
	+ ?(ЕстьХарактеристика, "
	| И Док.ХарактеристикаНоменклатуры  = РезервыПоСделке.ХарактеристикаНоменклатуры"
	,"") + "
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(,
	|		Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")) КАК КПередаче
	|ПО 
	|	Док.Номенклатура                = КПередаче.Номенклатура"
	+  ?(ЕстьСкладВТабЧасти, "
	|   И Док.Склад                     = КПередаче.Склад ", ?(ЕстьСклад, "
	|   И Док.Ссылка.Склад              = КПередаче.Склад ","
	|   И Док.Ссылка.СкладОтправитель   = КПередаче.Склад "))
	+ ?(ЕстьХарактеристика, "
	| И Док.ХарактеристикаНоменклатуры  = КПередаче.ХарактеристикаНоменклатуры"
	,"")
	+ ?(ЕстьКачество, "
	| И Док.Качество                    = КПередаче.Качество", "")
	+ ?(ИмяДокумента = "ПеремещениеТоваров", "
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыКПолучениюНаСклады.Остатки(,
	|		Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")) КАК КПолучению
	|ПО 
	|	Док.Номенклатура                = КПолучению.Номенклатура"
	+ ?(ЕстьСкладВТабЧасти, "
	|   И Док.Склад                     = КПолучению.Склад ", ?(ЕстьСклад, "
	|   И Док.Ссылка.Склад              = КПолучению.Склад ","
	|   И Док.Ссылка.СкладОтправитель   = КПолучению.Склад "))
	+ ?(ЕстьХарактеристика, "
	| И Док.ХарактеристикаНоменклатуры  = КПолучению.ХарактеристикаНоменклатуры"
	,"")
	+ ?(ЕстьКачество, "
	| И Док.Качество                    = КПолучению.Качество", "") + "
	| И (Док.ДокументРезерва            = &ПустойОрдер ИЛИ Не Док.ДокументРезерва ССЫЛКА Документ.ПриходныйОрдерНаТовары)","") + "
	|
	|
	|ГДЕ
	|	Док.Ссылка  =  &ДокументСсылка"
	+ ?(ЕстьСкладВТабЧасти, "", ?(ЕстьСклад, "
	|   И Док.Ссылка.Склад ССЫЛКА Справочник.Склады", "")) + "
	|
	|СГРУППИРОВАТЬ ПО
	|
	|	Док.Номенклатура"
	+ ?(ЕстьХарактеристика, ",
	|	Док.ХарактеристикаНоменклатуры "
	,"")
	+ ?(ЕстьКачество, ",
	|	Док.Качество "	,"")
	+ ?(ЕстьСкладВТабЧасти, ",
	|   Док.Склад", ?(ЕстьСклад, ",
	|   Док.Ссылка.Склад",",
	|   Док.Ссылка.СкладОтправитель")) + "
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки, РегистрНакопления.ТоварыНаСкладах.Остатки, РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки // Блокирующие чтение таблицы остатков регистра для разрешения коллизий многопользовательской работы"
	+ ?(ЕстьСерия, "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Запрос, контролирующий остатки на складах
	|	Док.Номенклатура.Представление                         КАК НоменклатураПредставление,
	|	Док.Номенклатура.ЕдиницаХраненияОстатков.Представление КАК ЕдиницаХраненияОстатковПредставление,
	|	Док.СерияНоменклатуры.Представление                    КАК СерияНоменклатурыПредставление,"
	+ ?(ЕстьХарактеристика, "
	|	Док.ХарактеристикаНоменклатуры.Представление           КАК ХарактеристикаНоменклатурыПредставление,"
	,"")
	+ ?(ЕстьКачество, "
	|	Док.Качество.Представление                             КАК КачествоПредставление,"
	,"")
	+ ?(ЕстьСкладВТабЧасти, "
	|   Док.Склад                                              КАК Склад,", ?(ЕстьСклад, "
	|	Док.Ссылка.Склад                                       КАК Склад,", "
	|	Док.Ссылка.СкладОтправитель                            КАК Склад,"))
	+ ?(ЕстьКоэффициент, "
	|	СУММА(Док.Количество * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК ДокументКоличество,", "
	|	СУММА(Док.Количество)                                  КАК ДокументКоличество,") + "
	|	0                                                      КАК РезервыКоличество,
	|	0                                                      КАК РезервыПоСделкеКоличество,"
	+? (ИмяДокумента = "ПеремещениеТоваров", "
	|	МАКСИМУМ(КПолучению.КоличествоОстаток)                 КАК КПолучению,", "
	|	0                                                      КАК КПолучению,") + "
	|	МАКСИМУМ(Остатки.КоличествоОстаток)                    КАК ОстаткиКоличество,
	|	МАКСИМУМ(КПередаче.КоличествоОстаток)                  КАК КПередачеКоличество
	|ИЗ 
	|	Документ." + ИмяТаблицы + " КАК Док
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(,
	|		Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")) КАК Остатки
	|ПО 
	|	Док.Номенклатура                = Остатки.Номенклатура"
	+ ?(ЕстьСкладВТабЧасти, "
	|   И Док.Склад                     = Остатки.Склад ", ?(ЕстьСклад, "
	|   И Док.Ссылка.Склад              = Остатки.Склад","
	|   И Док.Ссылка.СкладОтправитель   = Остатки.Склад")) + " 
	|   И Док.СерияНоменклатуры         = Остатки.СерияНоменклатуры" 
	+ ?(ЕстьХарактеристика, "
	| И Док.ХарактеристикаНоменклатуры  = Остатки.ХарактеристикаНоменклатуры"
	,"")
	+ ?(ЕстьКачество, "
	| И Док.Качество                    = Остатки.Качество", "") + "
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(,
	|		Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")) КАК Резервы
	|ПО 
	|	Док.Номенклатура                = Резервы.Номенклатура"
	+ ?(ЕстьСкладВТабЧасти, "
	|   И Док.Склад                     = Резервы.Склад ", ?(ЕстьСклад, "
	|   И Док.Ссылка.Склад              = Резервы.Склад", "
	|   И Док.Ссылка.СкладОтправитель   = Резервы.Склад")) 
	+ ?(ЕстьХарактеристика, "
	| И Док.ХарактеристикаНоменклатуры  = Резервы.ХарактеристикаНоменклатуры", "") + "
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(,
	|		Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")) КАК КПередаче
	|ПО 
	|	Док.Номенклатура                = КПередаче.Номенклатура"
	+ ?(ЕстьСкладВТабЧасти, "
	|   И Док.Склад                     = КПередаче.Склад ", ?(ЕстьСклад, "
	|   И Док.Ссылка.Склад              = КПередаче.Склад", " 
	|   И Док.Ссылка.СкладОтправитель   = КПередаче.Склад")) + " 
	| 	И Док.СерияНоменклатуры         = КПередаче.СерияНоменклатуры"
	+ ?(ЕстьХарактеристика, "
	| И Док.ХарактеристикаНоменклатуры  = КПередаче.ХарактеристикаНоменклатуры"
	,"")
	+ ?(ЕстьКачество, "
	| И Док.Качество                    = КПередаче.Качество", "")
	+ ?(ИмяДокумента = "ПеремещениеТоваров", "
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыКПолучениюНаСклады.Остатки(,
	|		Номенклатура В (" + ТекстЗапросаСписокНоменклатуры + ")) КАК КПолучению
	|ПО 
	|	Док.Номенклатура                = КПолучению.Номенклатура"
	+ ?(ЕстьСкладВТабЧасти, "
	|   И Док.Склад                     = КПолучению.Склад ", ?(ЕстьСклад, "
	|   И Док.Ссылка.Склад              = КПолучению.Склад ","
	|   И Док.Ссылка.СкладОтправитель   = КПолучению.Склад "))
 + " 
	| 	И Док.СерияНоменклатуры         = КПолучению.СерияНоменклатуры"
	+ ?(ЕстьХарактеристика, "
	| И Док.ХарактеристикаНоменклатуры  = КПолучению.ХарактеристикаНоменклатуры"
	,"")
	+ ?(ЕстьКачество, "
	| И Док.Качество                    = КПолучению.Качество", "") + "
	| И (Док.ДокументРезерва            = &ПустойОрдер ИЛИ Не Док.ДокументРезерва ССЫЛКА Документ.ПриходныйОрдерНаТовары)","") + "
	|
	|
	|
	|ГДЕ
	|	Док.Ссылка  =  &ДокументСсылка
	|	И Док.СерияНоменклатуры <> &ПустаяСерия"
	+ ?(ЕстьСкладВТабЧасти, "", ?(ЕстьСклад, "
	|   И Док.Ссылка.Склад ССЫЛКА Справочник.Склады", "")) + "
	|
	|СГРУППИРОВАТЬ ПО
	|
	|	Док.Номенклатура,
	|	Док.СерияНоменклатуры"
	+ ?(ЕстьХарактеристика, ",
	|	Док.ХарактеристикаНоменклатуры "
	,"")
	+ ?(ЕстьКачество, ",
	|	Док.Качество "	,"")
	+ ?(ЕстьСкладВТабЧасти, ",
	|   Док.Склад", ?(ЕстьСклад, ",
	|   Док.Ссылка.Склад",",
	|   Док.Ссылка.СкладОтправитель")) + "
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки, РегистрНакопления.ТоварыНаСкладах.Остатки, РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки // Блокирующие чтение таблицы остатков регистра для разрешения коллизий многопользовательской работы
	|", "");

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл

		КоличествоВРезерве         = ?(Выборка.РезервыКоличество   = NULL, 0, Выборка.РезервыКоличество);
		КоличествоВРезервеПоСделке = ?(Выборка.РезервыПоСделкеКоличество   = NULL, 0, Выборка.РезервыПоСделкеКоличество);
		КоличествоНаСкладе         = ?(Выборка.ОстаткиКоличество   = NULL, 0, Выборка.ОстаткиКоличество);
		КоличествоКПередаче        = ?(Выборка.КПередачеКоличество = NULL, 0, Выборка.КПередачеКоличество);
		КоличествоКПолучению       = ?(Выборка.КПолучению = NULL, 0, Выборка.КПолучению);
		СвободныйОстаток           = КоличествоНаСкладе - КоличествоВРезерве - КоличествоКПередаче - КоличествоКПолучению + КоличествоВРезервеПоСделке;

		Если СвободныйОстаток < Выборка.ДокументКоличество Тогда

			СтрокаСообщения = "Свободного остатка для резервирования " + 
			ПредставлениеНоменклатуры(Выборка.НоменклатураПредставление, 
			                          ?(ЕстьХарактеристика, Выборка.ХарактеристикаНоменклатурыПредставление, ""), ?(ЕстьСерия, Выборка.СерияНоменклатурыПредставление, "")) +									  
			" на складе """ + СокрЛП(Выборка.Склад) + """" +?(ЕстьКачество, " с качеством " + Выборка.КачествоПредставление, "") + " недостаточно.";

			ОшибкаНетОстатка(СтрокаСообщения, СвободныйОстаток, Выборка.ДокументКоличество,
			Выборка.ЕдиницаХраненияОстатковПредставление, Отказ, Заголовок);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // КонтрольОстатков()


Процедура ПередЗаписью(Отказ, Замещение)
		
	ПроверкаПериодаЗаписей(ЭтотОбъект, Отказ);
	
КонецПроцедуры



