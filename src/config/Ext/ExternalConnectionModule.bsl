Перем глТорговоеОборудование Экспорт;
Перем глТекущийПользователь Экспорт;
Перем глСоответствиеТекстовЭлектронныхПисем Экспорт;

Процедура ПриНачалеРаботыСистемы()

	ОпределитьТекущегоПользователя();
	ПараметрыСеанса.ТекущийПользователь = глТекущийПользователь;
	УстановитьПараметрыСеанса(глТекущийПользователь);
	УстановитьПараметрГраницыЗапретаИзмененияДанных();

КонецПроцедуры // ПриНачалеРаботыСистемы()

Процедура ОпределитьТекущегоПользователя()

	Если ПустаяСтрока(ИмяПользователя()) Тогда
		ИмяПользователя           = "ВнешнееCоединение";
		ПолноеИмяПользователя     = "Внешнее соединение";

	Иначе
		ИмяПользователя           = ИмяПользователя();

		Если ПустаяСтрока(ПолноеИмяПользователя()) Тогда
			ПолноеИмяПользователя = ИмяПользователя;
		Иначе
			ПолноеИмяПользователя = ПолноеИмяПользователя();
		КонецЕсли;
	КонецЕсли;

	глТекущийПользователь = Справочники.Пользователи.НайтиПоКоду(ИмяПользователя);

	Если глТекущийПользователь = Справочники.Пользователи.ПустаяСсылка() Тогда
		ОбъектПользователь = Справочники.Пользователи.СоздатьЭлемент();

		ОбъектПользователь.Код          = ИмяПользователя;
		ОбъектПользователь.Наименование = ПолноеИмяПользователя;

		ОбъектПользователь.Записать();

		// Запишем в качестве основного ответственного самого пользователя.
		ЗаписьРегистра = РегистрыСведений.НастройкиПользователей.СоздатьМенеджерЗаписи();
		ЗаписьРегистра.Пользователь = ОбъектПользователь.Ссылка;
		ЗаписьРегистра.Настройка    = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнойОтветственный;
		ЗаписьРегистра.Значение     = ОбъектПользователь.Ссылка;
		ЗаписьРегистра.Записать();
		
		глТекущийПользователь = ОбъектПользователь.Ссылка;
	КонецЕсли;

КонецПроцедуры // ОпределитьТекущегоПользователя()

глСоответствиеТекстовЭлектронныхПисем = Новый Соответствие;