Процедура КодПриИзменении(Элемент)
	
	Если НЕ ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	// Если задан субсчет, то в его коде должна быть точка
	Если Найти(Код, ".") > 0 Тогда

		//Найдем код родителя, для этого найдем последнюю точку в коде счета
		ПозицияТочки = СтрДлина(Код);

		Пока Сред(Код, ПозицияТочки, 1) <> "." Цикл

			ПозицияТочки = ПозицияТочки - 1;

		КонецЦикла;

		КодРодителя    = Лев(Код, ПозицияТочки - 1);
		РодительПоКоду = ПланыСчетов.Хозрасчетный.НайтиПоКоду(КодРодителя);

		Если ЗначениеНеЗаполнено(РодительПоКоду) Тогда

			Предупреждение("План счетов не содержит счета с кодом "+ КодРодителя);

		ИначеЕсли РодительПоКоду <> Ссылка Тогда

			Родитель       = РодительПоКоду;
			Вид            = Родитель.Вид;
			Забалансовый   = Родитель.Забалансовый;
			Количественный = Родитель.Количественный;
			Валютный       = Родитель.Валютный;

			ЭлементыФормы.ВидыСубконто.Колонки.Валютный.Видимость       = Валютный;
			ЭлементыФормы.ВидыСубконто.Колонки.Количественный.Видимость = Количественный;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Процедура ВалютныйПриИзменении(Элемент)

	ЭлементыФормы.ВидыСубконто.Колонки.Валютный.Видимость = Валютный;

КонецПроцедуры

Процедура КоличественныйПриИзменении(Элемент)

	ЭлементыФормы.ВидыСубконто.Колонки.Количественный.Видимость = Количественный;

КонецПроцедуры

Процедура ВидыСубконтоПриНачалеРедактирования(Элемент, НоваяСтрока)

	Если НоваяСтрока Тогда

		Элемент.ТекущаяСтрока.Суммовой       = Истина;
		Элемент.ТекущаяСтрока.Валютный       = Истина;
		Элемент.ТекущаяСтрока.Количественный = Истина;

	КонецЕсли;

КонецПроцедуры // ВидыСубконтоПриНачалеРедактирования()

Процедура ПриОткрытии()

	ЭлементыФормы.ВидыСубконто.Колонки.Валютный.Видимость       = Валютный;
	ЭлементыФормы.ВидыСубконто.Колонки.Количественный.Видимость = Количественный;

КонецПроцедуры // ПриОткрытии()










