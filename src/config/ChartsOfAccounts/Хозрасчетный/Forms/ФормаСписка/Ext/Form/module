
Процедура СписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Колонка  = ОформлениеСтроки.Ячейки;
	Субконто = ДанныеСтроки.ВидыСубконто;

	Если Субконто.Количество() >= 1 Тогда
		Колонка.Субконто1.Текст = ?(Субконто[0].ТолькоОбороты, "(об) ", "") + Субконто[0].ВидСубконто;
		Колонка.Субконто1.ОтображатьТекст = Истина;
	КонецЕсли;

	Если Субконто.Количество() >= 2 Тогда
		Колонка.Субконто2.Текст = ?(Субконто[1].ТолькоОбороты, "(об) ", "") + Субконто[1].ВидСубконто;
		Колонка.Субконто2.ОтображатьТекст = Истина;
	КонецЕсли;

	Если Субконто.Количество() = 3 Тогда
		Колонка.Субконто3.Текст = ?(Субконто[2].ТолькоОбороты, "(об) ", "") + Субконто[2].ВидСубконто;
		Колонка.Субконто3.ОтображатьТекст = Истина;
	КонецЕсли;

	Если ДанныеСтроки.ЗапретитьИспользоватьВПроводках Тогда
		ОформлениеСтроки.ЦветФона = WebЦвета.СветлоЖелтый;
	КонецЕсли; 

	Вид = ДанныеСтроки.Вид;
	Если Вид = ВидСчета.Активный  Тогда
		Колонка.Вид.Текст = "А";
	ИначеЕсли Вид = ВидСчета.Пассивный  Тогда
		Колонка.Вид.Текст = "П";
	Иначе
		Колонка.Вид.Текст = "АП";
	КонецЕсли;

	Колонка.Вид.ОтображатьТекст = Истина;

КонецПроцедуры

Процедура ДействияФормыОборотноСальдоваяВедомость(Кнопка)

	ФормаОтчета = Отчеты.ОборотноСальдоваяВедомостьХозрасчетный.ПолучитьФорму();
	ФормаОтчета.Открыть();
	ФормаОтчета.ОбновитьОтчет();

КонецПроцедуры

Процедура ДействияФормыОборотноСальдоваяВедомостьПоСчету(Кнопка)

	Если ЭлементыФормы.Список.ТекущаяСтрока <> Неопределено Тогда
		
		Отчет       = Отчеты.ОборотноСальдоваяВедомостьПоСчетуХозрасчетный.Создать();
		Отчет.Счет  = ЭлементыФормы.Список.ТекущаяСтрока;
		Отчет.ОбработкаВыбораСчета();
		
		ФормаОтчета = Отчет.ПолучитьФорму();
		ФормаОтчета.ОбновитьОтчет();
		ФормаОтчета.Открыть();
		
	КонецЕсли;

КонецПроцедуры

Процедура ДействияФормыКарточкаСчета(Кнопка)

	Если ЭлементыФормы.Список.ТекущаяСтрока <> Неопределено Тогда
		ФормаОтчета = Отчеты.КарточкаСчетаХозрасчетный.ПолучитьФорму();
		ФормаОтчета.Открыть();
		ФормаОтчета.Счет = ЭлементыФормы.Список.ТекущаяСтрока;
		ФормаОтчета.ОбновитьОтчет();
	КонецЕсли;

КонецПроцедуры

Процедура ДействияФормыПерейтиВЖурналПроводок(Кнопка)

	ФормаЖурналаПроводок = РегистрыБухгалтерии.Хозрасчетный.ПолучитьФорму("ФормаСписка");
	ФормаЖурналаПроводок.Отбор.Счет.Значение      = ЭлементыФормы.Список.ТекущаяСтрока;
	ФормаЖурналаПроводок.Отбор.Счет.Использование = Истина;
	ФормаЖурналаПроводок.ЭлементыФормы.ОтборСчет.Значение = ЭлементыФормы.Список.ТекущаяСтрока;
	ФормаЖурналаПроводок.ЭлементыФормы.ИспользованиеОтбораСчет.Значение = Истина;
	ФормаЖурналаПроводок.Открыть();

КонецПроцедуры

ПланСчетовСписок.Колонки.Добавить("ВидыСубконто", Ложь);
ПланСчетовСписок.Колонки.Добавить("ЗапретитьИспользоватьВПроводках", Ложь);
ПланСчетовСписок.Колонки.Добавить("Вид", Ложь);